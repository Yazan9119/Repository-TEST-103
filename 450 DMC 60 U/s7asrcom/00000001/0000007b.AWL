FUNCTION "FC_PW_VERWALTUNG" : VOID
TITLE =
//$Revision: 1.46.1.9 $
//$Date: 2008/06/25 06:24:34CEST $
//$Author: hgc $
//
AUTHOR : BAG
FAMILY : DM_Basis
NAME : PW_Main
VERSION : 0.1


VAR_TEMP
  tx_dummy : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE ="DB_NC_PLC".PW_Anwahl
//PW nicht angewaehlt oder Modul nicht angewaehlt
      U     "m_neustart"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._015_ANWAHL_PALWECHLSER; 
      L     0; 
      >I    ; 
      )     ; 
      S     "DB_NC_PLC".PW_Anwahl; 
      U     "m_neustart"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._015_ANWAHL_PALWECHLSER; 
      L     0; 
      ==I   ; 
      )     ; 
      R     "DB_NC_PLC".PW_Anwahl; 
      NOP   0; 
NETWORK
TITLE =

      U     "m_neustart"; 
      U     "DB_NC_PLC".PW_Anwahl; 
      SPBN  CTRL; 
NETWORK
TITLE =Dekadenanwahl für Palettenwechsler auswerten
// 3. Dekade auswerten
      L     "DB_PLC_MD_DB20".UDInt._015_ANWAHL_PALWECHLSER; 
      ITB   ; 
      SRW   8; 
      UW    W#16#F; 
      T     "MW_PW_Anwahl_Option2"; 

// 2. Dekade auswerten
      L     "DB_PLC_MD_DB20".UDInt._015_ANWAHL_PALWECHLSER; 
      ITB   ; 
      SRW   4; 
      UW    W#16#F; 
      T     "MW_PW_Anwahl_Option"; 

// 1. Dekade auswerten
      L     "DB_PLC_MD_DB20".UDInt._015_ANWAHL_PALWECHLSER; 
      ITB   ; 
      UW    W#16#F; 
      T     "MW_PW_Anwahl_Typ"; 


NETWORK
TITLE =PW: 0 Grad

      U     "m_eins"; 
      R     "DB_PW_DATA".Option.bPW_NCAchse; 
      R     "DB_PW_DATA".Option.bRP_NCAchse; 
      R     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      R     "DB_PW_DATA".Option.bPW_Hub_Mitte_langsam; 
      R     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      R     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
      R     "DB_PW_DATA".Option.bPW_NAchse_ohne_Eingang; 
      R     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
      R     "DB_PW_DATA".Option.bMaschineTyp_Horizontal; 
      R     "DB_PW_DATA".Option.bPW_Tisch_Spuelung; 
      R     "DB_PW_DATA".Option.bSchwenkrundtisch; 
      R     "DB_PW_DATA".Option.bKopfAchse; 
      R     "DB_PW_DATA".Option.bMaschineTypFD_100_125; 
      R     "DB_PW_DATA".Option.bHub_Mitte_Desina_Faehig; 
      R     "DB_NC_PLC".PW_Kopf_Axis_vorhanden; 
      R     "DB_NC_PLC".PW_NC_Axis_vorhanden; 
      R     "DB_NC_PLC".PW_Schwenktisch_vorhande; 
      R     "DB_PW_DATA".Option2.bMaschineTypFD_70_80; 
NETWORK
TITLE =Elektr. Palettenwechsler mit Eingänge für DMC 100/125 U2/P2/H2 
//- Elektrischer Palettenwechsler 
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Kein Klemmriegel am Rüstplatz
//- Tischspülung
//- Prüfung Palette vorhanden
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     3; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      =     L      1.0; 
      U     L      1.0; 
      BLD   102; 
      S     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
      U     L      1.0; 
      BLD   102; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      U     L      1.0; 
      BLD   102; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      U     L      1.0; 
      U(    ; 
      L     "MW_PW_Anwahl_Option"; 
      L     2; 
      <>I   ; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_Tisch_Spuelung; 
      U     L      1.0; 
      U(    ; 
      L     "MW_PW_Anwahl_Option"; 
      L     2; 
      ==I   ; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
NETWORK
TITLE =Hydraulischer Palettenwechsler für DMC 70/80 P2/U2/H2 
//- Hydraulischer Palettenwechsler 
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Klemmriegel am Rüstplatz
//- Spülung der Konen
//- Prüfung Palette vorhanden
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     4; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      S     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
NETWORK
TITLE =Elektr. Palettenwechsler mit Eingänge für DMC 100/125 FD2 
//- Maschinentyp: Vertikal/Horizontal
//- Elektrischer Palettenwechsler 
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Prüfung Palette vorhanden
//- Fräs/Dreh-Funktion
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     5; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      S     "DB_PW_DATA".Option.bPW_Tisch_Spuelung; 
      S     "DB_PW_DATA".Option.bMaschineTypFD_100_125; 
NETWORK
TITLE =Elektr. Palettenwechsler mit Eingänge für DMC 70/80 FD2 
//- Maschinentyp: Vertikal/Horizontal
//- Elektrischer Palettenwechsler 
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Prüfung Palette vorhanden
//- Fräs/Dreh-Funktion
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     6; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      S     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
      S     "DB_PW_DATA".Option2.bMaschineTypFD_70_80; 
      S     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
NETWORK
TITLE =Elektr. Palettenwechsler ohne Eingänge für DMC 70/80 P2/U2/H2 
//- Maschinentyp: Vertikal/Horizontal
//- Elektrischer Palettenwechsler ohne Eingänge
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Prüfung Palette vorhanden
//- Schwenkrundtisch
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     7; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_NAchse_ohne_Eingang; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      S     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
NETWORK
TITLE =Hydraulischer Palettenwechsler für DMC 70/80 FD2 
//- Maschinentyp: Vertikal/Horizontal
//- Hydraulischer Palettenwechsler 
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Prüfung Palette vorhanden
//- Fräs/Dreh-Funktion
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     8; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      S     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
      S     "DB_PW_DATA".Option2.bMaschineTypFD_70_80; 
NETWORK
TITLE =Elektr. Palettenwechsler ohne Eingänge für DMC 70/80 FD2 
//- Maschinentyp: Vertikal/Horizontal
//- Elektrischer Palettenwechsler 
//- Geklemmt bis Mittelstellung bei Hub nach oben
//- Prüfung Palette vorhanden
//- Fräs/Dreh-Funktion
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     9; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      )     ; 
      S     "DB_PW_DATA".Option.bPW_Palette_vorhanden; 
      S     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
      S     "DB_PW_DATA".Option.bPW_Konen_Spuelung; 
      S     "DB_PW_DATA".Option2.bMaschineTypFD_70_80; 
      S     "DB_PW_DATA".Option.bPW_NAchse_ohne_Eingang; 
NETWORK
TITLE =

      U     "DB_PLC_MD_DB20".UDHex._09_BIT2_Pal_nur1_Pal; 
      R     "DB_PW_DATA".Option.bPW_Hub_Mittelstellung; 
NETWORK
TITLE =Hub Mitte Desina Schalter

      L     "MW_PW_Anwahl_Option"; 
      L     1; 
      ==I   ; 
      S     "DB_PW_DATA".Option.bHub_Mitte_Desina_Faehig; 
NETWORK
TITLE =Palettenwechsler NC-Achse

      O     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
      O     "DB_PW_DATA".Option.bPW_NAchse_ohne_Eingang; 
      S     "DB_PW_DATA".Option.bPW_NCAchse; 
      S     "DB_NC_PLC".PW_NC_Axis_vorhanden; 
NETWORK
TITLE =H-Maschine

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      S     "DB_PW_DATA".Option.bMaschineTyp_Horizontal; 
NETWORK
TITLE =Kopfachse vorhanden

      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     2; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     4; 
      ==I   ; 
      )     ; 
      S     "DB_PW_DATA".Option.bKopfAchse; 
      S     "DB_NC_PLC".PW_Kopf_Axis_vorhanden; 
NETWORK
TITLE =Schwenkrundtisch vorhanden

      L     "DB_PLC_MD_DB20".UDInt._043_ANWAHL_TISCH; 
      L     2; 
      ==I   ; 
      S     "DB_PW_DATA".Option.bSchwenkrundtisch; 

      S     "DB_NC_PLC".PW_Schwenktisch_vorhande; 
NETWORK
TITLE =ABST: RP-Paletten-Klemmung hydraulische Ausführung

      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      U     "DB_PLC_MD_DB20".UDHex._17_BIT7_RP_HYD_KLEMMEN; 
      =     "DB_PW_DATA".Option.bRP_Hydr_Klemmung; 
NETWORK
TITLE =ABST: RP-Paletten-Klemmung Hand-Hebel-Ausführung
//Ohne SpannHydraulik ist Paletten-Klemmung immer mit Hand-Hebel
      UN    "DB_PW_DATA".Option.bRP_Hydr_Klemmung; 
      U(    ; 
      L     "MW_PW_Anwahl_Typ"; 
      L     5; 
      <>I   ; 
      )     ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      UN    "DB_PLC_MD_DB20".UDHex._33_Bit7_RPL_keine_Klemm; 
      =     "DB_PW_DATA".Option.bRP_Hand_Klemmung; 
NETWORK
TITLE =

CTRL: U     "DB_NC_PLC".PW_Anwahl; 
      SPBN  PROG; 
NETWORK
TITLE =Option Rundspeicher aktiv

      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS4_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; 
      =     "DB_PW_DATA".Option2.bRundspeicher; 
NETWORK
TITLE =Option Palettenverwaltung aktiv

      L     "DB_PLC_MD_DB20".UDInt._041_ANWAHL_PALVERWAL; 
      L     0; 
      <>I   ; 
      =     "DB_PW_DATA".Option2.bPalettenverwaltung; 

NETWORK
TITLE =

      U(    ; 
      O     "DB_PW_DATA".Out.OX_PW_Hub_heben; 
      O     "DB_PW_DATA".Out.OX_PW_Hub_senken; 
      )     ; 
      FP    "MX_FP_Hub_Bewegung"; 
      =     "MX_Hub_Bewegung"; 
NETWORK
TITLE =Hub Mitte von unten nach oben

      U     "I_PW_HUB_POS2"; 
      FN    "MX_FN_Hub_Mitte"; 
      S     "MX_Hub_Mitte_von_unten"; 
      U(    ; 
      O     "DB_PW_DATA".In.IX_PW_Hub_unten; 
      O     "DB_PW_DATA".In.IX_PW_Hub_oben; 
      O     "MX_Hub_Bewegung"; 
      )     ; 
      R     "MX_Hub_Mitte_von_unten"; 
      NOP   0; 
NETWORK
TITLE =PW: Hub Mitte von Oben

      U     "I_PW_HUB_POS2"; 
      FP    "MX_FP_Hub_Mitte"; 
      S     "MX_Hub_Mitte_von_oben"; 
      U(    ; 
      O     "DB_PW_DATA".In.IX_PW_Hub_unten; 
      O     "DB_PW_DATA".In.IX_PW_Hub_oben; 
      O     "MX_Hub_Bewegung"; 
      )     ; 
      R     "MX_Hub_Mitte_von_oben"; 
      NOP   0; 
NETWORK
TITLE =

      U     "I_PW_HUB_2_UNTEN"; 
      FP    "MX_FP_PW_HUB_VOR_UNTEN"; 
      =     "MX_PW_HUB_VOR_UNTEN_HEBE"; 

      U     "I_PW_HUB_2_UNTEN"; 
      FN    "MX_FN_PW_HUB_VOR_UNTEN"; 
      =     "MX_PW_HUB_VOR_UNTEN_SENK"; 

      U     "I_PW_HUB_2_MITTE"; 
      FP    "MX_FP_PW_HUB_VOR_MITTE"; 
      =     "MX_PW_HUB_VOR_MITTE_HEBE"; 

      U     "I_PW_HUB_2_MITTE"; 
      FN    "MX_FN_PW_HUB_VOR_MITTE"; 
      =     "MX_PW_HUB_VOR_MITTE_SENK"; 

NETWORK
TITLE =Eingänge der Palettenwechsler rangieren

      U     "DB_NC_PLC".PW_Anwahl; 
      =     L      1.0; 
      U     L      1.0; 
      U(    ; 
      UN    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "I_PW_Hub_oben"; 
      O     ; 
      U(    ; 
      U     "DB_PLC_MD_DB20".UDHex._49_Bit3_PW_HubPos_1234; 
      UN    "I_PW_HUB_POSITION_4"; 
      ON    "DB_PLC_MD_DB20".UDHex._49_Bit3_PW_HubPos_1234; 
      )     ; 
      U(    ; 
      O     "DB_PW_DATA".Option.bSchwenkrundtisch; 
      O     ; 
      UN    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "DB_PLC_MD_DB20".UDHex._23_Bit7_PW_HUB_ABFR_NEU; 
      )     ; 
      UN    "I_PW_HUB_POS1"; 
      UN    "I_PW_HUB_POS2"; 
      UN    "I_PW_HUB_POS3"; 
      )     ; 
      =     "DB_PW_DATA".In.IX_PW_Hub_oben; 
      U     L      1.0; 
      U(    ; 
      UN    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "I_PW_Hub_unten"; 
      O     ; 
      U(    ; 
      U     "DB_PLC_MD_DB20".UDHex._49_Bit3_PW_HubPos_1234; 
      U     "I_PW_HUB_POSITION_4"; 
      ON    "DB_PLC_MD_DB20".UDHex._49_Bit3_PW_HubPos_1234; 
      )     ; 
      U(    ; 
      O     "DB_PW_DATA".Option.bSchwenkrundtisch; 
      O     ; 
      UN    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "DB_PLC_MD_DB20".UDHex._23_Bit7_PW_HUB_ABFR_NEU; 
      )     ; 
      U     "I_PW_HUB_POS1"; 
      U     "I_PW_HUB_POS2"; 
      U     "I_PW_HUB_POS3"; 
      )     ; 
      =     "DB_PW_DATA".In.IX_PW_Hub_unten; 
      U     L      1.0; 
      U(    ; 
      UN    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "I_PW_Hub_in_Mitte"; 
      O     ; 
      U(    ; 
      O     "DB_PW_DATA".Option.bSchwenkrundtisch; 
      O     ; 
      UN    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "DB_PLC_MD_DB20".UDHex._23_Bit7_PW_HUB_ABFR_NEU; 
      )     ; 
      UN    "I_PW_HUB_POS1"; 
      U     "I_PW_HUB_POS3"; 
      U(    ; 
      O     "MX_Hub_Mitte_von_unten"; 
      O     "MX_Hub_Mitte_von_oben"; 
      )     ; 
      )     ; 
      =     "DB_PW_DATA".In.IX_PW_Hub_mitte; 
      U     L      1.0; 
      U(    ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._005_ANWAHL_SONDER_SPDL; 
      L     2; 
      <>I   ; 
      )     ; 
      U     "I_PW_Klemmung_geloest"; 
      O     ; 
      U     "I_PW_Klemmung_geloest"; 
      U(    ; 
      O     "I_FD_PW_KLE_geloest"; 
      ON    "DB_PLC_MD_DB20".UDHex._26_Bit0_FD_PalKle_Tisch; 
      )     ; 
      )     ; 
      =     "DB_PW_DATA".In.IX_PW_AR_PAL_geloest; 
      U     L      1.0; 
      U(    ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._005_ANWAHL_SONDER_SPDL; 
      L     2; 
      <>I   ; 
      )     ; 
      U     "I_PW_Palette_geklemmt"; 
      O     ; 
      U     "I_PW_Palette_geklemmt"; 
      U(    ; 
      O     "I_FD_PW_KLE_geklemmt"; 
      ON    "DB_PLC_MD_DB20".UDHex._26_Bit0_FD_PalKle_Tisch; 
      )     ; 
      )     ; 
      =     "DB_PW_DATA".In.IX_PW_AR_PAL_gespannt; 
      U     L      1.0; 
      U     "I_PW_RPL_PAL_GELOEST"; 
      =     "DB_PW_DATA".In.IX_PW_RP_PAL_geloest; 
      U     L      1.0; 
      U     "I_PW_RPL_PAL_GEKLEMMT"; 
      =     "DB_PW_DATA".In.IX_PW_RP_PAL_gespannt; 
      U     L      1.0; 
      U     "I_PW_180_GRAD_M62"; 
      =     "DB_PW_DATA".In.IX_PW_Dreh_180; 
      U     L      1.0; 
      U     "I_PW_0_GRAD_M61"; 
      =     "DB_PW_DATA".In.IX_PW_Dreh_0; 
      U     L      1.0; 
      U     "I_PW_MPL_belegt"; 
      =     L      1.1; 
      U     L      1.1; 
      BLD   102; 
      L     S5T#200MS; 
      SE    "Te_PW_MPL_belegt"; 
      U     L      1.1; 
      U     "Te_PW_MPL_belegt"; 
      =     "DB_PW_DATA".In.IX_PW_PAL_Vorhanden; 
      U     L      1.0; 
      U(    ; 
      U(    ; 
      O     "I_PW_Ruestpl_Uebergabe"; 
      O     "I_PW_UPL_PAL_VORH"; 
      O     ; 
      U     "DB_PW_DATA".Option.bRP_NCAchse; 
      U     "DB_PLC_MD_DB20".UDHex._51_Bit2_RPL_AX_OHNE_EES; 
      )     ; 
      U(    ; 
      U     "DB_SIEM_NCK".E_SWCamPlus[13]; 
      U     "M_15Achse_Cam_Aktiv"; 
      ON    "DB_PW_DATA".Option.bRP_NCAchse; 
      )     ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_VORHANDEN; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_VORHANDEN; 
      O     "DB_PW_DATA".Option2.bRundspeicher; 
      O     "DB_FEHLERMELDUNG".WARNUNG5._702805_UEBERBR_RP_GRD; 
      )     ; 
      =     "DB_PW_DATA".In.IX_RP_PAL_0Grad; 
      U     L      1.0; 
      UN    "I_PW_RP_GELOEST"; 
      UN    "DB_PW_DATA".Option2.bRundspeicher; 
      UN    "DB_FEHLERMELDUNG".WARNUNG5._702805_UEBERBR_RP_GRD; 
      =     "DB_PW_DATA".In.IX_RP_PAL_geklemmt; 
      U     L      1.0; 
      U(    ; 
      O     "I_PW_Tueren_zu"; 
      O     "DB_PW_DATA".Option2.bRundspeicher; 
      O     ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_VORHANDEN; 
      U     "I_ROBOT_PROT_DOOR_CLSD"; 
      O     "DB_FEHLERMELDUNG".WARNUNG5._702805_UEBERBR_RP_GRD; 
      )     ; 
      =     "DB_PW_DATA".In.IX_RPT_zu; 
NETWORK
TITLE =
//    U     "I_PW_Ruestpl_Uebergabe"
      U     "I_PW_UPL_PAL_VORH"; 
      =     "DB_NC_PLC".PW_RP_PAl_vorhanden; 
NETWORK
TITLE =

      UN    "M_PW_Aktiv"; 
      SPBNB _001; 
      L     "DB_NC_PLC".MFunktion; 
      T     "DB_PW_DATA".iMFunktion; 
_001: NOP   0; 
NETWORK
TITLE =

      U     "m_null"; 
      =     "M_4Achse_Cam_Aktiv"; 
      =     "M_5Achse_Cam_Aktiv"; 
      =     "M_8Achse_Cam_Aktiv"; 
      =     "M_15Achse_Cam_Aktiv"; 
NETWORK
TITLE =

      UN    "M_4Achse_existiert"; 
      SPB   K5TE; 
NETWORK
TITLE =

      U     "DB_ACHSE4".E_SWCam; 
      =     "M_4Achse_Cam_Aktiv"; 
NETWORK
TITLE =

K5TE: UN    "M_5Achse_existiert"; 
      SPB   K8TE; 
NETWORK
TITLE =

      U     "DB_ACHSE5".E_SWCam; 
      =     "M_5Achse_Cam_Aktiv"; 
NETWORK
TITLE =

K8TE: UN    "M_8Achse_existiert"; 
      SPB   K9TE; 
NETWORK
TITLE =

      U     "DB_ACHSE8".E_SWCam; 
      =     "M_8Achse_Cam_Aktiv"; 
NETWORK
TITLE =

K9TE: UN    "DB_SIEM_STARTUP".ActivAxis[15]; 
      SPB   K0TE; 
NETWORK
TITLE =

      U     "DB_ACHSE15".E_SWCam; 
      =     "M_15Achse_Cam_Aktiv"; 
NETWORK
TITLE =

K0TE: U(    ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      U     "DB_SIEM_NCK".E_SWCamMinus[7]; 
      O     ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      U     "DB_SIEM_NCK".E_SWCamPlus[7]; 
      )     ; 
      U     "DB_ACHSE1_X".E_SWCam; 
      =     "DB_PW_DATA".bAxisXInWechselpos; 
NETWORK
TITLE =Tisch-Achse(Y oder Z) in Wechselposition (Software-Nocken)

      U     "DB_SIEM_NCK".E_SWCamMinus[9]; 
      U     "DB_SIEM_NCK".E_SWCamPlus[9]; 
      U(    ; 
      UN    "DB_PW_DATA".Option.bMaschineTyp_Horizontal; 
      U     "DB_ACHSE2_Y".E_SWCam; 
      O     ; 
      U     "DB_PW_DATA".Option.bMaschineTyp_Horizontal; 
      U     "DB_ACHSE3_Z".E_SWCam; 
      )     ; 
      =     "DB_PW_DATA".bAxisTischInWechselpos; 
NETWORK
TITLE =Tisch-Achse(Y oder Z) in Rückzugsposition (Software-Nocken)

      U     "DB_SIEM_NCK".E_SWCamMinus[17]; 
      U     "DB_SIEM_NCK".E_SWCamPlus[17]; 
      U(    ; 
      UN    "DB_PW_DATA".Option.bMaschineTyp_Horizontal; 
      U     "DB_ACHSE2_Y".E_SWCam; 
      O     ; 
      U     "DB_PW_DATA".Option.bMaschineTyp_Horizontal; 
      U     "DB_ACHSE3_Z".E_SWCam; 
      )     ; 
      =     "DB_PW_DATA".bAxisTischDrehPosition; 
NETWORK
TITLE =Dreh-Achse(C/B) in Wechselposition (Software-Nocken)

      U(    ; 
      UN    "DB_REMANENT".Achse_4_modulo; 
      U     "DB_SIEM_NCK".E_SWCamPlus[10]; 
      U     "DB_SIEM_NCK".E_SWCamMinus[10]; 
      O     ; 
      U     "DB_REMANENT".Achse_4_modulo; 
      U     "DB_SIEM_NCK".E_SWCamPlus[10]; 
      )     ; 
      U(    ; 
      U(    ; 
      O     "DB_ACHSE4".E_IndexAxisPos; 
      O     "I_AL_4ACHS_KLE_zu"; 
      )     ; 
      U     "I_TAB_POSITION_0DEG"; 
      ON    "DB_PLC_MD_DB20".UDHex._28_Bit7_B_C_Takttisch; 
      )     ; 
      U     "DB_ACHSE4".E_SWCam; 
      =     "DB_PW_DATA".bAxisRundInWechselpos; 
NETWORK
TITLE =Kopf-Achse(B/A) oder Schwenkrundtisch(A) in Wechselposition

      U     "DB_PW_DATA".Option.bKopfAchse; 
      U     "DB_SIEM_NCK".E_SWCamPlus[6]; 
      U     "DB_SIEM_NCK".E_SWCamMinus[6]; 
      U     "DB_ACHSE5".E_SWCam; 
      ON    "DB_PW_DATA".Option.bKopfAchse; 
      =     "DB_PW_DATA".bAxisKopfInWechselpos; 
NETWORK
TITLE =

      U     "DB_PW_DATA".Option.bSchwenkrundtisch; 
      U     "DB_SIEM_NCK".E_SWCamPlus[6]; 
      U     "DB_SIEM_NCK".E_SWCamMinus[6]; 
      U     "DB_ACHSE5".E_SWCam; 
      ON    "DB_PW_DATA".Option.bSchwenkrundtisch; 
      =     "DB_PW_DATA".bAxisSchenkrundWechsPos; 
NETWORK
TITLE =

      UN    "DB_PW_DATA".Option2.bRundspeicher; 
      UN    "DB_PW_DATA".Option2.bPalettenverwaltung; 
      SPBNB _002; 
      L     0; 
      T     "DB_NC_PLC".PW_Pal_Nummer; 
_002: NOP   0; 
NETWORK
TITLE =Palettenwechsler auf 0 Grad (Eingänge und Softwarenocken)

      U(    ; 
      U     "DB_PW_DATA".In.IX_PW_Dreh_0; 
      UN    "DB_PW_DATA".In.IX_PW_Dreh_180; 
      O     "DB_PW_DATA".Option.bPW_NAchse_ohne_Eingang; 
      O     ; 
      U     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
      U     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      )     ; 
      U(    ; 
      U     "DB_SIEM_NCK".E_SWCamMinus[11]; 
      U     "DB_SIEM_NCK".E_SWCamPlus[11]; 
      U     "M_8Achse_Cam_Aktiv"; 
      ON    "DB_PW_DATA".Option.bPW_NCAchse; 
      )     ; 
      =     L      1.0; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_PW_DATA".bPW_0Grad; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_NC_PLC".PW_Dreh_in_0Grad; 
      U     L      1.0; 
      UN    "DB_PW_DATA".Option2.bRundspeicher; 
      UN    "DB_PW_DATA".Option2.bPalettenverwaltung; 
      SPBNB _003; 
      L     1; 
      T     "DB_NC_PLC".PW_Pal_Nummer; 
_003: NOP   0; 
NETWORK
TITLE =Palettenwechsler auf 180 Grad (Eingänge und Softwarenocken)

      U(    ; 
      UN    "DB_PW_DATA".In.IX_PW_Dreh_0; 
      U     "DB_PW_DATA".In.IX_PW_Dreh_180; 
      O     "DB_PW_DATA".Option.bPW_NAchse_ohne_Eingang; 
      O     ; 
      U     "DB_PW_DATA".Option.bPW_NCAchse_mit_Eingang; 
      U     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      )     ; 
      U(    ; 
      U     "DB_SIEM_NCK".E_SWCamMinus[12]; 
      U     "DB_SIEM_NCK".E_SWCamPlus[12]; 
      U     "M_8Achse_Cam_Aktiv"; 
      ON    "DB_PW_DATA".Option.bPW_NCAchse; 
      )     ; 
      =     L      1.0; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_PW_DATA".bPW_180Grad; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_NC_PLC".PW_Dreh_in_180Grad; 
      U     L      1.0; 
      UN    "DB_PW_DATA".Option2.bRundspeicher; 
      UN    "DB_PW_DATA".Option2.bPalettenverwaltung; 
      SPBNB _004; 
      L     2; 
      T     "DB_NC_PLC".PW_Pal_Nummer; 
_004: NOP   0; 
NETWORK
TITLE =Rüstplatz auf Wechselposition (Eingänge und Softwarenocken)

      U     "DB_PW_DATA".In.IX_RP_PAL_0Grad; 
      =     "DB_PW_DATA".bRP_0Grad; 
NETWORK
TITLE =Leistungsfreigabe vom PW-Hub
//Leistungsfreigabe fuer PW Achse sobald der Hub oben ist
//      U     "DB_PW_DATA".In.IX_PW_Hub_oben; 
//      UN    "DB_PW_DATA".In.IX_PW_Hub_unten; 
//      =     "DB_SAFE_ALLG".PW_Leistungsfreigabe; 
NETWORK
TITLE =Hauptachsen in Wechselposition

      U(    ; 
      O     "DB_PW_DATA".bAxisXInWechselpos; 
      O     "DB_NC_PLC".WZW_NC_Aktiv; 
      )     ; 
      U     "DB_PW_DATA".bAxisTischInWechselpos; 
      U     "DB_PW_DATA".bAxisRundInWechselpos; 
      U(    ; 
      O     "DB_PW_DATA".bAxisKopfInWechselpos; 
      O     "DB_NC_PLC".WZW_NC_Aktiv; 
      )     ; 
      U     "DB_PW_DATA".bAxisSchenkrundWechsPos; 
      =     "DB_PW_DATA".bAllAxisInWechselpos; 
NETWORK
TITLE =Palettenwechsler Arbeitsraum in Grundstellung

      U(    ; 
      U     "DB_PW_DATA".bPW_0Grad; 
      UN    "DB_PW_DATA".bPW_180Grad; 
      O     ; 
      UN    "DB_PW_DATA".bPW_0Grad; 
      U     "DB_PW_DATA".bPW_180Grad; 
      )     ; 
      U     "DB_PW_DATA".In.IX_PW_Hub_unten; 
      UN    "DB_PW_DATA".In.IX_PW_Hub_oben; 
      U     "DB_PW_DATA".In.IX_PW_AR_PAL_gespannt; 
      U(    ; 
      O     "I_SP_MPL_GEKUPPELT"; 
      ON    "DB_HR".ABST.SP_MPL_Kuppeln; 
      )     ; 
      U(    ; 
      U     "I_PW_MPL_MDK_ENTKUPPELT"; 
      UN    "O_PW_SPANNDRUCK_HYD_AUS"; 
      ON    "DB_PW_DATA".Option2.bMaschineTypFD_70_80; 
      )     ; 
      =     "DB_PW_DATA".bPW_GRDST; 
      =     "M_PW_GRDST"; 
      =     "DB_OEM".PW_in_Grdst; 
NETWORK
TITLE =Rüstplatz in Grundstellung

      U     "DB_PW_DATA".bRP_0Grad; 
      U     "DB_PW_DATA".In.IX_RPT_zu; 
      U(    ; 
      U     "DB_PW_DATA".Option.bRP_Hand_Klemmung; 
      UN    "DB_PW_DATA".In.IX_RP_PAL_geklemmt; 
      ON    "DB_PW_DATA".Option.bRP_Hand_Klemmung; 
      )     ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      O     ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      U     "I_FMS_APC_MOVEMENT_OK"; 
      O     "DB_FEHLERMELDUNG".WARNUNG5._702805_UEBERBR_RP_GRD; 
      =     "DB_PW_DATA".bRP_GRDST; 
NETWORK
TITLE =

      U     "DB_EINRICHTFUNKTION".Bewegung_100_Plus; 
      S     "DB_PW_DATA".bEinzelschritt_Aktiv; 
      U(    ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_100_Minus; 
      O     "m_neustart"; 
      )     ; 
      R     "DB_PW_DATA".bEinzelschritt_Aktiv; 
      NOP   0; 
NETWORK
TITLE =Aufruf Ablauf PW

PROG: U     "DB_NC_PLC".PW_Anwahl; 
      =     L      1.0; 

      BLD   103; 
      CALL "FB_PW_ABLAUF" , "DI_PW_ABLAUF" (
           E_Enabled                := L      1.0);
      NOP   0; 
NETWORK
TITLE =Aufruf Fehlermodul PW

      U     "DB_NC_PLC".PW_Anwahl; 
      =     L      1.0; 
      BLD   103; 
      CALL "FC_PW_FEHLERMODUL" (
           E_Enabled                := L      1.0);
      NOP   0; 
NETWORK
TITLE =Aufruf Schmierung PW

      CALL "FC_PW_SCHMIERUNG" ;
      NOP   0; 
NETWORK
TITLE =

      CALL "FC_PS_WRITE_GUD" (
           Enable                   := "DB_PLC_MD_DB20".UDHex._32_Bit1_RPL_Erweit_Data);

NETWORK
TITLE =Ausgänge der Palettenwechsler rangieren

      U     "DB_NC_PLC".PW_Anwahl; 
      =     L      1.0; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_Hub_heben; 
      =     "O_PW_Hub_heben"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_Hub_senken; 
      =     "O_PW_Hub_senken"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_AR_PAL_loesen; 
      =     "O_PW_KLE_loesen"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_AR_PAL_spannen; 
      =     "O_PW_KLE_klemmen"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_Dreh_180; 
      =     "O_PW_180_Grad_Drehen"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_Dreh_0; 
      =     "O_PW_0_Grad_Drehen"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_Hydraulik_schnell; 
      =     #tx_dummy; 
NETWORK
TITLE =

      U     "MX_ABST_Rundspeicher"; 
      SPB   ORS; 
NETWORK
TITLE =Ausgänge der Palettenwechsler rangieren

      U     "DB_NC_PLC".PW_Anwahl; 
      =     L      1.0; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_RP_PAL_loesen; 
      =     "O_PW_RPL_PAL_LOESEN"; 
      U     L      1.0; 
      U     "DB_PW_DATA".Out.OX_PW_RP_PAL_spannen; 
      =     "O_PW_RPL_PAL_KLEMMEN"; 
NETWORK
TITLE =

ORS:  U     "m_null"; 
      =     #tx_dummy; 
END_FUNCTION

