ORGANIZATION_BLOCK "OB_WECKALARM1"
TITLE =
//$Revision: 1.20 $
//$Date: 2008/10/02 10:59:04CEST $
//$Author: schmoelp $
VERSION : 0.1


VAR_TEMP
  OB35_EV_CLASS : BYTE ;	//Bits 0-3 = 1 (Coming event), Bits 4-7 = 1 (Event class 1)
  OB35_STRT_INF : BYTE ;	//16#36 (OB 35 has started)
  OB35_PRIORITY : BYTE ;	//11 (Priority of 1 is lowest)
  OB35_OB_NUMBR : BYTE ;	//35 (Organization block 35, OB35)
  OB35_RESERVED_1 : BYTE ;	//Reserved for system
  OB35_RESERVED_2 : BYTE ;	//Reserved for system
  OB35_PHASE_OFFSET : WORD ;	//Phase offset (msec)
  OB35_RESERVED_3 : INT ;	//Reserved for system
  OB35_EXC_FREQ : INT ;	//Frequency of execution (msec)
  OB35_DATE_TIME : DATE_AND_TIME ;	//Date and time OB35 started
  EB_37 : ARRAY  [0 .. 7 ] OF BOOL ;	
  FC21_error : BOOL ;	
  FC21_errcode : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =Abfrage der Peripherieeingänge im Rangierer
// Netzwerk-Übersicht
// ==================
// NW 1: Abfrage der Peripherieeingänge im Rangierer
// NW 2: Ablaufmodul getaktetes Magazin
// NW 3: Spannungsüberwachung Netz
// NW 4: Werkzeugbruch Lesemodul
// NW 5: Schreiben der Peripherieausgänge im Rangierer
// NW 6: Periepherie für Ausgabe an Dualport-RAM lesen
// NW 7: DPR 14.0: Wechslerklappe ist offen
// NW 8: DPR 14.1: WZ-Spanner ist geloest
// NW 9: DPR 14.2: WZ-Spanner ist gespannt MIToderOHNE Werkzeug
// NW 10: DPR 14.3: Werkzeugmagazin 1 in Position Wechselstelle
// NW 11: DPR 14.4: Bedingung Werkzeug holen
// NW 12: DPR 14.5: Magazinplatz ist frei
// NW 13: DPR 14.6: 
// NW 14: DPR 14.7: Freigabe zum Öffnen der Magazinklappe
// NW 15: DPR 15.0: Zange geöffnet - Spindelseitig
// NW 16: DPR 15.1: Zange geschlossen - Spindelseitig
// NW 17: DPR 15.2: WZ-Spanner gespannt MIT Werkzeug
// NW 18: DPR 15.3: WZ-Spanner gespannt OHNE Werkzeug
// NW 19: DPR 15.4: Hub weg von Spindel
// NW 20: DPR 15.5: Hub in Spindelstellung
// NW 21: DPR 15.6:
// NW 22: DPR 15.7:
// NW 23: Byte 14+15 im Dualport-RAM schreiben für WW-Ablauf
// NW 24: ToolInspect: AdaptivControl - Regelung
// NW 25: DPR-Word_372: ToolInspect: AdaptivControl - Regelung (an NC)
      CALL "FC_CIO_IO_RANGIER" (
           IN0                      := FALSE,
           IN1                      := FALSE,
           IN2                      := TRUE,
           IN3                      := FALSE);

NETWORK
TITLE =Ablaufmodul getaktetes Magazin

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_KETTE_GETAKTET; 
      SPBN  ENDE; 


      CALL "FC_GT_MAG_ABL" (
           E_Enable                 := TRUE,
           E_Maschine_Ein           := "M_MA_Maschine_Ein",
           E_Mag_Links_Drehen       := "M_MAG_Links_Fahren",
           E_Mag_Rechts_Drehen      := "M_MAG_Rechts_Fahren",
           E_Mag_Start              := "M_MAG_Start",
           E_MD_Anwahl_Magazin      := "DB_PLC_MD_DB20".UDInt._014_ANWAHL_MAGAZIN,
           E_MD_MAG_Max_Schaltzeit  := "DB_PLC_MD_DB20".UDInt._123_MAX_ZAEHLIMPULS,
           E_MD_Mag_Nachlaufzyklen  := "DB_PLC_MD_DB20".UDInt._124_ABSCHALT_VERZ_MAG,
           E_Anzahl_Mag_Plaetze     := "DB_PLC_MD_DB20".UDInt._120_ANZAHL_MAG_PLAETZE,
           E_Max_Zeit_ZaehlImp      := "MW_Max_ZaehlImpuls",
           E_Timer_Zaehlimpuls      := "Te_Mag_Zaehlimp",
           EA_Mag_Kalibriert        := "M_Mag_Kalibriert",
           EA_Mag_Ref_Gefunden      := "M_MAG_Ref_Gefunden",
           EA_Mag_Inposition        := "M_Mag_Inposition",
           EA_Mag_Freigabe          := "M_MAG_Freigabe",
           EA_Mag_Laeuft            := "M_MAG_Laeuft",
           EA_Mag_Fehler            := "M_MAG_Fehler",
           EA_Mag_Stop              := "M_MAG_Stop",
           EA_Mag_Trigg1            := "M_GT_MAG_Trigg2",
           EA_Mag_Trigg2            := "M_GT_MAG_Trigg3",
           EA_I_ZaehlImp_WZ_Mag     := "I_WM_Zaehlimpuls",
           EA_I_RefImp_WZ_Mag       := "I_WM_Referenzimpuls",
           EA_O_WZ_Mag_Rechts       := "O_WM_MAG_Rechtslauf",
           EA_O_WZ_Mag_Links        := "O_WM_MAG_Linkslauf",
           EA_Pos_Flanke_ZImpuls    := "M_NF_ZImp",
           EA_Neg_Flanke_ZImpuls    := "M_PF_ZImp",
           EA_Mag_Pos_Gueltig       := "M_MAG_Schalter_Gueltig",
           EA_SKZ_Tout              := "MB_SKZ_TOut_Mag1",
           EA_SKZ_Kal               := "MB_SKZ_Kal_Mag1",
           EA_SKZ_Abl               := "MB_SKZ_Abl_Mag1",
           EA_Paus_Zaehler          := "MB_Paus_Zaehler",
           EA_Pein_Zaehler          := "MB_Pein_Zaehler",
           EA_Max_Pein_Dauer        := "MB_Max_Pein_Dauer",
           EA_Max_Paus_Dauer        := "MB_Max_Paus_Dauer",
           EA_Min_Pein_Dauer        := "MB_Min_Pein_Dauer",
           EA_Min_Paus_Dauer        := "MB_Min_Paus_Dauer",
           EA_Kal_Zaehler           := "MB_Kal_Zaehler",
           EA_Aus_Zaehler           := "MB_Aus_Zaehler",
           EA_MAG_Fehler_Nr         := "MB_Mag_Fehler_Nr",
           EA_MP_Ist                := "MW_MAG_MP_Ist",
           EA_MP_Soll               := "MW_MAG_MP_Soll",
           EA_Mag_Plaetze_verfahren := "MW_Mag_Plaetze_verfahren");

ENDE: NOP   0; 
NETWORK
TITLE =Spannungsüberwachung Netz
//Die Spannungsüberwachung reagiert auf Über- oder Unterspannungen des Netzes von 
//10%. Wird eine Netzschwankung erkannt, wird der Eingang IX_AL_WARNMELD_PWR_SAG 
//gesetzt und ein Interrupt Nr. 5 ausgelöst. Dieser löst bei angewähltem Zyklus 
//DM_ESR mit einem automatischen Rückzug und Stillsetzen der Achsen
      UN    "I_AL_WARNMELD_PWR_SAG"; 
      FP    "M_FP_Warnmeldung_Netz"; 
      UN    "DB_FAMILIEN_MODUL".Aktive_Funktionen; 
      U     "M_Programm_Aktiv"; 
      =     "DB_SIEM_NCK".A_Set_Inp5; 
NETWORK
TITLE =PEB505 vom Profisafemodul DI 4/8xDC24V lesen
//Digitaleingabemodul DI 4/8xDC24V, PROFIsafe V2, sicherheitsgerichtet
//
//"M_PS_DRIVES_EN_PERIPH_E"  M    2068.7  Digitaleingabemodul , PROFIsafe V2 
//(E505.7 = DB_SPL".ISX_PS_DRIVES_EN_CNC)
      U     "DB_PLC_MD_DB20".UDHex._31_Bit1_SOLUTIONLINE; 
      SPBNB _001; 
      L     PEB  505; 
      T     "MB_PERIPHERIE_E_BYTE505"; 
_001: NOP   0; 
NETWORK
TITLE =Werkzeugbruch Lesemodul

      CALL "FC_WZB_Lesen" ;
      NOP   0; 
NETWORK
TITLE =Schreiben der Peripherieausgänge im Rangierer

      CALL "FC_CIO_IO_RANGIER" (
           IN0                      := FALSE,
           IN1                      := FALSE,
           IN2                      := FALSE,
           IN3                      := TRUE);

NETWORK
TITLE =Periepherie für Ausgabe an Dualport-RAM lesen
//E122.3
//E122.4
//E37.1
//E37.2
//E37.3
//  LAR1  P##EB_37
//  L     PEB   37
//  T     EB    37
//  T     B [AR1,P#0.0]
NETWORK
TITLE =DPR 14.0: Wechslerklappe ist offen

      U     "I_WW_1_Klappe_auf"; 
      U     "O_WW_1_Klappe_auf"; 
      UN    "O_WW_1_Klappe_zu"; 
      =     "DB_NC_PLC".WWT_Ablauf.Klappe_offen; 
NETWORK
TITLE =DPR 14.1: WZ-Spanner ist geloest

      O     "I_WZ_SPA_geloest"; 
      =     "DB_NC_PLC".WWT_Ablauf.Spanner_geloest; 
NETWORK
TITLE =DPR 14.2: WZ-Spanner ist gespannt MIToderOHNE Werkzeug

      O     "I_WZ_SPA_gespannt"; 
      =     "DB_NC_PLC".WWT_Ablauf.Spanner_gespannt; 
NETWORK
TITLE =DPR 14.3: Werkzeugmagazin 1 in Position Wechselstelle

      U     "DB_WZV".Mag1_inPos_WST; 
      =     "DB_NC_PLC".WWT_Ablauf.Mag1_inPos_WST; 
NETWORK
TITLE =DPR 14.4: Bedingung Werkzeug holen

      U     "DB_WZV".Mag1_inPos_WST; // MAg in Wechselstelle
      U     "I_WW_1_Klappe_auf"; 
      U     "I_WW_MagPlatz_frei"; 
      U     "O_WW_1_Klappe_auf"; 
      UN    "O_WW_1_Klappe_zu"; 
      =     "DB_NC_PLC".WWT_Ablauf.Bedingung_WZ_ablegen; 
NETWORK
TITLE =DPR 14.5: Magazinplatz ist frei

      U     "I_WW_MagPlatz_frei"; 
      =     "DB_NC_PLC".WWT_Ablauf.Magazinplatz_frei; 
NETWORK
TITLE =DPR 14.6: 

      CLR   ; 
      =     "DB_NC_PLC".WWT_Ablauf.frei_1; 
NETWORK
TITLE =DPR 14.7: Freigabe zum Öffnen der Magazinklappe

      U(    ; 
      U     "DB_WW".MagPlatz_ok; // Magazinplatz Belegung kontrolliert und i.O.
      O     "O_WW_1_Klappe_auf"; //"O_WWT_Klappe_auf"          //F8-06.4  Magazinklappe öffnen
      )     ; 
      UN(   ; 
      U(    ; 
      U     "I_AL_3_Betriebsart"; // Schlüsselschalter Betriebsart 3
      O     "I_AL_4_Betriebsart"; // Schlüsselschalter Betriebsart 4
      )     ; 
      UN    "I_AL_Kabine_Verriegelt"; 
      U     "I_AL_Kabine_zu"; // Magazinklappe geschlossen
      )     ; 
      =     "DB_NC_PLC".WWT_Ablauf.FG_Oeffnen_Klappe; 
NETWORK
TITLE =DPR 15.0: Zange geöffnet - Spindelseitig

      SET   ; 
      =     "DB_NC_PLC".WWT_Ablauf.Zange_geoeffnet; 
NETWORK
TITLE =DPR 15.1: Zange geschlossen - Spindelseitig

      SET   ; 
      =     "DB_NC_PLC".WWT_Ablauf.Zange_geschlossen; 
NETWORK
TITLE =DPR 15.2: WZ-Spanner gespannt MIT Werkzeug
//   O     "IX_WZ_SPA_GESP_MIT_WZ"
//  O      [AR1,P#0.1]
// I_WZ_SPA_gespannt
      U     "I_WZ-Spanner_Gesp_O_WZ"; 
      =     "DB_NC_PLC".WWT_Ablauf.gespannt_mit_WZ; 
NETWORK
TITLE =DPR 15.3: WZ-Spanner gespannt OHNE Werkzeug
// O     "I_WZ_SPA_gespannt"
// O      [AR1,P#0.2]
      CLR   ; 
      =     "DB_NC_PLC".WWT_Ablauf.gespannt_ohne_WZ; 
NETWORK
TITLE =DPR 15.4: Hub weg von Spindel

      U     "I_WW_ZYL1_FRG_MAG"; // MAgazin freigegeben zum drehen
//      U     "I_WW_1_ZYL_GST"; 
      =     "DB_NC_PLC".WWT_Ablauf.Hub_weg_von_Spindel; 
NETWORK
TITLE =DPR 15.5: Hub in Spindelstellung

      U     "I_WW_1_ZYL_SPI"; // MAgazin in Spindelstellung
      =     "DB_NC_PLC".WWT_Ablauf.Hub_in_Spindelstellung; 
NETWORK
TITLE =DPR 15.6:

      CLR   ; 
      =     "DB_NC_PLC".WWT_Ablauf.frei_2; 
NETWORK
TITLE =DPR 15.7:

      CLR   ; 
      =     "DB_NC_PLC".WWT_Ablauf.frei_3; 
NETWORK
TITLE =Byte 14+15 im Dualport-RAM schreiben für WW-Ablauf

      CALL "FC_SIEM_PLC_NCK" (
           Enable                   := TRUE,
           Funct                    := B#16#4,
           S7Var                    := "DB_NC_PLC".WWT_Ablauf,
           IVAR1                    := 14,// Offset Dualport-RAM nur geradzahlig
           IVAR2                    := -1,// Semaphoren-Adresse
           Error                    := #FC21_error,
           ErrCode                  := #FC21_errcode);

NETWORK
TITLE =ToolInspect: AdaptivControl - Regelung

      L     "DB_PLC_MD_DB20".UDInt._039_ANWAHL_DREHMOMENT; 
      L     3; 
      ==I   ; 
      =     L     26.0; 
      U     L     26.0; 
      SPBNB _002; 
      L     PEB 1303; 
      T     "DB_NC_PLC".Zusatz.PLC_an_NC.WZVS_OVR; 
_002: NOP   0; 
      U     L     26.0; 
      NOT   ; 
      SPBNB _003; 
      L     100; 
      T     "DB_NC_PLC".Zusatz.PLC_an_NC.WZVS_OVR; 
_003: NOP   0; 
NETWORK
TITLE =DPR-Word_372: ToolInspect: AdaptivControl - Regelung (an NC)
//DUAL-Port-RAM 372 an NC schreiben
      U     "m_eins"; 
      =     L     26.0; 
      BLD   103; 
      U     "m_eins"; 
      SPBNB _007; 
      CALL "FC_SIEM_PLC_NCK" (
           Enable                   := L     26.0,
           Funct                    := B#16#4,
           S7Var                    := "DB_NC_PLC".Zusatz.PLC_an_NC.WZVS_OVR,
           IVAR1                    := 372,
           IVAR2                    := -1,
           Error                    := #FC21_error,
           ErrCode                  := #FC21_errcode);
_007: NOP   0; 
END_ORGANIZATION_BLOCK

