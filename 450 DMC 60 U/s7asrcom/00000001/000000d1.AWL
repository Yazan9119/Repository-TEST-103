FUNCTION "FC_IKZ_ANALOG_DRUCK" : VOID
TITLE =
//$Revision: 1.10 $
//$Date: 2008/06/05 06:32:03CEST $
//$Author: hgc $
//
VERSION : 0.1


VAR_TEMP
  t_Druckstufen_aktiv : BOOL ;	
  t_max_IKZ_Druck : INT ;	//max. IKZ-Druck
  t_Wert_zu_klein : BOOL ;	
  t_Wert_zu_gross : BOOL ;	
  t_INT_UG : INT ;	//Integer: Untergrenze
  t_INT_OG : INT ;	//Integer: Obergrenze
END_VAR
BEGIN
NETWORK
TITLE =ABST: IKZ-Hochdruck-Anlage mit variablen Druckstufen H7
//"DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN
//>1000: Variabler Druck
// 1080: zulässiger Druckbereich 10...80  bar
// 1140:                         10...140 bar  
//
//M7 H7=x
//Standarddruck: 40 bar
//Reaktion bei falscher Druckeingabe: VSH alle Achsen
//                           Warnung: "Nicht zulässiger Druckbereich"
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN; 
      L     1000; 
      >I    ; 
      )     ; 
      U     "M_MA_Maschine_Ein"; 
      =     #t_Druckstufen_aktiv; 
      SPBN  H30E; 

NETWORK
TITLE =Auswertung auf max.Druck

      O     #t_Druckstufen_aktiv; 
      O     "DB_DM_M_FUNKTION".MX_IM[9]; 
      SPBN  H30E; 
NETWORK
TITLE =Auswertung: maximal definierter Druck
//Anwahl bei einem analogen Ausgang  >1000 >0-40Bar
//Anwahl bei zwei analogen Ausgängen >2000
//Anwahl bei einem analogen Ausgang  >3000 >0-80Bar
      L     "DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN; 
      L     3000; 
      >I    ; 
      SPB   ANA3; 

      L     "DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN; 
      L     2000; 
      >I    ; 
      SPB   ANA2; 

      L     "DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN; 
      L     1000; 
      -I    ; 
      T     #t_max_IKZ_Druck; 
      SPA   ANA1; 

ANA2: L     "DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN; 
      L     2000; 
      -I    ; 
      T     #t_max_IKZ_Druck; 
      SPA   ANA1; 

ANA3: L     "DB_PLC_MD_DB20".UDInt._045_ANWAHL_DRUCKSTUFEN; 
      L     3000; 
      -I    ; 
      T     #t_max_IKZ_Druck; 

ANA1: NOP   0; 
NETWORK
TITLE =H7-Funktions ist QUIT-Pflichtig

      SET   ; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[7]; 
NETWORK
TITLE =H7-Funktions-Auswertung

      CLR   ; 
      =     #t_Wert_zu_klein; 
      =     #t_Wert_zu_gross; 
      =     "DB_H_FUNKTION".HX_Quitt[7]; 

      U     "DB_H_FUNKTION".HX_M[7]; 
      SPBN  H7; 

// Auswertung: zu kleiner Wert (kleiner: 5)
      L     "DB_H_FUNKTION".HX_Real_Wert[7]; 
      L     5.000000e+000; // 1.000000e+001 
      <R    ; 
      =     #t_Wert_zu_klein; 
      SPB   H7a; 

// Auswertung: zu großer Wert (größer: (UDInt._045_ANWAHL_DRUCKSTUFEN - 1000 oder -2000)
      TAK   ; 
      RND   ; 
      L     #t_max_IKZ_Druck; 
      >I    ; 
      =     #t_Wert_zu_gross; 
      SPB   H7a; 

// Übergabe: zulässiger Wert
      TAK   ; 
      T     "DB_REMANENT".M7_analogDruckvorgabe; 
      T     "DB_REMANENT".M7_normiertDruckVentil1; //  "DB_HR".KSA.IKZ_PROP_Vorgabe

H7a:  U     "DB_H_FUNKTION".HX_M[7]; 
      =     "DB_H_FUNKTION".HX_Quitt[7]; 
      R     "DB_H_FUNKTION".HX_M[7]; 

H7:   NOP   0; 

NETWORK
TITLE =700211: IKZ: unzulässige Druck-Vorgabe mit H7

      O     #t_Wert_zu_klein; 
      O     #t_Wert_zu_gross; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt._700211_IKZ_H7; 
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt._700211_IKZ_H7; 
NETWORK
TITLE =PROP-Ventil auf "NULL" ansteuern mit M9

      U     "DB_DM_M_FUNKTION".MX_IM[9]; 
      SPBN  _m9; 
      L     0; 
      T     "DB_REMANENT".M7_normiertDruckVentil1; //  "DB_HR".KSA.IKZ_PROP_Vorgabe
_m9:  NOP   0; 
NETWORK
TITLE =PROP-Ventil auf "H7_letzte_Eingabe" ansteuern mit M7
// 
      U     "DB_DM_M_FUNKTION".MX_IM[7]; 
      SPBN  _m7; 
      L     "DB_REMANENT".M7_analogDruckvorgabe; // "DB_HR".KSA.H7_letzte_Eingabe
      T     "DB_REMANENT".M7_normiertDruckVentil1; //  "DB_HR".KSA.IKZ_PROP_Vorgabe
_m7:  NOP   0; 
NETWORK
TITLE =Definition für UNTER- und OBER-Grenze von PROP-Ventil-INTERLIT
//Unter- und Obergrenze sind PROP-Ventil-Parameter.
//
//INTERLIT SPP-20RS / 5-100 bar   Untergrenze:   5
//                                 Obergrenze: 100
//Bürener SPP-H15                 Untergrenze:   0
//                                 Obergrenze: 120
      L     10; 
      T     #t_INT_UG; 

      L     120; 
      T     #t_INT_OG; 


NETWORK
TITLE =H7-Wert normiert auf Analog-Ausgang schreiben
//      CALL  "FB_Norm_AnalogA" , "DI_FB_Norm_AnalogA"
//        Untergrenze    :=#t_INT_UG
//        Obergrenze     := #t_INT_OG
//        Max_Analogwert :=2.764800e+004
//        Normierter_Wert:="DB_HR".KSA.IKZ_PROP_Vorgabe
//        Analogwert     :="DB_HR".KSA.OA_IKZ_PROP_VENTIL
NETWORK
TITLE =Ende Ansteuerung IKZ-PROP-Ventil

H30E: NOP   0; 

END_FUNCTION

