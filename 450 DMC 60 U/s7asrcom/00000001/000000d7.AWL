FUNCTION "FC_HASYST_EROWA" : VOID
TITLE =
//$Revision: 1.8 $
//$Date: 2008/06/06 09:24:55CEST $
//$Author: hgc $
VERSION : 0.1


VAR_TEMP
  t_frg_rettungsmenue : BOOL ;	
  t_not_o_robot_cop1 : BOOL ;	
  t_not_o_robot_tcp1 : BOOL ;	
  t_frg_tuer_schliessen : BOOL ;	
  t_frg_achsen_fahren : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Anwahl EROWA bzw. PROMOT
//Anwahl Handlingsystem EROWA bzw. PROMOT
//USER_DATE_INT[027]
//5 = Variante A ohne JobManager
//6 = Variante B mit JobManager
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_OHNE_RPC; 
      SPB   varA; 
NETWORK
TITLE =Anwahl EROWA bzw. PROMOT
//Anwahl Handlingsystem EROWA bzw. PROMOT
//USER_DATE_INT[027]
//5 = Variante A ohne JobManager
//6 = Variante B mit JobManager
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_MIT_RPC; 
      SPB   varB; 
NETWORK
TITLE =Keine Anwahl Handlingsystem EROWA / PROMOT
//Weder EROWA noch PROMOT angewählt, springe ans Bausteinende
      U     "m_eins"; 
      SPB   ende; 
NETWORK
TITLE =Variante B mit JobManager, RPC-Schnittstelle
//Kommunikation zwischen dem JobManager und der Maschine mittels DB12
varB: NOP   0; 

NETWORK
TITLE =Neues Werkstück
//Werkstückstatus: neues Werkstück setzen
//1. wenn ein NC-Start Befehl vom JobManager kommt
//2. manuell in Rettungsmenü DECKEL MAHO II --> OPTIONEN --> HANDLINSYSTEM
      U     "DB_SINRPC".NC_Kommunikation.NC_Start; 
      FP    "M_FP_RPC_NC-START"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_104_Plus; 
      S     DB12.DBX   21.0; 
      R     DB12.DBX   21.5; 
      R     DB12.DBX   21.6; 
NETWORK
TITLE =Quittpflicht für  H67 setzen
//H67 ist quittierpflichtig
      U     "m_eins"; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[67]; 
NETWORK
TITLE =Werkstück fertig
//Werkstückstatus: Werkstück fertig
//1. wenn H67 = 1
//2. manuell in Rettungsmenü DECKEL MAHO II --> OPTIONEN --> HANDLINSYSTEM
      U     "DB_H_FUNKTION".HX_M[67]; 
      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     1.000000e+000; 
      ==R   ; 
      )     ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_105_Plus; 
      S     DB12.DBX   21.5; 
      R     DB12.DBX   21.0; 
      R     DB12.DBX   21.6; 
      R     "DB_DM_M_FUNKTION".MX_M[67]; 
NETWORK
TITLE =Werkstück fertig mit Fehler
//Werkstückstatus: Werkstück fertig mit Fehler
//1. wenn H67 = 2
//2. manuell in Rettungsmenü DECKEL MAHO II --> OPTIONEN --> HANDLINSYSTEM
      U     "DB_H_FUNKTION".HX_M[67]; 
      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     2.000000e+000; 
      ==R   ; 
      )     ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_106_Plus; 
      S     DB12.DBX   21.6; 
      R     DB12.DBX   21.0; 
      R     DB12.DBX   21.5; 
      R     "DB_DM_M_FUNKTION".MX_M[67]; 
NETWORK
TITLE =Rücksetzen H67
//Rücksetzen und Quittierung der H67 Funktion
      U     "DB_H_FUNKTION".HX_M[67]; 
      =     "DB_H_FUNKTION".HX_Quitt[67]; 
      R     "DB_H_FUNKTION".HX_M[67]; 
NETWORK
TITLE =RPC - Statusänderung
//Falls sich RPC - Mode ändert, wird dies mit Bit DB12.DBX14.5 signalisiert
      U     "M_8_ter_OB1_Zyklen"; 
      SPBN  _ne1; 

      L     1; 
      T     "DB_SINRPC".Trigger; 

_ne1: L     "DB_SINRPC".MODE_SinCOM; 
      T     "MB_RPC_MODE"; 

      L     "MB_RPC_MODE"; 
      L     "MB_RPC_MODE_OLD"; 
      ==I   ; 
      SPB   _ne2; 
      U     DB12.DBX   14.5; 
      SPBN  _ne3; 
      R     DB12.DBX   14.5; 
      SPA   _ne4; 
_ne3: S     DB12.DBX   14.5; 
_ne4: L     "MB_RPC_MODE"; 
      T     "MB_RPC_MODE_OLD"; 
_ne2: NOP   0; 

NETWORK
TITLE =EROWA/PROMOT RPC Sperre
//1. Wenn im Unbemannten jobmode die NC-Reset Taste betätigt wird ist für Roboter 
//Start/Stop/Reset gesperrt
      U     "MSSTstReset"; 
      UN    "DB_SINRPC".NC_Kommunikation.Reset; 
      U     DB12.DBX    4.0; 
      U(    ; 
      O     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      FP    "M_FP_MSST_RESET"; 
      S     "M_IMP_MSST_RESET"; 
      U(    ; 
      ON    DB12.DBX    4.0; 
      O     ; 
      UN    "DB_SIEM_BAG".E_AUTO; 
      UN    "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      O     "MSSTst_NC_Start"; 
      )     ; 
      R     "M_IMP_MSST_RESET"; 
      NOP   0; 
NETWORK
TITLE =EROWA/PROMOT RPC Sperre
//1. Wenn im Unbemannten jobmode die NC-Stop Taste betätigt wird ist für Roboter 
//Start/Stop/Reset gesperrt
      U     "MSSTst NC-Stop *"; 
      U     DB12.DBX    4.0; 
      U(    ; 
      O     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      FP    "M_FP_NC_STOP_MSST"; 
      S     "M_IMP_NC_STOP_MSST"; 
      U(    ; 
      ON    DB12.DBX    4.0; 
      O     ; 
      UN    "DB_SIEM_BAG".E_AUTO; 
      UN    "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      O     "MSSTst_NC_Start"; 
      )     ; 
      R     "M_IMP_NC_STOP_MSST"; 
      NOP   0; 
NETWORK
TITLE =RPC NC Start/Stop/Reset
//Start/Stop/Reset durch RPC gesperrt
      U     DB12.DBX   15.0; 
      UN    "M_IMP_MSST_RESET"; 
      UN    "M_IMP_NC_STOP_MSST"; 
      SPBN  kNCB; 
NETWORK
TITLE =NC Start
//RPC --> NC-Start
//Wird in FC_CHAN1 umgesetzt
      U     "DB_SINRPC".NC_Kommunikation.NC_Start; 
      FP    "M_FP_NC_START_RPC_3R"; 
      =     "M_NC_START_RPC_3R"; 
NETWORK
TITLE =NC Stop
//RPC --> NC-Stop
//Ausführung des NC-Stop in NW75
      U     "DB_SINRPC".NC_Kommunikation.NC_Stop; 
      FP    "M_FP_NC_STOP_RPC_3R"; 
      =     "M_NC_STOP_RPC_3R"; 
NETWORK
TITLE =NC Reset
//RPC --> NC-Reset
//Ausführung des Reset in FC_BETRIEBSARTEN
      U     "DB_SINRPC".NC_Kommunikation.Reset; 
      FP    "M_FP_RESET_RPC_3R"; 
      =     "M_RESET_RPC_3R"; 
NETWORK
TITLE =Sprung zu Maschinenzustandsänderung

      U     "m_eins"; 
      SPB   MASC; 
NETWORK
TITLE =NC-Start nicht erlaubt
//kein NC-Start erlaubt, rücksetzen der beteiligten Signale
kNCB: U     "m_null"; 
      =     "M_NC_START_RPC_3R"; 
      =     "M_NC_STOP_RPC_3R"; 
      =     "M_RESET_RPC_3R"; 
      =     "DB_SINRPC".NC_Kommunikation.NC_Start; 
      =     "DB_SINRPC".NC_Kommunikation.NC_Stop; 
      =     "DB_SINRPC".NC_Kommunikation.Reset; 
NETWORK
TITLE =Maschinenzustandsänderung
//Wenn sich der Maschinenzustandändert, wird der Trigger um eine Stelle weiter 
//geschoben und das Bit Zustandsänderung gesetzt
MASC: L     DB12.DBW   14; 
      T     "MW_STATUS_MASCH"; 

      L     "MW_STATUS_MASCH"; 
      L     "MW_STATUSMASCH_OLD"; 
      ==D   ; 
      SPB   _ng1; 

      SET   ; 
      =     DB12.DBX    0.2; 

      L     "DB_SINRPC".Trigger; 
      L     128; 
      ==I   ; 
      SPBN  _ng2; 

      L     1; 
      T     "DB_SINRPC".Trigger; 

      L     "MW_STATUS_MASCH"; 
      T     "MW_STATUSMASCH_OLD"; 

      SPA   _ng1; 

_ng2: L     "DB_SINRPC".Trigger; 
      SLW   1; 
      T     "DB_SINRPC".Trigger; 

      L     "MW_STATUS_MASCH"; 
      T     "MW_STATUSMASCH_OLD"; 

_ng1: NOP   0; 

NETWORK
TITLE =JOG
//Handbetrieb --> RPC DB12
      U     "M_Handbetrieb"; 
      =     DB12.DBX   15.2; 
NETWORK
TITLE =MDA
//MDA --> RPC DB12
      O     "DB_SIEM_BAG".E_MDA; 
      O     "MX_MDA"; 
      =     DB12.DBX   15.1; 
NETWORK
TITLE =Automatik
//Automatik --> RPC DB12
      O     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      =     DB12.DBX   15.0; 
NETWORK
TITLE =Kanal Reset
//Kanal in Reset --> RPC DB12
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      =     DB12.DBX   14.3; 
NETWORK
TITLE =Kanal Unterbrochen
//Kanal unterbrochen --> RPC DB12
      U     "DB_SIEM_KANAL_1".E_ChanInterrupt; 
      =     DB12.DBX   14.1; 
NETWORK
TITLE =Kanal Aktiv
//Kanal aktiv --> RPC DB12
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      =     DB12.DBX   14.0; 
NETWORK
TITLE =Maschine hochgelaufen
//Maschine ist hochgelaufen --> RPC DB12
      U     "M_8_ter_OB1_Zyklen"; 
      FP    "M_FP_8_ter_OB1_Zyklus"; 
      =     DB12.DBX   14.2; 
      R     "DB_SINRPC".NC_Kommunikation.NC_Start; 
NETWORK
TITLE =Haltestellen
//Anazahl der Haltestellen Handlingsystem = 1
      L     1; 
      T     "DB_SINRPC".DockPosCount; 

NETWORK
TITLE =Security Bit
//Security Bit, wenn im unbemannt mode Reset oder NC-Stop betätigt wurde
//Security Bit --> RPC DB12
      O     "M_IMP_NC_STOP_MSST"; 
      O     "M_IMP_MSST_RESET"; 
      =     "DB_SINRPC".Security_Bit; 
      =     DB12.DBX   14.6; 
NETWORK
TITLE =Variante A ohne JobManager
//Hier wird nur der eigentliche Werkstückwechsel berücksichtigt, d.h. 
//Adressübertragung und Wechselvorgang
varA: NOP   0; 

NETWORK
TITLE =Quitt
//Quitttaste
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510226_BTB_Roboter; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 

      R     "DB_FEHLERMELDUNG".MELDUNG5._510309_TuerRobot_geschl; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 
NETWORK
TITLE =Reset
//Resettaste
      U     "M_Reset_Taste"; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510226_BTB_Roboter; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700632_Spannvor_Gesp_0S; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510308_TimeOut_Robot; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt1._510117_Robot_Tuer_offen; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510309_TuerRobot_geschl; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 
      R     "DB_DM_M_FUNKTION".MX_M[65]; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[65]; 
      R     "DB_H_FUNKTION".HX_M[65]; 
      =     "DB_H_FUNKTION".HX_Quitt[65]; 
      R     "DB_H_FUNKTION".HX_M[66]; 
      =     "DB_H_FUNKTION".HX_Quitt[66]; 
      R     "M_HANDLINGSYSTEM_AKTIV"; 
      R     "O_ROBOT_START"; 
      R     "O_ROBOT_TCP1"; 

      R     "O_ROBOT_COP1"; 
      R     "T_SE_WERKSTUECK_W_OK"; 
NETWORK
TITLE =Reset Schrittkette
//Reset = 1 ? --> sonst an Stelle nRes springen
      U     "M_Reset_Taste"; 
      SPBN  nRes; 
NETWORK
TITLE =Definierte Ausgangslage herstellen
//Rücksetzen der Schrittkette und der Ausgangsbit der Adressübertragung
      L     0; 
      T     "DB_HASYST_EROWA".Schrittkette; 
      T     "MB_ADRESS_BYTE_1_EROWA"; 
      T     "MB_ADRESS_BYTE_2_EROWA"; 
NETWORK
TITLE =Handlingssystem 3R: Adressübertragung u. Beladen beendet melden
//Ausführungen wenn Resettaste = 1, Beladen beendet an Makro DM_M65 senden um 
//dieses abzuschliessen
//U M10.5
//= "DB_NC_PLC".Zusatz.PLC_an_NC.Roboter_Auftrag_fertig
NETWORK
TITLE =510226 "MEL, Roboter nicht betriebsbereit"
//Meldungsanzeige, wenn Roboter nicht im Automatikbetrieb ist
nRes: UN    "I_ROBOT_AUTO"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510226_BTB_Roboter; 
NETWORK
TITLE =Robot kein Auto
//Wenn der Roboter nicht im Automatikbetrieb ist, wird ans Bausteinende 
//gesprungen
      UN    "I_ROBOT_AUTO"; 
      SPB   NAkt; 
NETWORK
TITLE =Setzen der Quittierungspflichten
//Quittpflicht für H65, H66, M65, M393, M394
      U     "m_eins"; 
      S     "DB_H_FUNKTION".HX_QuitPflicht[65]; 
      S     "DB_H_FUNKTION".HX_QuitPflicht[66]; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[65]; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[393]; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[394]; 
NETWORK
TITLE =H65
//H65 aktiv? --> sonst weiter bei H66
      U     "DB_H_FUNKTION".HX_M[65]; 
      SPBN  H66; 
NETWORK
TITLE =Lesen Wert H65
//Wert der H65 Funktin in DB schreiben
      L     "DB_H_FUNKTION".HX_Real_Wert[65]; 
      RND   ; 
      T     "DB_HASYST_EROWA".CHUCK_ADRESSE; 
      NOP   0; 
NETWORK
TITLE =H66
//H66 aktiv? --> sonst weiter bei M65
H66:  U     "DB_H_FUNKTION".HX_M[66]; 
      SPBN  ADD; 
NETWORK
TITLE =Lesen Wert H66
//Wert der H65 Funktin in DB schreiben
      L     "DB_H_FUNKTION".HX_Real_Wert[66]; 
      RND   ; 
      T     "DB_HASYST_EROWA".WERKSTUECK_ADRESSE; 
      NOP   0; 
NETWORK
TITLE =Quitt H-Befehl
//H56 o. H66 aktiv? --> Quittierung H-Befehl
ADD:  U(    ; 
      O     "DB_H_FUNKTION".HX_M[65]; 
      O     "DB_H_FUNKTION".HX_M[66]; 
      )     ; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  M65; 
NETWORK
TITLE =Quittierung H-Befehl
//H-Befehle werden gesamt rückgesetzt 
      U     "m_eins"; 
      R     "DB_H_FUNKTION".HX_M[65]; 
      =     "DB_H_FUNKTION".HX_Quitt[65]; 
      R     "DB_H_FUNKTION".HX_M[66]; 
      =     "DB_H_FUNKTION".HX_Quitt[66]; 
NETWORK
TITLE =M65
//M65 startet Schrittkette u. setzt den Merker für den Wechselvorgang
M65:  U     "DB_DM_M_FUNKTION".MX_M[65]; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_NEUTRAL"; 
      SPBN  FNeu; 
NETWORK
TITLE =Start Werkstückwechsel
//M65 startet Wechselvorgang, setzen der Merker und öffnen der Beladetür
//R "DB_NC_PLC".Zusatz.PLC_an_NC.Roboter_Auftrag_fertig
      U     "m_eins"; 
      S     "M_HANDLINGSYSTEM_AKTIV"; 
      R     "M_ROBOT_DOOR_CLOSE"; 
      S     "M_ROBOT_DOOR_OPEN"; 
NETWORK
TITLE =Start Werkstückwechsel
//Initialisiere Schrittkette, rücksetzen Ausgangsbit
      L     1; 
      T     "DB_HASYST_EROWA".Schrittkette; 

      L     0; 
      T     "MB_ADRESS_BYTE_1_EROWA"; 
      T     "MB_ADRESS_BYTE_2_EROWA"; 
NETWORK
TITLE =510227 "MEL, Roboter im Störbereich"
//Roboter nicht in seiner Neutralstellung
FNeu: U     "DB_DM_M_FUNKTION".MX_M[65]; 
      UN    "I_ROBOT_NEUTRAL"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
NETWORK
TITLE =Handlingsystem aktiv?
//Falls Handlingsystem nicht aktiv ist nach NAkt springen
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  NAkt; 
NETWORK
TITLE =Schritt 1 - Adressübertragung
//In Schritt 1 wird die Adresse übertragen
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_DOOR_OPENED"; 
      UN    "I_ROBOT_DOOR_CLOSED"; 
      U(    ; 
      L     1; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      SPBN  TuAu; 
NETWORK
TITLE =Parity-Bit bilden
//Parity Bit = Quersumme: 
//gerade = 0
//ungerade = 1
      X     DB411.DBX    3.0; 
      X     DB411.DBX    3.1; 
      X     DB411.DBX    3.2; 
      X     DB411.DBX    3.3; 
      X     DB411.DBX    3.4; 
      X     DB411.DBX    3.5; 
      X     DB411.DBX    3.6; 
      X     DB411.DBX    3.7; 
      X     DB411.DBX    4.0; 
      X     DB411.DBX    1.0; 
      X     DB411.DBX    1.1; 
      =     "M_PARITY_BIT"; 

NETWORK
TITLE =Adressbits und Parity Bit senden
//Adresse auf die einzelnen Bits abbilden
      U     DB411.DBX    3.0; 
      S     "O_ROBOT_ADR_W0"; 
      U     DB411.DBX    3.1; 
      S     "O_ROBOT_ADR_W1"; 
      U     DB411.DBX    3.2; 
      S     "O_ROBOT_ADR_W2"; 
      U     DB411.DBX    3.3; 
      S     "O_ROBOT_ADR_W3"; 
      U     DB411.DBX    3.4; 
      S     "O_ROBOT_ADR_W4"; 
      U     DB411.DBX    3.5; 
      S     "O_ROBOT_ADR_W5"; 
      U     DB411.DBX    3.6; 
      S     "O_ROBOT_ADR_W6"; 
      U     DB411.DBX    3.7; 
      S     "O_ROBOT_ADR_W7"; 
      U     DB411.DBX    4.0; 
      S     "O_ROBOT_ADR_W8"; 
      U     DB411.DBX    1.0; 
      S     "O_ROBOT_ADR_C0"; 
      U     DB411.DBX    1.1; 
      S     "O_ROBOT_ADR_C1"; 
      U     "M_PARITY_BIT"; 
      S     "O_ROBOT_ADR_PARITY"; 

NETWORK
TITLE =nächster Schritt
//Schrittkette um 1 erhöhen
      L     "DB_HASYST_EROWA".Schrittkette; 
      L     1; 
      +I    ; 
      T     "DB_HASYST_EROWA".Schrittkette; 
      NOP   0; 
NETWORK
TITLE =510309 "MEL, Hubtür des Roboter ist noch nicht offen"
//Beladetür ist noch nicht offen, warten in Schritt 1
TuAu: UN    "I_ROBOT_DOOR_OPENED"; 
      U(    ; 
      L     1; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510309_TuerRobot_geschl; 
NETWORK
TITLE =Schritt 2 - Start Robot
//Beladevorgang durch den Roboter anstoßen
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      L     2; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      SPBN  BeFe; 
NETWORK
TITLE =Start Roboter
//Setzen der Freigaben für den Roboter
      U     "m_eins"; 
      S     "O_ROBOT_START"; 
      S     "O_ROBOT_TCP1"; 
      S     "O_ROBOT_COP1"; 
NETWORK
TITLE =nächster Schritt
//Schrittkette um 1 erhöhen
      L     "DB_HASYST_EROWA".Schrittkette; 
      L     1; 
      +I    ; 
      T     "DB_HASYST_EROWA".Schrittkette; 
      NOP   0; 
NETWORK
TITLE =Schritt 3 - Beladen fertig
//Beladen ende oder mit Fehler muss 200msec anstehen
BeFe: U(    ; 
      O     "I_ROBOT_BELADEN_ENDE"; 
      ON    "I_ROBOT_NEG_ERROR"; 
      )     ; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      L     3; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      L     S5T#200MS; 
      SE    "T_SE_BELADEN_ENDE"; 
NETWORK
TITLE =Eingang Abfrage min. 200msec
//Das Ende muss min. 200msec signalisiert werden um gültig zu sein
      U     "T_SE_BELADEN_ENDE"; 
      SPBN  TuZu; 
NETWORK
TITLE =Rücksetzen Wechselvorgang am Ende
//Rücknahme der Feigaben für den Roboter
      U     "m_eins"; 
      R     "O_ROBOT_START"; 
      R     "O_ROBOT_TCP1"; 
      R     "O_ROBOT_COP1"; 
NETWORK
TITLE =Reset Adressbits
//Rücksetzen der Adressbits
      L     0; 
      T     "MB_ADRESS_BYTE_1_EROWA"; 
      T     "MB_ADRESS_BYTE_2_EROWA"; 

NETWORK
TITLE =nächster Schritt
//Schrittkette um 1 erhöhen
      L     "DB_HASYST_EROWA".Schrittkette; 
      L     1; 
      +I    ; 
      T     "DB_HASYST_EROWA".Schrittkette; 
      NOP   0; 
NETWORK
TITLE =Schritt 4 - Tür schliessen
//Tür am Ende, wenn Robot wieder in seiner Neutralstellung ist schliessen
TuZu: U     "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_NEUTRAL"; 
      U(    ; 
      L     4; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      SPBN  RoSt; 
NETWORK
TITLE =Beladetür schliessen
//Merker zum schliessen der Tür setzen
      U     "m_eins"; 
      S     "M_ROBOT_DOOR_CLOSE"; 
      R     "M_ROBOT_DOOR_OPEN"; 
NETWORK
TITLE =nächster Schritt
//Schrittkette um 1 erhöhen
      L     "DB_HASYST_EROWA".Schrittkette; 
      L     1; 
      +I    ; 
      T     "DB_HASYST_EROWA".Schrittkette; 
      NOP   0; 
NETWORK
TITLE =510227 "MEL, Roboter im Störbereich"
//Wenn Roboter im Störbereich ist, in Schritt 4 verweilen und Tür nicht 
//schliessen
RoSt: U     "M_HANDLINGSYSTEM_AKTIV"; 
      UN    "I_ROBOT_NEUTRAL"; 
      U(    ; 
      L     4; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
NETWORK
TITLE =Schritt 5 - Überwachung Tür
//Wenn Tür offen wird ein Fehler gesetzt
//
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      UN    "I_ROBOT_DOOR_OPENED"; 
      U(    ; 
      L     5; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      SPBN  FEHL; 
NETWORK
TITLE =Merker Handlingsystem aktiv rücksetzen
//Beladung fertig
      U     "m_eins"; 
      R     "M_HANDLINGSYSTEM_AKTIV"; 
      R     "DB_DM_M_FUNKTION".MX_M[65]; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[65]; 
NETWORK
TITLE =Schrittkette zurücksetzen

      L     0; 
      T     "DB_HASYST_EROWA".Schrittkette; 

NETWORK
TITLE =510310 "MEL, Hubtür des Roboter ist noch nicht geschlossen"
//Fehler Türe offen 
FEHL: U     "M_HANDLINGSYSTEM_AKTIV"; 
      UN    "I_ROBOT_DOOR_CLOSED"; 
      U(    ; 
      L     6; 
      L     "DB_HASYST_EROWA".Schrittkette; 
      ==I   ; 
      )     ; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 
NETWORK
TITLE =Wechsel mit Fehler beendet
//Beladevorgang wurde mit Fehler von Roboter beendet
      UN    "I_ROBOT_NEG_ERROR"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 
NETWORK
TITLE =Überwachungszeit aus DB70 laden
//Im GUD8 wird die Überwachungzeit definiert und im Makro DM_M65 in DB70 
//geschrieben
      L     "DB_NC_PLC".Zusatz.NC_an_PLC.Zeit_Roboter; 
      SRW   8; 
      ITB   ; 
      L     8192; 
      OW    ; 
      T     "DB_HASYST_EROWA".Ueberwachungszeit; 

NETWORK
TITLE =Überwachungszeit starten
//Wenn Beladung gestartet wird, wird auch die Überwachungszeit gestartet
      U     "O_ROBOT_START"; 
      L     "DB_HASYST_EROWA".Ueberwachungszeit; 
      SE    "T_SE_WERKSTUECK_W_OK"; 
NETWORK
TITLE =510308 "MEL, Roboter Time Out bei Werkstückwechsel"
//Fehler wenn die Beladung doppelt so lange wie normal dauert
      U     "T_SE_WERKSTUECK_W_OK"; 
      S     "DB_FEHLERMELDUNG".MELDUNG5._510308_TimeOut_Robot; 
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510308_TimeOut_Robot; 
      NOP   0; 
NETWORK
TITLE =700632 "SPVS-HA-HPT, Eingang Spannvorrichtung gespannt 0-Signal"
//Überwachung des Spannfutter
NAkt: UN    "M_HANDLINGSYSTEM_AKTIV"; 
      UN    "I_ROBOT_CHUCKS_CLOSED"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700632_Spannvor_Gesp_0S; 
NETWORK
TITLE =Vorschub Halt Beladetür offen
//Beladetür offen --> Achssperre X,Y,Z,Spindel
      UN    "I_ROBOT_DOOR_CLOSED"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
NETWORK
TITLE =Merker für Makro DM_M65.SPF
//Beenden des Makro DM_M65 wenn 1
//U M608.1
//R "DB_NC_PLC".Zusatz.PLC_an_NC.Roboter_Auftrag_fertig
NETWORK
TITLE =NC-Stop
//Wenn die Überwachungszeit angeschlagen hat, so wird ein NC-Stop ausgeführt
      O     "T_SE_WERKSTUECK_W_OK"; 
      O     "M_NC_STOP_RPC_3R"; 
      =     "DB_SIEM_KANAL_1".A_NCStop; 
NETWORK
TITLE =Freigabe Rettungsmenü
//Freigabe für das Rettungsmenü des EROWA
      U     "M_MA_Maschine_Ein"; 
      U     "DB_SIEM_BAG".E_JOG; 
      =     #t_frg_rettungsmenue; 
NETWORK
TITLE =Rettungsmenü Beladetür
//Freigabe für Beladetür schliessen
      U     #t_frg_rettungsmenue; 
      U     "I_ROBOT_NEUTRAL"; 
      =     #t_frg_tuer_schliessen; 
NETWORK
TITLE =510227 "MEL, Roboter im Störbereich"
//Fehler wenn in Rettung Türe nicht geschlossen werden kann
      U     "DB_OEM".Handlingsystem_Aktiv; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN    "I_ROBOT_NEUTRAL"; 
      S     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
      U     "I_ROBOT_NEUTRAL"; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
      NOP   0; 
NETWORK
TITLE =Ansteuerung u. Rettung Beladetür
//Beladetür durch Merker in Auto bedienen und durch Rettungsmenü, sowie Anzeige 
//in Rettungsmenü
      U     "m_null"; 
      =     L      1.0; 
      BLD   103; 
      U     "m_takt_0,1sec."; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     "I_ROBOT_DOOR_OPENED"; 
      =     L      1.3; 
      BLD   103; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      =     L      1.4; 
      BLD   103; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      =     L      1.5; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      1.6; 
      BLD   103; 
      U     "M_ROBOT_DOOR_OPEN"; 
      =     L      1.7; 
      BLD   103; 
      U     "M_ROBOT_DOOR_CLOSE"; 
      =     L      2.0; 
      BLD   103; 
      U     "DB_EINRICHTFUNKTION".Bewegung_103_Minus; 
      =     L      2.1; 
      BLD   103; 
      U     "DB_EINRICHTFUNKTION".Bewegung_103_Plus; 
      =     L      2.2; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      2.3; 
      BLD   103; 
      U     #t_frg_tuer_schliessen; 
      =     L      2.4; 
      BLD   103; 
      U     "m_eins"; 
      =     L      2.5; 
      BLD   103; 
      U     "m_null"; 
      =     L      2.6; 
      BLD   103; 
      U     "M_Ruecksetze_Fehler"; 
      =     L      2.7; 
      BLD   103; 
      U     "m_eins"; 
      =     L      3.0; 
      BLD   103; 
      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_HASYST" (
           EX_Init                  := L      1.0,
           Zeittakt                 := L      1.1,
           OutputMode               := L      1.2,
           Endlage_AST              := L      1.3,
           Endlage_GST              := L      1.4,
           Automatik                := L      1.5,
           Hand                     := L      1.6,
           Freig_Ventil_AST_Auto    := L      1.7,
           Freig_Ventil_GST_Auto    := L      2.0,
           Taste_AST_Hand           := L      2.1,
           Taste_GST_Hand           := L      2.2,
           Freigabe_AST_Hand        := L      2.3,
           Freigabe_GST_Hand        := L      2.4,
           Freig_Ventil_Hardware    := L      2.5,
           SST_Anlage_Aus           := L      2.6,
           QUIT                     := L      2.7,
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := L      3.0,
           Bewegungsnummer          := 103,
           Ventil_AST               := "O_ROBOT_DOOR_OPEN",
           Ventil_GST               := "O_ROBOT_DOOR_CLOSE");

      NOP   0; 
NETWORK
TITLE =Rettungsmenü WPC-Status rücksetzen
//Anzeigen des Status auf neues Teil rücksetzen
      U     #t_frg_rettungsmenue; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     DB12.DBX   21.0; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 104);
      NOP   0; 
NETWORK
TITLE =Rettungsmenü WPC-Status Fertigteil
//Anzeigen des Status auf Fertigteil setzen
      U     #t_frg_rettungsmenue; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     DB12.DBX   21.5; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 105);
      NOP   0; 
NETWORK
TITLE =Rettungsmenü WPC-Status Fehlerhaft
//Anzeigen des Status auf Fehlerhaft setzen
      U     #t_frg_rettungsmenue; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     DB12.DBX   21.6; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 106);
      NOP   0; 
NETWORK
TITLE =Rettungsmenü Spannfutterfreigabe
//Freigabe für das Spannfutter setzen und Rücksetzen
      U(    ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_107_Plus; 
      O     ; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      ON    "DB_OEM".Handlingsystem_Aktiv; 
      ON    "DB_SIEM_BAG".E_JOG; 
      )     ; 
      )     ; 
      R     "O_ROBOT_COP1"; 
      U     "DB_EINRICHTFUNKTION".Bewegung_107_Minus; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      U     "DB_SIEM_BAG".E_JOG; 
      S     "O_ROBOT_COP1"; 
      NOP   0; 
NETWORK
TITLE =Temp. COP1 nicht
//Negierten Temporären Merker für die Anzeige in Rettung bilden
      UN    "O_ROBOT_COP1"; 
      =     #t_not_o_robot_cop1; 
NETWORK
TITLE =Anzeige Rettungsmenü - COP1
//Anzeige der Endlagen und Ventile im Rettungsmenü
//
      U     #t_frg_rettungsmenue; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      1.1; 
      BLD   103; 
      U     "O_ROBOT_COP1"; 
      =     L      1.2; 
      BLD   103; 
      U     #t_not_o_robot_cop1; 
      =     L      1.3; 
      BLD   103; 
      U     "O_ROBOT_COP1"; 
      =     L      1.4; 
      BLD   103; 
      U     #t_not_o_robot_cop1; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 107);
      NOP   0; 
NETWORK
TITLE =Rettungsmenü Wechselfreigabe
//Freigabe für Wechsel setzen und Rücksetzen
      U(    ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_108_Plus; 
      O     ; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      ON    "DB_OEM".Handlingsystem_Aktiv; 
      ON    "DB_SIEM_BAG".E_JOG; 
      )     ; 
      )     ; 
      R     "O_ROBOT_TCP1"; 
      U     "DB_EINRICHTFUNKTION".Bewegung_108_Minus; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      U     "DB_SIEM_BAG".E_JOG; 
      S     "O_ROBOT_TCP1"; 
      NOP   0; 
NETWORK
TITLE =Temp. TCP1 nicht
//Negierten Temporären Merker für die Anzeige in Rettung bilden
      UN    "O_ROBOT_TCP1"; 
      =     #t_not_o_robot_tcp1; 
NETWORK
TITLE =Anzeige Rettungsmenü - TCP1
//Anzeige der Endlagen und Ventile im Rettungsmenü
//
      U     #t_frg_rettungsmenue; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_rettungsmenue; 
      =     L      1.1; 
      BLD   103; 
      U     "O_ROBOT_TCP1"; 
      =     L      1.2; 
      BLD   103; 
      U     #t_not_o_robot_tcp1; 
      =     L      1.3; 
      BLD   103; 
      U     "O_ROBOT_TCP1"; 
      =     L      1.4; 
      BLD   103; 
      U     #t_not_o_robot_tcp1; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 108);
      NOP   0; 
NETWORK
TITLE =Fahren auf Wechselposition
//Rettungsmenü fahren auf Wechselposition
      U     "DB_EINRICHTFUNKTION".Bewegung_109_Plus; 
      =     "M_ROBOT_FAHREN_AUF_WEPOS"; 
NETWORK
TITLE =Fahren auf Spülposition
//Rettungsmenü fahren auf Spülposition
      U     "DB_EINRICHTFUNKTION".Bewegung_110_Plus; 
      =     "M_ROBOT_FAHREN_AUF_SPPOS"; 
NETWORK
TITLE =Wechselposition ASUP starten
//Starten des ASUP MACHINE 
      U     "M_ROBOT_FAHREN_AUF_WEPOS"; 
      U     "M_Leistung_Steht_an"; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      FP    "M_FP_ASUP_WEPOS"; 
      SPBN  Asu2; 
NETWORK
TITLE =ASUP-fc201 2 Palettenwechslerwechsler Rettung (Einrichten)
//Start ASUP
      U     "m_eins"; 
      S     "Start_ASUP_Machine_Nr.5"; 
NETWORK
TITLE =Rettungs Funktion Nummer

      L     1; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   EnAs; 
NETWORK
TITLE =Spülposition ASUP starten
//Starten des ASUP MACHINE 
Asu2: U     "M_ROBOT_FAHREN_AUF_SPPOS"; 
      U     "M_Leistung_Steht_an"; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      FP    "M_FP_ASUP_SPPOS"; 
      SPBN  EnAs; 
NETWORK
TITLE =ASUP-fc201 2 Palettenwechslerwechsler Rettung (Einrichten)
//Start ASUP
      U     "m_eins"; 
      S     "Start_ASUP_Machine_Nr.5"; 
NETWORK
TITLE =Rettungs Funktion Nummer

      L     2; 
      T     "DB_NC_PLC".Rettung_FktNr; 
NETWORK
TITLE =Rücksetzten des ASUP
//Wird die Rettungstaste losgelassen, so wird ASUP gestoppt
EnAs: U(    ; 
      U(    ; 
      L     109; 
      L     "DB_OEM".Bewegungsnummer; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_109_Plus; 
      O     ; 
      U(    ; 
      L     110; 
      L     "DB_OEM".Bewegungsnummer; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_110_Plus; 
      )     ; 
      U     "DB_OEM".Handlingsystem_2_Aktiv; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      SPBN  KAsu; 
NETWORK
TITLE =WZW: Reset auslösen bei Abbruch Asup durch loslassen der Rettung
//Reset und ASUP rücksetzen
      U     "m_eins"; 
      =     "M_RESET_RPC_3R"; 
      R     "Start_ASUP_Machine_Nr.5"; 
NETWORK
TITLE =Freigabe fahren der Achsen
//Freigabe zum fahren der Achsen über Rettungsmenü
KAsu: U     #t_frg_rettungsmenue; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      =     #t_frg_achsen_fahren; 
NETWORK
TITLE =Auf Position
//M393 meldet auf Wechsel position
      U     "DB_DM_M_FUNKTION".MX_M[393]; 
      S     "M_AUF_WE_POS"; 
      U(    ; 
      ON    "DB_ACHSE1_X".E_Stat; 
      ON    "DB_ACHSE2_Y".E_Stat; 
      ON    "DB_ACHSE3_Z".E_Stat; 
      ON    "DB_ACHSE4".E_Stat; 
      O     ; 
      UN    "DB_ACHSE5".E_Stat; 
      U     "DB_ACHSE5".E_AxisReady; 
      )     ; 
      R     "M_AUF_WE_POS"; 
      U     "M_AUF_WE_POS"; 
      R     "DB_DM_M_FUNKTION".MX_M[393]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[393]; 
NETWORK
TITLE =Auf Position
//M394 meldet auf Spül position
      U     "DB_DM_M_FUNKTION".MX_M[394]; 
      S     "M_AUF_SP_POS"; 
      U(    ; 
      ON    "DB_ACHSE1_X".E_Stat; 
      ON    "DB_ACHSE2_Y".E_Stat; 
      ON    "DB_ACHSE3_Z".E_Stat; 
      ON    "DB_ACHSE4".E_Stat; 
      O     ; 
      UN    "DB_ACHSE5".E_Stat; 
      U     "DB_ACHSE5".E_AxisReady; 
      )     ; 
      R     "M_AUF_SP_POS"; 
      U     "M_AUF_SP_POS"; 
      R     "DB_DM_M_FUNKTION".MX_M[394]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[394]; 
NETWORK
TITLE =Anzeige Rettungsmenü - Achse auf Wechselposition
//Anzeige der Endlagen und Ventile im Rettungsmenü
//
      U     #t_frg_achsen_fahren; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_achsen_fahren; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     "M_AUF_WE_POS"; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "M_ROBOT_FAHREN_AUF_WEPOS"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 109);
      NOP   0; 
NETWORK
TITLE =Anzeige Rettungsmenü - Achse auf Spuelposition
//Anzeige der Endlagen und Ventile im Rettungsmenü
//
      U     #t_frg_achsen_fahren; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_achsen_fahren; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     "M_AUF_SP_POS"; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "M_ROBOT_FAHREN_AUF_SPPOS"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 110);
      NOP   0; 
NETWORK
TITLE =Handlingsystem Grundstellung

      U     "I_ROBOT_DOOR_CLOSED"; 
      UN    "O_ROBOT_TCP1"; 
      UN    "O_ROBOT_COP1"; 
      =     "DB_OEM".Handlingsystem_GrdSt; 
NETWORK
TITLE =ENDE
//Bausteinende
ende: BEA   ; 

END_FUNCTION

