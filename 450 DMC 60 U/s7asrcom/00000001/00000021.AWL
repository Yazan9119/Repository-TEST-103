FUNCTION "FC_WZW_ABLAUF" : VOID
TITLE =
//$Revision: 1.87 $
//$Date: 2008/09/24 15:48:02CEST $
//$Author: schmoelp $
AUTHOR : FUH
VERSION : 0.1


VAR_INPUT
  E_Enable : BOOL ;	
END_VAR
VAR_TEMP
  Wartezeit : S5TIME ;	//Wartezeit für nächste T-Anwahl nach Magazin von Hand positionieren
  tx_req_ablegen_ZwSp : BOOL ;	
  RegalRset : BOOL ;	//Reset für Regalablauf
  t_AST_Hand : BOOL ;	
  t_GST_Hand : BOOL ;	
  t_Hand_aktiv : BOOL ;	
  t_Auto_aktiv : BOOL ;	
  t_Auto_AST : BOOL ;	
  t_Auto_GST : BOOL ;	
  t_WW_Freigabe : BOOL ;	
  t_WZW_VKETTE_OHNE_TPU : BOOL ;	
  TX_FREIGABE_HANDBETRIEB : BOOL ;	
  t_Frg_Hand_Tool_Unl_MAG : BOOL ;	
  t_Frg_Hand_Tool_Lock_MAG : BOOL ;	
  t_hand_mag_tool_unlock : BOOL ;	
  t_hand_mag_tool_lock : BOOL ;	
  ReqAutAst : BOOL ;	
  ReqAutGst : BOOL ;	
  t_Frg_Hand_WZW : BOOL ;	
  t_Frg_Auto_WZW : BOOL ;	
  TX_STOER_LAUFZ_AST1 : BOOL ;	
  TX_STOER_LAUFZ_GST1 : BOOL ;	
  TX_STOER_PAARUEB1 : BOOL ;	
  TX_STOER_LAUFZ_AST2 : BOOL ;	
  TX_STOER_LAUFZ_GST2 : BOOL ;	
  TX_STOER_PAARUEB2 : BOOL ;	
  t_hand_greifer_0Grad : BOOL ;	
  t_hand_greifer_180Grad : BOOL ;	
  t_Frg_Hand_Tool_Unl_RPL : BOOL ;	
  t_Frg_Hand_Tool_Lock_RPL : BOOL ;	
  t_hand_tload_tool_unlock : BOOL ;	
  t_hand_tload_tool_lock : BOOL ;	
  t_Frg_TLoad_Lock_Hand : BOOL ;	
  t_Frg_TLoad_Lock_Auto : BOOL ;	
  t_hand_tload_tool_not_lo : BOOL ;	
  t_hand_mag_tool_not_lock : BOOL ;	
  t_dummy : BOOL ;	
  t_Frg_Hand_Klappe_auf : BOOL ;	
  t_Frg_Hand_Klappe_zu : BOOL ;	
  Anf_Auto_Klappe_auf : BOOL ;	
  t_Auto_Klappe_auf : BOOL ;	
  t_Auto_Klappe_zu : BOOL ;	
  t_hand_klappe_auf : BOOL ;	
  t_hand_klappe_zu : BOOL ;	
  t_Frg_Hand_Klappe : BOOL ;	
  t_Frg_Auto_Klappe : BOOL ;	
  t_new_tool_Greifer : BOOL ;	
  t_Reset_vorbereitet : BOOL ;	
  t_PI_Reset_WZVorbereitet : BOOL ;	
  tx_dummy2 : BOOL ;	
  TX_WERKZEUGBRUCH_AKTIV : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =WZW Übergabeplatz nicht 0-Signal  
// Netzwerk-Übersicht
// ==================
// NW 1: WZW Übergabeplatz nicht 0-Signal  
// NW 2: Rücksetzen Fehlermeldungen
// NW 3: Rücksetzen Fehlermeldungen nur mit Reset
// NW 4: Rücksetzen Werkzeugwechsel  
// NW 5: WZB: T wurde eingewechselt
// NW 6: Werkzeugwechsler Freigabe
// NW 7: WZW: Aktiver Prozess
// NW 8: WZW: Freigabe von PLC
// NW 9: Magazintürüberwachung bei Fahranforderung
// NW 10: "M_WZW_Aktiv"
// NW 11: Wartezeit für nächste MAG-Positionierung durch T-Befehl
// NW 12: WZW: Magazin verfahren über Suchen & Positionieren  bzw. Taste 
// NW 13: "M_Sperre_TBef_FB193"
// NW 14: WZW: Aktiver Ablauf
// NW 15: CALL "FB_WZV_ABL_REGAL"
// NW 16: -- Leer --
// NW 17: Greiferverriegelung Greifer 2 pneumatisch (für Winkelfräskopf)
// NW 18: Werkzeugverriegelung 
// NW 19:  Anforderung Automatisches Spanner loesen
// NW 20: Ausgang Werkzeugreinigung Stellung reinigen
// NW 21: Freigabe für Handbetrieb
// NW 22: Freigabe Hand/Auto WZW
// NW 23: Freigabe Magazinklappe
// NW 24: Werkzeug an Magazinposition entriegeln und verriegeln
// NW 25: Zusätzliche WZ-Verriegelung am Magazinplatz für SK50/HSK100
// NW 26: WZ-Verriegelung am Magazinplatz Fehler 
// NW 27: Werkzeug am Rüstplatz entriegeln und verriegeln
// NW 28: Request HUB fahren
// NW 29: Türe horzontale Kette
// NW 30: Werkzeugwechslerklappe öffnen und schliessen
// NW 31: GREIFER
// NW 32: WZW-Achsen in Rettung über Asup starten
// NW 33: Taste für Werkzeugwechsler rücksetzen
// NW 34: Omron-Regal Rettung
// NW 35: Greifer Vertikalkette ohne TPU aus- einfahren
// NW 36: 
// NW 37: Querhub in Stellung zur Spindel
// NW 38: Querhub in Stellung zum Magazin
// NW 39: Abbruch Positionieren
// NW 40: Istposition lesen
// NW 41: Greifer Vertikalkette ohne TPU Querhub bewegen
// NW 42: Wiegekopf-Klemmung (Option)
// NW 43: Anpassung an alte Ausgangsmerker
//
      UN    #E_Enable; // Freigabe Modul
      BEB   ; // Nein => zum Ende

// Anwahl WZW = 6 oder 11

      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_VKETTE_OHNE_TPU; 
      =     #t_WZW_VKETTE_OHNE_TPU; 

      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_HORIZONTALE_KETTE; 
      O     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   STRT; // wenn nicht ans Ende und Ausgänge rücksetzen

      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_ANGEWAEHLT; // WZW nicht angewaehlt oder
      SPBN  MEND; 


      CALL "FC_WZW_STATUS" (
           E_Enabled                := TRUE);


      SET   ; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700702_WZ_UGP_nicht_1S; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1._701612_WZWV_Grundst_0S; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1._701616_Grf_nicht_Grdst; 
      BEA   ; 

STRT: NOP   0; 

      CALL "FC_WZW_STATUS" (
           E_Enabled                := TRUE);


      CALL "FC_WZW_FEHLER" (
           E_Enabled                := TRUE);


NETWORK
TITLE =Rücksetzen Fehlermeldungen

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700702_WZ_UGP_nicht_1S; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1._701612_WZWV_Grundst_0S; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700821_Grf_nicht_Grdst; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1._701621_Grf1_2_ni_Mag; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1._701556_MAG_Tuer_offen; 
NETWORK
TITLE =Rücksetzen Fehlermeldungen nur mit Reset

      U     "M_Reset_Taste"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      R     "DB_NC_PLC".WZW_NC_Aktiv; 
      R     "M_RegGr_inGasse_aktiv"; 
      R     "M_RegGr_schliessen_aktiv"; 
      R     "M_RegGr_auf_VP"; 
      R     "M_RegGr_auf_WP"; 
      R     "DB_NC_PLC".MANWZW_NC_Aktiv; 
      R     "DB_COMM_WZREGAL".Kommando[9].Neg_Quitt; 
      R     "DB_COMM_WZREGAL".Kommando[13].Neg_Quitt; 
      R     "DB_COMM_WZREGAL".Kommando[14].Neg_Quitt; 
      R     "DB_COMM_WZREGAL".Kommando[15].Neg_Quitt; 
      R     "MX_WZB_Ov_0"; 
      R     "MX_WZB_G0_Sperre"; 
      R     "DB_DM_M_FUNKTION".MX_M[278]; // Spanner öffnen
      R     "DB_DM_M_FUNKTION".MX_M[279]; // Spanner schliessen
      R     "DB_DM_M_FUNKTION".MX_M[283]; 
      R     "DB_DM_M_FUNKTION".MX_M[284]; 
      R     "O_TM_TPU_AIR_CLEANING"; 
NETWORK
TITLE =Werkzeugbruchüberwachung angewählt

      L     "DB_PLC_MD_DB20".UDInt._010_ANWAHL_WERZEUGBRUCH; 
      L     0; 
      <>I   ; 
      =     #TX_WERKZEUGBRUCH_AKTIV; 
NETWORK
TITLE =Rücksetzen Werkzeugwechsel  

      U     "M_Reset_WZW"; 
      SPBN  WS00; 
//---(1) --- WzwDaten retten für Wzw.Grundstlg.fahren -----------
      UN    "DB_WWZ".gd0.FTMDsi; 
      SPB   e001; 
      R     "DB_WWZ".gd0.FTMDsi; 
      U     "DB_DM_M_FUNKTION".MX_M[276]; //Gr1-->Spi
      =     "DB_WWZ".TMDR.M276; 
      U     "DB_DM_M_FUNKTION".MX_M[277]; //Gr1-->Mag
      =     "DB_WWZ".TMDR.M277; 
      U     "DB_DM_M_FUNKTION".MX_M[278]; //Spi.spannen
      =     "DB_WWZ".TMDR.M278; 
      U     "DB_DM_M_FUNKTION".MX_M[279]; //Spi.lösen
      =     "DB_WWZ".TMDR.M279; 
      U     "DB_DM_M_FUNKTION".MX_M[283]; //Req.Wz.im Gr1 im Mag.ablegen
      =     "DB_WWZ".TMDR.M283; 
      U     "DB_DM_M_FUNKTION".MX_M[284]; //Req.Wz.im Gr2 im Mag.ablegen
      =     "DB_WWZ".TMDR.M284; 
      U     "DB_NC_PLC".WZW_NC_Aktiv; //$A_DBB[111] A_NC_WZW_AKTIV
      =     "DB_WWZ".TMDR.wwNcAkt; 
      U     "M_WZV_Vorber_Req"; 
      =     "DB_WWZ".TMDR.VorbReq; 
      U     "M_WZW_Vorber_laeuft"; 
      =     "DB_WWZ".TMDR.VorbLft; 
      U     "M_WZV_Bereitst_Fertig"; 
      =     "DB_WWZ".TMDR.BerStFm; 
      U     "M_Req_Ablegen_WZ"; 
      =     "DB_WWZ".TMDR.AblReq; 
      U     "M_WZW_Ausw"; 
      =     "DB_WWZ".TMDR.AblLft; 
      U     "M_WZ_Rdy_Ablegen"; 
      =     "DB_WWZ".TMDR.AblFm; 
      U     "DB_NC_PLC".WZW_WZ_Vorbereitet; 
      =     "DB_WWZ".TMDR.toNc_VorbRdy; 
      U     "DB_NC_PLC".WZW_WZ_Einwechseln; 
      =     "DB_WWZ".TMDR.toNcEinw; 
      U     "DB_NC_PLC".WZW_WZ_Vollwechsel; 
      =     "DB_WWZ".TMDR.toNcVollw; 
      U     "DB_NC_PLC".WZW_WZ_Auswechseln; 
      =     "DB_WWZ".TMDR.toNcAusw; 
      U     "DB_NC_PLC".WZW_WZ_AUS_EINWechseln; 
      =     "DB_WWZ".TMDR.toNcAEw; 
      UN    "M_WZM_Regal_angewaehlt"; 
      L     "B_SKZ_Ablauf"; //kein Regal:<B_SKZ_Ablauf> sichern
      SPB   m001; 
      L     "DI_WZV_ABL_REGAL".Schritt; //Bei Regal <"DI_WZV_ABL_REGAL".Schritt> sichern
m001: T     "DB_WWZ".TMDR.WwSr; 
e001: SET   ; 
//---(1) ---------------------------------------------------------

      R     "DB_DM_M_FUNKTION".MX_M[276]; 
      R     "DB_DM_M_FUNKTION".MX_M[277]; 
      R     "DB_DM_M_FUNKTION".MX_M[278]; 
      R     "DB_DM_M_FUNKTION".MX_M[279]; 
      R     "DB_DM_M_FUNKTION".MX_M[271]; 
      R     "DB_DM_M_FUNKTION".MX_M[272]; 
      R     "DB_DM_M_FUNKTION".MX_M[288]; 
      R     "DB_DM_M_FUNKTION".MX_M[283]; 
      R     "DB_DM_M_FUNKTION".MX_M[284]; 
      R     "DB_NC_PLC".WZW_NC_Aktiv; 
      R     "DB_NC_PLC".MANWZW_NC_Aktiv; 
      R     "M_WZW_Aktiv"; 
      R     "Te_WZW_Hub_Verz"; 
      R     "M_WZW_Vorber_laeuft"; 
      R     "M_WZW_Hub_GrdSt_req"; 
      R     "M_WZW_Hub_Mag_req"; 
      R     "M_AUTO_MAG_TOOL_UNLOCK"; 
      R     "M_AUTO_MAG_TOOL_LOCK"; 
      R     "M_req_Greifer_ausfahren"; 
      R     "M_req_Greifer_einfahren"; 
      R     "M_WZW_Ausw"; 
      R     "M_WZ_Reinigen_Aktiv"; 
      R     "M_WZWV_Grundstell_req"; 
      R     "M_WZWV_Verrieg_req"; 
      R     "M_WZW_Gre1_Mag_req"; 
      R     "M_WZW_Gre1_Spi_req"; 
      R     "O_WW_1GRF_SPI"; 
      R     "O_WW_1GRF_MAG"; 
      R     "O_WW_Hub_ZYL_in_Rein"; 
      R     "M_WZ_Rdy_Ablegen"; 
      R     "M_Req_Ablegen_WZ"; 
      R     "M_WZV_Bereitst_Fertig"; 
      R     "DB_NC_PLC".WZW_WZ_Vorbereitet; 
      R     "M_Grf2_entr_req"; 
      R     "M_Grf2_verr_req"; 
      R     "O_WW_GRF_ENTRIEGELN"; 
      R     "O_TM_TPU_AIR_CLEANING"; 
      L     0; 
      T     "B_SKZ_Ablauf"; 
      T     "DB_NC_PLC".ZustZaehlerWZW; 
WS00: NOP   0; 
NETWORK
TITLE =WZB: T wurde eingewechselt
//Rückmeldung Einwechseln fertig an WZB-Überwachung
      U     #TX_WERKZEUGBRUCH_AKTIV; 
      SPBN  wzb; 
      U     "DB_NC_PLC".WZW_NC_EINW_beendet; 
      FP    "MX_FP_EinwRdy"; 
      S     "MX_T_WechselRdy"; 

wzb:  NOP   0; 
NETWORK
TITLE =Werkzeugwechsler Freigabe
//      U     "I_WW_Freigabe"
      U     "I_WM_Beladung_zu"; 
      U     "I_WM_Energiefreigabe"; 
      O     "M_WZM_Regal_angewaehlt"; 
      =     #t_WW_Freigabe; 
NETWORK
TITLE =WZW: Aktiver Prozess

      O     "M_WZW_Aktiv"; 
      O     "DB_NC_PLC".WZW_PLC_Aktiv; // "DB_NCPlc_DB70".WZW_310
      O     "DB_NC_PLC".WZW_NC_Aktiv; 
      =     "M_WZW_Prozess_Aktiv"; 
NETWORK
TITLE =WZW: Freigabe von PLC

      U     #t_WW_Freigabe; // WZW Freigabe 1-Signal
      U     "M_WZW_Grdst"; // und der WZW ist in Grundstellung
      UN    "M_Einrichtbetrieb"; 
      U(    ; 
      UN(   ; 
      O     "M_4Te_Betriebsart"; 
      O     "M_3Te_Betriebsart"; 
      )     ; 
      O     ; 
      U(    ; 
      O     "M_4Te_Betriebsart"; 
      O     "M_3Te_Betriebsart"; 
      )     ; 
      UN(   ; 
      L     "TMSpindleIF".IF[1].SMag; 
      L     1; 
      ==I   ; 
      )     ; 
      UN(   ; 
      L     "TMSpindleIF".IF[1].TMag; 
      L     1; 
      ==I   ; 
      )     ; 
      U(    ; 
      U     "TMSpindleIF".IF[1].ManTIn; 
      O     "TMSpindleIF".IF[1].ManTOut; 
      )     ; 
      )     ; 
      U     "M_Leistung_Steht_an"; 
      UN    "MX_TDS_Aktiv"; 
      U(    ; 
      U     "I_LA_eingeschwenkt"; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     0; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     3; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     4; 
      ==I   ; 
      )     ; 
      )     ; 
      UN    "DB_FEHLERMELDUNG".Warnung._701857_Err_WZV_Abbruch; 
      UN    "DB_OUT".AmxLockToolChanger; 
      =     "DB_NC_PLC".WZW_PLC_Freigabe; // Alle Bedingungen ok Startsignal an NC 


NETWORK
TITLE =Magazintürüberwachung bei Fahranforderung
//   U     "DB_Achs8".MX_FahrAnf_Plus
//   O     "DB_Achs8".MX_FahrAnf_Minus
//   O     "DB_Achs9".MX_FahrAnf_Plus
//   O     "DB_Achs9".MX_FahrAnf_Minus
//   O     "DB_Achs8".MX_Anf_Positionieren
//   O     "DB_Achs9".MX_Anf_Positionieren
//
// hier nur achse 7
      U     "M_WZM_Regal_angewaehlt"; 
      SPB   nw8a; 

// Störung Magazintürüberwachung 
      UN    "I_WM_Beladung_zu"; 
      U(    ; 
      U     "M_WZW_Aktiv"; 
      UN    "I_WW_1_Klappe_zu"; 
      O(    ; 
      L     "B_SKZ_Ablauf"; 
      L     0; 
      >I    ; 
      )     ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async1._701556_MAG_Tuer_offen; 

// Meldung Magazintürüberwachung  
      UN    "I_WM_Beladung_zu"; 
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      =     "DB_FEHLERMELDUNG".Meldung._702005_Mag_Tuer_Zu_0S; 
nw8a: NOP   0; 
NETWORK
TITLE ="M_WZW_Aktiv"
//Greifer und Hub in Grundstellung fahren
//- Wird vom NC-Makro für M46 mit M270 nach Wechselvorgang angestossen
      U     "DB_NC_PLC".WZW_NC_Aktiv; 
      =     "M_M6_aktiv"; 
      =     "M_WZW_Aktiv"; 
NETWORK
TITLE =Wartezeit für nächste MAG-Positionierung durch T-Befehl

      L     "DB_PLC_MD_DB20".UDInt._028_WARTEZEIT_T_NEXT; 
      L     60; // Maximalwert 60 Sekunden
      <=I   ; 
      SPB   ok; 
      L     60; 
      T     "DB_PLC_MD_DB20".UDInt._028_WARTEZEIT_T_NEXT; 

ok:   L     "DB_PLC_MD_DB20".UDInt._028_WARTEZEIT_T_NEXT; 
      ITB   ; 
      L     8192; // Zeitbasis 1s laden
      OW    ; 
      T     #Wartezeit; 
NETWORK
TITLE =WZW: Magazin verfahren über Suchen & Positionieren  bzw. Taste 
//"M_SKZ_Be_Entladen" im Schritt 22 == Suchen & Positionieren 
//
//  Original Symbol :USER_DATA_INT [028]
//DM Symbol :WARTEZEIT_T_NEXT
//Wartezeit (Sekunden) für die nächste Magazinpositionierung durch
//T-Befehl nach: 
//- Magazinpositionierung über die Tasten links/rechts 
//- Suchen/Positionieren
//
//0=keine Wartezeit 
      O(    ; 
      U(    ; 
      O(    ; 
      L     "M_SKZ_Be_Entladen"; 
      L     22; 
      ==I   ; 
      )     ; 
      O     "M_MAG_Manuell_Rechts"; 
      O     "M_MAG_Manuell_Links"; 
      )     ; 
      L     #Wartezeit; 
      SA    "TA_WZW_SUCHEN_U_POSITION"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      U     "TA_WZW_SUCHEN_U_POSITION"; 
      )     ; 
      ON    #t_WW_Freigabe; 
      =     "M_WZW_SPERRE_TBEF_V_MAG"; 
NETWORK
TITLE ="M_Sperre_TBef_FB193"
//Nächsten T-Befehl sperren solange Auftrag noch nicht beendet ist
//      U     "DB_DM_M_FUNKTION".MX_M[288]
      O     "M_WZW_Ausw"; 
      O     "M_WZW_Vorber_laeuft"; 
      O     "M_M6_aktiv"; 
      O     "DB_NC_PLC".WZWBe_entladen_aktiv; 
      O     "M_WZW_SPERRE_TBEF_V_MAG"; 
      L     S5T#500MS; 
      SA    "Ta_sperre_T_Aufruf"; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      U(    ; 
      U     "M_WZW_Ausw"; 
      U     "DB_NC_PLC".WZW_WZ_Auswechseln; 
      O     "DB_NC_PLC".WZWBe_entladen_aktiv; 
      O     "M_WZW_SPERRE_TBEF_V_MAG"; 
      )     ; 
      O     ; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      U     "Ta_sperre_T_Aufruf"; 
      =     "M_Sperre_TBef_FB193"; 

NETWORK
TITLE =WZW: Aktiver Ablauf
//WZW-Ablauf Schrittkette
//- SK00 - SK11: Vorbereiten Einwechseln
//- SK12 - SK21: WZ zurücklegen in Magazin
//
//
      U     "M_WZV_Bereitst_Fertig"; 
      FP    "M_FP_WZ_Vorbereitet"; 
      O(    ; 
      U     "M_WZV_Quit_Bereitst"; 
      FP    "M_FP_Quit_Bereitst"; 
      )     ; 
      =     "DB_WWZ".gd0.FpWzvb; 
      S     "DB_NC_PLC".WZW_WZ_Vorbereitet; 

      L     "TMSpindleIF".IF[1].SMag; 
      L     9998; 
      ==I   ; 
      U(    ; 
      L     "TMSpindleIF".IF[1].SLoc; 
      L     2; 
      ==I   ; 
      O(    ; 
      L     "TMSpindleIF".IF[1].SLoc; 
      L     3; 
      ==I   ; 
      )     ; 
      )     ; 
      =     #t_new_tool_Greifer; 

      UN    #t_WZW_VKETTE_OHNE_TPU; 
      U     "Mx_X_ACHSE_WZW_POS"; //  "DB_NC_PLC".WZW_NC_Aktiv
      U     "I_WZ_SPA_geloest"; 
      O     ; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      UN    "M_WZW_Gre1_Pos_Mag"; 
      UN    "M_WZW_Gre1_Pos_Spi"; 
      O     ; 
      U     "TMSpindleIF".IFNo[1]; 
      U     "TMSpindleIF".IF[1].Prepare; 
      UN    "DI_TM_QUITT_FC8".Start; 
      U(    ; 
      UN    "M_WZM_Regal_angewaehlt"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      ON    "TMSpindleIF".IF[1].ManTIn; 
      )     ; 
      FP    "M_FP_Reset_WZVorbereitet"; 
      =     #t_PI_Reset_WZVorbereitet; 

      U     #t_PI_Reset_WZVorbereitet; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      U(    ; 
      ON    "M_WZM_Regal_angewaehlt"; 
      ON    #t_new_tool_Greifer; 
      )     ; 
      O     ; 
      U     #t_PI_Reset_WZVorbereitet; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      U(    ; 
      ON    #t_new_tool_Greifer; 
      ON    "DB_NC_PLC".WZW_WZ_Vorbereitet; 
      )     ; 
      R     "M_WZV_Bereitst_Fertig"; 
      R     "DB_NC_PLC".WZW_WZ_Vorbereitet; 

//Greifer 2 zu Magazin muß Werkzeug holen bei Typ 4 oder 5
// beim Vorbereiten Greifer 2 zu Magazin wenn
// a: Einwechseln 
// b: Vollwechsel und neuer Typ gross
// c: Aus Einwechseln, beim Einwechseln 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      R     "M_Req_GRF2_Mag"; 
      R     "M_Req_GRF1_Mag"; 
      SPB   EHK1; 

      U(    ; 
      U(    ; 
      L     "DI_TM_T_BEFEHL".T_NewTool_Typ; 
      L     2; 
      ==I   ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     3; 
      ==I   ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     4; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     5; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     6; 
      ==I   ; 
      U     "DB_PLC_MD_DB20".UDHex._16_BIT4_WZTyp6_Greif2; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     7; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     99; 
      ==I   ; 
      )     ; 
      )     ; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      =     "M_Req_GRF2_Mag"; 

      U     "DB_NC_PLC".WZW_WZ_Vollwechsel; 
      UN    "DB_NC_PLC".WZW_WZ_Hand_Aus; 
      U(    ; 
      U(    ; 
      L     "DI_TM_T_BEFEHL".T_SpWz_Typ; 
      L     2; 
      ==I   ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     3; 
      ==I   ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     4; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     5; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     6; 
      ==I   ; 
      U     "DB_PLC_MD_DB20".UDHex._16_BIT4_WZTyp6_Greif2; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     7; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     99; 
      ==I   ; 
      )     ; 
      )     ; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      =     "M_Req_GRF1_Mag"; 

EHK1: NOP   0; 

//Umschreiben des OEM-Datums für Werkzeugbruchüberwachung
//      UN    "DB_NC_PLC".WZW_NC_Aktiv
//      U     "M_WZW_Grdst"
//      UN    "DB_DM_M_FUNKTION".MX_M[283]
//      UN    "DB_DM_M_FUNKTION".MX_M[284]
//      FP    "M_FP_WZBR_WR"
//      =     "M_WZBR_WR"

//      U     "M_WZBR_WR"
//      SPBN  ABL1
//      L     "DB_REMANENT".WZB_GREIFER
//      T     "MD_WZBRUCH_OEM"
//ABL1: NOP   0



//Wenn Omron-Regal angewählt dann Schrittkette überspringen
      U     "M_WZM_Regal_angewaehlt"; 
      SPB   omr; 
//----------------------------------------------------------------------------------------
      U     "M_WZW_Ausw"; 
      UN    "I_WW_Uebergabeplatz"; 
      L     S5T#200MS; 
      SE    "Te_WZW_Hub_Verz"; 


      L     "B_SKZ_Ablauf"; 
      SPL   SK99; 
      SPA   SK00; 
// Werkzeug einwechseln
      SPA   SK01; // Schritt 1: Kontrolle Magazinplatz belegt
      SPA   SK02; // Schritt 2: Greifer 1 oder 2 Richtung Magazin drehen
      SPA   SK03; // Schritt 3: Hub Richtung Magazin
      SPA   SK04; // Schritt 4: Werkzeug im Magazin entriegeln
      SPA   SK05; // Schritt 5: Greifer ausfahren
      SPA   SK06; // Schritt 6: Hub zur Grundstellung / Werkzeug im Magazin verriegeln
      SPA   SK07; // Schritt 7: Greifer einfahren
      SPA   SK08; // Schritt 8: Kontrolle Magazinplatz leer / Umspeichern Mag > Greifer
      SPA   SK09; // Schritt 9: Start Kegelreinigung / Bruchkontrolle
      SPA   SK10; // Schritt 10:Warten auf Ende Kegelreinigung / Bruchkontrolle 
      SPA   SK11; // Schritt 11:Vorbereiten beendet 
// Werkzeug ablegen
      SPA   SK12; // Schritt 12:Greiferdaten abfragen
      SPA   SK13; // Schritt 13:Warten bis Greiferdaten gelesen
      SPA   SK14; // Schritt 14:Rücksetzen WZW_WZW_Beendet an NC
      SPA   SK15; // Schritt 15:Werkzeugbruchkontrolle 2te Messung
      SPA   SK16; // Schritt 16:Ende Bruchkontrolle
      SPA   SK17; // Schritt 17:Werkzeug im Magazin entriegeln / Greifer ausfahren
      SPA   SK18; // Schritt 18:Hub Richtung Magazin
      SPA   SK19; // Schritt 19:Greifer einfahren / Rückhalt verriegeln
      SPA   SK20; // Schritt 20:Werkzeug im Magazin verriegeln
      SPA   SK21; // Schritt 21:Hub Richtung Grundstellung
      SPA   SK22; // Schritt 22:Rückhalt zurückfahren / Umspeichern Greifer > Magazin
      SPA   SK23; // Schritt 23:Ablegen ready
      SPA   SK24; // Schritt 24:Ende

SK99: SPA   MEND; 


// -- Schritt 0 ---------------------------------------------------------------
SK00: SET   ; 
      R     "M_WZW_Ausw"; 
      R     "M_WZ_Rdy_Ablegen"; 
      U     "M_Req_Ablegen_WZ"; //Ablegen aus FB193
      UN    "M_WZW_Ausw"; 
      UN    "M_WZW_Vorber_laeuft"; 
      UN    "DB_NC_PLC".WZWBe_entladen_aktiv; //"MX_BeEntladen_Aktiv"
      UN    "M_WZV_Vorber_Req"; 
      U(    ; 
      U     "M_WW_X_imArbeitsraum"; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      ON    "DB_NC_PLC".WZW_NC_Aktiv; 
      )     ; 
      U     "DB_REMANENT".WZ_Im_Greifer; 
      SPBN  A001; 

// Wenn bei WZB ein WZ im Greifer ist Vorschub-Override Sperren
      U     "DI_WZ_BRUCH".SX_GRF_ANF; 
      U     #TX_WERKZEUGBRUCH_AKTIV; 
      SPBN  E142; 

      L     "DB_REMANENT".WZB_GREIFER; 
      L     2; 
      <>I   ; 
      U     "DI_WZ_BRUCH".SX_GRF_ANF; 
      S     "MX_WZB_Ov_0"; 

// Eilgang Sperre, OEM=3
      L     "DB_REMANENT".WZB_GREIFER; 
      L     3; 
      ==I   ; 
      U     "DI_WZ_BRUCH".SX_GRF_ANF; 
      S     "MX_WZB_G0_Sperre"; 

E142: NOP   0; 

      SET   ; 
      S     "M_WZW_Ausw"; 
      S     "DI_TM_T_BEFEHL".Req_FB192; 
      R     "M_WZ_Rdy_Ablegen"; 
      L     14; 
      T     "B_SKZ_Ablauf"; 
      SPA   SK14; 

A001: U     "M_WZV_Vorber_Req"; //Vorbereiten aus FB193
      UN    "M_WZW_Vorber_laeuft"; 
      UN    "DB_NC_PLC".WZWBe_entladen_aktiv; //"MX_BeEntladen_Aktiv"
      UN    "M_WZW_Ausw"; 
      UN    "M_Req_Ablegen_WZ"; 
      UN    "M_M46_Gre_Hub_Grdst_Req"; 
      U(    ; 
      UN    "TMSpindleIF".IF[1].ManTOut; 
      O     ; 
      U     "TMSpindleIF".IF[1].ManTOut; 
      UN    "M_MAG_Laeuft"; 
      UN    "M_WZW_Hub_Mag_req"; 
      U     "M_WZW_Hub_GrdSt"; 
      )     ; 
      SPBN  MEND; 
      SET   ; 
      R     "M_WZV_Bereitst_Fertig"; 
      S     "M_WZW_Vorber_laeuft"; 
      L     1; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 1 ----------------------------------------------------------------

SK01: U     "M_WZW_Vorber_laeuft"; 
      U(    ; 
      U     "M_WZW_Gre1_Pos_Mag"; //Greifer in Grundstellung
      O     "M_WZW_Gre1_Pos_Spi"; 
      )     ; 
      U(    ; 
      U     "I_WW_Rueck_hlt_GST"; //WZ-Verriegelung in Grundstellung
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      O     ; 
      U     "M_TM_MAG_TOOL_LOCK"; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      )     ; 
      U     #t_WW_Freigabe; 
      UN    "M_MAG_Laeuft"; 
      SPBN  MEND; 


      U(    ; 
      L     "TMSpindleIF".IF[1].SMag; //Bei T0 abwarten auf Magazin positioniert
      L     0; 
      ==I   ; 
      )     ; 
      U(    ; 
      L     "TMSpindleIF".IF[1].SLoc; 
      L     0; 
      ==I   ; 
      )     ; 
      SPBN  A015; 

      U     "M_MAG_In_Position"; //und fertig melden
      U(    ; 
      L     "DB_NC_PLC".Magazin_Platz_Unload; 
      L     "MW_MAG_MP_Ist"; 
      ==I   ; 
      )     ; 
      UN    "M_MAG_Laeuft"; 
      SPBN  MEND; 

      O     "I_WW_Uebergabeplatz"; 
      O     "I_TM_TST_OCC"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      SPB   MEND; 

      SET   ; 
      S     "M_WZV_Bereitst_Fertig"; 
      R     "M_WZW_Vorber_laeuft"; 
      L     0; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


A015: L     "DB_NC_PLC".Magazin_Platz_Load; 
      L     "MW_MAG_MP_Ist"; 
      ==I   ; 
      UN    "M_MAG_Laeuft"; 
      SPBN  MEND; 

      UN    "I_WW_Uebergabeplatz"; 
      U     "DB_PLC_MD_DB20".UDHex._08_BIT0_UGP_BELEGT; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700702_WZ_UGP_nicht_1S; 
      SPB   MEND; 

// bei vertikaler Kette keine Greifer drehen, weiter im Schritt 3
      U     #t_WZW_VKETTE_OHNE_TPU; 
      R     "M_WZW_Gre1_Spi_req"; //Greifer 2 Richtung Magazin
      R     "M_WZW_Gre1_Mag_req"; 
      SPB   A016; 
      L     2; 
      SPA   A017; 
A016: L     3; 
A017: T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 2 ----------------------------------------------------------------
SK02: U     "M_Req_GRF2_Mag"; 
      S     "M_WZW_Gre1_Spi_req"; //Greifer 2 Richtung Magazin
      R     "M_WZW_Gre1_Mag_req"; 

      U     "M_Req_GRF1_Mag"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      R     "M_WZW_Gre1_Spi_req"; //Greifer 2 Richtung Magazin
      S     "M_WZW_Gre1_Mag_req"; 

      L     3; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 3 ----------------------------------------------------------------
//Wenn: Greifer in Position
//Dann: Hub ins Magazin
SK03: U(    ; 
      U     "M_Req_GRF2_Mag"; 
      U     "M_WZW_Gre1_Pos_Spi"; // "I_WW_1GRF_MAG"
      )     ; 
      O(    ; 
      U     "M_Req_GRF1_Mag"; 
      U     "M_WZW_Gre1_Pos_Mag"; 
      )     ; 
      O(    ; 
      U     "M_WZW_Gre1_Pos_Mag"; 
      X     "M_WZW_Gre1_Pos_Spi"; //xxxxxxxxx
      UN    "M_Req_GRF2_Mag"; //xxxxxxxx
      UN    "M_Req_GRF1_Mag"; 
      )     ; 
      UN    "M_WZW_Gre1_Spi_req"; 
      UN    "M_WZW_Gre1_Mag_req"; 
      U(    ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      O(    ; 
      L     "DB_TOOL_DATA".New_Tool_Siem_Typ; 
      L     9.000000e+002; 
      <>R   ; 
      O     "DB_NC_PLC".WZW_Tuere_offen; 
      )     ; 
      )     ; 
      SPBN  MEND; 

      U     "I_TM_TST_OCC"; 
      U     "DB_PLC_MD_DB20".UDHex._08_BIT0_UGP_BELEGT; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      SPB   MEND; 

      S     "M_WZW_Hub_Mag_req"; 

      L     4; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 4 ----------------------------------------------------------------
//Wenn: Hub im Magazin
//Dann: Werkzeug entriegeln

SK04: U     "M_WZW_Hub_Magazin"; 
      SPBN  MEND; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   VK01; 

      L     6; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

VK01: S     "M_AUTO_MAG_TOOL_UNLOCK"; 
      R     "M_AUTO_MAG_TOOL_LOCK"; 

      L     5; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 5 ----------------------------------------------------------------
//Wenn: Werkzeug entriegelt
//Dann: Greifer ausfahren

SK05: U     "I_TM_MAG_TOOL_UNLOCK"; 
      UN    "I_TM_MAG_TOOL_LOCK"; 
      SPBN  MEND; 

      S     "M_req_Greifer_ausfahren"; 
      R     "M_req_Greifer_einfahren"; 

      L     6; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 6 ----------------------------------------------------------------
//Wenn: Horizontale Kette WZ-Verriegelung in Grundstellung
//Dann: Hub in Grundstellung fahren
//Wenn: Vertikale Kette Greifer ausgefahren
//Dann: Hub in Grundstellung fahren / Werkzeug entriegeln im Magazin

SK06: U     "I_WW_Rueck_hlt_GST"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  M061; 
      S     "M_WZW_Hub_GrdSt_req"; 

      L     8; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

M061: U     "DB_Achs11_VMAG_Greifer".MX_Greifer_ausgefahren; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  MEND; 

      SET   ; 
      R     "M_req_Greifer_ausfahren"; 
      S     "M_AUTO_MAG_TOOL_LOCK"; 
      R     "M_AUTO_MAG_TOOL_UNLOCK"; 
      S     "M_WZW_Hub_GrdSt_req"; 

      L     7; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 7 ----------------------------------------------------------------
//Wenn: Hub in Grundstellung
//Dann: Greifer in Grundstellung

SK07: U     "M_WZW_Hub_GrdSt"; 
      U     "I_TM_MAG_TOOL_LOCK"; 
      SPBN  MEND; 

      S     "M_req_Greifer_einfahren"; 
      R     "M_req_Greifer_ausfahren"; 
      S     "O_TM_TPU_AIR_CLEANING"; 

      L     8; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 8 ----------------------------------------------------------------

SK08: U     "DB_Achs11_VMAG_Greifer".MX_Greifer_eingefahren; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      O     ; 
      U     "M_WZW_Hub_GrdSt"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  MEND; 

      R     "M_req_Greifer_einfahren"; 
      R     "O_TM_TPU_AIR_CLEANING"; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  A08b; 

      UN    "I_TM_TST_OCC"; 
      U     "DB_PLC_MD_DB20".UDHex._08_BIT0_UGP_BELEGT; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700702_WZ_UGP_nicht_1S; 
      SPB   MEND; 

A08b: U     "I_WW_Uebergabeplatz"; 
      SPBN  A08a; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      SPA   MEND; 

A08a: NOP   0; 

      SET   ; 
      U     "M_WZW_Gre1_Pos_Mag"; //17.07.03 FUH: Umspeichern hier eingefügt wegen Satzsuchlauf
      S     "M_Umsp_Mag-Gr1"; 

      U     "M_WZW_Gre1_Pos_Spi"; 
      S     "M_Umsp_Mag-Gr2"; 

      L     9; 
      T     "B_SKZ_Ablauf"; 
      SPA   SK09; 


// -- Schritt 9 ----------------------------------------------------------------
//Wenn: Greifer in Grundstellung
//Dann: Abfrage ob WZ reinigen

SK09: U(    ; 
      U     "DB_DM_M_FUNKTION".MX_M[47]; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     2; 
      ==I   ; 
      )     ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     4; 
      ==I   ; 
      )     ; 
      SPBN  A09a; 
      SET   ; 
      S     "M_Req_WZ_Reinigen"; 
      R     "M_Ready_WZ_Reinigen"; 

//Wenn: Werkzeugbruchüberwachung angewählt
//Dann: Ablauf WZ-Bruch-Überwachung starten

A09a: U     #TX_WERKZEUGBRUCH_AKTIV; 
      SPBN  A09b; 


//Werkzeugbruchanforderung 1te Messung
      SET   ; 
      S     "M_WZB_Anforderung"; 
      R     "M_WZB_Messung"; 
      R     "M_WZB_Ready"; 

A09b: L     10; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 10 ----------------------------------------------------------------


SK10: SET   ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     2; 
      <>I   ; 
      )     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     4; 
      <>I   ; 
      )     ; 
      SPB   A10a; 

      U     "M_Ready_WZ_Reinigen"; 
      SPBN  MEND; 

A10a: U     #TX_WERKZEUGBRUCH_AKTIV; 
      SPBN  A10b; 

      U     "M_WZB_Ready"; 
      SPBN  MEND; 
      R     "M_WZB_Ready"; 
      R     "MX_WZB_NeuWahl"; 

A10b: R     "M_Ready_WZ_Reinigen"; 
      L     11; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

//Ende Werkzeugbruchablauf
//Ende Werkzeug reinigen

// -- Schritt 11 ----------------------------------------------------------------
//Wenn: WZW in Grundstellung
//Dann: Ende WZ-Vorbereitet melden

SK11: U     "M_WZW_Greifer_OK"; 
      U     "M_WZW_Hub_GrdSt"; 
      U(    ; 
      L     "DI_TM_T_BEFEHL".Schritt; 
      L     8; 
      ==I   ; 
      )     ; 
      SPBN  MEND; 

      U     "I_WW_Uebergabeplatz"; 
      SPBN  A11a; 
      SET   ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      SPA   MEND; 

A11a: S     "M_WZV_Bereitst_Fertig"; 
      R     "M_WZW_Vorber_laeuft"; 
      R     "M_WZW_Aktiv"; 
      R     "M_WZR_Mag_Verrieg"; // Magazinverriegelung ruecksetzen
      S     "M_Req_write_Bruchdaten"; 

//Beschreiben der Werkzeugbruchvariablen
//      U     "DB_DM_M_FUNKTION".MX_M[36] //M-Funktion für WZB-Warnung ausgeben aktiviert
//      U(    
//      L     "DI_TM_WZBRUCH_R".TC_TPC1   //und WZBruch aktiviert
//      L     1.000000e+000
//      >=R   
//      )     
//      SPB   E041

//      U     "DB_DM_M_FUNKTION".MX_M[37] //M-Funktion für WZB-Warnung ausgeben aktiviert
//      U(    
//      L     "DI_TM_WZBRUCH_R".TC_TPC1   //und WZBruch aktiviert
//      L     1.000000e+000
//      >=R   
//      )     
//      SPB   E043

//      L     "DI_TM_WZBRUCH_R".TC_TPC1
//      RND   
//      T     "DB_REMANENT".WZB_GREIFER
//      SPA   E042

// M36, OEM=2
//E041: SET   
//      R     "DB_DM_M_FUNKTION".MX_M[36]

//      L     2                           //Für dieses WZ nur Warnung ausgeben
//      T     "DB_REMANENT".WZB_GREIFER

//      SET   
//      R     "MX_WZB_Ov_0"
//      SPA   E042

// M37, OEM=3
//E043: SET   
//      R     "DB_DM_M_FUNKTION".MX_M[37]

//      L     3                           //Für dieses WZ Vorschub und Eilgang sperren
//      T     "DB_REMANENT".WZB_GREIFER

//E042: NOP   0

      L     0; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 




// -- Schritt 12 --WZ-Ablegen-----------------------------------------------------
SK12: U     "DI_TM_T_BEFEHL".Req_FB192; 
      O     "DI_TM_T_BEFEHL".Req_FB92; 
      SPB   MEND; 
      S     "Mx_Werkzeugdaten_lesen"; //Request Greiferdaten lesen
      R     "Mx_WZdaten_lesen_ready"; 
      L     13; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

SK13: U     "Mx_WZdaten_lesen_ready"; // warten auf gelesen
      UN    "Mx_Wzdaten_lesen_error"; 
      R     "Mx_Werkzeugdaten_lesen"; 
      SPBN  MEND; 
      L     14; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


//Wenn: Werkzeugbruchüberwachung angewählt
//Dann: Ablauf WZ-Bruch-Überwachung starten
SK14: SET   ; 
      R     "DB_NC_PLC".WZW_WZW_Beendet; 

      O     "DI_TM_T_BEFEHL".NDR_FB192; 
      O     "DI_TM_T_BEFEHL".Error_FB192; 
      R     "DI_TM_T_BEFEHL".Req_FB192; 

      UN    "DI_TM_T_BEFEHL".Req_FB192; 
      SPBN  MEND; 

      L     15; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 15 --WZ-Ablegen-----------------------------------------------------
SK15: U     "M_WZW_Hub_GrdSt"; 
      SPBN  MEND; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  A15b; 

      UN    "I_TM_TST_OCC"; 
      U     "DB_PLC_MD_DB20".UDHex._08_BIT0_UGP_BELEGT; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700702_WZ_UGP_nicht_1S; 
      SPB   MEND; 

A15b: ON    #TX_WERKZEUGBRUCH_AKTIV; 
      ON    "DI_WZ_BRUCH".SX_GRF_ANF; 
      O(    ; 
      U     "DB_PLC_MD_DB20".UDHex._20_Bit1_WZ_ni_ablegen; 
      UN    "TMSpindleIF".IF[1].T0; 
      )     ; 
      SPBN  A15a; 

      L     17; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

A15a: U     "I_WZ_SPA_gespannt"; 
      UN    "I_WZ_SPA_geloest"; 
      SPBN  MEND; 
//WZ-Bruch Anforderung 2.Messung
      SET   ; 
      S     "M_WZB_Anforderung"; 
      S     "M_WZB_Messung"; // 2te Messung
      R     "M_WZB_Ready"; 

      L     16; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 16 --WZ-Ablegen-----------------------------------------------------
//Werkzeugbruchüberwachung
SK16: U     "M_WZB_Ready"; 
      U     "I_WZB_Parkstellung"; 
      UN    "M_WZB_Anforderung"; 
      SPBN  MEND; 
      R     "M_WZB_Ready"; 
      R     "MX_WZB_NeuWahl"; 

      L     17; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 17 --WZ-Ablegen-----------------------------------------------------
//Wenn: WZW in Grundstellung
//Dann: Greifer Richtung Magazin
SK17: U     "M_MAG_In_Position"; 
      O     "DB_REMANENT".WZ_Im_Greifer; 
      U(    ; 
      U(    ; 
      L     "DB_NC_PLC".Magazin_Platz_Unload; 
      L     "MW_MAG_MP_Ist"; 
      ==I   ; 
      )     ; 
      O     "M_Req_Ablegen_WZ"; 
      )     ; 
      UN    "M_MAG_Laeuft"; 
      U     "M_WZW_Greifer_OK"; 
      U(    ; 
      ON    #TX_WERKZEUGBRUCH_AKTIV; 
      O     "I_WZB_Parkstellung"; 
      )     ; 
      SPBN  MEND; 


// Überwachungen fürs ablegen
      U     "I_WW_Uebergabeplatz"; 
      U     "M_MAG_In_Position"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 

      UN    "I_WW_Rueck_hlt_GST"; //Rückhaltestift oben
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async1._701612_WZWV_Grundst_0S; 

      U     "I_WW_Uebergabeplatz"; 
      U     "M_MAG_In_Position"; 
      ON    "Te_WZW_Hub_Verz"; 
      O(    ; 
      U     "M_WZW_Gre1_Pos_Mag"; // Greifer 1 steht Richtung Magazin
      U     "M_WZGr2_belegt"; // Greifer 2 belegt
      )     ; 
      O(    ; 
      U     "M_WZW_Gre1_Pos_Spi"; // Greifer 1 steht Richtung Spindel
      U     "M_WZGr1_belegt"; // Greifer 1  belegt
      )     ; 
      SPB   MEND; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   M17a; 

      L     18; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

M17a: S     "M_AUTO_MAG_TOOL_UNLOCK"; 
      R     "M_AUTO_MAG_TOOL_LOCK"; 
      S     "M_req_Greifer_ausfahren"; 
      R     "M_req_Greifer_einfahren"; 

      L     18; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 18 ---------------------------------------------------------------
//Wenn: Greifer Richtung Magazin
//Dann: Hub ins Magazin

SK18: U     "M_WZW_Gre1_Pos_Mag"; 
      X     "M_WZW_Gre1_Pos_Spi"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      O     ; 
      U     "DB_Achs11_VMAG_Greifer".MX_Greifer_ausgefahren; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  MEND; 

      R     "M_req_Greifer_ausfahren"; 

      O     "I_WW_Uebergabeplatz"; 
      O     "I_TM_TST_OCC"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      SPB   MEND; 

      S     "M_WZW_Hub_Mag_req"; 
      S     "M_WZW_Hub_Beweg"; 
      L     19; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 19 ---------------------------------------------------------------
//Wenn: Hub im Magazin
//Dann: Vertikale Kette Greifer einfahren vertikale Kette
//      Horizontale Kette Verriegelung ausfahren und weiter in Schritt 21

SK19: U     "M_WZW_Hub_Magazin"; 
      U(    ; 
      U     "I_TM_MAG_TOOL_UNLOCK"; 
      UN    "I_TM_MAG_TOOL_LOCK"; 
      ON    #t_WZW_VKETTE_OHNE_TPU; 
      )     ; 
      SPBN  MEND; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   M19a; 

      UN    "I_WW_Uebergabeplatz"; 
      U     "DB_PLC_MD_DB20".UDHex._08_BIT0_UGP_BELEGT; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700702_WZ_UGP_nicht_1S; 
      SPB   MEND; 

      S     "M_WZWV_Verrieg_req"; //Rückhaltestift nach unten
      S     "M_WZWV_Beweg"; 

      L     21; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

M19a: S     "M_req_Greifer_einfahren"; 
      R     "M_req_Greifer_ausfahren"; 

      L     20; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 20 ---------------------------------------------------------------
//Wenn: Greifer eingefahren
//Dann: Werkzeug im Magazin verriegeln

SK20: U     "DB_Achs11_VMAG_Greifer".MX_Greifer_eingefahren; 
      SPBN  MEND; 

      R     "M_req_Greifer_einfahren"; 
      S     "M_AUTO_MAG_TOOL_LOCK"; 
      R     "M_AUTO_MAG_TOOL_UNLOCK"; 

      L     21; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 21 ---------------------------------------------------------------
//Wenn: WZ-Verriegelung ausgefahren / verriegelt
//Dann: Hub in Grundstellung

SK21: U     "I_WW_Rueck_hlt_AUSGEF"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      O     ; 
      U     "M_TM_MAG_TOOL_LOCK"; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  MEND; 

      S     "M_WZW_Hub_GrdSt_req"; 

      U     "I_WW_POS_FRG_WZM"; 
      =     "M_FI_WW_POS_FRG_WZM"; 

      L     22; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 22 ---------------------------------------------------------------
//Wenn: Hub in Grundstellung
//Dann: WZ-Verriegelung einfahren und WZV umspeichern

SK22: U     "M_WZW_Hub_GrdSt"; 
      O     "I_WW_POS_FRG_WZM"; 
      SPBN  MEND; 

      S     "M_WZWV_Grundstell_req"; 
      R     "M_WZR_Mag_Verrieg"; // Magazinverriegelung ruecksetzen

      SET   ; 
      U     "M_WZW_Gre1_Pos_Spi"; 
      S     "M_Umsp_Gr2-Mag"; 

      U     "M_WZW_Gre1_Pos_Mag"; 
      S     "M_Umsp_Gr1-Mag"; 

      L     23; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 

// -- Schritt 23 ---------------------------------------------------------------
//Wenn: WZ-Verriegelung in Grundstellung
//Dann: Ablauf Ende

SK23: U(    ; 
      U     "I_WW_Rueck_hlt_GST"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      O     ; 
      U     "M_TM_MAG_TOOL_LOCK"; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      )     ; 
      U(    ; 
      U     "M_WZW_Gre1_Pos_Mag"; 
      X     "M_WZW_Gre1_Pos_Spi"; 
      )     ; 
      O     ; 
      SPBN  MEND; 
      S     "M_WZ_Rdy_Ablegen"; 
      R     "M_WZW_Ausw"; 
      L     24; 
      T     "B_SKZ_Ablauf"; 
      SPA   MEND; 


// -- Schritt 24 ---------------------------------------------------------------
//Wenn: WZ-Verriegelung in Grundstellung
//Dann: Ablauf Ende

SK24: UN    "M_Req_Ablegen_WZ"; 
      U(    ; 
      U     "I_WW_Rueck_hlt_GST"; 
      UN    #t_WZW_VKETTE_OHNE_TPU; 
      O     ; 
      U     "M_TM_MAG_TOOL_LOCK"; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      )     ; 
      SPBN  MEND; 
      R     "M_WZW_Aktiv"; 
      R     "DB_REMANENT".WZ_Im_Greifer; 
      S     "DB_NC_PLC".WZW_WZW_Beendet; 
      R     "DB_DM_M_FUNKTION".MX_M[283]; 
      R     "DB_DM_M_FUNKTION".MX_M[284]; 

      L     0; 
      T     "B_SKZ_Ablauf"; 

MEND: NOP   0; 

      SPA   nomr; 
NETWORK
TITLE =CALL "FB_WZV_ABL_REGAL"

omr:  O     "DB_DM_M_FUNKTION".MX_M[283]; 
      O     "DB_DM_M_FUNKTION".MX_M[284]; 
      =     #tx_req_ablegen_ZwSp; 
      O     "DB_OEM".WZW_Reset; 
      O     "M_Reset_WZW"; 
      =     #RegalRset; //slh,5.12.2006

      CALL "FB_WZV_ABL_REGAL" , "DI_WZV_ABL_REGAL" (
           E_Reset_Taste            := #RegalRset,//"DB_OEM".WZW_Reset; slh,5.12.2006
           E_Nr_Bereitst_Platz      := "MW_Nr_Bereitst",
           E_M6_aktiv               := "M_M6_aktiv",
           E_Sperre_T_Befehl        := "M_Sperre_TBef_FB193",
           E_Req_Ablegen_ZwSp       := #tx_req_ablegen_ZwSp,//"DB_DM_M_FUNKTION".MX_M[288]
           A_Quit_Bereitstellen     := "M_WZV_Quit_Bereitst",
           A_Anf_load_M6            := "DB_NC_PLC".WZW_WZ_Einwechseln,
           A_Anf_unload_M6          := "DB_NC_PLC".WZW_WZ_Auswechseln,
           A_Anf_change_M6          := "DB_NC_PLC".WZW_WZ_Vollwechsel,
           A_Anf_unload_load_M6     := "DB_NC_PLC".WZW_WZ_AUS_EINWechseln,
           A_Anf_HWZ_load_M6        := "DB_NC_PLC".WZW_WZ_Hand_Ein,
           A_ANF_HWZ_unload_M6      := "DB_NC_PLC".WZW_WZ_Hand_Aus,
           EA_Mag_load_M6           := "DB_NC_PLC".Magazin_Load,
           EA_Loc_load_M6           := "DB_NC_PLC".Magazin_Platz_Load,
           EA_Mag_unload_M6         := "DB_NC_PLC".Magazin_Unload,
           EA_Loc_unload_M6         := "DB_NC_PLC".Magazin_Platz_Unload);

NETWORK
TITLE =-- Leer --

nomr: NOP   0; 

NETWORK
TITLE =Greiferverriegelung Greifer 2 pneumatisch (für Winkelfräskopf)

      U     "DB_PLC_MD_DB20".UDHex._05_BIT5_Gr2_Entriegel; 
      SPBN  n14e; 

      U     "M_WZM_Regal_angewaehlt"; 
      U     "DB_OEM".OEM_Winkelkopfrettaktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      SPBN  n14d; 

//X-Achse auf WFK-Wechselposition
      U     "DB_EINRICHTFUNKTION".Bewegung_68_Plus; 
      U     "M_Leistung_Steht_an"; 
      U     "I_WW_1_Klappe_auf"; 
      U(    ; 
      UN    "DB_SIEM_NCK".E_SWCamPlus[27]; 
      UN    "DB_SIEM_NCK".E_SWCamMinus[27]; 
      O(    ; 
      UN    "DB_SIEM_NCK".E_SWCamPlus[28]; 
      UN    "DB_SIEM_NCK".E_SWCamMinus[28]; 
      )     ; 
      )     ; 
      U     "M_Sp_inM19PosWFK"; 
      FP    "HM_PI_X_AX_auf_WFKpos"; 
      SPBN  nx1; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     5; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n14d; 

//X-Achse in Arbeitsraum
nx1:  U     "DB_EINRICHTFUNKTION".Bewegung_68_Minus; 
      U(    ; 
      O     "I_WZ_SPA_gespannt"; 
      O     "I_WZ-Spanner_Gesp_mit_WZ"; 
      O     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      )     ; 
      FP    "HM_PI_Bew_68_Minus"; 
      SPBN  ny1; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     2; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n14d; 


//Vertikalachse auf WFK-Auswechselposition
ny1:  U     "DB_EINRICHTFUNKTION".Bewegung_69_Plus; 
      FP    "HM_PI_Bew_69_Plus"; 
      SPBN  ny2; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     7; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n14d; 

//Vertikalachse auf WFK-Einwechselposition
ny2:  U     "DB_EINRICHTFUNKTION".Bewegung_69_Minus; 
      SPBN  ns1; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     6; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n14d; 


//Spindel auf Wechselposition
ns1:  U     "DB_EINRICHTFUNKTION".Bewegung_70_Plus; 
      U     "M_Leistung_Steht_an"; 
      U     "I_WZ_SPA_gespannt"; 
      FP    "HM_PI_Bew_70_Plus"; 
      SPBN  ng1; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     8; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n14d; 


//Greifer 2 entriegeln
ng1:  U     "DB_EINRICHTFUNKTION".Bewegung_71_Plus; 
      U     "DB_PLC_MD_DB20".UDHex._05_BIT5_Gr2_Entriegel; 
      S     "M_Grf2_verr_req"; 

      U     "DB_EINRICHTFUNKTION".Bewegung_71_Minus; 
      U     "DB_PLC_MD_DB20".UDHex._05_BIT5_Gr2_Entriegel; 
      S     "M_Grf2_entr_req"; 



n14d: U     "M_Grf2_entr_req"; 
      O     "DB_DM_M_FUNKTION".MX_M[270]; 
      O     "m_wwgf_Gr2entr"; 
      S     "O_WW_GRF_ENTRIEGELN"; 

      U     "M_Grf2_verr_req"; 
      O     "DB_DM_M_FUNKTION".MX_M[275]; 
      R     "O_WW_GRF_ENTRIEGELN"; 

      U     "DB_NC_PLC".WZW_Grf2_entriegelt; 
      R     "DB_DM_M_FUNKTION".MX_M[270]; 
      R     "M_Grf2_entr_req"; 

      U     "DB_NC_PLC".WZW_Grf2_verriegelt; 
      R     "DB_DM_M_FUNKTION".MX_M[275]; 
      R     "M_Grf2_verr_req"; 

n14e: NOP   0; 
NETWORK
TITLE =Werkzeugverriegelung 
//Rückhaltestift auf/ab
      O     "M_WZM_Regal_angewaehlt"; //OMRON   --> keine Verriegelung
      O     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   NW16; 

      U     "M_WZWV_Verrieg_req"; 
      O(    ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_57_Minus; 
//      U     "M_pos_Trigg_SKR_Taste"
      UN    DB101.DBX    6.0; 
      )     ; 
      S     "O_WW_Rueck_MAG"; 

      U     "M_WZWV_Grundstell_req"; 
      O(    ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_57_Plus; 
//      U     "M_pos_Trigg_SKR_Taste"
      UN    DB101.DBX    6.0; 
      )     ; 
      O     "m_wwgf_WzwvGf"; 
      R     "O_WW_Rueck_MAG"; 


      U     "M_WZWV_Grundstell_req"; 
      U     "I_WW_Rueck_hlt_GST"; 
      R     "M_WZWV_Grundstell_req"; 


      U     "M_WZWV_Verrieg_req"; 
      U     "I_WW_Rueck_hlt_AUSGEF"; 
      R     "M_WZWV_Verrieg_req"; 
NETWORK
TITLE = Anforderung Automatisches Spanner loesen
//WZ-Spanner
NW16: O     "DB_DM_M_FUNKTION".MX_M[278]; // M278
      O     "m_wwgf_SpiZNcLoe"; 
      O     "m_vwgf_SpiLoe"; 
      SPBN  n14a; 
      S     "M_Auto_WZSP_Auf"; 
      U     "M_WZ_Spanner_Auf"; 
      R     "DB_DM_M_FUNKTION".MX_M[278]; // M-Funktion ruecksetzen

n14a: O     "DB_DM_M_FUNKTION".MX_M[279]; // M279
      O     "m_wwgf_SpiZNcSpa"; 
      O     "m_wwgf_SpiSpa"; 
      SPBN  REIN; 
      R     "M_Auto_WZSP_Auf"; 
      U     "M_WZ_Spanner_Zu"; // 
      O     "M_WZ_Spanner_Zu_Ohne_WZ"; 
      R     "DB_DM_M_FUNKTION".MX_M[279]; // M-Funktion ruecksetzen

NETWORK
TITLE =Ausgang Werkzeugreinigung Stellung reinigen
//Werkzeugreinigung auf/ab
REIN: U(    ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_56_Plus; 
      U     "DB_OEM".OEM_WZW3_Rettbildaktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      U     "M_WZW_Hub_GrdSt"; 
      U     "M_WZW_Greifer_OK"; 
      O     "m_wwgf_ReinGf"; 
      )     ; 
      U     "M_Leistung_Steht_an"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     0; 
      <>I   ; 
      )     ; 
      R     "O_WW_Hub_ZYL_in_Rein"; 
      R     "O_WW_Buersten_Reinigung"; 
      R     "O_WZK_Reinig_Blasluft"; 


      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     0; 
      <>I   ; 
      U     "M_Leistung_Steht_an"; 
      U     "M_WZW_Hub_GrdSt"; 
      U     "M_WZW_Greifer_OK"; 
      U     "M_WZW_Hub_GrdSt"; 
      U(    ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_56_Minus; 
      U     "DB_OEM".OEM_WZW3_Rettbildaktiv; 
      )     ; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      S     "O_WW_Hub_ZYL_in_Rein"; 
      S     "O_WW_Buersten_Reinigung"; 
      S     "O_WZK_Reinig_Blasluft"; 

NETWORK
TITLE =Freigabe für Handbetrieb

      U     "M_Leistung_Steht_an"; 
      U     "I_WM_Energiefreigabe"; 
      U     "M_Handbetrieb"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      =     #TX_FREIGABE_HANDBETRIEB; 
NETWORK
TITLE =Freigabe Hand/Auto WZW

      U     "M_Handbetrieb"; //"DB_SIEM_BAG".E_JOG
      UN    "M_MAG_Laeuft"; 
      U     "DB_ACHSE7".E_Stat; 
      =     #t_Frg_Hand_WZW; 

      UN    "M_Handbetrieb"; 
      UN    "M_Einrichtbetrieb"; 
      UN    "M_3Te_Betriebsart"; 
      UN    "M_4Te_Betriebsart"; 
      UN    "M_MAG_Laeuft"; 
      U     "DB_ACHSE7".E_Stat; 
      =     #t_Frg_Auto_WZW; 
NETWORK
TITLE =Freigabe Magazinklappe

      U     "M_Handbetrieb"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "DB_DM_M_FUNKTION".MX_M[272]; 
      =     #t_Frg_Hand_Klappe; 

      UN    #t_Frg_Hand_Klappe; 
      UN    "M_Einrichtbetrieb"; 
      UN    "M_3Te_Betriebsart"; 
      UN    "M_4Te_Betriebsart"; 
      =     #t_Frg_Auto_Klappe; 

NETWORK
TITLE =Werkzeug an Magazinposition entriegeln und verriegeln
//Freigaben Hand
      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPBN  maen; 


      U     #TX_FREIGABE_HANDBETRIEB; 
      UN    "M_MAG_Laeuft"; 
      =     #t_Frg_Hand_Tool_Unl_MAG; 

      U     #TX_FREIGABE_HANDBETRIEB; 
      =     #t_Frg_Hand_Tool_Lock_MAG; 

//Ausführbefehle Hand
      U     "DB_EINRICHTFUNKTION".Bewegung_94_Minus; 
      =     #t_hand_mag_tool_unlock; 

      U     "DB_EINRICHTFUNKTION".Bewegung_94_Plus; 
      =     #t_hand_mag_tool_lock; 

//Anforderung Automatik
      O     "M_AUTO_MAG_TOOL_UNLOCK"; 
      O     "m_vwgf_MagTUnlock"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutAst; 

      O     "M_AUTO_MAG_TOOL_LOCK"; 
      O     "m_vwgf_MagTLock"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutGst; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WZ_MAG" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_null",
           Endlage_AST              := "I_TM_MAG_TOOL_UNLOCK",
           Endlage_GST              := "I_TM_MAG_TOOL_LOCK",
           Automatik                := #t_Frg_Auto_WZW,
           Hand                     := #t_Frg_Hand_WZW,
           Freig_Ventil_AST_Auto    := #ReqAutAst,
           Freig_Ventil_GST_Auto    := #ReqAutGst,
           Taste_AST_Hand           := #t_hand_mag_tool_unlock,
           Taste_GST_Hand           := #t_hand_mag_tool_lock,
           Freigabe_AST_Hand        := #t_Frg_Hand_Tool_Unl_MAG,
           Freigabe_GST_Hand        := #t_Frg_Hand_Tool_Lock_MAG,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Bewegungsnummer          := 94,
           Ventil_AST               := "O_TM_MAG_TOOL_UNLOCK",
           Ventil_GST               := "O_TM_MAG_TOOL_LOCK",
           Stoer_Laufz_AST          := #TX_STOER_LAUFZ_AST1,
           Stoer_Laufz_GST          := #TX_STOER_LAUFZ_GST1,
           Stoer_Paarueb            := #TX_STOER_PAARUEB1);

      U     "m_eins"; 
      R     #TX_STOER_LAUFZ_AST2; 
      R     #TX_STOER_LAUFZ_GST2; 
      R     #TX_STOER_PAARUEB2; 

NETWORK
TITLE =Zusätzliche WZ-Verriegelung am Magazinplatz für SK50/HSK100

      CLR   ; 
      =     #TX_STOER_LAUFZ_AST2; 
      =     #TX_STOER_LAUFZ_GST2; 
      =     #TX_STOER_PAARUEB2; 

      UN    "DB_PLC_MD_DB20".UDHex._20_Bit5_VM_MAG_SK50; 
      SPB   K_SK; 

      U     "I_TM_TOOL_SPIPOS_UNLKD"; 
      U     "M_Handbetrieb"; 
      L     S5T#1S; 
      SE    "TE_Mag_Verr2_Verz"; 
      U     "TE_Mag_Verr2_Verz"; 
      =     #t_hand_mag_tool_not_lock; 

//Anforderung Automatik
      U     "M_AUTO_MAG_TOOL_RIEG_LOC"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutAst; 

      U     "M_AUTO_MAG_TOOL_RIEG_UNL"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutGst; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WZ_MAG_RI" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_null",
           Endlage_AST              := "I_TM_TOOL_SPIPOS_UNLKD",
           Endlage_GST              := "I_TM_TOOL_SPIPOS_LOCKED",
           Automatik                := #t_Frg_Auto_WZW,
           Hand                     := #t_Frg_Hand_WZW,
           Freig_Ventil_AST_Auto    := #ReqAutAst,
           Freig_Ventil_GST_Auto    := #ReqAutGst,
           Taste_AST_Hand           := "DB_EINRICHTFUNKTION".Bewegung_94_Plus,//#t_hand_mag_tool_lock
           Taste_GST_Hand           := #t_hand_mag_tool_not_lock,
           Freigabe_AST_Hand        := #t_Frg_Hand_Tool_Unl_MAG,
           Freigabe_GST_Hand        := #t_Frg_Hand_Tool_Unl_MAG,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Ventil_AST               := "O_TM_TOOL_SPIPOS_LOCK",
           Stoer_Laufz_AST          := #TX_STOER_LAUFZ_AST2,
           Stoer_Laufz_GST          := #TX_STOER_LAUFZ_GST2,
           Stoer_Paarueb            := #TX_STOER_PAARUEB2);

K_SK: NOP   0; 
NETWORK
TITLE =WZ-Verriegelung am Magazinplatz Fehler 

      U     "m_eins"; 
      =     L      9.0; 
      U     L      9.0; 
      U(    ; 
      O     #TX_STOER_LAUFZ_AST1; 
      O     #TX_STOER_LAUFZ_GST2; 
      )     ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702248_Entr_Mag_fehlt; 
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702248_Entr_Mag_fehlt; 
      NOP   0; 
      U     L      9.0; 
      U(    ; 
      O     #TX_STOER_LAUFZ_GST1; 
      O     #TX_STOER_LAUFZ_AST2; 
      )     ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702249_Verr_Mag_fehlt; 
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702249_Verr_Mag_fehlt; 
      NOP   0; 
      U     L      9.0; 
      U(    ; 
      O     #TX_STOER_PAARUEB1; 
      O     #TX_STOER_PAARUEB2; 
      )     ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702250_Verr_Mag_undef; 
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702250_Verr_Mag_undef; 
      NOP   0; 
NETWORK
TITLE =Werkzeug am Rüstplatz entriegeln und verriegeln
//Ausführbefehle Hand
      U     "M_Leistung_Steht_an"; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "M_MAG_Laeuft"; 
      =     #t_Frg_Hand_Tool_Unl_RPL; 

      U     "M_Leistung_Steht_an"; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "M_MAG_Laeuft"; 
      =     #t_Frg_Hand_Tool_Lock_RPL; 


      U     "DB_EINRICHTFUNKTION".Bewegung_96_Minus; 
      =     #t_hand_tload_tool_unlock; 

      U     "DB_EINRICHTFUNKTION".Bewegung_96_Plus; 
      =     #t_hand_tload_tool_lock; 

      U     "I_WM_ZUSTIMMTASTE"; 
      FN    "M_FP_Zustimmtaste"; 
      =     "M_IM_Zustimmtaste"; 

      U(    ; 
      U     "M_IM_Zustimmtaste"; 
      UN    "I_WM_Beladung_zu"; 
      UN    "M_MAG_Laeuft"; 
      UN    "M_AUTO_TLOAD_TOOL_LOCK"; 
      O     ; 
      U     "I_WM_ZUSTIMMTASTE"; 
      U(    ; 
      O     "I_WM_Taste_links"; 
      O     "I_WM_Taste_rechts"; 
      )     ; 
      )     ; 
      UN    "M_Taste_TLOAD_TOOL_LOCK"; 
      O     "I_WM_Beladung_zu"; 
      S     "M_Taste_TLOAD_TOOL_LOCK"; 

      U     "M_IM_Zustimmtaste"; 
      UN    "M_Sperre_entr_Ladest"; 
      UN    "I_WM_Beladung_zu"; 
      UN    "M_MAG_Laeuft"; 
      U     "M_AUTO_TLOAD_TOOL_LOCK"; 
      R     "M_Taste_TLOAD_TOOL_LOCK"; 

      U     "I_WM_ZUSTIMMTASTE"; 
      U(    ; 
      O     "I_WM_Taste_links"; 
      O     "I_WM_Taste_rechts"; 
      )     ; 
      UN    "I_WM_Beladung_zu"; 
      S     "M_Sperre_entr_Ladest"; 

      UN    "I_WM_ZUSTIMMTASTE"; 
      R     "M_Sperre_entr_Ladest"; 

      U     "M_Taste_TLOAD_TOOL_LOCK"; 
      U     "M_MA_Maschine_Ein"; 
      =     "M_AUTO_TLOAD_TOOL_LOCK"; 


      UN    "M_AUTO_TLOAD_TOOL_LOCK"; 
      U     "M_MA_Maschine_Ein"; 
      =     "M_AUTO_TLOAD_TOOL_UNLOCK"; 


      U     "DB_OEM".OEM_WZW6_Rettbildaktiv; 
      U     "M_Handbetrieb"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      =     #t_Frg_TLoad_Lock_Hand; 
      NOT   ; 
      =     #t_Frg_TLoad_Lock_Auto; 

      U     "DB_PLC_MD_DB20".UDHex._20_Bit5_VM_MAG_SK50; 
      SPB   SK50; 

//Anforderung Automatik
      U     "M_AUTO_TLOAD_TOOL_UNLOCK"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutAst; 

      U     "M_AUTO_TLOAD_TOOL_LOCK"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutGst; 


      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WZ_RPL" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_null",
           Endlage_AST              := "I_TM_TLOAD_TOOL_UNLOCK",
           Endlage_GST              := "I_TM_TLOAD_TOOL_LOCK",
           Automatik                := #t_Frg_TLoad_Lock_Auto,
           Hand                     := #t_Frg_TLoad_Lock_Hand,
           Freig_Ventil_AST_Auto    := #ReqAutAst,
           Freig_Ventil_GST_Auto    := #ReqAutGst,
           Taste_AST_Hand           := #t_hand_tload_tool_unlock,
           Taste_GST_Hand           := #t_hand_tload_tool_lock,
           Freigabe_AST_Hand        := #t_Frg_Hand_Tool_Unl_RPL,
           Freigabe_GST_Hand        := #t_Frg_Hand_Tool_Lock_RPL,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Bewegungsnummer          := 96,
           Ventil_AST               := "O_TM_TLOAD_TOOL_UNLOCK",
           Ventil_GST               := "O_TM_TLOAD_TOOL_LOCK",
           Stoer_Laufz_AST          := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702251_Entr_Ruest_fehlt,
           Stoer_Laufz_GST          := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702252_Verr_Ruest_fehlt,
           Stoer_Paarueb            := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702263_Verr_Ruest_undef);

      SPA   maen; 

//Ansteuerung Verriegelung für SK50/HSK100
SK50: U     "M_AUTO_TLOAD_TOOL_LOCK"; 
      FP    "M_FP_Auto_TLoad_Tool_Loc"; 
      R     "M_AUTO_TLOAD_CYL_LOCK"; 
      S     "M_AUTO_TLOAD_CYL_UNLOCK"; 

      U     "I_TM_TOOL_CYL_LOCKED"; 
      L     S5T#1S; 
      SE    "Te_Ruest_Verr2_Verz"; 
      U     "Te_Ruest_Verr2_Verz"; 
      R     "M_AUTO_TLOAD_CYL_UNLOCK"; 
      S     "M_AUTO_TLOAD_CYL_LOCK"; 
      =     #t_hand_tload_tool_not_lo; 

//Anforderung Automatik
      U     "M_AUTO_TLOAD_TOOL_UNLOCK"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutAst; 

      U     "M_AUTO_TLOAD_TOOL_LOCK"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutGst; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WZ_RPL" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_null",
           Endlage_AST              := "I_TM_TLOAD_TOOL_UNLOCK",
           Endlage_GST              := "I_TM_TLOAD_TOOL_LOCK",
           Automatik                := #t_Frg_TLoad_Lock_Auto,
           Hand                     := #t_Frg_TLoad_Lock_Hand,
           Freig_Ventil_AST_Auto    := #ReqAutAst,
           Freig_Ventil_GST_Auto    := #ReqAutGst,
           Taste_AST_Hand           := #t_hand_tload_tool_unlock,
           Taste_GST_Hand           := #t_hand_tload_tool_lock,
           Freigabe_AST_Hand        := #t_Frg_Hand_Tool_Unl_RPL,
           Freigabe_GST_Hand        := #t_Frg_Hand_Tool_Lock_RPL,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Bewegungsnummer          := 96,
           Ventil_AST               := "O_TM_TLOAD_TOOL_UNLOCK",
           Ventil_GST               := "O_TM_TLOAD_TOOL_LOCK",
           Stoer_Laufz_AST          := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702251_Entr_Ruest_fehlt,
           Stoer_Laufz_GST          := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702252_Verr_Ruest_fehlt,
           Stoer_Paarueb            := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702263_Verr_Ruest_undef);

//Anforderung Automatik
      U     "M_AUTO_TLOAD_CYL_UNLOCK"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutAst; 

      U     "M_AUTO_TLOAD_CYL_LOCK"; 
      UN    "M_Schrittkette_Stop"; 
      =     #ReqAutGst; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WZ_RIEGEL" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_null",
           Endlage_AST              := "I_TM_TOOL_CYL_UNLOCKED",
           Endlage_GST              := "I_TM_TOOL_CYL_LOCKED",
           Automatik                := #t_Frg_TLoad_Lock_Auto,
           Hand                     := #t_Frg_TLoad_Lock_Hand,
           Freig_Ventil_AST_Auto    := #ReqAutAst,
           Freig_Ventil_GST_Auto    := #ReqAutGst,
           Taste_AST_Hand           := #t_hand_tload_tool_lock,
           Taste_GST_Hand           := #t_hand_tload_tool_not_lo,
           Freigabe_AST_Hand        := #t_Frg_Hand_Tool_Unl_RPL,
           Freigabe_GST_Hand        := #t_Frg_Hand_Tool_Unl_RPL,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Ventil_AST               := "O_TM_TOOL_CYL_LOCK",
           Stoer_Laufz_AST          := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702254_Ruest_Verr2_fehl,
           Stoer_Laufz_GST          := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702255_Ruest_Entr2_fehl,
           Stoer_Paarueb            := "DB_FEHLERMELDUNG".VS_Halt_Async1_2._702262_Ruest_Verr2_unde);


maen: NOP   0; 

NETWORK
TITLE =Request HUB fahren
//WZW Hub
//
// 
//
//Hub ins Magazin
      U(    ; 
      O     "DB_OEM".OEM_WZW_REttbildaktiv; 
      O     "DB_EINRICHTFUNKTION".Bewegung_64_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_64_Minus; 
      )     ; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      O     "m_wwgf_WzHubGf"; 
      SPB   n17e; 

      U     "DB_DM_M_FUNKTION".MX_M[273]; // Hub ins Magazin fahren
      O(    ; 
      U     "M_WZW_Hub_Mag_req"; 
      U(    ; 
      U     "M_WZW_Ausw"; 
      UN    "I_WW_Uebergabeplatz"; 
      ON    "M_WZW_Ausw"; 
      )     ; 
      )     ; 
      UN    "M_MAG_Laeuft"; 
      U(    ; 
      O     "M_WZW_Greifer_OK"; // und die Greifer stehen richtig
      O     "DB_Achs11_VMAG_Greifer".MX_Greifer_ausgefahren; 
      )     ; 
      UN    "M_WZW_Hub_Magazin"; 
      U(    ; 
      UN    "M_WZM_Regal_angewaehlt"; 
      O     "DB_SCHNITTST_OMRON".IW_WMR_CNC_0.Greifer_in_Uebergabeber; 
      )     ; 
      U(    ; 
      ON    #TX_WERKZEUGBRUCH_AKTIV; 
      O     "I_WZB_Parkstellung"; // Bruch in Parkstellung
      )     ; 
      U     "M_Leistung_Steht_an"; 
      UN    "I_TM_TST_OCC"; 
      S     "O_TC_MAGAZINPOS"; 
      R     "O_TC_SPIPOS"; 


      U     "DB_DM_M_FUNKTION".MX_M[273]; 
      O     "M_WZW_Hub_Mag_req"; 
      U     "M_WZW_Hub_Magazin"; 
      R     "M_WZW_Hub_Mag_req"; 
      R     "DB_DM_M_FUNKTION".MX_M[273]; // M-Funktion rueckmelden


//Hub in Grundstellung
      U     "DB_DM_M_FUNKTION".MX_M[274]; 
      O(    ; 
      U     "M_WZW_Hub_GrdSt_req"; 
      )     ; 
      UN    "M_WZW_Hub_GrdSt"; 
      U     "M_Leistung_Steht_an"; 
      U(    ; 
      ON    #TX_WERKZEUGBRUCH_AKTIV; 
      O     "I_WZB_Parkstellung"; // Bruch in Parkstellung
      )     ; 
      S     "O_TC_SPIPOS"; 
      R     "O_TC_MAGAZINPOS"; 

      U     "DB_DM_M_FUNKTION".MX_M[274]; 
      O     "M_WZW_Hub_GrdSt_req"; 
      U     "M_WZW_Hub_GrdSt"; // Hub im Magazin
      R     "DB_DM_M_FUNKTION".MX_M[274]; // M-Funktion ruecksetzen
      R     "M_WZW_Hub_GrdSt_req"; 



      SPA   N18; 

n17e: U     "DB_EINRICHTFUNKTION".Bewegung_2_Minus; //SKR Hub ins Magazin
      U     "M_Frg_Hub_MagPos"; 
      S     "O_TC_MAGAZINPOS"; 


      UN    "DB_EINRICHTFUNKTION".Bewegung_2_Minus; //SKR Hub ins Magazin
      U     "M_Leistung_Steht_an"; 
      UN    "M_WZW_Hub_Magazin"; 
      O(    ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_2_Plus; 
      U     "M_Leistung_Steht_an"; 
      )     ; 
      O     "m_wwgf_WzHubGf"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_64_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_64_Minus; 
      R     "O_TC_MAGAZINPOS"; 


      U(    ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_2_Plus; 
      O     "m_wwgf_WzHubGf"; 
      )     ; 
      U     "M_Frg_Hub_SpiPos"; 
      S     "O_TC_SPIPOS"; 

      UN    "DB_EINRICHTFUNKTION".Bewegung_2_Plus; 
      UN    "m_rqwwgf"; 
      U     "M_Leistung_Steht_an"; 
      UN    "M_WZW_Hub_GrdSt"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_2_Minus; //SKR Hub ins Magazin
      O     "DB_EINRICHTFUNKTION".Bewegung_64_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_64_Minus; 
      R     "O_TC_SPIPOS"; 

//Fehlerausgabe bei falscher Greiferposition
      U     "DB_EINRICHTFUNKTION".Bewegung_2_Minus; //SKR Hub ins Magazin
      U     "M_pos_Trigg_SKR_Taste"; 
      UN(   ; 
      U     "M_WZW_Gre1_Pos_Mag"; 
      X     "M_WZW_Gre1_Pos_Spi"; 
      )     ; 
      U     "M_WZW_Hub_Mag_req"; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async1._701621_Grf1_2_ni_Mag; 
NETWORK
TITLE =Türe horzontale Kette

N18:  U     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   VK02; 

      U     "DB_OEM".OEM_WZW_REttbildaktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      SPB   n18e; 

      U(    ; 
      U     "MX_H5020"; //"DB_DM_M_FUNKTION".MX_M[271]
      UN    "M_Einrichtbetrieb"; 
      UN    "M_3Te_Betriebsart"; 
      UN    "M_4Te_Betriebsart"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_4_Minus; // oder SKR Tuer Auf
      )     ; 
      U     "M_Leistung_Steht_an"; 
      UN    "M_WZW_Gre1_Spi_req"; 
      UN    "M_WZW_Gre1_Mag_req"; 
      R     "O_WW_1_Klappe_zu"; 
      S     "O_WW_1_Klappe_auf"; 

      U     "I_WW_1_Klappe_auf"; 
      UN    "I_WW_1_Klappe_zu"; // Tuere auf
      U     "MX_H5020"; //"DB_DM_M_FUNKTION".MX_M[271]
      R     "MX_H5020"; //"DB_DM_M_FUNKTION".MX_M[271]    // M-Funktion rueckmelden

      U(    ; 
      U     "MX_H5021"; //"DB_DM_M_FUNKTION".MX_M[272]    // Tuere Zu
      UN    "M_Einrichtbetrieb"; 
      UN    "M_3Te_Betriebsart"; 
      UN    "M_4Te_Betriebsart"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_4_Plus; // oder SKR Tuer Zu
      O     "m_wwgf_Tzu"; 
      )     ; 
      U     "M_Leistung_Steht_an"; 
      U(    ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      ON    "DB_DM_M_FUNKTION".MX_M[294]; 
      )     ; 
      R     "O_WW_1_Klappe_auf"; 
      S     "O_WW_1_Klappe_zu"; 

      U     "I_WW_1_Klappe_zu"; 
      UN    "I_WW_1_Klappe_auf"; // Tuere auf
      U     "MX_H5021"; //"DB_DM_M_FUNKTION".MX_M[272]
      R     "MX_H5021"; //"DB_DM_M_FUNKTION".MX_M[272]    // M-Funktion rueckmelden

      SPA   N19; 
// Rettung
n18e: U     "DB_EINRICHTFUNKTION".Bewegung_4_Minus; 
      U     "M_Leistung_Steht_an"; 
      S     "O_WW_1_Klappe_auf"; 

      UN    "DB_EINRICHTFUNKTION".Bewegung_4_Minus; 
      U     "M_Leistung_Steht_an"; 
      UN    "I_WW_1_Klappe_auf"; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_4_Plus; 
      U     "M_WW_X_imArbeitsraum"; 
      U     "M_Leistung_Steht_an"; 
      R     "O_WW_1_Klappe_auf"; 

      U     "DB_EINRICHTFUNKTION".Bewegung_4_Plus; 
      U     "M_WW_X_imArbeitsraum"; 
      U     "M_Leistung_Steht_an"; 
      S     "O_WW_1_Klappe_zu"; 

      UN    "DB_EINRICHTFUNKTION".Bewegung_4_Plus; 
      U     "M_Leistung_Steht_an"; 
      UN    "I_WW_1_Klappe_zu"; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_4_Minus; 
      U     "M_Leistung_Steht_an"; 
      R     "O_WW_1_Klappe_zu"; 

//--sth 21.03.05 -------------------------------------
N19:  U     "I_WW_1_Klappe_zu"; 
      U     "O_WW_1_Klappe_zu"; 
      R     "O_WW_1_Klappe_zu"; 
//----------------------------------------------------
      SPA   EKLZ; 
NETWORK
TITLE =Werkzeugwechslerklappe öffnen und schliessen
//Freigaben Hand
VK02: U     #TX_FREIGABE_HANDBETRIEB; 
      =     #t_Frg_Hand_Klappe_auf; 

      U     #TX_FREIGABE_HANDBETRIEB; 
      U     "M_WW_X_imArbeitsraum"; 
      =     #t_Frg_Hand_Klappe_zu; 

      U     "DB_NC_PLC".NC_WZW_SPOS; 
      UN    "DB_NC_PLC".MANWZW_NC_Aktiv; 
      UN    "DB_OUT".AmxLockToolChanger; 
      =     #Anf_Auto_Klappe_auf; 

      U     #Anf_Auto_Klappe_auf; 
      O     "m_wwgf_XArbRaum"; 
      U(    ; 
      UN    "M_MAG_Manuell_Rechts"; 
      UN    "M_MAG_Manuell_Links"; 
      U     "I_WM_Energiefreigabe"; 
      )     ; 
      UN    "M_Schrittkette_Stop"; 
      =     #t_Auto_Klappe_auf; 

      U     #Anf_Auto_Klappe_auf; 
      UN    "I_WM_Energiefreigabe"; 
      =     "DB_FEHLERMELDUNG".WARNUNG3._700350; 

      U(    ; 
      O     "DB_DM_M_FUNKTION".MX_M[272]; 
      O     "m_wwgf_Tzu"; 
      )     ; 
      UN    #t_Auto_Klappe_auf; 
      U     "M_WW_X_imArbeitsraum"; 
      UN    "M_Schrittkette_Stop"; 
      =     #t_Auto_Klappe_zu; 

//Ausführbefehle Hand
      U     "DB_EINRICHTFUNKTION".Bewegung_4_Minus; 
      =     #t_hand_klappe_auf; 

      U     "DB_EINRICHTFUNKTION".Bewegung_4_Plus; 
      =     #t_hand_klappe_zu; 

      U     "I_WW_1_Klappe_auf"; 
      UN    "I_WW_1_Klappe_zu"; // Tuere auf
      R     "MX_H5020"; //"DB_DM_M_FUNKTION".MX_M[271]    // M-Funktion rueckmelden
      R     "DB_DM_M_FUNKTION".MX_M[271]; 

      U     "I_WW_1_Klappe_zu"; 
      UN    "I_WW_1_Klappe_auf"; // Tuere auf
      R     "MX_H5021"; //"DB_DM_M_FUNKTION".MX_M[272]    // M-Funktion rueckmelden
      R     "DB_DM_M_FUNKTION".MX_M[272]; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WWKlappe" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_eins",
           Endlage_AST              := "I_WW_1_Klappe_auf",
           Endlage_GST              := "I_WW_1_Klappe_zu",
           Automatik                := #t_Frg_Auto_Klappe,
           Hand                     := #t_Frg_Hand_Klappe,
           Freig_Ventil_AST_Auto    := #t_Auto_Klappe_auf,//"MX_H5020"
           Freig_Ventil_GST_Auto    := #t_Auto_Klappe_zu,//"MX_H5021"
           Taste_AST_Hand           := #t_hand_klappe_auf,
           Taste_GST_Hand           := #t_hand_klappe_zu,
           Freigabe_AST_Hand        := #t_Frg_Hand_Klappe_auf,
           Freigabe_GST_Hand        := #t_Frg_Hand_Klappe_zu,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Bewegungsnummer          := 4,
           Ventil_AST               := "O_WW_1_Klappe_auf",
           Ventil_GST               := "Mx_Klappe_WZW_zu",
           Stoer_Laufz_AST          := "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700706_Tuere_Auf_0Sig,
           Stoer_Laufz_GST          := "DB_FEHLERMELDUNG".VS_Halt_Async1._701562_Tuere_zu_0Signal,
           Stoer_Paarueb            := "DB_FEHLERMELDUNG".VS_Halt_Async1._701558_Tuer_Beide_1Sig);

      U     "I_WW_1_Klappe_zu"; 
      R     "Mx_Klappe_WZW_zu"; 

      U     "M_CU_Aktiv"; 
      SPB   EKLZ; 

      U     "Mx_Klappe_WZW_zu"; 
      =     "O_WW_1_Klappe_zu"; 

EKLZ: NOP   0; 

NETWORK
TITLE =GREIFER
//WZW Greifer
//
//USER_DATA_INT[76]
//
//0: immer langsam
//1: schnell / langsam abh. Vom WZ-Typ
//9999: immer schnell schwenken
      CLR   ; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[276]; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[277]; 

      U     #t_WZW_VKETTE_OHNE_TPU; 
      SPB   E_GR; 

      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._076_GREIFER_SCHNELLLA; 
      L     0; // immer langsam
      ==I   ; 
      )     ; 
      O(    ; 
      U     "M_WZM_Regal_angewaehlt"; 
      U(    ; 
      L     "DI_WZV_ABL_REGAL".T_SpWz_Typ; // SpindelWerkzeugTyp grösser 1
      L     1; 
      >I    ; 
      UN    "DI_TM_WZTYP_RD".T_Sp_leer; 
      UN    "DI_WZV_ABL_REGAL".T_SpWz_schnell; 
      O(    ; 
      L     "DI_WZV_ABL_REGAL".T_NewTool_Typ; // Neues WZTyp grösser 1
      L     1; 
      >I    ; 
      UN    "DI_WZV_ABL_REGAL".T_NewTool_schnell; 
      )     ; 
      O     "DI_WZV_ABL_REGAL".T_SpWz_langsam; 
      O     "DI_WZV_ABL_REGAL".T_NewTool_langsam; 
      )     ; 
      )     ; 

      O(    ; 
      U(    ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_KETTE_GETAKTET; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_KETTE_GEREGELT; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_KETTE_MASTER_SLAVE; 
      )     ; 
      U(    ; 
      L     "DI_TM_T_BEFEHL".T_SpWz_Typ; // SpindelWerkzeugTyp grösser 1
      L     1; 
      >I    ; 
      UN    "DI_TM_T_BEFEHL".T_SpWz_schnell; 
      O(    ; 
      L     "DI_TM_T_BEFEHL".T_NewTool_Typ; // Neues WZTyp grösser 1
      L     1; 
      >I    ; 
      UN    "DI_TM_T_BEFEHL".T_NewTool_schnell; 
      )     ; 
      O     "DI_TM_T_BEFEHL".T_SpWz_langsam; 
      O     "DI_TM_T_BEFEHL".T_NewTool_langsam; 
      )     ; 
      )     ; 
      UN(   ; 
      L     "DB_PLC_MD_DB20".UDInt._076_GREIFER_SCHNELLLA; // immer schnell  
      L     9999; 
      ==I   ; 
      )     ; 
      O     "DB_OEM".OEM_WZW_REttbildaktiv; 
      O     "DB_OEM".OEM_WZW2_Rettbildaktiv; 
      =     "O_WW_GRF_Drehen_slow"; 
      NOT   ; 
      =     "O_WW_GRF_Drehen_fast"; 


//Greifer 1 zum Magazin
      U     "DB_EINRICHTFUNKTION".Bewegung_6_Plus; 
      U     "M_WZW_Hub_GrdSt"; 
      U     "M_Leistung_Steht_an"; 
      O(    ; 
      U     "M_WZW_Gre1_Spi_req"; 
      UN    "M_WZW_Gre1_Pos_Spi"; 
      )     ; 
      O     "DB_DM_M_FUNKTION".MX_M[276]; 
      O     "m_wwgf_Gr1Spi"; 
      U     "M_Leistung_Steht_an"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     0; 
      ==I   ; 
      O     "I_WW_Rein_in_GST"; 
      )     ; 
      U(    ; 
      U(    ; 
      U     "I_WZ_SPA_geloest"; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; // da es sein kann, das grf noch im vorbereiten gedreht werden muss
      )     ; 
      U     "Mx_X_ACHSE_WZW_POS"; 
      O     "M_WW_X_imArbeitsraum"; 
      )     ; 
      UN    "M_WZW_Gre1_Pos_Spi"; 
      =     "O_WW_1GRF_SPI"; 
      R     "O_WW_1GRF_MAG"; 

      U     "M_WZW_Gre1_Spi_req"; 
      O     "DB_DM_M_FUNKTION".MX_M[276]; 
      U     "M_WZW_Gre1_Pos_Spi"; 
      R     "M_WZW_Gre1_Spi_req"; 
      R     "DB_DM_M_FUNKTION".MX_M[276]; 


//Greifer 1 zur Spindel
      U     "DB_EINRICHTFUNKTION".Bewegung_6_Minus; 
      U     "M_WZW_Hub_GrdSt"; 
      U     "M_Leistung_Steht_an"; 
      O(    ; 
      U     "M_WZW_Gre1_Mag_req"; 
      UN    "M_WZW_Gre1_Pos_Mag"; 
      )     ; 
      O     "DB_DM_M_FUNKTION".MX_M[277]; 
      O     "m_wwgf_Gr1Mag"; 
      U     "M_Leistung_Steht_an"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     0; 
      ==I   ; 
      O     "I_WW_Rein_in_GST"; 
      )     ; 
      U(    ; 
      U     "Mx_X_ACHSE_WZW_POS"; 
      O     "M_WW_X_imArbeitsraum"; 
      )     ; 
      UN    "M_WZW_Gre1_Pos_Mag"; 
      =     "O_WW_1GRF_MAG"; 
      R     "O_WW_1GRF_SPI"; 


      U     "DB_DM_M_FUNKTION".MX_M[277]; // Greifer nach Stellung 2
      O     "M_WZW_Gre1_Mag_req"; 
      U     "M_WZW_Gre1_Pos_Mag"; 
      R     "M_WZW_Gre1_Mag_req"; 
      R     "DB_DM_M_FUNKTION".MX_M[277]; // M-Funktion rueckmelden

E_GR: NOP   0; 

NETWORK
TITLE =WZW-Achsen in Rettung über Asup starten

      U     "M_CU_Aktiv"; 
      SPBN  _DS; 

      U     "DB_EINRICHTFUNKTION".Bewegung_189_Plus; 
      =     #t_hand_greifer_180Grad; 

      U     "DB_EINRICHTFUNKTION".Bewegung_189_Minus; 
      =     #t_hand_greifer_0Grad; 
      SPA   ret1; 

_DS:  U     "DB_EINRICHTFUNKTION".Bewegung_6_Plus; 
      =     #t_hand_greifer_180Grad; 

      U     "DB_EINRICHTFUNKTION".Bewegung_6_Minus; 
      =     #t_hand_greifer_0Grad; 

ret1: UN    #t_hand_greifer_0Grad; 
      UN    #t_hand_greifer_180Grad; 
      R     "M_Grf1_0_Grad"; 
      R     "M_Grf1_180_Grad"; 

//*** Freigaben ****
//Freigabe Greifer drehen von Hand
      U     "M_Leistung_Steht_an"; 
      U     "I_WM_Energiefreigabe"; 
      U     "M_Handbetrieb"; 
      U(    ; 
      U     "I_WZ_SPA_geloest"; 
      U     "Mx_X_ACHSE_WZW_POS"; 
      O     "M_WW_X_imArbeitsraum"; 
      )     ; 
      U     "M_WZW_Hub_GrdSt"; 
      U     #t_WZW_VKETTE_OHNE_TPU; 
      =     "DB_WWZ".gd0.Frg_Hand_Greifer_drehen; 

//Freigabe X-Achse zum Arbeitsraum von Hand
      U     "M_Leistung_Steht_an"; 
      U     "M_Handbetrieb"; 
      U     "I_WW_1_Klappe_auf"; 
      U(    ; 
      O     "I_WZ_SPA_gespannt"; 
      O     "I_WZ-Spanner_Gesp_mit_WZ"; 
      O     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      )     ; 
      =     "DB_WWZ".gd0.Frg_Hand_X_zum_Arbeitsr; 

//Freigabe X-Achse zur WZW-Position von Hand
      U     "DB_WWZ".gd0.Frg_Hand_X_zum_Arbeitsr; 
      UN    "DB_SIEM_NCK".E_SWCamPlus[14]; 
      UN    "DB_SIEM_NCK".E_SWCamMinus[14]; 
      U     "I_WW_Y_inWWPos"; 
      U     "M_Sp_in_M19Pos"; 
      U     "M_WZW_Greifer_OK"; 
      =     "DB_WWZ".gd0.Frg_Hand_X_zur_WW_Pos; 

//Freigabe Vertikal-Achse zur WZW-Position von Hand
      U(    ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      UN    "DB_SIEM_NCK".E_SWCamPlus[15]; 
      )     ; 
      O(    ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      U     "DB_SIEM_NCK".E_SWCamPlus[15]; 
      )     ; 
      U     "M_WW_X_imArbeitsraum"; 
      U     "M_Leistung_Steht_an"; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      =     "DB_WWZ".gd0.Frg_Hand_Y_zur_WW_Pos; 

//Freigabe Spindel zur WZW-Position
      U     "M_Leistung_Steht_an"; 
      U     "M_Handbetrieb"; 
      U(    ; 
      O     "I_WZ_SPA_gespannt"; 
      O     "I_WZ-Spanner_Gesp_mit_WZ"; 
      )     ; 
      U     "M_WW_X_imArbeitsraum"; 
      =     "DB_WWZ".gd0.Frg_Hand_SPI_zur_WW_Pos; 

      U     "DB_EINRICHTFUNKTION".Bewegung_14_Minus; //X-Achse zum Arbeitsraum
      U     "DB_WWZ".gd0.Frg_Hand_X_zum_Arbeitsr; 
      O     ; 
      U     "m_wwgf_XArbRaum"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "DB_ASUP_MACHINE".Start_Asup; 
      FP    "HM_PI_Bew_68_Minus"; 
      SPBN  n20a; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     2; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n20z; 

n20a: U     "DB_EINRICHTFUNKTION".Bewegung_14_Plus; //X-Achse auf Wechselposition
      U     "DB_WWZ".gd0.Frg_Hand_X_zur_WW_Pos; 
      FP    "HM_PI_X_AX_auf_WFKpos"; 
      SPBN  n20b; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     1; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n20z; 

n20b: U     "DB_EINRICHTFUNKTION".Bewegung_15_Minus; //Vert-Achse auf Wechselposition
      U     "DB_WWZ".gd0.Frg_Hand_Y_zur_WW_Pos; 
      FP    "HM_PI_Bew_69_Plus"; 
      SPBN  n20c; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     3; 
      T     "DB_NC_PLC".Rettung_FktNr; 

n20c: U     "DB_EINRICHTFUNKTION".Bewegung_16_Plus; //Spindel auf Wechselposition
      U     "DB_WWZ".gd0.Frg_Hand_SPI_zur_WW_Pos; 
      FP    "HM_PI_Bew_70_Plus"; 
      SPBN  n20d; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     4; 
      T     "DB_NC_PLC".Rettung_FktNr; 

n20d: U     #t_hand_greifer_180Grad; 
      U     "DB_WWZ".gd0.Frg_Hand_Greifer_drehen; 
      O     ; 
      U     "m_wwgf_Gr1Spi"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      FP    "M_PF_RF_WW_180"; 
      SPBN  n20e; 
      S     "Start_ASUP_Machine_Nr.1"; 
      S     "M_Grf1_180_Grad"; 
      UN    "M_CU_Aktiv"; 
      SPB   n21a; 
      L     5; 
      SPA   n21b; 
n21a: L     9; 
n21b: T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n20z; 

n20e: U     #t_hand_greifer_0Grad; 
      U     "DB_WWZ".gd0.Frg_Hand_Greifer_drehen; 
      O     ; 
      U     "m_wwgf_Gr1Mag"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      FP    "M_PF_RF_WW_360"; 
      SPBN  n20z; 
      S     "Start_ASUP_Machine_Nr.1"; 
      S     "M_Grf1_0_Grad"; 
      UN    "M_CU_Aktiv"; 
      SPB   n21c; 
      L     6; 
      SPA   n21d; 
n21c: L     10; 
n21d: T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   n20z; 

n20z: NOP   0; 

//Asup abbrechen wenn Taste wieder losgelassen, Befehl zurückgenommen wird
      U(    ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_6_Plus; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_6_Minus; 
      U(    ; 
      L     "DB_OEM".Bewegungsnummer; 
      L     6; 
      ==I   ; 
      )     ; 
      O     ; 
      U(    ; 
      L     "DB_OEM".Bewegungsnummer; 
      L     14; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_14_Plus; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_14_Minus; 
      O     ; 
      U(    ; 
      L     "DB_OEM".Bewegungsnummer; 
      L     15; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_15_Minus; 
      O     ; 
      U(    ; 
      L     "DB_OEM".Bewegungsnummer; 
      L     16; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_16_Plus; 
      O     ; 
      U(    ; 
      L     "DB_OEM".Bewegungsnummer; 
      L     68; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_68_Minus; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_68_Plus; 
      O     ; 
      U(    ; 
      L     "DB_OEM".Bewegungsnummer; 
      L     69; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_69_Minus; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_69_Plus; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      O     "m_vwgf_stpAsup1"; 
      SPBN  n20v; 
      =     "m_WZWAsupAbbrechen"; 
n20v: NOP   0; 
NETWORK
TITLE =Taste für Werkzeugwechsler rücksetzen

      U     "DB_EINRICHTFUNKTION".Bewegung_17_Plus; 
      =     "DB_OEM".WZW_Reset; 
      =     "DB_SCHNITTST_OMRON".OW_WMR_CNC_0.WZW_Reset; 

      U     "DB_EINRICHTFUNKTION".Bewegung_18_Plus; 
      =     "DB_OEM".WZ_ueberprueft; 
NETWORK
TITLE =Omron-Regal Rettung

      U     "M_WZM_Regal_angewaehlt"; 
      U     "DB_OEM".OEM_Regalrettaktiv; 
      SPBN  nwen; 

//Regalgreifer auf Grundstellung
      U     "DB_EINRICHTFUNKTION".Bewegung_62_Plus; 
      U     "DB_SCHNITTST_OMRON".IW_WMR_CNC_0.Regal_bereit; 
      FP    "HM_PI_RegGreifer_GRST"; 
      SPBN  n22a; 
      L     0; 
      T     "DB_COMM_WZREGAL".WG_Auftrag_WZ_Nr[9]; 
      S     "DB_COMM_WZREGAL".Kommando[9].Request; 
      S     "M_RegGr_inGasse_aktiv"; 


n22a: U     "DB_COMM_WZREGAL".Kommando[9].Pos_Quitt; 
      O     "DB_COMM_WZREGAL".Kommando[9].Neg_Quitt; 
      U     "M_RegGr_inGasse_aktiv"; 
      FP    "M_FP_Quit_Kd9"; 
      R     "M_RegGr_inGasse_aktiv"; 

      U     "DB_COMM_WZREGAL".Kommando[9].Neg_Quitt; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async4._701827_Neg_Quitt_Auftra; 

//Werkzeug aus Spindel entnehmen
      U     "DB_EINRICHTFUNKTION".Bewegung_63_Plus; 
      U     "DB_SCHNITTST_OMRON".IW_WMR_CNC_0.Regal_bereit; 
      FP    "HM_PI_WZ_aus_Spi"; 
      SPBN  n22b; 
      L     "DB_TOOL_DATA".T_NR; 
      T     "DB_COMM_WZREGAL".WG_Auftrag_WZ_Nr[17]; 
      S     "DB_COMM_WZREGAL".Kommando[17].Request; 
      S     "M_Sp_leeren aktiv"; 

n22b: U     "DB_COMM_WZREGAL".Kommando[17].Pos_Quitt; 
      O     "DB_COMM_WZREGAL".Kommando[17].Neg_Quitt; 
      U     "M_Sp_leeren aktiv"; 
      FP    "M_FP_Quit_Kd17"; 
      R     "M_Sp_leeren aktiv"; 

      U     "DB_COMM_WZREGAL".Kommando[17].Neg_Quitt; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async4._701827_Neg_Quitt_Auftra; 



nwen: NOP   0; 
NETWORK
TITLE =Greifer Vertikalkette ohne TPU aus- einfahren

      U     #t_WZW_VKETTE_OHNE_TPU; 
      =     L      9.0; 
      BLD   103; 
      U(    ; 
      O     "M_req_Greifer_ausfahren"; 
      O     "m_vwgf_GrAus"; 
      )     ; 
      =     L      9.1; 
      BLD   103; 
      U(    ; 
      O     "M_req_Greifer_einfahren"; 
      O     "m_vwgf_GrEin"; 
      )     ; 
      =     L      9.2; 
      BLD   103; 
      U     "M_WZW_Gre1_Pos_Mag"; 
      =     L      9.3; 
      BLD   103; 
      U     "M_WZW_Gre1_Pos_Spi"; 
      =     L      9.4; 
      BLD   103; 
      U     "DB_OEM".Anwahl.WZW.Maske1; 
      =     L      9.5; 
      BLD   103; 
      U     "m_eins"; 
      SPBNB _001; 
      CALL "FC_WZW_GREIFER_AUSF" (
           EX_Enable                := L      9.0,
           EX_Greifer_ausfahren     := L      9.1,
           EX_Greifer_einfahren     := L      9.2,
           EX_WZGR1_Pos_Mag         := L      9.3,
           EX_WZGR2_Pos_Mag         := L      9.4,
           EX_Rettungsmaske_aktiv   := L      9.5,
           ER_WZGR_Istposition      := "DB_Achs11_VMAG_Greifer".MR_IstPosition,
           ER_Hub_Greifer           := "DB_REMANENT".Hub_Greifer_ausfahren,
           EAX_Greifer_in_Bewegung  := "DB_Achs11_VMAG_Greifer".MX_Greifer_in_Bewegung,
           EAX_Greifer_ausgefahren  := "DB_Achs11_VMAG_Greifer".MX_Greifer_ausgefahren,
           EAX_Greifer_eingefahren  := "DB_Achs11_VMAG_Greifer".MX_Greifer_eingefahren,
           EAX_WZGR_InPos           := "DB_Achs11_VMAG_Greifer".MX_Ax_InPosition,
           EAX_WZGR_Anf_Positionier := "DB_Achs11_VMAG_Greifer".MX_Anf_Positionieren,
           EAX_WZGR_Pos_lesen       := "DB_Achs11_VMAG_Greifer".MX_Ax_Position_Lesen,
           EAR_WZGR_Sollposition    := "DB_Achs11_VMAG_Greifer".MR_SollPosition,
           EAR_WZGR_Geschwindigkeit := "DB_Achs11_VMAG_Greifer".MR_Achsgeschwindigkeit,
           EAB_SKZ                  := "MW_Greifer_ausfahren_SKZ");
_001: NOP   0; 
NETWORK
TITLE =

      U     "DB_PLC_MD_DB20".UDHex._61_Bit0_WWoT_Achse_quer; 
      SPBN  E_IN; 
NETWORK
TITLE =Querhub in Stellung zur Spindel

      U(    ; 
      L     "DB_ACHSE_WZW_QUER".MR_IstPosition; 
      L     -1.000000e-001; 
      >R    ; 
      )     ; 
      U(    ; 
      L     "DB_ACHSE_WZW_QUER".MR_IstPosition; 
      L     1.000000e-001; 
      <R    ; 
      )     ; 
      UN    "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      UN    "DB_ACHSE_WZW_QUER".MX_FahrAnf_Plus; 
      UN    "DB_ACHSE_WZW_QUER".MX_FahrAnf_Minus; 
      =     "I_TC_SPIPOS"; 
NETWORK
TITLE =Querhub in Stellung zum Magazin

E_IN: U(    ; 
      L     "DB_ACHSE_WZW_QUER".MR_IstPosition; 
      L     9.990000e+001; 
      >R    ; 
      )     ; 
      U(    ; 
      L     "DB_ACHSE_WZW_QUER".MR_IstPosition; 
      L     1.001000e+002; 
      <R    ; 
      )     ; 
      UN    "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      UN    "DB_ACHSE_WZW_QUER".MX_FahrAnf_Plus; 
      UN    "DB_ACHSE_WZW_QUER".MX_FahrAnf_Minus; 
      =     "I_TC_MAGAZINPOS"; 
NETWORK
TITLE =Abbruch Positionieren

      U(    ; 
      U(    ; 
      U     "O_TC_SPIPOS"; 
      U     "I_TC_SPIPOS"; 
      O     ; 
      U     "O_TC_MAGAZINPOS"; 
      U     "I_TC_MAGAZINPOS"; 
      )     ; 
      UN    "DB_ACHSE_WZW_QUER".MX_Ax_InPosition; 
      O     ; 
      UN    "O_TC_SPIPOS"; 
      UN    "O_TC_MAGAZINPOS"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      O     "DB_ACHSE_WZW_QUER".MX_Ax_FahrAnf; 
      )     ; 
      =     "DB_ACHSE_WZW_QUER".MX_Abbruch_Positionieren; 
NETWORK
TITLE =Istposition lesen

      UN    "DB_ACHSE_WZW_QUER".MX_FahrAnf_Plus; 
      UN    "DB_ACHSE_WZW_QUER".MX_FahrAnf_Minus; 
      U     "DB_ACHSE18".E_ExactFine; 
      FP    "DB_ACHSE_WZW_QUER".MX_FP_Achse_in_Pos; 
      O     ; 
      UN    "O_TC_SPIPOS"; 
      UN    "O_TC_MAGAZINPOS"; 
      U(    ; 
      O     "I_TC_SPIPOS"; 
      O     "I_TC_MAGAZINPOS"; 
      )     ; 
      S     "DB_ACHSE_WZW_QUER".MX_Ax_Position_Lesen; 
NETWORK
TITLE =Greifer Vertikalkette ohne TPU Querhub bewegen

      U(    ; 
      O(    ; 
      U     "O_TC_SPIPOS"; 
      UN    "O_TC_MAGAZINPOS"; 
      UN    "I_TC_SPIPOS"; 
      UN    "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      U     "DB_PLC_MD_DB20".UDHex._61_Bit0_WWoT_Achse_quer; 
      SPBNB _002; 
      L     0.000000e+000; 
      T     "DB_ACHSE_WZW_QUER".MR_SollPosition; 
      SET   ; 
      SAVE  ; 
      CLR   ; 
_002: U     BIE; 
      )     ; 
      O(    ; 
      U     "O_TC_MAGAZINPOS"; 
      UN    "O_TC_SPIPOS"; 
      UN    "I_TC_MAGAZINPOS"; 
      UN    "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      U     "DB_PLC_MD_DB20".UDHex._61_Bit0_WWoT_Achse_quer; 
      SPBNB _003; 
      L     1.000000e+002; 
      T     "DB_ACHSE_WZW_QUER".MR_SollPosition; 
      SET   ; 
      SAVE  ; 
      CLR   ; 
_003: U     BIE; 
      )     ; 
      )     ; 
      S     "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      U(    ; 
      O     "DB_ACHSE_WZW_QUER".MX_Abbruch_Positionieren; 
      O     "DB_ACHSE_WZW_QUER".MX_Ax_InPosition; 
      )     ; 
      R     "DB_ACHSE_WZW_QUER".MX_Anf_Positionieren; 
      NOP   0; 
NETWORK
TITLE =Wiegekopf-Klemmung (Option)

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      SPB   wfk; 

      CLR   ; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[292]; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[293]; 
      =     "DB_DM_M_FUNKTION".MX_M[292]; 
      =     "DB_DM_M_FUNKTION".MX_M[293]; 
      =     "DB_DM_M_FUNKTION".MX_M[294]; 
      =     "DB_DM_M_FUNKTION".MX_M[295]; 
      SPA   kwfk; 

wfk:  SET   ; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[292]; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[293]; 

//Befehl Klemmung lösen
      U     "DB_DM_M_FUNKTION".MX_M[292]; 
      UN    "I_SO_WFK_KLE_GELOEST"; 
      =     #t_Auto_AST; 

//Befehl Klemmung klemmen
      U     "DB_DM_M_FUNKTION".MX_M[293]; 
      =     #t_Auto_GST; 

//Quittierung Klemmung lösen
      U     "I_SO_WFK_KLE_GELOEST"; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[292]; 

//Quittierung Klemmung schliessen
      U     "I_SO_WFK_KLE_GEKLEMMT"; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[293]; 

//Meldung an NC Wiegekopf ist angedockt (gleichzeitig M8 sperren)
      U     "I_SO_WFK_ANGEDOCKT"; 
      =     "DB_NC_PLC".Wiko_angedockt; 
      =     "M_M8_Sperre_Wiko"; 

//Innere Kühlmittelzufuhr automatisch ab Drehzahl 300 einschalten
      U     "I_SO_WFK_ANGEDOCKT"; 
      U(    ; 
      L     "DB_TOOL_DATA".Spindel_Ist_Drehzahl; 
      L     3.000000e+002; 
      >=R   ; 
      )     ; 
      UN    "DB_ACHSE6_SPINDEL".E_PosMode; 
      =     "M_M7_aktivieren_Wiko"; 

//Medienkupplung reinigen
      U     "DB_NC_PLC".PW_WiKo_spuel; 
      =     "O_SO_WFK_MEDIENK_REINIG"; 

//Handfunktion aktiv
      U     "DB_OEM".Einrichten_Aktiv; 
      U     "M_Leistung_Steht_an"; 
      U     "M_Handbetrieb"; 
      =     #t_Hand_aktiv; 

//Automatikbetrieb aktiv
      U     "M_Leistung_Steht_an"; 
      UN    #t_Hand_aktiv; 
      =     #t_Auto_aktiv; 

//Handfunktion Taste Klemmung öffnen
      UN    "I_SO_WFK_ANGEDOCKT"; 
      O(    ; 
      U     "Mx_X_ACHSE_WZW_POS"; 
      U(    ; 
      U     "M_WZW_Gre1_Pos_Mag"; // und die Greifer stehen richtig
      X     "M_WZW_Gre1_Pos_Spi"; 
      )     ; 
      U     "M_WZW_Hub_GrdSt"; 
      )     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_60_Minus; 
      =     #t_AST_Hand; 

//Handfunktion Taste Klemmung schliessen
      U     "DB_EINRICHTFUNKTION".Bewegung_60_Plus; 
      =     #t_GST_Hand; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_WIKO_CLA" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_eins",
           Endlage_AST              := "I_SO_WFK_KLE_GELOEST",
           Endlage_GST              := "I_SO_WFK_KLE_GEKLEMMT",
           Automatik                := #t_Auto_aktiv,
           Hand                     := #t_Hand_aktiv,
           Freig_Ventil_AST_Auto    := #t_Auto_AST,
           Freig_Ventil_GST_Auto    := #t_Auto_GST,
           Taste_AST_Hand           := #t_AST_Hand,
           Taste_GST_Hand           := #t_GST_Hand,
           Freigabe_AST_Hand        := #t_Hand_aktiv,
           Freigabe_GST_Hand        := #t_Hand_aktiv,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := "DB_OEM".Einrichten_Aktiv,
           Bewegungsnummer          := 60,
           Ventil_AST               := "O_SO_WFK_KLE_LOESEN",
           Ventil_GST               := "O_SO_WFK_KLE_KLEMMEN",
           Sammelstoerung           := "DB_FEHLERMELDUNG".WARNUNG4._702343_Kl_Fehler);

//Klemmung gelöst darf nur angesteuert sein bis gelöst ist
      U     "I_SO_WFK_KLE_GELOEST"; 
      UN    "DB_OEM".Einrichten_Aktiv; 
      R     "O_SO_WFK_KLE_LOESEN"; 

kwfk: NOP   0; 
NETWORK
TITLE =Überwachung Grundstellung Werkzeugbruch 

      U     #TX_WERKZEUGBRUCH_AKTIV; 
      U     "M_Leistung_Steht_an"; 
      =     L      9.0; 
      BLD   103; 
      U     "I_WZB_Parkstellung"; 
      =     L      9.1; 
      BLD   103; 
      UN    "O_WZB_Messung_Start"; 
      =     L      9.2; 
      BLD   103; 
      U     "m_null"; 
      =     L      9.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      9.4; 
      BLD   103; 
      U     "M_Ruecksetze_Fehler"; 
      =     L      9.5; 
      BLD   103; 
      CALL "FC_UT_DYN_UEBW" (
           Enable                   := L      9.0,
           Eingang1                 := L      9.1,
           Ausgang1                 := L      9.2,
           Eingang2                 := L      9.3,
           Ausgang2                 := L      9.4,
           Ueberwachungszeit        := 5,
           Zeitzaehler              := "T1_DynUebw_Zeitzaehler",
           Fehlerquitt              := L      9.5,
           Fehlerdyn1               := "DB_FEHLERMELDUNG".VS_Halt_Async1._701625_WZB_Grdst_fehlt,
           Fehlerdyn2               := #tx_dummy2,
           Fehlerstatisch           := #tx_dummy2);
      NOP   0; 
NETWORK
TITLE =Anpassung an alte Ausgangsmerker
// Anpassung an alte Ausgangsmerker
      U     "O_TC_SPIPOS"; 
      =     "O_WW_1_ZYL_GST"; 
      U     "O_TC_MAGAZINPOS"; 
      =     "O_WW_1_ZYL_SPI"; 

END_FUNCTION

