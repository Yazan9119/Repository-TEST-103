FUNCTION "FC_SINRPC" : VOID
TITLE =
//$Revision: 1.29 $
//$Date: 2008/09/03 08:23:27CEST $
//$Author: riedlh $
VERSION : 0.1


VAR_INPUT
  I_Req_WZ_melden : BOOL ;	
  IW_Mag_nummer : WORD ;	//Magazinnummer für Werkzeug, das gemeldet werden soll
  IW_Platznummer : WORD ;	
  IW_T_Nummer : WORD ;	
END_VAR
VAR_OUTPUT
  O_Ready_WZ_melden : BOOL ;	
END_VAR
VAR_IN_OUT
  EA_Pal_Nr_RP : INT ;	//Palettennummer auf Rüstplatz
  EA_Pal_Nr_MA : INT ;	//Palettennumemr Maschine
END_VAR
VAR_TEMP
  ZS_DT : ARRAY  [1 .. 12 ] OF BYTE ;	
  ZS_ClampCubeSide : WORD ;	
  ZS_BA : BYTE ;	
  ZS_Zustand : BYTE ;	
  ZS_PLCMode : BYTE ;	
  PalNr_Dez_1 : BYTE ;	
  PalNr_Dez_10 : BYTE ;	
  PalNr_Dez_100 : BYTE ;	
  letztesByte : DWORD ;	
  Ret_AR2 : DWORD ;	
  FrgMeldHost : BOOL ;	
  QuitBeEntladen : BOOL ;	
  Dez1gelesen : BOOL ;	
  Dez10gelesen : BOOL ;	
  Dez100gelesen : BOOL ;	
  Platz_Ma : BOOL ;	
  StartTelWzg : BOOL ;	
  t_byte_Palnum_dez1 : BYTE ;	
  t_byte_Palnum_dez2 : BYTE ;	
END_VAR
BEGIN
NETWORK
TITLE =NW-Übersicht: FC_SINRPC
// Netzwerk-Übersicht
// ==================
// NW 1: NW-Übersicht: FC_SINRPC
// NW 2: Anforderung WZ melden 
// NW 3: Auswertung SinCOM-Modus und Anforderung SinCOM
// NW 4: "DB_NC_PLC".PW_Pal_Nummer
// NW 5: "MB_Palnum_auf_Uebergabep"
// NW 6: dATENTRÄGERTAUSCH
// NW 7: Telegramm von FASTEMS
// NW 8: Palettenstatus
// NW 9: SINRPC: Maschine min einmal ein
// NW 10: Zustandsänderung
// NW 11: Rücksetzen Telegramm bei Offline
// NW 12: Freigabe Telegramm an Host
// NW 13: Telegramm Änderung Werkstückträger
// NW 14: Telegramm Werkzeug melden
// NW 15: Telegramm Werkzeug entladen
// NW 16: Telegramm Zustandsänderung
// NW 17: Telegramm Ausgabe
// NW 18: PI Datenübergabe von Fastems an Rüstplatz
// NW 19: Auswertung von Fastems: Palette OHNE SpannHydraulik
// NW 20: Auswertung von Fastems: Druck-Vorgabe für Palette = 0
// NW 21: Auswertung von Fastems: Druck-Vorgabe für Palette nicht OK
// NW 22: Auswertung von Fastems: Druck-Vorgabe für Palette = 0
// NW 23: 702311 "WAR, falsche Spanndruck-Daten von Beladung; PW-Freigabe 
// NW 24: Auswertung von Fastems: Palette MIT SpannHydraulik
      NOP   0; 

NETWORK
TITLE =Anforderung WZ melden 
//Umspeichern der Anforderung in die RPC Schnittstelle
      U     "m_eins"; 
      R     #O_Ready_WZ_melden; 

NETWORK
TITLE =Werkzeug melden an RPC

      U     #I_Req_WZ_melden; 
// und Option CTS
      FP    "Mx_FP_wz_melden"; 
      SPBN  n1e; 


NETWORK
TITLE =Anforderung WZ melden 
//Umspeichern der Anforderung in die RPC Schnittstelle
      L     #IW_Mag_nummer; 
      T     "DB_SINRPC".PLC_TEL.WZ_Mag_Nr; 

      L     #IW_Platznummer; 
      T     "DB_SINRPC".PLC_TEL.WZ_Platz_Nr; 

      L     #IW_T_Nummer; 
      T     "DB_SINRPC".PLC_TEL.WZ_TNr; 


      S     "DB_SINRPC".PLC_TEL.Werkzeug; 

n1e:  NOP   0; 


NETWORK
TITLE =Auswertung SinCOM-Modus und Anforderung SinCOM

      SET   ; 
      R     "DB_SINRPC".Mode.FLR_unbemannt; 
      R     "DB_SINRPC".Mode.FLR_bemannt; 
      R     "DB_SINRPC".Mode.FLR_manuell; 
      R     "DB_SINRPC".Mode.FLR_Sonder; 
      R     "DB_SINRPC".Mode.FLR_1_offline; 
      R     "DB_SINRPC".Mode.FLR_2_offline; 
      R     "DB_SINRPC".Mode.Synch_aktiv; 
      R     "DB_SINRPC".Mode.M_Aus; 
      R     "DB_SINRPC".Mode.WPC_W_enable; 

// Auswertung SinCOM-Modus

      L     "DB_SINRPC".MODE_SinCOM; 
      SRW   1; 
      SPZ   NB_1; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_unbemannt; 

NB_1: SRW   1; 
      SPZ   NB_2; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_bemannt; 

NB_2: SRW   1; 
      SPZ   NB_3; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_manuell; 

NB_3: SRW   1; 
      SPZ   NB_4; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_Sonder; 

NB_4: SRW   1; 
      SPZ   NB_5; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_1_offline; 

NB_5: SRW   1; 
      SPZ   NB5b; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_2_offline; 


NB5b: NOP   0; 

// Anforderung SinCOM

      L     "DB_SINRPC".SCReq; 
      SRW   1; 
      SPZ   NB_6; 

      SET   ; 
      S     "DB_SINRPC".Mode.Synch_aktiv; 

NB_6: SRW   1; 
      SPZ   NB_7; 

      SET   ; 
      S     "DB_SINRPC".Mode.M_Aus; 

NB_7: SRW   2; 
      SPZ   NB_8; 

      SET   ; 
      S     "DB_SINRPC".Mode.WPC_W_enable; 

NB_8: NOP   0; 
//   SPA   NW4

NETWORK
TITLE ="DB_NC_PLC".PW_Pal_Nummer

      CALL "FC_UT_CHAR_TO_BYTE" (
           c_Num1                   := "DB_SINRPC".Dock[1].WPC[4],//"DB_SINRPC".I_Fastems.WPC[4]    //'2'
           c_Num2                   := "DB_SINRPC".Dock[1].WPC[5],//"DB_SINRPC".I_Fastems.WPC[5]    //'3'
           b1_NUM                   := #t_byte_Palnum_dez1,
           b2_NUM                   := #t_byte_Palnum_dez2);


      L     #t_byte_Palnum_dez1; 
      L     0; 
      ==I   ; 
      SPBN  x0; 

      L     #t_byte_Palnum_dez2; 
      T     #t_byte_Palnum_dez1; 
      SPA   dua; 
// T     "DB_NC_PLC".PW_Pal_NUMMER



x0:   L     #t_byte_Palnum_dez1; 
      L     1; 
      ==I   ; 
      SPBN  x1; 

      L     10; 
      SPA   xe; 

x1:   L     #t_byte_Palnum_dez1; 
      L     2; 
      ==I   ; 
      SPBN  x2; 

      L     20; 
      SPA   xe; 

x2:   L     #t_byte_Palnum_dez1; 
      L     3; 
      ==I   ; 
      SPBN  x3; 

      L     30; 
      SPA   xe; 

x3:   L     #t_byte_Palnum_dez1; 
      L     4; 
      ==I   ; 
      SPBN  xerr; 

      L     40; 
      SPA   xe; 

xe:   T     #t_byte_Palnum_dez1; 


      L     #t_byte_Palnum_dez1; 
      L     #t_byte_Palnum_dez2; 
      +I    ; 
      T     #t_byte_Palnum_dez1; 

      SPA   dua; 

xerr: L     0; 
      T     #t_byte_Palnum_dez1; 

dua:  L     #t_byte_Palnum_dez1; 
      T     "DB_NC_PLC".PW_Pal_Nummer; 


NETWORK
TITLE ="MB_Palnum_auf_Uebergabep"

      CALL "FC_UT_CHAR_TO_BYTE" (
           c_Num1                   := "DB_SINRPC".Dock[2].WPC[4],//"DB_SINRPC".I_Fastems.WPC[4]    //'2'
           c_Num2                   := "DB_SINRPC".Dock[2].WPC[5],//"DB_SINRPC".I_Fastems.WPC[5]    //'3'
           b1_NUM                   := #t_byte_Palnum_dez1,
           b2_NUM                   := #t_byte_Palnum_dez2);


      L     0; 

      L     #t_byte_Palnum_dez1; 
      L     0; 
      ==I   ; 
      SPBN  x10; 

      L     #t_byte_Palnum_dez2; 
      T     #t_byte_Palnum_dez1; 
      SPA   dua1; 


x10:  L     #t_byte_Palnum_dez1; 
      L     1; 
      ==I   ; 
      SPBN  x11; 

      L     10; 
      SPA   xe1; 

x11:  L     #t_byte_Palnum_dez1; 
      L     2; 
      ==I   ; 
      SPBN  x12; 

      L     20; 
      SPA   xe1; 

x12:  L     #t_byte_Palnum_dez1; 
      L     3; 
      ==I   ; 
      SPBN  x13; 

      L     30; 
      SPA   xe1; 

x13:  L     #t_byte_Palnum_dez1; 
      L     4; 
      ==I   ; 
      SPBN  xer1; 

      L     40; 
      SPA   xe1; 

xe1:  T     #t_byte_Palnum_dez1; 


      L     #t_byte_Palnum_dez1; 
      L     #t_byte_Palnum_dez2; 
      +I    ; 
      T     "MB_Palnum_auf_Uebergabep"; 

      SPA   dua1; 

xer1: L     0; 
      T     "MB_Palnum_auf_Uebergabep"; 
//  T     MB   713

dua1: NOP   0; 



NETWORK
TITLE =Meldung Status Palette von RPC okay(Staus <> 0)
//Abfrage ob ein Programm der Palette schon zugeordnet ist.
//Bitnummer:
//0 = neu angeliefert ohne Programm / keine Palette vorhanden
//1 = neu angeliefert mit Programmzuordnung
//2 = Programmanwahl vorbereiten
//3 = Prgammanwahl erreicht
//4 = In Bearbeitung
//5 = Bearbeitung beendet
//6 = Bearbeitung abgebrochen
//7 = Bearbeitung nicht vorgesehen ( Nur Pufferung)
//
//  
      L     "DB_SINRPC".Dock[2].WPCStatus; 
      L     0; 
      <>I   ; 
      =     "mx_Pal_status_okay"; 

NETWORK
TITLE =dATENTRÄGERTAUSCH

      U     "Mx_PAldata_FASTEMS"; 
      U     "mx_Pal_status_okay"; 
      S     "DB_SINRPC".PLC_TEL.WPC; 
      SPBN  E_DT; 

// abspeichern Werkstückträger 1

      L     "DB_SINRPC".Dock[1].DockPosStatus; 
      T     #ZS_DT[1]; 

      L     "DB_SINRPC".Dock[1].WPCStatus; 
      T     #ZS_DT[2]; 

      L     "DB_SINRPC".Dock[1].WPC[1]; 
      T     #ZS_DT[3]; 

      L     "DB_SINRPC".Dock[1].WPC[2]; 
      T     #ZS_DT[4]; 

      L     "DB_SINRPC".Dock[1].WPC[3]; 
      T     #ZS_DT[5]; 

      L     "DB_SINRPC".Dock[1].WPC[4]; 
      T     #ZS_DT[6]; 

      L     "DB_SINRPC".Dock[1].WPC[5]; 
      T     #ZS_DT[7]; 

      L     "DB_SINRPC".Dock[1].WPC[6]; 
      T     #ZS_DT[8]; 

      L     "DB_SINRPC".Dock[1].ClampCubeSide; 
      T     #ZS_ClampCubeSide; 

      L     "DB_SINRPC".Dock[1].Folge; 
      T     #ZS_DT[9]; 

      L     "DB_SINRPC".Dock[1].ClampActualValue; 
      T     #ZS_DT[10]; 

// Übertrag Daten Werkstückträger 2 nach 1

      L     "DB_SINRPC".Dock[2].DockPosStatus; 
      T     "DB_SINRPC".Dock[1].DockPosStatus; 

      L     "DB_SINRPC".Dock[2].WPCStatus; 
      T     "DB_SINRPC".Dock[1].WPCStatus; 

      L     "DB_SINRPC".Dock[2].WPC[1]; 
      T     "DB_SINRPC".Dock[1].WPC[1]; 

      L     "DB_SINRPC".Dock[2].WPC[2]; 
      T     "DB_SINRPC".Dock[1].WPC[2]; 

      L     "DB_SINRPC".Dock[2].WPC[3]; 
      T     "DB_SINRPC".Dock[1].WPC[3]; 

      L     "DB_SINRPC".Dock[2].WPC[4]; 
      T     "DB_SINRPC".Dock[1].WPC[4]; 

      L     "DB_SINRPC".Dock[2].WPC[5]; 
      T     "DB_SINRPC".Dock[1].WPC[5]; 

      L     "DB_SINRPC".Dock[2].WPC[6]; 
      T     "DB_SINRPC".Dock[1].WPC[6]; 

      L     "DB_SINRPC".Dock[2].ClampCubeSide; 
      T     "DB_SINRPC".Dock[1].ClampCubeSide; 

      L     "DB_SINRPC".Dock[2].Folge; 
      T     "DB_SINRPC".Dock[1].Folge; 

      L     "DB_SINRPC".Dock[2].ClampActualValue; 
      T     "DB_SINRPC".Dock[1].ClampActualValue; 

// gespeicherte Daten Werkstückträger 1 eintragen auf 2

      L     #ZS_DT[1]; 
      T     "DB_SINRPC".Dock[2].DockPosStatus; 

      L     #ZS_DT[2]; 
      T     "DB_SINRPC".Dock[2].WPCStatus; 

      L     #ZS_DT[3]; 
      T     "DB_SINRPC".Dock[2].WPC[1]; 

      L     #ZS_DT[4]; 
      T     "DB_SINRPC".Dock[2].WPC[2]; 

      L     #ZS_DT[5]; 
      T     "DB_SINRPC".Dock[2].WPC[3]; 

      L     #ZS_DT[6]; 
      T     "DB_SINRPC".Dock[2].WPC[4]; 

      L     #ZS_DT[7]; 
      T     "DB_SINRPC".Dock[2].WPC[5]; 

      L     #ZS_DT[8]; 
      T     "DB_SINRPC".Dock[2].WPC[6]; 

      L     #ZS_ClampCubeSide; 
      T     "DB_SINRPC".Dock[2].ClampCubeSide; 

      L     #ZS_DT[9]; 
      T     "DB_SINRPC".Dock[2].Folge; 

      L     #ZS_DT[10]; 
      T     "DB_SINRPC".Dock[2].ClampActualValue; 


      SET   ; 
      R     "Mx_PAldata_FASTEMS"; 

E_DT: NOP   0; 

NETWORK
TITLE =Telegramm von FASTEMS

      L     "DB_SINRPC".I_Fastems.PalChange; // Palletten wurde gebracht
      L     0; 
      <>I   ; 
      S     "DB_SINRPC".PLC_TEL.WPC; 
      SPBN  E_IF; 

      T     "DB_SINRPC".I_Fastems.PalChange; 

      L     "DB_SINRPC".I_Fastems.Command; // Pallette wurde auf Übergabeplatz gebracht
      L     1; 
      ==I   ; 
      SPB   K_01; 

      SPA   E_IF; 

// Kommando 1: Werkstückträger wurde auf Rüstplatz gebracht
K_01: L     0; 
      T     #PalNr_Dez_1; 
      T     #PalNr_Dez_10; 
      T     #PalNr_Dez_100; 

      SET   ; 
      R     #Dez1gelesen; 
      R     #Dez10gelesen; 
      R     #Dez100gelesen; 
      S     "DB_HR".FASTEMS.DatUeGabe_Fastems; 

      L     "DB_SINRPC".I_Fastems.DockPlace; 
      L     1; 
      ==I   ; 
      =     #Platz_Ma; 
      SPBN  RP; 

      LAR1  P#20.0; // erstes Byte WPC-Daten Maschinenplatz

      SPA   AWPC; 

RP:   TAK   ; 
      L     2; 
      ==I   ; 
      SPBN  E_IF; 

      LAR1  P#32.0; // erstes Byte WPC-Daten Rüstplatz

AWPC: L     P#69.0; // erstes Byte Eingang Fastems
      LAR2  ; 

      L     P#5.0; 
      -D    ; 
      T     #letztesByte; 

      L     "DB_SINRPC".I_Fastems.WPCStatus; 
      T     DBB [AR1,P#1.0]; 

SCH1: L     DBB [AR2,P#0.0]; 
      T     DBB [AR1,P#7.0]; 

      L     '0'; 
      <I    ; 
      O(    ; 
      TAK   ; 
      L     '9'; 
      >I    ; 
      )     ; 
      SPB   NB; 

      TAK   ; 

      U     #Dez1gelesen; 
      U     #Dez10gelesen; 
      U     #Dez100gelesen; 
      SPB   NB; 

      U     #Dez1gelesen; 
      U     #Dez10gelesen; 
      SPB   R100; 

      U     #Dez1gelesen; 
      SPB   R_10; 

      S     #Dez1gelesen; 

      T     #PalNr_Dez_1; 

      SPA   NB; 

R_10: S     #Dez10gelesen; 

      T     #PalNr_Dez_10; 

      SPA   NB; 

R100: S     #Dez100gelesen; 

      T     #PalNr_Dez_100; 

      SPA   NB; 

NB:   TAR1  ; 
      L     P#1.0; 
      -D    ; 
      LAR1  ; 

      TAR2  ; 
      L     P#1.0; 
      -D    ; 
      LAR2  ; 

      L     #letztesByte; 
      <D    ; 
      SPBN  SCH1; 

      L     #PalNr_Dez_100; 
      UW    W#16#F; 
      SLW   4; 
      L     #PalNr_Dez_10; 
      UW    W#16#F; 
      OW    ; 
      SLW   4; 
      L     #PalNr_Dez_1; 
      UW    W#16#F; 
      OW    ; 
      BTD   ; 

      L     255; 
      >I    ; 
      SPBN  KLPV; 

      L     0; 
      SPA   PVIN; 

KLPV: TAK   ; 

PVIN: U     #Platz_Ma; 
      SPB   PVMA; 

      T     #EA_Pal_Nr_RP; // Palettennummer Rüstplatz in Palettenverwaltung

      SPA   E_IF; 

PVMA: T     #EA_Pal_Nr_MA; // Palettennummer Maschinenplatz in Palettenverwaltung

E_IF: SPA   xxx; 
// Daten im DB12 werden von Fatems je nach  
//Zustand der Palette geschrieben.
//Es gab Fehler bei Hydac wenn Status DB12.DBB33 = 0 war.


      U     "I_PW_Ruestpl_Uebergabe"; 
      FN    "M_FN_RP_Palette_weg"; 
      =     "M_FN_RP_Pal_auf_0"; 

      U     "M_FN_RP_Pal_auf_0"; 
      UN    "M_PW_Aktiv"; 
      SPBN  xxx; 

      L     0; 
      T     "DB_SINRPC".Dock[2].DockPosStatus; 
      T     "DB_SINRPC".Dock[2].WPCStatus; 
      T     "DB_SINRPC".Dock[2].WPC[1]; 
      T     "DB_SINRPC".Dock[2].WPC[2]; 
      T     "DB_SINRPC".Dock[2].WPC[3]; 
      T     "DB_SINRPC".Dock[2].WPC[4]; 
      T     "DB_SINRPC".Dock[2].WPC[5]; 
      T     "DB_SINRPC".Dock[2].WPC[6]; 
      T     "DB_SINRPC".Dock[2].ClampCubeSide; 
      T     "DB_SINRPC".Dock[2].Folge; 
      T     "DB_SINRPC".Dock[2].ClampActualValue; 
      T     "DB_SINRPC".I_Fastems.WPC[1]; 
      T     "DB_SINRPC".I_Fastems.WPC[2]; 
      T     "DB_SINRPC".I_Fastems.WPC[3]; 
      T     "DB_SINRPC".I_Fastems.WPC[4]; 
      T     "DB_SINRPC".I_Fastems.WPC[5]; 
      T     "DB_SINRPC".I_Fastems.WPC[6]; // Solldruck Spannhydraulik


xxx:  NOP   0; 
NETWORK
TITLE =IST-Spanndruck in Maschine (Uebergabe an Fastems)
//Druck in bar
      L     "DB_HR".HAWE.von.SK2_St1_P_Druck; 
      L     10; 
      /I    ; 
      T     "DB_SINRPC".Dock[1].ClampActualValue; 

      U     "DB_PV_SpH".SpH[0].Stelle[1].KONF_mitSPH; 
      SPB   mSPH; 

      L     255; 
      T     "DB_SINRPC".Dock[1].ClampActualValue; 

mSPH: NOP   0; 

NETWORK
TITLE =H67
//H67"H-Parameter für Leitrechner Applikationen/PW_Verwaltung
//
//H67=1 Palette auf Fertigteil
//H67=2 Palette auf Defekt
//H67=40 PW Übergabeplatz sperren für 
//Palettentransporter
//H67=41 PW Übergabeplatz freigeben für Palettentransporter
//
//
      SET   ; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[67]; 

      U     "DB_H_FUNKTION".HX_M[67]; 
      SPBN  H67; 


      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     1.000000e+000; 
      ==R   ; 
      )     ; 
      S     "Mx_Pal_Fertigteil_H67"; 

      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     2.000000e+000; 
      ==R   ; 
      )     ; 
      S     "Mx_Pal_Defekt_H67"; 

      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     4.000000e+001; 
      ==R   ; 
      )     ; 
      U     "I_FMS_APC_MOVEMENT_OK"; // und PW Freigegeben von LEitrechner 
      S     "Mx_APC_Disable_H67"; 

      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     4.100000e+001; 
      ==R   ; 
      )     ; 
      O     "M_Reset_Taste"; 
      R     "Mx_APC_Disable_H67"; 


      U     "Mx_Pal_Fertigteil_H67"; 
      U(    ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     32; 
      ==I   ; 
      )     ; 
      O(    ; 
      U     "Mx_Pal_Defekt_H67"; 
      U(    ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     64; 
      ==I   ; 
      )     ; 
      )     ; 
      O(    ; 
      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     4.000000e+001; 
      ==R   ; 
      )     ; 
      U     "I_FMS_APC_MOVEMENT_OK"; 
      )     ; 
      U     "DB_H_FUNKTION".HX_M[67]; 
      =     "DB_H_FUNKTION".HX_Quitt[67]; 
      R     "DB_H_FUNKTION".HX_M[67]; 
      R     "Mx_Pal_Fertigteil_H67"; 
      R     "Mx_Pal_Defekt_H67"; 


NETWORK
TITLE =

H67:  O     "M_Reset_Taste"; 
      O     "DB_SIEM_KANAL_1".E_ChanReset; 
      R     "Mx_APC_Disable_H67"; 


NETWORK
TITLE =Meldung Übergabeplatz gesperrt

      U     "Mx_APC_Disable_H67"; 
      =     "DB_FEHLERMELDUNG".SAFE_MELD._701524; 
NETWORK
TITLE =Programm läuft in Automatik verzögert

      UN    "M_PW_Aktiv"; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; // Programm läuft
      U     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO        // AUTOMATIK
      L     S5T#3S; 
      SE    "T_PRG_LAEUFT_VERZ"; 


NETWORK
TITLE =SINRPC: Bearbeitungsprogramm Ende

      U(    ; 
      O     "M_Hauptprogramm_Ende"; // "O_Programm_Ende"           // "DB_SIEM_KANAL_1".E_M30     // M2/M30 Impuls  
      O     "Mx_Pal_Fertigteil_H67"; 
      )     ; 
      FP    "M_FP_HAUPTPROGRAMM_ENDE"; 
      S     "M_Bearb_Ende"; // gespeicherter Start für Meldung Bearbeitungsende


NETWORK
TITLE =WZB: Störung Werkzeugbruch
//Bei bemannten Betrieb wird die Meldung vom Werkzeugbruch gelöscht, wenn das 
//Programm wieder gestartet wurde.
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      U     "DB_SINRPC".Mode.FLR_bemannt; 
      R     "Mx_Pal_Stoerung_WZBruch"; 
      R     "MX_Pal_auf_Stoerung"; 


NETWORK
TITLE =SINRPC: Bearbeitungsprogramm Ende

      U(    ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     16; 
      <>I   ; 
      )     ; 
      U     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO        // AUTOMATIK
      UN    "Mx_Pal_Fertigteil_H67"; 
      R     "M_Bearb_Ende"; // gespeicherter Start für Meldung Bearbeitungsende

NETWORK
TITLE =Palettenstatus
//
//// Wenn nicht der Fertigungsablauf von SinCOM benutzt wird, können die Schritte 
//2, 4 und 8 übersprungen werden.
//// Dies ist dann der Fall, wenn der Leitrechner das Programm lädt, anwählt und 
//startet.
//// Mit dem NC-Start vom Leitrechner wird der Status dann direkt auf 'in 
//Bearbeitung' gesetzt.
// 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     1; 
      ==I   ; 
      O(    ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     16; 
      >I    ; 
      )     ; 
      UN(   ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     32; // beendet
      ==I   ; 
      )     ; 
      UN(   ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     64; // Störung
      ==I   ; 
      )     ; 
      U     "T_PRG_LAEUFT_VERZ"; 
      O(    ; 
      O     "MX_Pal_auf_Stoerung"; // Palette auf Störung
      O     "Mx_Pal_Stoerung_WZBruch"; 
      O     "Mx_Pal_Defekt_H67"; 
      )     ; 
      SPB   BEAR; // in Bearbeitung

      U(    ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     2; 
      ==I   ; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; // Kanal im Reset
      U     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO        // AUTOMATIK
      SPB   PRGV; // Programmanwahl vorbereiten

      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     8; 
      ==I   ; 
      O(    ; //xxxxxx
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     32; 
      ==I   ; 
      )     ; 
      U     "T_PRG_LAEUFT_VERZ"; 
      SPB   BEAR; // in Bearbeitung


      U     "M_Bearb_Ende"; // gespeicherter Start für Meldung Bearbeitungsende
      UN    "DB_SINRPC".PLC_TEL.WPC; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "MX_Pal_auf_Stoerung"; // Palette auf Störung
      UN    "Mx_Pal_Stoerung_WZBruch"; 
      R     "M_Bearb_Ende"; // gespeicherter Start für Meldung Bearbeitungsende
      SPB   BEND; // Bearbeitung beendet

      SPA   ESTA; 


// Programmanwahl vorbereiten
PRGV: L     4; 
      SPA   AEST; 


// in Bearbeitung
BEAR: L     16; 
      SPA   XXX; 


// Bearbeitung beendet
BEND: L     32; 
      R     "Mx_Pal_Fertigteil_H67"; 
      SPA   AEST; 


XXX:  UN    "MX_Pal_auf_Stoerung"; // UN    "MX_Pal_auf_Stoerung"       // Palette auf Störung
      UN    "Mx_Pal_Stoerung_WZBruch"; 
      UN    "Mx_Pal_Defekt_H67"; 
      SPB   AEST; 

// Bei bemannten Betrieb nicht 64 an Fastems schicken. 
// Die Entscheidung trifft der Bediener


// Bearbeitung abbgebrochen ( Bohrerbruch )
      U     "DB_SINRPC".Mode.FLR_bemannt; 
      SPB   ESTA; 

      L     64; 
      U     "m_eins"; 
      R     "Mx_Pal_Defekt_H67"; 
      SPA   AEST; 

AEST: T     "DB_SINRPC".Dock[1].WPCStatus; 

      SET   ; 

      S     "DB_SINRPC".PLC_TEL.WPC; 

ESTA: U     "MX_Pal_auf_Stoerung"; // Palette auf Störung
      O     "Mx_Pal_Stoerung_WZBruch"; 
      U(    ; 
      L     "DB_SINRPC".Dock[1].WPCStatus; 
      L     64; 
      ==I   ; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      U(    ; 
      O     "DB_SINRPC".Mode.FLR_unbemannt; // Palette auf Störung
      O     "M_Ruecksetze_Fehler"; 
      )     ; 
      R     "MX_Pal_auf_Stoerung"; // Palette auf Störung
      R     "Mx_Pal_Stoerung_WZBruch"; 

NETWORK
TITLE =SINRPC: Maschine min einmal ein

      U     "M_MA_Maschine_Ein"; 
      S     "Mx_Maschine_min_1mal_ein"; 
NETWORK
TITLE =Zustandsänderung
//      Auswertung Maschinenzustand
      L     0; 

      U     "DB_SIEM_KANAL_1".E_ProgRunn; // Programm läuft
//    O     "DB_SIEM_KANAL_1".E_ProgWait
      O     "DB_SIEM_KANAL_1".E_ProgStop; 
//      O     "DB_SIEM_KANAL_1".E_ProgInterrupt
//      O     "DB_SIEM_KANAL_1".E_ProgrAborted
      UN    "DI_FB_IF_FASTEMS".MC_FMS_plc_Alarm; // Störung
      U     "Mx_Maschine_min_1mal_ein"; //"M__26.1"  // Erster Paketdurchlauf *
      SPBN  ZUS1; 

      L     1; 
      SPA   TZUS; 

ZUS1: U     "DI_FB_IF_FASTEMS".MC_FMS_plc_Alarm; // "A_Stoerung"  // Störung
      U     "Mx_Maschine_min_1mal_ein"; //"M__26.1"  // Erster Paketdurchlauf *
      SPBN  ZUS2; 

      L     2; 
      SPA   TZUS; 


ZUS2: UN    "Mx_Maschine_min_1mal_ein"; // m_7ter_ob1zyklus              // "M__26.1"  // Erster Paketdurchlauf *
      SPBN  TZUS; 

      L     4; 
      SPA   TZUS; 

TZUS: T     #ZS_Zustand; 

// Auswertung NC-Betriebsarten

      L     0; 

      U     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO        // AUTOMATIK
      SPBN  BA_1; 

      L     1; 
      SPA   T_BA; 

BA_1: UN    "DB_SIEM_BAG".E_TEACHIN; // TEACH IN
      U     "MX_MDA"; //"DB_SIEM_BAG".E_MDA         // MDA
      SPBN  BA_2; 

      L     2; 
      SPA   T_BA; 


BA_2: U     "M_Handbetrieb"; //"DB_SIEM_BAG".E_JOG         // JOG
      SPBN  BA_3; 

      L     4; 
      SPA   T_BA; 


BA_3: U     "MX_MDA"; //"DB_SIEM_BAG".E_MDA         // TEACH IN
      SPBN  T_BA; 

      L     8; 
      SPA   T_BA; 


T_BA: T     #ZS_BA; 

// Auswertung Maschinenmodus

      L     0; 

      UN    "M_Leistung_Steht_an"; // Antriebe eingeschaltet
      SPBN  TMOD; 

      L     1; 
      SPA   TMOD; 

TMOD: T     #ZS_PLCMode; 

// Start Telegramm

      L     "DB_SINRPC".MODE_SinCOM; 
      L     "DB_SINRPC".PLC_TEL.MODE_SinCOM; 
      <>I   ; 
      O(    ; 
      L     #ZS_Zustand; 
      L     "DB_SINRPC".PLC_TEL.MachineStatus; 
      <>I   ; 
      )     ; 
      O(    ; 
      L     #ZS_BA; 
      L     "DB_SINRPC".PLC_TEL.MachineMode; 
      <>I   ; 
      )     ; 
      O(    ; 
      L     #ZS_PLCMode; 
      L     "DB_SINRPC".PLC_TEL.MODE_PLC; 
      <>I   ; 
      )     ; 
      S     "DB_SINRPC".PLC_TEL.Zustand; 
      SPBN  EZUS; 

      L     "DB_SINRPC".MODE_SinCOM; 
      T     "DB_SINRPC".PLC_TEL.MODE_SinCOM; 

      L     #ZS_Zustand; 
      T     "DB_SINRPC".PLC_TEL.MachineStatus; 

      L     #ZS_BA; 
      T     "DB_SINRPC".PLC_TEL.MachineMode; 

      L     #ZS_PLCMode; 
      T     "DB_SINRPC".PLC_TEL.MODE_PLC; 

EZUS: NOP   0; 

NETWORK
TITLE =Rücksetzen Telegramm bei Offline

      U     "DB_SINRPC".Mode.FLR_1_offline; 
      U     "DB_SINRPC".Mode.FLR_2_offline; 
      R     "DB_SINRPC".PLC_TEL.WPC; 
      R     "DB_SINRPC".PLC_TEL.Werkzeug; 
      R     "DB_SINRPC".PLC_TEL.Zustand; 
      SPBN  ERES; 

      L     0; 
      T     "DB_SINRPC".MagNum; 
      T     "DB_SINRPC".PlaceNum; 
      T     "DB_SINRPC".PLCReq; 

ERES: NOP   0; 

NETWORK
TITLE =Freigabe Telegramm an Host

      U(    ; 
      L     "DB_SINRPC".PLCReq; 
      L     0; 
      ==I   ; 
      )     ; 
      U(    ; 
      ON    "DB_SINRPC".Mode.FLR_1_offline; 
      ON    "DB_SINRPC".Mode.FLR_2_offline; 
      )     ; 
      =     #FrgMeldHost; 
NETWORK
TITLE =Telegramm Änderung Werkstückträger

      U     #FrgMeldHost; 
      U     "DB_SINRPC".PLC_TEL.WPC; 
      R     "DB_SINRPC".PLC_TEL.WPC; 
      SPBN  TEL1; 

      L     1; 
      T     "DB_SINRPC".PLCReq; 

      SPA   TELO; 

NETWORK
TITLE =Telegramm Werkzeug melden

TEL1: U     #FrgMeldHost; 
      U     "DB_SINRPC".PLC_TEL.Werkzeug; 
      R     "DB_SINRPC".PLC_TEL.Werkzeug; 
      SPBN  TE1a; 



      L     "DB_SINRPC".PLC_TEL.WZ_Mag_Nr; 
      T     "DB_SINRPC".MagNum; 

      L     "DB_SINRPC".PLC_TEL.WZ_Platz_Nr; 
      T     "DB_SINRPC".PlaceNum; 

      L     "DB_SINRPC".PLC_TEL.WZ_TNr; 
      T     "DB_SINRPC".TNum; 

      L     21; 
      T     "DB_SINRPC".DataType; // 21 alle Daten, 22, 23


      L     2; 
      T     "DB_SINRPC".PLCReq; 

      SET   ; 
      =     #O_Ready_WZ_melden; 

      SPA   TELO; 

NETWORK
TITLE =Telegramm Werkzeug entladen

TE1a: SPA   TEL2; //U     #FrgMeldHost
      U     "DB_SINRPC".PLC_TEL.WZ_Entladen; 
      R     "DB_SINRPC".PLC_TEL.WZ_Entladen; 
      SPBN  TEL2; 



      L     "DB_SINRPC".PLC_TEL.WZ_Mag_Nr; 
      L     4; 
      T     "DB_SINRPC".MagNum; 

      L     "DB_SINRPC".PLC_TEL.WZ_Platz_Nr; 
      L     1; 
      T     "DB_SINRPC".PlaceNum; 

      L     "DB_SINRPC".PLC_TEL.WZ_TNr; 
      T     "DB_SINRPC".TNum; 

      L     27; 
      T     "DB_SINRPC".DataType; // 21 alle Daten, 22, 23


      L     2; 
      T     "DB_SINRPC".PLCReq; 

      SET   ; 
      =     #O_Ready_WZ_melden; 

      SPA   TELO; 


NETWORK
TITLE =Telegramm Zustandsänderung

TEL2: U     #FrgMeldHost; 
      U     "DB_SINRPC".PLC_TEL.Zustand; 
      R     "DB_SINRPC".PLC_TEL.Zustand; 
      SPBN  EANH; 

      L     "DB_SINRPC".PLC_TEL.MachineMode; 
      T     "DB_SINRPC".MachineMode; 

      L     "DB_SINRPC".PLC_TEL.MachineStatus; 
      T     "DB_SINRPC".MachineStatus; 

      L     "DB_SINRPC".PLC_TEL.MODE_PLC; 
      T     "DB_SINRPC".MODE_PLC; 

      L     4; 
      T     "DB_SINRPC".PLCReq; 

      SPA   TELO; 

NETWORK
TITLE =Telegramm Ausgabe

TELO: U(    ; 
      L     0; 
      L     "DB_SINRPC".Trigger; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     128; 
      >=I   ; 
      )     ; 
      SPBN  ESTR; 

      L     1; 
      SPA   TRIG; 

ESTR: TAK   ; 
      SLW   1; 

TRIG: T     "DB_SINRPC".Trigger; 

EANH: NOP   0; 

NETWORK
TITLE =PI Datenübergabe von Fastems an Rüstplatz

      U     "DB_HR".FASTEMS.DatUeGabe_Fastems; 
      FP    "DB_HR".FASTEMS.HM_DatUeGabe_Fastems; 
      =     "DB_HR".FASTEMS.PI_DatUeGabe_Fastems; 

      SET   ; 
      R     "DB_HR".FASTEMS.DatUeGabe_Fastems; 
NETWORK
TITLE =Auswertung von Fastems: Palette OHNE SpannHydraulik
//In "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element muss "0" eingetragen werden,
//damit Spannstelle OHNE "Spannhydraulik" ist.
//Auswertung von "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element erfolgt in FB_SPH
//
//02.09.2008 / Riedl
//Die Palette wird auf Status "Rohteil" gesetzt.
      U(    ; 
      U     "DB_HR".FASTEMS.ABST_FASTEMS; 
      U     "DB_HR".FASTEMS.PI_DatUeGabe_Fastems; 
      SPBNB _001; 
      L     "DB_SINRPC".I_Fastems.WPC[6]; 
      T     "DB_HR".FASTEMS.VorgabeDruck; 
      SET   ; 
      SAVE  ; 
      CLR   ; 
_001: U     BIE; 
      )     ; 
      U(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     255; 
      ==I   ; 
      )     ; 
      =     L     31.0; 
      U     L     31.0; 
      BLD   102; 
      =     "DB_HR".FASTEMS.RPL_ohneSPH; 
      U     L     31.0; 
      BLD   102; 
      R     "DB_PV_SpH".SpH[1].Stelle[1].KONF_mitSPH; 
      U     L     31.0; 
      BLD   102; 
      R     "DB_PV_SpH".SpH[1].Stelle[1].STAT_gespannt; 
      U     L     31.0; 
      BLD   102; 
      R     "DB_PV_SpH".SpH[1].Stelle[1].STAT_entspannt; 
      U     L     31.0; 
      SPBNB _002; 
      L     0; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_P_Vorgabe; 
_002: NOP   0; 
      U     L     31.0; 
      SPBNB _003; 
      L     0; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; 
_003: NOP   0; 
      U     L     31.0; 
      SPBNB _006; 
      L     2; 
      T     "DB_PAL_NR".Platz[1].Status; 
_006: NOP   0; 
NETWORK
TITLE =Auswertung von Fastems: Druck-Vorgabe für Palette = 0
//Reaktion: Sperre PW-Freigabe
      U     "DB_HR".FASTEMS.ABST_FASTEMS; 
      U     "DB_HR".FASTEMS.PI_DatUeGabe_Fastems; 
      UN    "DB_HR".FASTEMS.RPL_ohneSPH; 
      =     L     31.0; 
      U     L     31.0; 
      U(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     0; 
      ==I   ; 
      )     ; 
      S     "DB_HR".FASTEMS.RPL_DruckVorgabe_0; 
      U     L     31.0; 
      U(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     0; 
      <>I   ; 
      )     ; 
      R     "DB_HR".FASTEMS.RPL_DruckVorgabe_0; 
NETWORK
TITLE =Auswertung von Fastems: Druck-Vorgabe für Palette nicht OK
//Reaktion: Sperre PW-Freigabe
      U     "DB_HR".FASTEMS.ABST_FASTEMS; 
      U     "DB_HR".FASTEMS.PI_DatUeGabe_Fastems; 
      UN    "DB_HR".FASTEMS.RPL_ohneSPH; 
      U(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     1; 
      >=I   ; 
      )     ; 
      =     L     31.0; 
      U     L     31.0; 
      U(    ; 
      O(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     20; 
      <I    ; 
      )     ; 
      O(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     240; 
      >I    ; 
      )     ; 
      )     ; 
      S     "DB_HR".FASTEMS.RPL_DruckVorgabe_nok; 
      U     L     31.0; 
      U(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     20; 
      >=I   ; 
      )     ; 
      U(    ; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      L     240; 
      <=I   ; 
      )     ; 
      R     "DB_HR".FASTEMS.RPL_DruckVorgabe_nok; 
NETWORK
TITLE =Auswertung von Fastems: Druck-Vorgabe für Palette = 0

      UN    "DB_HR".FASTEMS.ABST_FASTEMS; 
      R     "DB_HR".FASTEMS.RPL_DruckVorgabe_0; 
      R     "DB_HR".FASTEMS.RPL_DruckVorgabe_nok; 
NETWORK
TITLE =702311 "WAR, falsche Spanndruck-Daten von Beladung; PW-Freigabe 

      O     "DB_HR".FASTEMS.RPL_DruckVorgabe_0; 
      O     "DB_HR".FASTEMS.RPL_DruckVorgabe_nok; 
      =     "DB_FEHLERMELDUNG".WARNUNG4._702311_PWFG_gesperrt; 
NETWORK
TITLE =Auswertung von Fastems: Palette MIT SpannHydraulik
//In "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element muss "1" eingetragen werden,
//damit Spannstelle MIT "Spannhydraulik" ist.
//Auswertung von "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element erfolgt in FB_SPH
//
//02.09.2008 / Riedl
//Die Palette muss auch auf Status "Rohteil" (nicht "Leerpalette") gesetzt werden,
//damit die Spannhydraulik-Funktionen im MPL ablaufen.
      U     "DB_HR".FASTEMS.ABST_FASTEMS; 
      U     "DB_HR".FASTEMS.PI_DatUeGabe_Fastems; 
      UN    "DB_HR".FASTEMS.RPL_ohneSPH; 
      UN    "DB_HR".FASTEMS.RPL_DruckVorgabe_0; 
      UN    "DB_HR".FASTEMS.RPL_DruckVorgabe_nok; 
      SPBNB _007; 
      L     "DB_HR".FASTEMS.VorgabeDruck; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_P_Vorgabe; 
      SET   ; 
      SAVE  ; 
      CLR   ; 
_007: U     BIE; 
      =     L     31.0; 
      U     L     31.0; 
      BLD   102; 
      S     "DB_PV_SpH".SpH[1].Stelle[1].KONF_mitSPH; 
      U     L     31.0; 
      BLD   102; 
      S     "DB_PV_SpH".SpH[1].Stelle[1].STAT_gespannt; 
      U     L     31.0; 
      BLD   102; 
      R     "DB_PV_SpH".SpH[1].Stelle[1].STAT_entspannt; 
      U     L     31.0; 
      SPBNB _00b; 
      L     1; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; 
_00b: NOP   0; 
      U     L     31.0; 
      SPBNB _014; 
      L     2; 
      T     "DB_PAL_NR".Platz[1].Status; 
_014: NOP   0; 
END_FUNCTION

