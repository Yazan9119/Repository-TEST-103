FUNCTION "FC_SAFETY_GATES" : VOID
TITLE =
//$Revision: 1.31 $
//$Date: 2008/10/22 15:46:44CEST $
//$Author: hgc $
//$ProjectRevision: Last Checkpoint: 1.62 $
//
//TRANSLINE 2000 Baustein 
//
//Verwaltung der Schutztüren / Schutztüren geschlossen gesamt
AUTHOR : Anpassen
FAMILY : GMOD
NAME : SAF_DOOR
VERSION : 1.0


VAR_TEMP
  enable_unlock_ar : BOOL ;	//Freigabe Türen entriegeln Schutzbereich Arbeitsraum
  enable_unlock_mag : BOOL ;	//Freigabe Türen entriegeln Schutzbereich Magazin
  enable_unlock_load : BOOL ;	//Freigabe Türen entriegeln Schutzbereich Rüstplatz
  all_doors_closed : BOOL ;	//Alle Schutztüren (ohne Servicetür) geschlossen
  all_doors_closed_locked : BOOL ;	//Alle Schutztüren (ohne Servicetür) geschlossen und verriegelt
  mag_tuer_entriegeln : BOOL ;	//Magazintüre entriegeln
  mag_tuer_verriegeln : BOOL ;	//Magazintüre entriegeln
  enable_general : BOOL ;	//Freigabe für Tastendruck
  Tuere_vorn_ent_BHG : BOOL ;	
  Tuere_vorn_ver_BHG : BOOL ;	
  Spindel_1_WZ_be_entladen : BOOL ;	
  Spindel_2_WZ_be_entladen : BOOL ;	
  Magazin_1_WZ_be_entladen : BOOL ;	
  Magazin_2_WZ_be_entladen : BOOL ;	
  TX_Freigabe_Tuer : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Meldung Kabinentüre offen
// Netzwerk-Übersicht
// ==================
// NW 1: Meldung Kabinentüre offen
// NW 2: Reset Tür-Störungen 700720 / 700721 
// NW 3: Reset 700840 "SPVS-HA-HPT, Zustimmtaste schon 1 Signal"
// NW 4: Kabinentüre mit Taste ent / verriegeln 
// NW 5: Kabinentüre automatisch entriegeln
// NW 6: Kabinentüre verriegeln
// NW 7: M-AUSGANG: Magnetverriegelung Kabinentüre entriegeln
// NW 8: MA: Kabinentür entriegeln
// NW 9: Fehlerüberwachung Tür zu
// NW 10: Fehlerüberwachung Tür entriegeln
// NW 11: Kabinentüre entriegeln bei Maschine Aus
// NW 12: Meldung: Kabinentüre entriegelt
// NW 13: Kabinentüre geschlossen und verriegelt
// NW 14: Sammelsignal: Alle Schutztüren geschlossen und verriegelt
// NW 15: Sammelsignal: Alle Schutztüren geschlossen und verriegelt
      UN    "I_AL_Kabine_zu"; 
      =     "DB_FEHLERMELDUNG".Meldung._702023_Kab_Tuer_auf; 
NETWORK
TITLE =Reset Tür-Störungen 700720 / 700721 / 700840

      U     "M_Ruecksetze_Fehler"; 
      =     L      2.0; 
      U     L      2.0; 
      BLD   102; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700720_Tuer_Zu_0; 
      U     L      2.0; 
      BLD   102; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700721_TuerVer_0Sig; 
      U     L      2.0; 
      UN    "I_AL_Zustimmtaste"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700840_Zustimm_1Sign; 
NETWORK
TITLE =Türe entriegeln bei angeforderter Zwangsdynamisierung
//Safety Zwangsdynamisierung nur anstossen wenn Taste > 2,5 sec gedrückt wird 
      U     "I_Kab_Tuer_Entr"; 
      UN    "M_Restweg_aktiv"; 
      L     S5T#2S500MS; 
      SE    "T_Tüere_entr_bei_ZD"; 
NETWORK
TITLE =Reset Kabinentür entriegeln  bei angeforderter Zwangsdyn.
//Safety Zwangsdynamisierung nur anstossen wenn Taste > 2,5 sec gedrückt wird 
//Meldefenster: ZD steht an -> Taste 3 Sekunden gedrückt halten
      U     "DB_PLC_MD_DB20".UDHex._04_BIT7_SAFETY; 
      U     "DB_FEHLERMELDUNG".SAFE_MELD._701502_ZD_ANF_HAX; 
      UN    "DB_SAFE_ALLG1".ZD_Z_disable; 
      UN    "T_Tüere_entr_bei_ZD"; 
      U     "M_Kabine_verriegeln"; 
      =     L      2.0; 
      U     L      2.0; 
      U     "I_Kab_Tuer_Entr"; 
      =     "DB_FEHLERMELDUNG".MELDEFENSTER3._511301; 
      U     L      2.0; 
      BLD   102; 
      R     "I_Kab_Tuer_Entr"; 
NETWORK
TITLE =Meldung: Zwangsdyn. nicht möglich, bitte überspringen

      U     "DB_PLC_MD_DB20".UDHex._04_BIT7_SAFETY; 
      U     "DB_FEHLERMELDUNG".SAFE_MELD._701502_ZD_ANF_HAX; 
      U     "M_IM_Taste_Tuerver_entr"; //"I_Kab_Tuer_Entr"
      U     "M_Restweg_aktiv"; 
      S     "DB_FEHLERMELDUNG".SAFE_MELD._701523_ZD_ni_moeglich; 
      U(    ; 
      O     "M_Ruecksetze_Fehler"; 
      ON    "I_AL_Kabine_Verriegelt"; 
      )     ; 
      R     "DB_FEHLERMELDUNG".SAFE_MELD._701523_ZD_ni_moeglich; 
      NOP   0; 
NETWORK
TITLE =Taste Kabinentüre entriegeln Impuls

      U     "I_Kab_Tuer_Entr"; 
      =     L      2.0; 
      U(    ; 
      U     L      2.0; 
      UN    "M_MWZW_Tuer_Auf"; 
      U(    ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_OHNE_RPC; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_MIT_RPC; 
      O     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_OHNE_RPC; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_MIT_RPC; 
      )     ; 
      U(    ; 
      O     "I_WA_ENABLE_DOOR_OPEN"; 
      O     "I_ROBOT_DOOR_CLOSED"; 
      O     "DB_PLC_MD_DB20".UDHex._52_Bit2_EROWA_PROMOT_1; 
      )     ; 
      )     ; 
      FP    "M_FP_Taste_Tuerver"; 
      )     ; 
      UN    "Tv_T_KabTuer_entriegeln"; 
      =     "M_IM_Taste_Tuerver_entr"; 
      U     L      2.0; 
      BLD   102; 
      =     "O_WA_REQUEST_DOOR_UNLOC"; 
NETWORK
TITLE =702143 "MEL, Kabinentüren entriegeln gesperrt durch Handlingsyst

      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_OHNE_RPC; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_MIT_RPC; 
      )     ; 
      U     "I_Kab_Tuer_Entr"; 
      UN    "DB_PLC_MD_DB20".UDHex._52_Bit2_EROWA_PROMOT_1; 
      UN    "I_ROBOT_DOOR_CLOSED"; 
      UN    "I_WA_ENABLE_DOOR_OPEN"; 
      =     "DB_FEHLERMELDUNG".Meldung._702143_Tuer_sper_HASYST; 
NETWORK
TITLE =Taste Kabinentüre entriegeln verlängerter Impuls

      U     "M_IM_Taste_Tuerver_entr"; 
      L     S5T#1S; 
      SV    "Tv_T_KabTuer_entriegeln"; 
NETWORK
TITLE =WZW aktiv verzögern
//Bei Regalmagazin muß Klappe zu sein, 
//da sonst PNOZ und somit die Freigaben für Regal abgeschaltet werden obwohl 
//dieses noch in Bewegung läuft.
//Optimallösung wäre Signal vom Regal für Freigabe zum entriegeln.
//
      UN    "M_WZW_Aktiv"; 
      U(    ; 
      O     "I_WW_1_Klappe_zu"; 
      ON    "M_WZM_Regal_angewaehlt"; 
      )     ; 
      L     S5T#2S; 
      SA    "Ta_WZW_Aktiv_beendet"; 
NETWORK
TITLE =Kabinentüre entriegeln durch M-Funktion gespeichert

      U     "M_Hauptprogramm_Ende"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      FP    "Mx_FP_HPT_ENDE"; 
      U     "DB_PLC_MD_DB20".UDHex._06_BIT0_KAB_ENT_M30; 
      O     ; 
      U(    ; 
      O     "DB_DM_M_FUNKTION".MX_M[0]; 
      O     "DB_DM_M_FUNKTION".MX_M[1]; 
      )     ; 
      U     "DB_PLC_MD_DB20".UDHex._06_BIT1_KAB_ENT_M0; 
      O     ; 
      U(    ; 
      O     "DB_DM_M_FUNKTION".MX_M[61]; 
      O     "DB_DM_M_FUNKTION".MX_M[62]; 
      )     ; 
      U     "DB_PLC_MD_DB20".UDHex._06_BIT2_KAB_ENT_M6162; 
      O     ; 
      U     "DB_DM_M_FUNKTION".MX_M[2]; 
      U     "M_Satzfolge_aktiv"; 
      U     "DB_PLC_MD_DB20".UDHex._06_BIT3_KAB_ENT_M2; 
      S     "M_Kab_Tuer_Entr_MFKTgesp"; 
NETWORK
TITLE =Kabinentüre automatisch entriegeln

      O     "M_Kab_Tuer_Entr_MFKTgesp"; 
      O(    ; 
      O     "M_MWZW_Tuer_Auf"; 
      U     "M_Kabine_verriegeln"; 
      U     "M_Hauptspindel_steht"; 
      U     "M_Hauptachsen_stehen"; 
      )     ; 
      O     "DB_DM_M_FUNKTION".MX_M[29]; 
      UN    "M_Simulation_Aktiv"; 
      U(    ; 
      U     "I_WW_1_Klappe_zu"; 
      O     "DB_NC_PLC".WZW_WZ_Hand_Aus; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_RAD; 
      ON    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_ANGEWAEHLT; 
      )     ; 
      UN    "M_WZW_Aktiv"; 
      UN    "DB_FAMILIEN_MODUL".Aktive_Funktionen; 
      S     "M_Kab_Tuer_Entr"; 

      U     "M_Kab_Tuer_Entr"; 
      O     "M_Ruecksetze_Fehler"; 
      R     "M_Kab_Tuer_Entr_MFKTgesp"; 

      U     "DB_DM_M_FUNKTION".MX_M[29]; 
      R     "DB_DM_M_FUNKTION".MX_M[29]; 

NETWORK
TITLE =Freigabe Türe entriegeln

      U     "M_Kabine_verriegeln"; 
      U     "M_Hauptspindel_steht"; 
      U     "M_Hauptachsen_stehen"; 
      U     "M_Zusatzachsen_stehen"; 
      UN    "M_WZ_Reinigen_Aktiv"; // oder reinigen
      UN    "M_Programm_Aktiv"; 
      U     "Ta_WZW_Aktiv_beendet"; 
      =     #TX_Freigabe_Tuer; 
NETWORK
TITLE =Anforderung Türe auf

      U     "M_IM_Taste_Tuerver_entr"; 
      U     #TX_Freigabe_Tuer; 
      S     "MX_Anf_Tuer_Auf"; 
NETWORK
TITLE =Freigabe zum Türe entriegeln fehlt(Achsen stehen/Programm steht)
//Meldung
      U(    ; 
      U     "I_Kab_Tuer_Entr"; 
      UN    "M_MWZW_Tuer_Auf"; 
      O     "M_Kab_Tuer_Entr"; 
      )     ; 
      U     "M_Kabine_verriegeln"; 
      U(    ; 
      ON    "M_Hauptspindel_steht"; 
      ON    "M_Hauptachsen_stehen"; 
      ON    "M_Zusatzachsen_stehen"; 
      O     "M_WZ_Reinigen_Aktiv"; 
      O     "M_Programm_Aktiv"; 
      ON    "Ta_WZW_Aktiv_beendet"; 
      )     ; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510223_FRG_TUERE_ENTR; 
NETWORK
TITLE =Kabinentüre entriegeln

      U(    ; 
      U     "MX_Anf_Tuer_Auf"; 
      O     "M_Kab_Tuer_Entr"; 
      )     ; 
      U     #TX_Freigabe_Tuer; 
      U     "MX_SF_Frg_Tuer_auf"; 
      UN    "Mx_KSA_war_Aktiv"; 
      R     "M_Kab_Tuer_Entr"; 
      R     "M_Kabine_verriegeln"; 
      R     "MX_Anf_Tuer_Auf"; 
      SPB   tx1; 
NETWORK
TITLE =Kabinentüre verriegeln
//"DB_PLC_MD_DB20".UDHex._06_BIT7_KAB_ZU_VERRIE = 1:
//"DB_PLC_MD_DB20".UDHex._26_Bit7_Ext_Taste_Kab = 0:
//Arbeitraumtür wird verriegelt, wenn
//a) Tür zu & Maschine wird eingeschaltet
//b) Maschine ist eingeschaltet +++ Tür wird zu gemacht
//
//
//"DB_PLC_MD_DB20".UDHex._06_BIT7_KAB_ZU_VERRIE = 0:
//"DB_PLC_MD_DB20".UDHex._26_Bit7_Ext_Taste_Kab = 0:
//Arbeitraumtür wird verriegelt, wenn
//a) Tür ist zu +++ Taste Arbeitraumtür öffnen/schließen
//
//
//"DB_PLC_MD_DB20".UDHex._06_BIT7_KAB_ZU_VERRIE = 0:
//"DB_PLC_MD_DB20".UDHex._26_Bit7_Ext_Taste_Kab = 1:
//Arbeitraumtür wird verriegelt, wenn
//a) Tür ist zu +++ Externe Taste Arbeitraumtür verriegeln
      U(    ; 
      U(    ; 
      U     "M_MA_FP_Maschine_Ein"; 
      U     "I_AL_Kabine_zu"; 
      O(    ; 
      U     "I_AL_Kabine_zu"; 
      FP    "M_Tuer_Trigg1"; 
      U     "M_MA_Maschine_Ein"; 
      )     ; 
      )     ; 
      U     "DB_PLC_MD_DB20".UDHex._06_BIT7_KAB_ZU_VERRIE; 
      O(    ; 
      U(    ; 
      U     "M_IM_Taste_Tuerver_entr"; 
      UN    "DB_PLC_MD_DB20".UDHex._26_Bit7_Ext_Taste_Kab; 
      O(    ; 
      U     "I_WA_ENABLE_LOCKED_CNC"; 
      FP    "M_FP_ext_Taste_Tuer_verr"; 
      U     "DB_PLC_MD_DB20".UDHex._26_Bit7_Ext_Taste_Kab; 
      )     ; 
      )     ; 
      U     "I_AL_Kabine_zu"; 
      )     ; 
      )     ; 
      UN    "M_Kabine_verriegeln"; 
      S     "M_Kabine_verriegeln"; 
NETWORK
TITLE =M-AUSGANG: Magnetverriegelung Kabinentüre entriegeln

      UN    "M_Kabine_verriegeln"; 
      UN    "DB_SAFE_ZD".GR1_ZD.Tuere_zuhalten; // Tuere zuhalten
      =     "O_AL_KABINE_ENTRIEGELN"; 
NETWORK
TITLE =MA: Kabinentür entriegeln

      UN    "I_AL_Kabine_Verriegelt"; 
      R     "M_Kab_Tuer_Entr"; 
NETWORK
TITLE =Fehlerüberwachung Tür zu

      UN    "O_AL_KABINE_ENTRIEGELN"; 
      UN    "I_AL_Kabine_zu"; 
      UN    "M_3Te_Betriebsart"; 
      UN    "I_AL_4_Betriebsart"; 
      UN    "I_AL_2_Betriebsart"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700720_Tuer_Zu_0; 
NETWORK
TITLE =Fehlerüberwachung Tür entriegeln

tx1:  U(    ; 
      U     "O_AL_KABINE_ENTRIEGELN"; 
      L     S5T#3S; // Zeitbasis 2s laden
      SE    "Te_Tuer_zu"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      U     "Te_Tuer_zu"; 
      )     ; 
      U     "I_AL_Kabine_Verriegelt"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700721_TuerVer_0Sig; 
      R     "M_Kabine_verriegeln"; 
NETWORK
TITLE =Kabinentüre entriegeln bei Maschine Aus

      UN    "M_MA_Maschine_Ein"; 
      U     "M_Hauptspindel_steht"; 
      U     "M_Hauptachsen_stehen"; 
      U     "M_Zusatzachsen_stehen"; 
      R     "M_Kabine_verriegeln"; 
NETWORK
TITLE =Meldung: Kabinentüre entriegelt
//Spritzschutztuer zu bilden
      UN    "I_AL_Kabine_Verriegelt"; 
      U     "I_AL_Kabine_zu"; 
      =     "DB_FEHLERMELDUNG".MELDUNG3._700450_RPT; 
NETWORK
TITLE =Kabinentüre geschlossen und verriegelt

      U     "I_AL_Kabine_zu"; 
      U     "I_AL_Kabine_Verriegelt"; 
      =     "M_Spritz_Tuer_Zu"; 
NETWORK
TITLE =Sammelsignal: Alle Schutztüren geschlossen und verriegelt

      U     "I_AL_Kabine_zu"; 
      UN    "O_AL_KABINE_ENTRIEGELN"; 
      U     "I_WM_Beladung_zu"; 
      UN    "O_WM_Beladung_entr_auf"; 
      U(    ; 
      U     "I_PW_Tueren_zu"; 
      U     "O_PW_TUER_ENTRIEGELN"; 
      UN    "MX_ABST_Rundspeicher"; 
      O     ; 
      U(    ; 
      U     "I_PS_RPL_TUER_VORNE_ZU"; 
      U     "I_PS_RPL_TUER_VORN_VERR"; 
      UN    "O_PS_RPL_TUER_VORN_ENTR"; 
      O     ; 
      U     "I_PS_RPL_TUER_HINTEN_ZU"; 
      U     "I_PS_RPL_TUER_HINT_VERR"; 
      UN    "O_PS_RPL_TUER_HINT_ENTR"; 
      )     ; 
      U     "MX_ABST_Rundspeicher"; 

      )     ; 
      =     "m_tueren_ges_geschl+verr"; 
END_FUNCTION

