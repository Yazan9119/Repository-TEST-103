FUNCTION "FC_ACHSE_RUND_ZUSATZ" : VOID
TITLE =
//$Revision: 1.16 $
//$Date: 2008/11/04 15:38:54CET $
//$Author: sth $
AUTHOR : FUH
VERSION : 0.1


VAR_INPUT
  EX_Enable : BOOL ;	//Bausteinfreigabe
  EX_Reset_Taste : BOOL ;	
  EX_Ruecksetze_Fehler : BOOL ;	//Fehler löschen
  EX_Init : BOOL ;	//Initialisierung OB-Zyklus
  EX_Option_FD : BOOL ;	//Option Fräs-Drehen vorhanden
  EX_Resetstatus_Klemmung : BOOL ;	
  EB_Achs_Typ : BYTE ;	//Achstyp: Achs-Typ: 0,1 Anzeige, 2=perm Geregelt, 3=mit Klemmung
  EX_Ax_Spindel : BOOL ;	//Achse in Spindelbetrieb
  EX_Kabine_zu : BOOL ;	//Kabinentür geschlossen
  EX_Leistung_steht_an : BOOL ;	//Leistung steht an
  EX_BHG_aktiv : BOOL ;	//Minibedienhandgerät aktiv
  EX_Klemmung_FD_geloest : BOOL ;	//FD: Klemmung gelöst
  EX_Klemmung_FD_geklemmt : BOOL ;	//FD: Klemmung geklemmt
  EX_Klemmung_geloest : BOOL ;	//Normale Klemmung gelöst
  EX_Klemmung_geklemmt : BOOL ;	//Normale Klemmung geklemmt
  EX_Klemmung_loesen : BOOL ;	//Befehl Klemmung lösen
  EX_Rundachsanf_aktiv : BOOL ;	//Klemmen über M-Funktion aktiv
  ET_Timer_Fehlerueb : TIMER ;	//Timer für Fehlerüberwachung
  EX_Opt_Dru_erh_Tisch : BOOL ;	//Option Druckerhoehung Tischklemmung 
  EX_1s_Takt : BOOL ;	//Taktsignal 1Sek
  EX_Maschine_Ein : BOOL ;	//Maschine Ein
  EX_Kle_Dru_Ventil : BOOL ;	//Klemmung geklemmt Druckerhoehung Tischklemmung
  EX_Fahranforderung_Plus : BOOL ;	
  EX_Fahranforderung_Minus : BOOL ;	
END_VAR
VAR_OUTPUT
  AX_Achse_geloest : BOOL ;	//Klemmung Achse gelöst
  AX_Achse_geklemmt : BOOL ;	//Klemmung Achse geklemmt
  AX_PLC_NC_FD_geloest : BOOL ;	//PLC-NC: Klemmung gelöst
  AX_PLC_NC_FD_geklemmt : BOOL ;	//PLC-NC: Klemmung geklemmt
  AX_Klemmung_offen_halten : BOOL ;	//Klemmung offen halten
  AX_FRG_Spindelmodul : BOOL ;	//Freigabe Spindelmodul
  AX_FRG_Achsmodul : BOOL ;	//Freigabe Achsmodul
  AX_FD_Klemmung_loesen : BOOL ;	//Ausgang FD-Tisch Klemmung lösen
  AX_Tischklemmung_loesen : BOOL ;	//Ausgang normale Tischklemmung lösen
  AX_Fehler_Kl_beide_0_Sig : BOOL ;	//Fehler Klemmung beide 0-Signal
  AX_Fehler_Kl_beide_1_Sig : BOOL ;	//Fehler Klemmung beide 1-Signal
  AX_Tischklemmung_loese : BOOL ;	//Tischklemmung loesen bei Option Druckerhoehung 180 Bar 
END_VAR
VAR_IN_OUT
  EAX_FP_Ax_Spindel : BOOL ;	
  EAX_M_Fkt_Klemm_loesen : BOOL ;	
  AB_Spindel_Override : BYTE ;	//Spindeloverride
  EAX_FP_Tischkl_loesen : BOOL ;	
  EAX_FP_1s_Takt : BOOL ;	
  EAX_Fehler_Klemmung : BOOL ;	
  EAB_Zeit_Klem : BYTE ;	
  EAB_Anzahl_Takt : BYTE ;	
END_VAR
VAR_TEMP
  TX_Fehler_beide_1Sig : BOOL ;	
  TX_Fehler_beide_0Sig : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Bausteinfreigabe

      U     #EX_Enable; 
      U(    ; 
      L     #EB_Achs_Typ; 
      L     3; 
      ==I   ; 
      O(    ; 
      L     #EB_Achs_Typ; 
      L     4; 
      ==I   ; 
      )     ; 
      )     ; 
      SPB   M000; 
      S     #AX_FRG_Achsmodul; 
      R     #AX_Fehler_Kl_beide_1_Sig; 
      R     #AX_Fehler_Kl_beide_0_Sig; 
      S     #AX_Klemmung_offen_halten; 
      S     #AX_Tischklemmung_loesen; 
      BEA   ; 
NETWORK
TITLE =Fehler ablöschen

M000: U     #EX_Ruecksetze_Fehler; 
      R     #AX_Fehler_Kl_beide_1_Sig; 
      R     #AX_Fehler_Kl_beide_0_Sig; 
NETWORK
TITLE =Überwachung Messtaster ausserhalb Zyklus aktiv

      U     "M_PW_Aktiv"; 
      FN    "M_FN_PW_AKTIV"; 
      =     "M_IMP_FN_PW_AKTIV"; 

      U     "M_IMP_FN_PW_AKTIV"; 
      O     "M_8_ter_OB1_Zyklen"; 
      U     #EX_Resetstatus_Klemmung; 
      S     "Mx_ReqKlemm_Grundzustand"; 
NETWORK
TITLE =Klemmung nach Fahranforderung lösen

      U(    ; 
      O     #EX_Fahranforderung_Plus; 
      O     #EX_Fahranforderung_Minus; 
      )     ; 
      UN    "DB_SIEM_BAG".E_REF; 
      U     "DB_SIEM_KANAL_1".E_AxesRef; 
      U     #EX_Resetstatus_Klemmung; 
      U     "Mx_ReqKlemm_Grundzustand"; 
      S     #EAX_M_Fkt_Klemm_loesen; 
      R     "Mx_ReqKlemm_Grundzustand"; 
NETWORK
TITLE =Meldung an NC für FD-Klemmung gelöst

      U     #EX_Option_FD; 
      U     #EX_Klemmung_FD_geloest; 
      UN    #EX_Klemmung_FD_geklemmt; 
      UN    #EX_Rundachsanf_aktiv; 
      =     #AX_PLC_NC_FD_geloest; 
NETWORK
TITLE =Meldung an NC für FD-Klemmung geklemmt

      U     #EX_Option_FD; 
      U     #EX_Klemmung_FD_geklemmt; 
      UN    #EX_Klemmung_FD_geloest; 
      UN    #EX_Rundachsanf_aktiv; 
      =     #AX_PLC_NC_FD_geklemmt; 
NETWORK
TITLE =Option FD

      U     #EX_Option_FD; 
      SPBN  M001; 
NETWORK
TITLE =FD Klemmung Achse gelöst --> Freigabe für Achsmodul

      U     #EX_Klemmung_FD_geloest; 
      UN    #EX_Klemmung_FD_geklemmt; 
      =     #AX_Achse_geloest; 
NETWORK
TITLE =FD Klemmung Achse geklemmt --> Freigabe für Achsmodul

      U     #EX_Klemmung_FD_geklemmt; 
      UN    #EX_Klemmung_FD_geloest; 
      =     #AX_Achse_geklemmt; 
NETWORK
TITLE =Sprung 

      SPA   M002; 

NETWORK
TITLE =Klemmung Achse gelöst --> Freigabe für Achsmodul

M001: U     #EX_Klemmung_geloest; 
      UN    #EX_Klemmung_geklemmt; 
      =     #AX_Achse_geloest; 
NETWORK
TITLE =Klemmung Achse geklemmt --> Freigabe für Achsmodul

      U     #EX_Klemmung_geklemmt; 
      UN    #EX_Klemmung_geloest; 
      =     #AX_Achse_geklemmt; 
NETWORK
TITLE =Ausgang FD Tisch Klemmung lösen

M002: U     #EX_Option_FD; 
      U     #EX_Klemmung_loesen; 
      =     #AX_FD_Klemmung_loesen; 
NETWORK
TITLE =Option Druckerhoehung Tischklemmung 180 Bar

      U     #EX_Opt_Dru_erh_Tisch; 
      SPB   DRU; 
NETWORK
TITLE =Tischklemmung 120Bar

      UN    #EX_Opt_Dru_erh_Tisch; 
      UN    #EX_Option_FD; 
      U     #EX_Klemmung_loesen; 
      =     #AX_Tischklemmung_loesen; 
NETWORK
TITLE =Sprungmarke normale Tischklemmung 120Bar

      UN    #EX_Opt_Dru_erh_Tisch; 
      SPB   NOR; 
NETWORK
TITLE =Tischklemmung loesen Druckerhoehung

DRU:  UN    #EX_Option_FD; 
      U     #EX_Klemmung_loesen; 
      U     #EX_Opt_Dru_erh_Tisch; 
      =     #AX_Tischklemmung_loese; 
NETWORK
TITLE =Tischklemmung klemmen -> Druckerhoehung 

      U(    ; 
      UN    #EAX_Fehler_Klemmung; 
      UN    #AX_Tischklemmung_loese; 
      U     #EX_Maschine_Ein; 
      UN    #EX_Kle_Dru_Ventil; 
      U     #EX_1s_Takt; 
      O     ; 
      UN    #EAX_Fehler_Klemmung; 
      U     #EX_Maschine_Ein; 
      UN    #AX_Tischklemmung_loese; 
      U     #EX_Kle_Dru_Ventil; 
      )     ; 
      U     #EX_Opt_Dru_erh_Tisch; 


      NOT   ; 
      =     #AX_Tischklemmung_loesen; 

NETWORK
TITLE =Anzahl Druckerhoehuung Tisch-Achse 200Bar hochzaehlen

      U     #AX_Tischklemmung_loesen; 
      FP    #EAX_FP_Tischkl_loesen; 
      SPBN  nnn; 


      L     #EAB_Anzahl_Takt; 
      INC   1; 
      T     #EAB_Anzahl_Takt; 


nnn:  NOP   0; 

NETWORK
TITLE =Zeit und Fehlerauswertung Tisch-Achse 200Bar 

      U     #EX_1s_Takt; 
      FP    #EAX_FP_1s_Takt; 
      SPBN  mmm; 

      L     #EAB_Zeit_Klem; 
      INC   1; 
      T     #EAB_Zeit_Klem; 
      L     21; 
      ==I   ; 
      SPBN  mmm; 

      L     1; 
      T     #EAB_Zeit_Klem; 


      U(    ; 
mmm:  L     #EAB_Zeit_Klem; 
      L     20; 
      ==I   ; 
      )     ; 
      O     #EX_Reset_Taste; 
      SPBN  ccc; 
      L     0; 
      T     #EAB_Anzahl_Takt; 


ccc:  L     #EAB_Anzahl_Takt; 
      L     18; 
      >=I   ; 
      S     #EAX_Fehler_Klemmung; 

NETWORK
TITLE =Achs- oder Spindelbetrieb

NOR:  U     #EX_Ax_Spindel; 
      =     #AX_FRG_Spindelmodul; 
      NOT   ; 
      =     #AX_FRG_Achsmodul; 
NETWORK
TITLE =Klemmung generell offen halten

      O     #EX_Ax_Spindel; 
      O     ; 
      U     #EX_BHG_aktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      UN    #EX_Kabine_zu; 
      U     #EX_Leistung_steht_an; 
      =     #AX_Klemmung_offen_halten; 
NETWORK
TITLE =Spindeloverride schreiben

      U(    ; 
      U     #EX_Ax_Spindel; 
      FP    #EAX_FP_Ax_Spindel; 
      O     #EX_Init; 
      )     ; 
      SPBNB _001; 
      L     B#16#E; 
      T     #AB_Spindel_Override; 
_001: NOP   0; 
NETWORK
TITLE =Fehler: Klemmung beide 1 Signal

      U     #EX_Option_FD; 
      U     #EX_Klemmung_FD_geloest; 
      U     #EX_Klemmung_FD_geklemmt; 
      O     ; 
      UN    #EX_Option_FD; 
      U     #EX_Klemmung_geloest; 
      U     #EX_Klemmung_geklemmt; 
      =     #TX_Fehler_beide_1Sig; 
NETWORK
TITLE =Fehler: Klemmung beide 0 Signal

      U     #EX_Option_FD; 
      UN    #EX_Klemmung_FD_geloest; 
      UN    #EX_Klemmung_FD_geklemmt; 
      O     ; 
      UN    #EX_Option_FD; 
      UN    #EX_Klemmung_geloest; 
      UN    #EX_Klemmung_geklemmt; 
      =     #TX_Fehler_beide_0Sig; 
NETWORK
TITLE =Timer Fehlerüberwachung

      U(    ; 
      O     #TX_Fehler_beide_1Sig; 
      O     #TX_Fehler_beide_0Sig; 
      )     ; 
      L     S5T#1S; 
      SE    #ET_Timer_Fehlerueb; 
      NOP   0; 
      NOP   0; 

      NOP   0; 
      NOP   0; 
NETWORK
TITLE =Fehlerausgabe: Klemmung beide 1 Signal

      U     #TX_Fehler_beide_1Sig; 
      U     #ET_Timer_Fehlerueb; 
      S     #AX_Fehler_Kl_beide_1_Sig; 
NETWORK
TITLE =Fehlerausgabe: Klemmung beide 0 Signal

      U     #TX_Fehler_beide_0Sig; 
      U     #ET_Timer_Fehlerueb; 
      S     #AX_Fehler_Kl_beide_0_Sig; 
END_FUNCTION

