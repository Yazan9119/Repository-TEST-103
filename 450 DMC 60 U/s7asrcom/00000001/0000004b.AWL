FUNCTION "FC_TEMP_FUNKTIONEN" : VOID
TITLE =
//$Revision: 1.6.1.1 $
//$Date: 2008/01/14 10:06:20CET $
//$Author: sth $

//
//Funktion_1 ... Auswerten von Maschinenparametern
VERSION : 0.1


VAR_INPUT
  int_funktion : INT ;	//Identifikation der Funktion (Integer)
  int_mp : INT ;	//Maschinenparameter (Adresse)
  int_dekade : INT ;	//Dekade (Integer)
  int_wert : INT ;	//Wert (Integer)
END_VAR
VAR_OUTPUT
  boo_ergebnis : BOOL ;	//Rückgabewert (0= MP ist nicht gesetzt / 1= MP ist gesetzt)
END_VAR
BEGIN
NETWORK
TITLE =Auswerten von Maschinenparametern (int_funktion = 1)
//Definition der Dekaden eines Wortes (4/3/2/1)
//Mittels dieser Funktion kann überprüft werden ob eine beliebiger "wert" (0 ... 9
//) in einer beliebigen "dekade" (1 ... 4) im Maschinenparameter "mp" gesetzt 
//wurde. Als Ergebnis wird der Wert TRUE oder FALSE zurückgegeben.
//Entspricht der übergebene Parameter in Dekade nicht 1 ... 4 wird FALSE 
//zurückgegeben.
//... erstellt am 19.10.2001 (Andreas Petz)
// ------------------------------------------
      L     #int_funktion; 
      L     1; 
      ==I   ; 
      SPBN  END1; 

      L     #int_dekade; //lade Wert in Akku1-L-L
      SPL   F1_0; //Akku1-L-L > 4
      SPA   F1_0; //Akku1-L-L = 0 
      SPA   F1_1; //Akku1-L-L = 1
      SPA   F1_2; //Akku1-L-L = 2
      SPA   F1_3; //Akku1-L-L = 3
      SPA   F1_4; //Akku1-L-L = 4

//... ungültige Werte
F1_0: SET   ; 
      R     #boo_ergebnis; 
      SPA   END1; 

//... Dekade 1
F1_1: L     #int_mp; 
      ITD   ; 
      DTB   ; 
      SLD   28; 
      SRD   28; 
      BTD   ; 
      L     #int_wert; 
      ==I   ; 
      =     #boo_ergebnis; 
      SPA   END1; 

//... Dekade 2
F1_2: L     #int_mp; 
      ITD   ; 
      DTB   ; 
      SLD   24; 
      SRD   28; 
      BTD   ; 
      L     #int_wert; 
      ==I   ; 
      =     #boo_ergebnis; 
      SPA   END1; 

//... Dekade 3
F1_3: L     #int_mp; 
      ITD   ; 
      DTB   ; 
      SLD   20; 
      SRD   28; 
      BTD   ; 
      L     #int_wert; 
      ==I   ; 
      =     #boo_ergebnis; 
      SPA   END1; 

//... Dekade 4
F1_4: L     #int_mp; 
      ITD   ; 
      DTB   ; 
      SLD   16; 
      SRD   28; 
      BTD   ; 
      L     #int_wert; 
      ==I   ; 
      =     #boo_ergebnis; 
      SPA   END1; 

END1: BEA   ; 




END_FUNCTION

