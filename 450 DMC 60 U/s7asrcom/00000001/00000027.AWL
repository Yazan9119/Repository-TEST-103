FUNCTION "FC_WZVS_ARTIS" : VOID
TITLE =
//$Revision: 1.5 $
//$Date: 2008/04/01 16:55:44CEST $
//$Author: riedlh $
//
// Werkzeug Verschleisskontrolle Artis
AUTHOR : PS
FAMILY : DMC_H
NAME : WVS_ART
VERSION : 0.0


VAR_INPUT
  Freigabe : BOOL ;	
  Start_K1 : BOOL ;	
  Start_K2 : BOOL ;	
  Start_K3 : BOOL ;	
  Start_K4 : BOOL ;	
  Lernschnitt : BOOL ;	
  Artis_aus : BOOL ;	
  Reset : BOOL ;	
  T_Reset : BOOL ;	
  VS_OVR_100 : BOOL ;	
  Prg_laeuft : BOOL ;	
  VS_Halt : BOOL ;	
  K3K4_Nacharbeit : BOOL ;	//OVR-Änderung: Kanal 3+4 Nacharbeit (2-PLC-Zyklen)
  PrgNr : INT ;	
  T_Nr : INT ;	
  D_Nr : INT ;	
  Schnitt_Nr : INT ;	
  PBAdresse : INT ;	
END_VAR
VAR_IN_OUT
  F_System : BOOL ;	
  F_Bruch : BOOL ;	
  F_stumpf : BOOL ;	
  F_fehlt : BOOL ;	
  F_Nacharbeit : BOOL ;	
  F_Beruehrung_M : BOOL ;	
  F_Alarme_aus : BOOL ;	
  F_Artis_aus : BOOL ;	
  F_Sammelalarm : BOOL ;	
  F_Kanal_1 : BOOL ;	
  F_Kanal_2 : BOOL ;	
  F_Kanal_3 : BOOL ;	
  F_Kanal_4 : BOOL ;	
  ACC_OVR : INT ;	
END_VAR
VAR_TEMP
  OVR_100 : BOOL ;	
  Prglaeuft : BOOL ;	
  VSHalt : BOOL ;	
  Lernen : BOOL ;	
  Meldungen_aus : BOOL ;	
  PBStart_E : DWORD ;	
  PBStart_A : DWORD ;	
  E_Kanal_1 : STRUCT 	
   Systemfehler : BOOL ;	
   BruchAlarm : BOOL ;	
   StumpfAlarm : BOOL ;	
   FehltAlarm : BOOL ;	
   TiefeAlarm : BOOL ;	
   Materialberuehrung : BOOL ;	
   Alarme_aus : BOOL ;	
   Sammelalarm : BOOL ;	
   ACC_OVR : BYTE ;	
  END_STRUCT ;	
  E_Kanal_2 : STRUCT 	
   Systemfehler : BOOL ;	
   BruchAlarm : BOOL ;	
   StumpfAlarm : BOOL ;	
   FehltAlarm : BOOL ;	
   TiefeAlarm : BOOL ;	
   Materialberuehrung : BOOL ;	
   Alarme_aus : BOOL ;	
   Sammelalarm : BOOL ;	
   ACC_OVR : BYTE ;	
  END_STRUCT ;	
  E_Kanal_3 : STRUCT 	
   Systemfehler : BOOL ;	
   BruchAlarm : BOOL ;	
   StumpfAlarm : BOOL ;	
   FehltAlarm : BOOL ;	
   TiefeAlarm : BOOL ;	
   Materialberuehrung : BOOL ;	
   Alarme_aus : BOOL ;	
   Sammelalarm : BOOL ;	
   ACC_OVR : BYTE ;	
  END_STRUCT ;	
  E_Kanal_4 : STRUCT 	
   Systemfehler : BOOL ;	
   BruchAlarm : BOOL ;	
   StumpfAlarm : BOOL ;	
   FehltAlarm : BOOL ;	
   TiefeAlarm : BOOL ;	
   Materialberuehrung : BOOL ;	
   Alarme_aus : BOOL ;	
   Sammelalarm : BOOL ;	
   ACC_OVR : BYTE ;	
  END_STRUCT ;	
  A_allgemein : STRUCT 	
   Programmende : BOOL ;	
   Programm_1_laden : BOOL ;	
   Programm_1_speichern : BOOL ;	
   frei_0_3 : BOOL ;	
   frei_0_4 : BOOL ;	
   frei_0_5 : BOOL ;	
   frei_0_6 : BOOL ;	
   frei_0_7 : BOOL ;	
  END_STRUCT ;	
  A_Kanal_1 : STRUCT 	
   Stufen : BOOL ;	
   Lernen : BOOL ;	
   Reset : BOOL ;	
   Vorschub_aktiv : BOOL ;	
   Nacharbeit : BOOL ;	
   Zaehlen : BOOL ;	
   Spindelueberwachung : BOOL ;	
   frei_0_7 : BOOL ;	
   frei_1_0 : BOOL ;	
   frei_1_1 : BOOL ;	
   frei_1_2 : BOOL ;	
   frei_1_3 : BOOL ;	
   frei_1_4 : BOOL ;	
   frei_1_5 : BOOL ;	
   frei_1_6 : BOOL ;	
   frei_1_7 : BOOL ;	
  END_STRUCT ;	
  A_Kanal_2 : STRUCT 	
   Stufen : BOOL ;	
   Lernen : BOOL ;	
   Reset : BOOL ;	
   Vorschub_aktiv : BOOL ;	
   Nacharbeit : BOOL ;	
   Zaehlen : BOOL ;	
   Spindelueberwachung : BOOL ;	
   frei_0_7 : BOOL ;	
   frei_1_0 : BOOL ;	
   frei_1_1 : BOOL ;	
   frei_1_2 : BOOL ;	
   frei_1_3 : BOOL ;	
   frei_1_4 : BOOL ;	
   frei_1_5 : BOOL ;	
   frei_1_6 : BOOL ;	
   frei_1_7 : BOOL ;	
  END_STRUCT ;	
  A_Kanal_3 : STRUCT 	
   Stufen : BOOL ;	
   Lernen : BOOL ;	
   Reset : BOOL ;	
   Vorschub_aktiv : BOOL ;	
   Nacharbeit : BOOL ;	
   Zaehlen : BOOL ;	
   Spindelueberwachung : BOOL ;	
   frei_0_7 : BOOL ;	
   frei_1_0 : BOOL ;	
   frei_1_1 : BOOL ;	
   frei_1_2 : BOOL ;	
   frei_1_3 : BOOL ;	
   frei_1_4 : BOOL ;	
   frei_1_5 : BOOL ;	
   frei_1_6 : BOOL ;	
   frei_1_7 : BOOL ;	
  END_STRUCT ;	
  A_Kanal_4 : STRUCT 	
   Stufen : BOOL ;	
   Lernen : BOOL ;	
   Reset : BOOL ;	
   Vorschub_aktiv : BOOL ;	
   Nacharbeit : BOOL ;	
   Zaehlen : BOOL ;	
   Spindelueberwachung : BOOL ;	
   frei_0_7 : BOOL ;	
   frei_1_0 : BOOL ;	
   frei_1_1 : BOOL ;	
   frei_1_2 : BOOL ;	
   frei_1_3 : BOOL ;	
   frei_1_4 : BOOL ;	
   frei_1_5 : BOOL ;	
   frei_1_6 : BOOL ;	
   frei_1_7 : BOOL ;	
  END_STRUCT ;	
  A_Prg_Nr : WORD ;	
  A_T_Nr : WORD ;	
  A_D_Nr : WORD ;	
  A_Schnitt_Nr : WORD ;	
  FC21_Error : BOOL ;	
  FC21_ErrCode : INT ;	
  Ret_AR1 : DWORD ;	
  Ret_AR2 : DWORD ;	
END_VAR
BEGIN
NETWORK
TITLE = Netzwerk 1: Retten Adressregister und Löschen Lokaldaten 
// Netzwerk-Übersicht
// ==================
// NW 1:  Netzwerk 1: Retten Adressregister und Löschen Lokaldaten 
// NW 2: Übernahme der Eingangsparameter
// NW 3: Übernahme der Eingänge vom Profibus auf Lokaldaten
// NW 4: Ausgabe ACC_OVR Kanal 1
// NW 5: Ausgabe ACC_OVR Kanal 2
// NW 6: Ausgabe ACC_OVR Kanal 3
// NW 7: Ausgabe ACC_OVR Kanal 4
// NW 8: Keine Fehlermeldung
// NW 9: Fehlerauswertung Artis ausgeschaltet
// NW 10: Fehlerauswertung Tiefe
// NW 11: Fehlerauswertung Nacharbeit OVR nicht 100%
// NW 12: Fehlerauswertung Alarme aus
// NW 13: Fehlerauswertung Systemfehler
// NW 14: Fehlerauswertung Bruch-Alarm
// NW 15: Fehlerauswertung Stumpf-Alarm
// NW 16: Fehlerauswertung Fehlt-Alarm
// NW 17: Fehlerauswertung Materialberührung
// NW 18: Fehlerauswertung Voralarm
// NW 19: Fehlerauswertung Kanal
// NW 20: Allgemeine Ausgangsbits an Artis
// NW 21: Kanal 1 lernen
// NW 22: Kanal 2 lernen
// NW 23: Kanal 3 lernen
// NW 24: Kanal 4 lernen
// NW 25: Kanal 1 Vorschub aktiv
// NW 26: Kanal 2 Vorschub aktiv
// NW 27: Kanal 3 Vorschub aktiv
// NW 28: Kanal 4 Vorschub aktiv
// NW 29: Kanal 1 Nacharbeit
// NW 30: Kanal 2 Nacharbeit
// NW 31: Kanal 3 Nacharbeit
// NW 32: Kanal 4 Nacharbeit
// NW 33: Adressregister für Ausgabe an Profibus setzen
// NW 34: Übergabe Programmnummer Kanal 1 an Profibus
// NW 35: Übergabe T-Nummer Kanal 1 an Profibus
// NW 36: Übergabe D-Nummer an Profibus
// NW 37: Übergabe Bearbeitungsabschnitt an Profibus
// NW 38: Übergabe Bits an Profibus
// NW 39: Zurückschreiben Adressregister

      UN    #Freigabe; 
      BEB   ; 

      TAR1  #Ret_AR1; 
      TAR2  #Ret_AR2; 

      LAR2  P##A_allgemein; 

      L     0; 
      T     LW [AR2,P#0.0]; 
      T     LW [AR2,P#2.0]; 
      T     LW [AR2,P#4.0]; 
      T     LW [AR2,P#6.0]; 
      T     LW [AR2,P#8.0]; 

NETWORK
TITLE =Übernahme der Eingangsparameter

      L     #PBAdresse; 
      L     40; 
      +D    ; 
      T     #PBStart_E; 

      L     #PBAdresse; 
      L     42; 
      +D    ; 
      T     #PBStart_A; 

      U     #VS_OVR_100; 
      =     #OVR_100; 

      U     #Prg_laeuft; 
      =     #Prglaeuft; 

      U     #VS_Halt; 
      =     #VSHalt; 

      U     #Lernschnitt; 
      =     #Lernen; 

NETWORK
TITLE =Übernahme der Eingänge vom Profibus auf Lokaldaten

      L     #PBStart_E; 
      SLD   3; 
      LAR1  ; 

      LAR2  P##E_Kanal_1; 

      L     PEB [AR1,P#0.0]; // Alarme Kanal 1
      T     LB [AR2,P#0.0]; 

      L     PEB [AR1,P#1.0]; // Alarme Kanal 2
      T     LB [AR2,P#2.0]; 

      L     PEB [AR1,P#2.0]; // Alarme Kanal 3
      T     LB [AR2,P#4.0]; 

      L     PEB [AR1,P#3.0]; // Alarme Kanal 4
      T     LB [AR2,P#6.0]; 

      L     PEB [AR1,P#4.0]; // ACC Kanal 1
      T     LB [AR2,P#1.0]; 

      L     PEB [AR1,P#5.0]; // ACC Kanal 2
      T     LB [AR2,P#3.0]; 

      L     PEB [AR1,P#6.0]; // ACC Kanal 3
      T     LB [AR2,P#5.0]; 

      L     PEB [AR1,P#7.0]; // ACC Kanal 4
      T     LB [AR2,P#7.0]; 

NETWORK
TITLE =Ausgabe ACC_OVR Kanal 1

      U     #Start_K1; 
      SPBN  OVE1; 

      L     #E_Kanal_1.ACC_OVR; 
      T     #ACC_OVR; 

OVE1: NOP   0; 
NETWORK
TITLE =Ausgabe ACC_OVR Kanal 2

      U     #Start_K2; 
      SPBN  OVE2; 

      L     #E_Kanal_2.ACC_OVR; 
      T     #ACC_OVR; 

OVE2: NOP   0; 
NETWORK
TITLE =Ausgabe ACC_OVR Kanal 3

      U     #Start_K3; 
      SPBN  OVE3; 

      L     #E_Kanal_3.ACC_OVR; 
      T     #ACC_OVR; 

OVE3: NOP   0; 
NETWORK
TITLE =Ausgabe ACC_OVR Kanal 4

      U     #Start_K4; 
      SPBN  OVE4; 

      L     #E_Kanal_4.ACC_OVR; 
      T     #ACC_OVR; 

OVE4: NOP   0; 
NETWORK
TITLE =Keine Fehlermeldung

      U     #E_Kanal_1.Alarme_aus; 
      U     #E_Kanal_2.Alarme_aus; 
      U     #E_Kanal_3.Alarme_aus; 
      U     #E_Kanal_4.Alarme_aus; 
      U     #Artis_aus; 
      =     #Meldungen_aus; 

NETWORK
TITLE =Fehlerauswertung Artis ausgeschaltet

      U     #Artis_aus; 
      UN    #Meldungen_aus; 
      =     #F_Artis_aus; 

NETWORK
TITLE =Fehlerauswertung Tiefe
// wird nicht benutzt, da der Fehler gemeldet werden kann, ohne dass es eine 
//Auswirkung haben soll.
//      U     #E_Kanal_1.TiefeAlarm
//      O     #E_Kanal_2.TiefeAlarm
//      O     #E_Kanal_3.TiefeAlarm
//      O     #E_Kanal_4.TiefeAlarm
//      =     #F_Tiefe
      O     #E_Kanal_1.Sammelalarm; 
      O     #E_Kanal_2.Sammelalarm; 
      O     #E_Kanal_3.Sammelalarm; 
      O     #E_Kanal_4.Sammelalarm; 
      =     #F_Sammelalarm; 
NETWORK
TITLE =Fehlerauswertung Nacharbeit OVR nicht 100%
//Kanal 1: immer, wenn OVR-Poti ungleich 100% und Kanal aktiv
//Kanal 2: immer, wenn OVR-Poti ungleich 100% und Kanal aktiv
//Kanal 3: keine
//Kanal 4: keine
      U(    ; 
      O     #Start_K1; 
      O     #Start_K2; 
      )     ; 
      UN    #VS_OVR_100; 
      UN    #Artis_aus; 
      UN    #Meldungen_aus; 
      U     #Prg_laeuft; 
      U     "M_WZVS_M77_aktiviert"; 
      =     #F_Nacharbeit; 
NETWORK
TITLE =Fehlerauswertung Alarme aus

      U(    ; 
      O     #E_Kanal_1.Alarme_aus; 
      O     #E_Kanal_2.Alarme_aus; 
      O     #E_Kanal_3.Alarme_aus; 
      O     #E_Kanal_4.Alarme_aus; 
      )     ; 
      UN    #Meldungen_aus; 
      =     #F_Alarme_aus; 
NETWORK
TITLE =Fehlerauswertung Systemfehler

      U(    ; 
      O     #E_Kanal_1.Systemfehler; 
      O     #E_Kanal_2.Systemfehler; 
      O     #E_Kanal_3.Systemfehler; 
      O     #E_Kanal_4.Systemfehler; 
      )     ; 
      UN    #Meldungen_aus; 
      =     #F_System; 

NETWORK
TITLE =Fehlerauswertung Bruch-Alarm

      U(    ; 
      O     #E_Kanal_1.BruchAlarm; 
      O     #E_Kanal_2.BruchAlarm; 
      O     #E_Kanal_3.BruchAlarm; 
      O     #E_Kanal_4.BruchAlarm; 
      )     ; 
      UN    #Meldungen_aus; 
      =     #F_Bruch; 
NETWORK
TITLE =Fehlerauswertung Stumpf-Alarm

      U(    ; 
      O     #E_Kanal_1.StumpfAlarm; 
      O     #E_Kanal_2.StumpfAlarm; 
      O     #E_Kanal_3.StumpfAlarm; 
      O     #E_Kanal_4.StumpfAlarm; 
      )     ; 
      UN    #Meldungen_aus; 
      =     #F_stumpf; 
NETWORK
TITLE =Fehlerauswertung Fehlt-Alarm

      U(    ; 
      O     #E_Kanal_1.FehltAlarm; 
      O     #E_Kanal_2.FehltAlarm; 
      O     #E_Kanal_3.FehltAlarm; 
      O     #E_Kanal_4.FehltAlarm; 
      )     ; 
      UN    #Meldungen_aus; 
      =     #F_fehlt; 
NETWORK
TITLE =Fehlerauswertung Materialberührung
//darf nicht benutzt werden, da sonst die Impulsauswertung nicht benutzt werden 
//kann
//      U(    
//      U     #E_Kanal_1.Materialberuehrung
//      O     #E_Kanal_2.Materialberuehrung
//      O     #E_Kanal_3.Materialberuehrung
//      O     #E_Kanal_4.Materialberuehrung
//      )     
//      UN    #Meldungen_aus
//      =     #F_Beruehrung_M
//


NETWORK
TITLE =Fehlerauswertung Voralarm
//      U(    
//      U     #E_Kanal_1.Voralarm
//      O     #E_Kanal_2.Voralarm
//      O     #E_Kanal_3.Voralarm
//      O     #E_Kanal_4.Voralarm
//      )     
//      UN    #Meldungen_aus
      CLR   ; 
      =     #F_Sammelalarm; 
NETWORK
TITLE =Fehlerauswertung Kanal

      L     LB [AR2,P#0.0]; 
      L     0; 
      <>I   ; 
      UN    #E_Kanal_1.Alarme_aus; 
      =     #F_Kanal_1; 

      L     LB [AR2,P#2.0]; 
      <>I   ; 
      UN    #E_Kanal_2.Alarme_aus; 
      =     #F_Kanal_2; 

      TAK   ; 
      L     LB [AR2,P#4.0]; 
      <>I   ; 
      UN    #E_Kanal_3.Alarme_aus; 
      =     #F_Kanal_3; 

      TAK   ; 
      L     LB [AR2,P#6.0]; 
      <>I   ; 
      UN    #E_Kanal_4.Alarme_aus; 
      =     #F_Kanal_4; 

NETWORK
TITLE =Allgemeine Ausgangsbits an Artis
// Programmende
// Minimale Zeitdauer 2 Zyklen
      U     #Reset; // M2 M30 Reset verlängert
      =     #A_allgemein.Programmende; 

      U     #T_Reset; 
      =     #A_Kanal_1.Reset; 
      =     #A_Kanal_2.Reset; 
      =     #A_Kanal_3.Reset; 
      =     #A_Kanal_4.Reset; 

NETWORK
TITLE =Kanal 1 lernen

      U     #Lernen; 
      =     #A_Kanal_1.Lernen; 

NETWORK
TITLE =Kanal 2 lernen

      U     #Lernen; 
      =     #A_Kanal_2.Lernen; 

NETWORK
TITLE =Kanal 3 lernen

      U     #Lernen; 
      =     #A_Kanal_3.Lernen; 

NETWORK
TITLE =Kanal 4 lernen

      U     #Lernen; 
      =     #A_Kanal_4.Lernen; 

NETWORK
TITLE =Kanal 1 Vorschub aktiv

      U     #Start_K1; 
      U     #Prglaeuft; 
      UN    #VSHalt; 
      UN    #Artis_aus; 
      UN    #E_Kanal_1.BruchAlarm; 
      =     #A_Kanal_1.Vorschub_aktiv; 

NETWORK
TITLE =Kanal 2 Vorschub aktiv

      U     #Start_K2; 
      U     #Prglaeuft; 
      UN    #VSHalt; 
      UN    #Artis_aus; 
      UN    #E_Kanal_2.BruchAlarm; 
      =     #A_Kanal_2.Vorschub_aktiv; 

NETWORK
TITLE =Kanal 3 Vorschub aktiv

      U     #Start_K3; 
      U     #Prglaeuft; 
      UN    #VSHalt; 
      UN    #Artis_aus; 
      UN    #E_Kanal_3.BruchAlarm; 
      =     #A_Kanal_3.Vorschub_aktiv; 

NETWORK
TITLE =Kanal 4 Vorschub aktiv

      U     #Start_K4; 
      U     #Prglaeuft; 
      UN    #VSHalt; 
      UN    #Artis_aus; 
      UN    #E_Kanal_4.BruchAlarm; 
      =     #A_Kanal_4.Vorschub_aktiv; 

NETWORK
TITLE =Kanal 1 Nacharbeit

      UN    #OVR_100; 
      =     #A_Kanal_1.Nacharbeit; 

NETWORK
TITLE =Kanal 2 Nacharbeit

      UN    #OVR_100; 
      =     #A_Kanal_2.Nacharbeit; 

NETWORK
TITLE =Kanal 3 Nacharbeit

      U     #K3K4_Nacharbeit; 
      =     #A_Kanal_3.Nacharbeit; 

NETWORK
TITLE =Kanal 4 Nacharbeit

      U     #K3K4_Nacharbeit; 
      =     #A_Kanal_4.Nacharbeit; 

NETWORK
TITLE =Adressregister für Ausgabe an Profibus setzen

      L     #PBStart_A; 
      SLD   3; 
      LAR1  ; 

NETWORK
TITLE =Übergabe Programmnummer Kanal 1 an Profibus

      L     #PrgNr; 
      T     PAW [AR1,P#24.0]; 
      T     PAW [AR1,P#34.0]; 
      T     PAW [AR1,P#44.0]; 
      T     PAW [AR1,P#54.0]; 

NETWORK
TITLE =Übergabe T-Nummer Kanal 1 an Profibus

      L     #T_Nr; 
      T     PAW [AR1,P#26.0]; 
      T     PAW [AR1,P#36.0]; 
      T     PAW [AR1,P#46.0]; 
      T     PAW [AR1,P#56.0]; 

NETWORK
TITLE =Übergabe D-Nummer an Profibus

      L     #D_Nr; 
      T     PAW [AR1,P#28.0]; 
      T     PAW [AR1,P#38.0]; 
      T     PAW [AR1,P#48.0]; 
      T     PAW [AR1,P#58.0]; 

NETWORK
TITLE =Übergabe Bearbeitungsabschnitt an Profibus

      L     #Schnitt_Nr; 
      T     PAW [AR1,P#30.0]; 
      T     PAW [AR1,P#40.0]; 
      T     PAW [AR1,P#50.0]; 
      T     PAW [AR1,P#60.0]; 

NETWORK
TITLE =Übergabe Bits an Profibus

      LAR2  P##A_allgemein; 

      L     LB [AR2,P#0.0]; 
      T     PAB [AR1,P#0.0]; 

      L     LB [AR2,P#2.0]; 
      T     PAB [AR1,P#22.0]; 

      L     LB [AR2,P#4.0]; 
      T     PAB [AR1,P#32.0]; 

      L     LB [AR2,P#6.0]; 
      T     PAB [AR1,P#42.0]; 

      L     LB [AR2,P#8.0]; 
      T     PAB [AR1,P#52.0]; 

NETWORK
TITLE =Zurückschreiben Adressregister

      LAR1  #Ret_AR1; 
      LAR2  #Ret_AR2; 

END_FUNCTION

