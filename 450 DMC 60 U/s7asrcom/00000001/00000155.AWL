FUNCTION_BLOCK "FB_SMARTKEY_COM"
TITLE =
//$Revision: 1.6 $
//$Date: 2008/10/07 15:21:27CEST $
//$Author: wri $
//
{ S7_language := '7(1) Deutsch (Deutschland)  26.03.2008  08:00:15' }
VERSION : 0.1


VAR_INPUT
  EX_Enable : BOOL ;	//Aktivierung des Bausteins
  EX_Init : BOOL ;	//Initalisierung des Bausteins und damit in den Grundzustand zu setzen
  EX_Reset : BOOL ;	//Zurücksetzen des Fehlerzustandes
  EX_MaschineEin : BOOL ;	//Einschaltsignal der Maschine zum Start der Schrittkette
  EX_TagRemoved : BOOL ;	//Smartkey wurde entfernt. Signal um Schrittkette in den Grundzustand zu setzen
  EX_GEN_OM_ID_SYNC : BOOL ;	//Smartkeyreader-Eingang: Synchronisations-Impuls
  EX_GEN_OM_ID_CLOCK : BOOL ;	//Smartkeyreader-Eingang: Clock-Impuls für die Datenübertragung
  EX_GEN_OM_ID_DATA_0 : BOOL ;	//Smartkeyreader-Eingang: Datenleitung 0
  EX_GEN_OM_ID_DATA_1 : BOOL ;	//Smartkeyreader-Eingang: Datenleitung 1
  EB_Number_Attempt : BYTE ;	//Wert zur maximalen Anzahl an Versuchen zur Übertragung der Identnummer
END_VAR
VAR_IN_OUT
  EADW_UnicatNumber : DWORD ;	//Ausgabe der Unikatsnummer, die auf dem Tag abgedruckt ist
  EADW_ConsecutiveNumber : DWORD ;	//Ausgabe der laufenden Seriennummer. Die hintere 7stellige Nummer der Unicatsnr.
  EAB_AuthorizationNumber : BYTE ;	//Ausgabe der Berechtigungsnummer. Enthalten BA und Master/Service-Bit
  EAB_OperatingMode : BYTE ;	//Ausgabe der BA-Nummer
  EAX_MasterKey : BOOL ;	//Ausgabe des Master-Key-Bits
  EAX_ServiceKey : BOOL ;	//Ausgabe des Service-Key-Bits
  EAX_GEN_OM_ID_DATA_0 : BOOL ;	//Smartkeyreader-Ausgang: Bestätigung der Datenleitung 0
  EAX_GEN_OM_ID_DATA_1 : BOOL ;	//Smartkeyreader-Ausgang: Bestätigung der Datenleitung 1
  EAX_ErrorMaxAttempt : BOOL ;	//Fehleranzeige-Bit nach Erreichen der eingestellten max. Übertragungsversuche
  EAX_CommunicationReady : BOOL ;	//Bestätigungsbit nach erfolgreicher Übertragung der Identnummer
END_VAR
VAR
  SW_Schrittkette : WORD ;	//Schrittkettenzähler zur Übertragung der Identnummer
  SW_Receive_Data_0 : WORD ;	//Speichern der empfangenen Daten auf der Datenleitung 0
  SW_Receive_Data_1 : WORD ;	//Speichern der empfangenen Daten auf der Datenleitung 1
  SB_Data_Counter : BYTE ;	//Zähler für die empfangenen Bits auf der Datenleitung
  SB_Attempt_Counter : BYTE ;	//Zähler für die aktuellen Übertragungsversuche
  SX_FP_Tag_Removed : BOOL ;	//Positiver Zustandsflankenmerker für TAG entfernt
  SX_Tag_Removed : BOOL ;	//Positiver Flankenmerker für TAG entfernt
END_VAR
BEGIN
NETWORK
TITLE =

      UN    #EX_Enable; 
      BEB   ; 
NETWORK
TITLE =

      U     #EX_TagRemoved; 
      FP    #SX_FP_Tag_Removed; 
      =     #SX_Tag_Removed; 

NETWORK
TITLE =M-EINGANG: Smartkey -  Daten-Spur 1

      UN    #EX_MaschineEin; 
      O     #EX_Init; 
      O     #SX_Tag_Removed; 
      SPBN  INIT; 

      SET   ; 
      R     #EAX_ErrorMaxAttempt; 
      R     #EAX_CommunicationReady; 
      R     #EAX_MasterKey; 
      R     #EAX_ServiceKey; 
      R     #EAX_GEN_OM_ID_DATA_0; 
      R     #EAX_GEN_OM_ID_DATA_1; 

      L     0; 
      T     #SB_Attempt_Counter; 
      T     #SW_Schrittkette; 
      T     #EADW_UnicatNumber; 
      T     #EADW_ConsecutiveNumber; 
      T     #EAB_AuthorizationNumber; 
      T     #EAB_OperatingMode; 

INIT: NOP   0; 

NETWORK
TITLE =

      U     #EX_Reset; 
      R     #EAX_ErrorMaxAttempt; 

NETWORK
TITLE =

SSRT: NOP   0; 

      U(    ; 
      L     #SB_Attempt_Counter; 
      L     #EB_Number_Attempt; 
      >=I   ; 
      )     ; 
      SPBN  ERRO; 

      SET   ; 
      S     #EAX_ErrorMaxAttempt; 

      L     7; 
      T     #SW_Schrittkette; 

ERRO: NOP   0; 
NETWORK
TITLE =

      U     "DB_PLC_MD_DB20".UDHex._53_Bit1_SKEY_Test_Ident; 
      U     "MX_SKEY_IDENT_SENDEN"; 
      SPBN  _IBN; 

      U     "M_TagRemoved"; 
      O     ; 
      UN    "M_TagRemoved"; 
      U(    ; 
      L     "MB_SMARTKEY_AUTHOR"; 
      L     B#16#F; 
      ==I   ; 
      )     ; 
      SPBN  _NEU; 

      L     0; 
      T     #EADW_UnicatNumber; 

      SPA   _ALT; 

_NEU: NOP   0; 

      L     "MB_SMARTKEY_AUTHOR"; 
      SLD   24; 
      OD    DW#16#1; 
      T     #EADW_UnicatNumber; 

_ALT: NOP   0; 

      L     6; 
      T     #SW_Schrittkette; 

_IBN: NOP   0; 

NETWORK
TITLE =

      L     #SW_Schrittkette; 
      SPL   SDFT; 
      SPA   SK00; // Pos. Flanke von Synch
      SPA   SK01; // Neg. Flanke von Synch
      SPA   SK02; // Aufbereitung Daten
      SPA   SK03; // Pos. Flanke von Clock
      SPA   SK04; // Neg. Flanke von Clock
      SPA   SK05; // Auswertung empfangener Daten
      SPA   SK06; // Auswertung der ID-Nummer
      SPA   SEND; // Letzter Schritt in Schrittkette

SDFT: SPA   SEND; 

NETWORK
TITLE =Syncronisation
//Wenn die Maschine eingeschalten wird, wird für 50msec der 
//Synchronisationsimpuls 
//auf High ausgegeben
SK00: NOP   0; 

      U     #EX_MaschineEin; 
      SPBN  SEND; 

      U     #EX_GEN_OM_ID_SYNC; 
      SPBN  SEND; 

      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 

NETWORK
TITLE =Verzugszeit bis Adressübertragung startet
//Nach Synchronisationsimpuls wird für 50msec der Synchronisationsimpuls auf Low 
//ausgegeben, als Pause vor der Datenübermittlung
SK01: NOP   0; 

      UN    #EX_GEN_OM_ID_SYNC; 
      SPBN  SEND; 

      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 

NETWORK
TITLE =

SK02: NOP   0; 

      SET   ; 
      R     #EAX_MasterKey; 
      R     #EAX_ServiceKey; 

      L     0; 
      T     #SW_Receive_Data_0; 
      T     #SW_Receive_Data_1; 
      T     #EADW_UnicatNumber; 
      T     #EADW_ConsecutiveNumber; 
      T     #EAB_AuthorizationNumber; 
      T     #EAB_OperatingMode; 

      L     13; 
      T     #SB_Data_Counter; 

      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 


NETWORK
TITLE =Übertragung der Adresse
//Generierung des Takt mit 50msec 1:1. Im positiven Taktzustand wird ein 
//Adressbit 
//ausgegeben im negativen geschieht nichts. Mit Abschluss des negativen wird die 
//Adresse geschoben und die Schrittkette erhöht
SK03: NOP   0; 

      U     #EX_GEN_OM_ID_SYNC; 
      SPBN  S03a; 

      L     0; 
      T     #SW_Schrittkette; 

      L     #SB_Attempt_Counter; 
      INC   1; 
      T     #SB_Attempt_Counter; 

      SPA   SSRT; 

S03a: NOP   0; 


      U     #EX_GEN_OM_ID_CLOCK; 
      SPBN  SEND; 


      U     #EX_GEN_OM_ID_DATA_0; 
      SPBN  S03b; 

      L     #SB_Data_Counter; 
      L     1; 
      SLW   ; 
      L     #SW_Receive_Data_0; 
      OW    ; 
      T     #SW_Receive_Data_0; 

S03b: NOP   0; 


      U     #EX_GEN_OM_ID_DATA_1; 
      SPBN  S03c; 

      L     #SB_Data_Counter; 
      L     1; 
      SLW   ; 
      L     #SW_Receive_Data_1; 
      OW    ; 
      T     #SW_Receive_Data_1; 

S03c: NOP   0; 


      U     #EX_GEN_OM_ID_DATA_0; 
      =     #EAX_GEN_OM_ID_DATA_0; 

      U     #EX_GEN_OM_ID_DATA_1; 
      =     #EAX_GEN_OM_ID_DATA_1; 


      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 

NETWORK
TITLE =Aufnehmen der zurückgesandten Adresse
//Mit einer neg. Flanke des Takt wird die Adressrückmeldung vom PILZ-Smartkey 
//ausgewertet
SK04: NOP   0; 

      U     #EX_GEN_OM_ID_SYNC; 
      SPBN  S04a; 

      L     0; 
      T     #SW_Schrittkette; 

      L     #SB_Attempt_Counter; 
      INC   1; 
      T     #SB_Attempt_Counter; 

      SPA   SSRT; 

S04a: NOP   0; 


      UN    #EX_GEN_OM_ID_CLOCK; 
      SPBN  SEND; 

      U(    ; 
      L     #SB_Data_Counter; 
      L     0; 
      >I    ; 
      )     ; 
      SPBN  S04b; 

      L     #SB_Data_Counter; 
      DEC   1; 
      T     #SB_Data_Counter; 

      L     #SW_Schrittkette; 
      DEC   1; 
      T     #SW_Schrittkette; 

      SPA   SSRT; 

S04b: NOP   0; 

      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 

NETWORK
TITLE =

SK05: NOP   0; 

      L     #SW_Receive_Data_0; 
      SLD   14; 
      L     #SW_Receive_Data_1; 
      OD    ; 
      T     #EADW_UnicatNumber; 

      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 

NETWORK
TITLE =

SK06: NOP   0; 

      L     #EADW_UnicatNumber; 
      UD    DW#16#FFFFFF; 
      T     #EADW_ConsecutiveNumber; 

      L     #EADW_UnicatNumber; 
      SRD   24; 
      UD    DW#16#F; 
      T     #EAB_AuthorizationNumber; 

      L     #EAB_AuthorizationNumber; 
      UW    W#16#7; 
      T     #EAB_OperatingMode; 

      L     #EAB_AuthorizationNumber; 
      L     W#16#C; 
      ==I   ; 
      =     #EAX_MasterKey; 

      L     #EAB_AuthorizationNumber; 
      L     W#16#D; 
      ==I   ; 
      =     #EAX_ServiceKey; 

      SET   ; 
      S     #EAX_CommunicationReady; 

      L     #SW_Schrittkette; 
      INC   1; 
      T     #SW_Schrittkette; 

NETWORK
TITLE =
//Roboter meldet Fehler
SEND: NOP   0; 

END_FUNCTION_BLOCK

