FUNCTION "FC_HMI_IO_TO_TLINE" : VOID
TITLE =
//$Revision: 1.7 $
//$Date: 2008/03/03 14:41:46CET $
//$Author: schmoelp $

VERSION : 0.1


VAR_INPUT
  Freigabe_AST_Hand : BOOL ;	//Freigabe Ventil zur Arbeitsstellung Hand
  Freigabe_GST_Hand : BOOL ;	//Freigabe Ventil zur Grundstellung Hand
  Endlage_AST : BOOL ;	//Endlage Ventil Arbeitsstellung
  Endlage_GST : BOOL ;	//Endlage Ventil Grundstellung
  Ventil_AST : BOOL ;	//Ansteuerung Ventil zur Arbeitsstellung
  Ventil_GST : BOOL ;	//Ansteuerung Ventil zur Grundstellung
  Bewegungsnummer : INT ;	//Nummer der Bewegung die am Bildschirm angezeigt werden soll
END_VAR
VAR_TEMP
  ti_beweg_calc_nr : INT ;	
  ti_byte_offset_db59 : INT ;	
  ti_bit_offset_db59 : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =Bewegungsnummer

      L     #Bewegungsnummer; 
      L     0; 
      ==I   ; 
      SAVE  ; 
      BEB   ; 
NETWORK
TITLE =Datenbausteine öffnen

      AUF   "DB_HMI"; 

NETWORK
TITLE =Für Offset-Berechnung Bewegungsnummer um Eins dekrementieren

      L     #Bewegungsnummer; 
      L     1; 
      -I    ; 
      T     #ti_beweg_calc_nr; 

NETWORK
TITLE =Byte-Offset für DB59 berechnen

      L     #ti_beweg_calc_nr; 
      L     4; 
      /I    ; 
      T     #ti_byte_offset_db59; 

NETWORK
TITLE =Bit-Offset für DB59 berechnen

      L     #ti_beweg_calc_nr; 
      L     4; 
      MOD   ; 
      L     2; 
      *I    ; 
      T     #ti_bit_offset_db59; 

NETWORK
TITLE =Berechnung des Adressregister 1 für die Pfeil-Anzeige

      L     198; 
      L     #ti_byte_offset_db59; 
      +I    ; 
      SLW   3; 
      L     #ti_bit_offset_db59; 
      +D    ; 
      LAR1  ; 

NETWORK
TITLE =Zuweisung des Minus Pfeils - Arbeitsstellung - Gelb

//      U     "M_Handbetrieb"
      U     #Freigabe_AST_Hand; 
      UN    #Endlage_AST; 
      =     DBX [AR1,P#0.0]; 

NETWORK
TITLE =Zuweisung des Plus Pfeils - Grundstellung - Blau

//      U     "M_Handbetrieb"
      U     #Freigabe_GST_Hand; 
      UN    #Endlage_GST; 
      =     DBX [AR1,P#0.1]; 

NETWORK
TITLE =Berechnung des Adressregister 1 für die Ausgang-Anzeige

      L     523; 
      L     #ti_byte_offset_db59; 
      +I    ; 
      SLW   3; 
      L     #ti_bit_offset_db59; 
      +D    ; 
      LAR1  ; 

NETWORK
TITLE =Zuweisung des Minus Ausgang - Arbeitsstellung - Grün

      U     #Ventil_AST; 
      =     DBX [AR1,P#0.0]; 

NETWORK
TITLE =Zuweisung des Plus Ausgang - Grundstellung - Grün

      U     #Ventil_GST; 
      =     DBX [AR1,P#0.1]; 

NETWORK
TITLE =Berechnung des Adressregister 1 für die Eingang-Anzeige

      L     130; 
      L     #ti_byte_offset_db59; 
      +I    ; 
      SLW   3; 
      L     #ti_bit_offset_db59; 
      +D    ; 
      LAR1  ; 

NETWORK
TITLE =Zuweisung des Minus Eingang - Arbeitsstellung - Grün

      U     #Endlage_AST; 
      =     DBX [AR1,P#0.0]; 

NETWORK
TITLE =Zuweisung des Plus Eingang - Grundstellung - Grün

      U     #Endlage_GST; 
      =     DBX [AR1,P#0.1]; 

END_FUNCTION

