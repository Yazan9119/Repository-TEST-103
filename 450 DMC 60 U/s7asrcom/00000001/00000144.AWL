FUNCTION_BLOCK "FB_TEMP_TAB_SPINDEL"
TITLE =
//$Revision: 1.23.1.2 $
//$Date: 2008/04/09 16:13:05CEST $
//$Author: sth $
{ S7_language := '7(1) Deutsch (Deutschland)  09.04.2008  07:33:37' }
VERSION : 0.0


VAR_INPUT
  Tab1_act : BOOL ;	
  Tab2_act : BOOL ;	
  Temp_akt1 : REAL ;	
  Temp_akt2 : REAL ;	
END_VAR
VAR
  FP_req_old : BOOL ;	
  start : BOOL ;	
  error : BOOL ;	
  done : BOOL ;	
  gud_les_akt : BOOL ;	
  error_state : WORD ;	
  Zeilenzahl : BYTE ;	
  index1tmp : INT ;	
  index2tmp : INT ;	
  grdtmp1 : DWORD ;	
  X_aend1_mm : REAL ;	
  Y_aend1_mm : REAL ;	
  Z_aend1_mm : REAL ;	
  X_aend2_mm : REAL ;	
  Y_aend2_mm : REAL ;	
  Z_aend2_mm : REAL ;	
  aend_mm_1 : STRUCT 	
   X : REAL ;	
   Y : REAL ;	
   Z : REAL ;	
  END_STRUCT ;	
  aend_mm_2 : STRUCT 	
   X : REAL ;	
   Y : REAL ;	
   Z : REAL ;	
  END_STRUCT ;	
  aend_mm : STRUCT 	
   X : REAL ;	
   Y : REAL ;	
   Z : REAL ;	
  END_STRUCT ;	
  Req_fb2 : BOOL ;	
  Fp_req_old1 : BOOL ;	
  error1 : BOOL ;	
  done1 : BOOL ;	
  error_state1 : WORD ;	
  daten_temp : STRING  [16 ] := 'TEMP_KOMP';	
  A3_SE_TEMP_COMP_ABS_VAL : STRUCT 	
   SYNTAX_ID : BYTE  := B#16#82;	
   bereich_u_einheit : BYTE ;	
   spalte : WORD  := W#16#AB7C;	
   zeile : WORD  := W#16#1;	
   bausteintyp : BYTE  := B#16#16;	
   ZEILENANZAHL : BYTE  := B#16#1;	
   typ : BYTE  := B#16#F;	
   laenge : BYTE  := B#16#8;	
  END_STRUCT ;	
  gradwerte : ARRAY  [0 .. 9 ] OF DWORD ;	
  x_werte : ARRAY  [0 .. 9 ] OF DWORD ;	
  y_werte : ARRAY  [0 .. 9 ] OF DWORD ;	
  z_werte : ARRAY  [0 .. 9 ] OF DWORD ;	
  GUD_Lesen : "FB_SIEM_GETGUD";	
  SET_schr : "FB_SIEM_PUT";	
  GUD_Lesen_Verz : "FB_SIEM_GETGUD";	
  GUD_Lesen_Interv : "FB_SIEM_GETGUD";	
  Addr_Verzoegerung : STRING  [32 ] := 'GLAET_TIME';	
  Addr_Intervall : STRING  [254 ] := 'GLAET_SPW';	
  Wert_Verzoegerung : DWORD ;	
  Wert_Intervall : DWORD ;	
  SPI_TAB1 : "FB_SPI_GLAETT";	
  SPI_TAB2 : "FB_SPI_GLAETT";	
  req_read_time_1 : BOOL ;	
  req_read_time_2 : BOOL ;	
  req_read_spw_1 : BOOL ;	
  req_read_spw_2 : BOOL ;	
  GUD_Lesen_Verz_2 : "FB_SIEM_GETGUD";	
  GUD_Lesen_Interv_2 : "FB_SIEM_GETGUD";	
  Wert_Verzoegerung_1 : DWORD ;	
  Wert_Intervall_1 : DWORD ;	
  T_Anwahl : BOOL ;	
  Fehler_Temp_Spindel : BOOL ;	
END_VAR
VAR_TEMP
  ber0act_tab_1 : BOOL ;	
  ber1act_tab_1 : BOOL ;	
  ber2act_tab_1 : BOOL ;	
  ber3act_tab_1 : BOOL ;	
  ber4act_tab_1 : BOOL ;	
  ber5act_tab_1 : BOOL ;	
  ber0act_tab_2 : BOOL ;	
  ber1act_tab_2 : BOOL ;	
  ber2act_tab_2 : BOOL ;	
  ber3act_tab_2 : BOOL ;	
  ber4act_tab_2 : BOOL ;	
  ber5act_tab_2 : BOOL ;	
  res : DINT ;	
  diftemp : REAL ;	
  difx : REAL ;	
  dify : REAL ;	
  difz : REAL ;	
  tanx : REAL ;	
  tany : REAL ;	
  tanz : REAL ;	
  num_var : INT ;	
  dummy : BOOL ;	
  DmmyW : INT ;	
  errorver : BOOL ;	
  errorint : BOOL ;	
  donever : BOOL ;	
  doneint : BOOL ;	
  State_Ver : WORD ;	
  State_Int : WORD ;	
  aend_mm_X : REAL ;	
  req_lesen_verz : BOOL ;	
  FP_req_lesen_verz_1 : BOOL ;	
  FP_req_lesen_interv : BOOL ;	
  errorver_1 : BOOL ;	
  errorint_1 : BOOL ;	
  donever_1 : BOOL ;	
  doneint_1 : BOOL ;	
  State_Int_1 : WORD ;	
  State_Ver_1 : WORD ;	
  Glaettung_aus : BOOL ;	
  Glaettung_aus_1 : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =

      CALL "FC_TEMP_FUNKTIONEN" (
           int_funktion             := 1,
           int_mp                   := "DB_PLC_MD_DB20".UDInt._025_ANWAHL_TEMP_KOMP,
           int_dekade               := 2,
           int_wert                 := 0,
           boo_ergebnis             := #T_Anwahl);

      U     #T_Anwahl; 
      SPB   fin; 
NETWORK
TITLE =fehlerhafte Eingabe auswerten      (Folgetemp < als vorherige )
//erweitert um 0-Auswertung
      SET   ; 
      R     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      L     0; 
      T     #error_state; // Initialisieren
      T     #error_state1; 
      R     #Fehler_Temp_Spindel; 

      U     #gud_les_akt; 
      SPB   M001; 

      UN    #Tab1_act; 
      SPB   M002; 

      L     #gradwerte[0]; 
      L     #gradwerte[1]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      TAK   ; 
      L     #gradwerte[2]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      TAK   ; 
      L     #gradwerte[3]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      TAK   ; 
      L     #gradwerte[4]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe

// Wenn erster Wert in der Tabelle Null ist!!
      U(    ; 
      L     #gradwerte[0]; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe


M002: NOP   0; 
      UN    #Tab2_act; 
      SPB   M003; 
      L     #gradwerte[5]; 
      L     #gradwerte[6]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      TAK   ; 
      L     #gradwerte[7]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      TAK   ; 
      L     #gradwerte[8]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      TAK   ; 
      L     #gradwerte[9]; 
      >D    ; 
      O(    ; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe

// Wenn erster Wert in der Tabelle Null ist!!
      U(    ; 
      L     #gradwerte[5]; 
      L     0; 
      ==D   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
NETWORK
TITLE =aktiven Bereich 0-5 Tabellen 1 erkennen und auswerten

M003: UN    "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      SPB   M03; 
      L     0.000000e+000; 
      T     #X_aend1_mm; 
      T     #Y_aend1_mm; 
      T     #Z_aend1_mm; 
      T     #X_aend2_mm; 
      T     #Y_aend2_mm; 
      T     #Z_aend2_mm; 
      S     #Fehler_Temp_Spindel; 
      SPA   M001; 

M03:  UN    #Tab1_act; 
      SPB   M037; 
      L     #Temp_akt1; 
//Tabelle 1:
      U     #Tab1_act; 
      SPBN  M037; 

      L     0.000000e+000; 
      L     #Temp_akt1; 
      <=R   ; 
      U(    ; 
      L     #gradwerte[0]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber0act_tab_1; 
      SPBN  M036; 

      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := DW#16#0,
           grad_ob                  := #gradwerte[0],
           X_unt                    := DW#16#0,
           X_ob                     := #x_werte[0],
           Y_unt                    := DW#16#0,
           Y_ob                     := #y_werte[0],
           Z_unt                    := DW#16#0,
           Z_ob                     := #z_werte[0],
           temp_lager               := #Temp_akt1,
           X_aend_mm                := #X_aend1_mm,
           Y_aend_mm                := #Y_aend1_mm,
           Z_aend_mm                := #Z_aend1_mm);
      SPA   M005; 

M036: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[1]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber1act_tab_1; 
      SPBN  M004; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[0],
           grad_ob                  := #gradwerte[1],
           X_unt                    := #x_werte[0],
           X_ob                     := #x_werte[1],
           Y_unt                    := #y_werte[0],
           Y_ob                     := #y_werte[1],
           Z_unt                    := #z_werte[0],
           Z_ob                     := #z_werte[1],
           temp_lager               := #Temp_akt1,
           X_aend_mm                := #X_aend1_mm,
           Y_aend_mm                := #Y_aend1_mm,
           Z_aend_mm                := #Z_aend1_mm);
      SPA   M005; 
M004: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[2]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber2act_tab_1; 
      SPBN  M006; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[1],
           grad_ob                  := #gradwerte[2],
           X_unt                    := #x_werte[1],
           X_ob                     := #x_werte[2],
           Y_unt                    := #y_werte[1],
           Y_ob                     := #y_werte[2],
           Z_unt                    := #z_werte[1],
           Z_ob                     := #z_werte[2],
           temp_lager               := #Temp_akt1,
           X_aend_mm                := #X_aend1_mm,
           Y_aend_mm                := #Y_aend1_mm,
           Z_aend_mm                := #Z_aend1_mm);
      SPA   M005; 
M006: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[3]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber3act_tab_1; 
      SPBN  M007; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[2],
           grad_ob                  := #gradwerte[3],
           X_unt                    := #x_werte[2],
           X_ob                     := #x_werte[3],
           Y_unt                    := #y_werte[2],
           Y_ob                     := #y_werte[3],
           Z_unt                    := #z_werte[2],
           Z_ob                     := #z_werte[3],
           temp_lager               := #Temp_akt1,
           X_aend_mm                := #X_aend1_mm,
           Y_aend_mm                := #Y_aend1_mm,
           Z_aend_mm                := #Z_aend1_mm);
      SPA   M005; 
M007: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[4]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber4act_tab_1; 
      SPBN  M038; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[3],
           grad_ob                  := #gradwerte[4],
           X_unt                    := #x_werte[3],
           X_ob                     := #x_werte[4],
           Y_unt                    := #y_werte[3],
           Y_ob                     := #y_werte[4],
           Z_unt                    := #z_werte[3],
           Z_ob                     := #z_werte[4],
           temp_lager               := #Temp_akt1,
           X_aend_mm                := #X_aend1_mm,
           Y_aend_mm                := #Y_aend1_mm,
           Z_aend_mm                := #Z_aend1_mm);
      SPA   M005; 
M038: TAK   ; 
      <R    ; 
      U(    ; 
      L     1.000000e+002; //grenze 100grd Fehler??
      <=R   ; 
      )     ; 
      =     #ber5act_tab_1; 
      SPBN  M037; 
      L     #x_werte[4]; 
      DTR   ; 
      L     1.000000e+003; 
      /R    ; 
      T     #X_aend1_mm; 
      L     #y_werte[4]; 
      DTR   ; 
      L     1.000000e+003; 
      /R    ; 
      T     #Y_aend1_mm; 
      L     #z_werte[4]; 
      DTR   ; 
      L     1.000000e+003; 
      /R    ; 
      T     #Z_aend1_mm; 
      SPA   M005; 

M037: NOP   0; 
      L     0; 
      T     #Z_aend1_mm; 
      T     #Y_aend1_mm; 
      T     #X_aend1_mm; 
      SPA   M005; 
NETWORK
TITLE =aktiven Bereich 0-5 Tabellen 2 erkennen und auswerten
//Tabelle 2:
M005: UN    #Tab2_act; 
      SPB   M035; 
      L     0.000000e+000; 
      L     #Temp_akt2; 
      <=R   ; 
      U(    ; 
      L     #gradwerte[5]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber0act_tab_2; 
      SPBN  M039; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := DW#16#0,
           grad_ob                  := #gradwerte[5],
           X_unt                    := DW#16#0,
           X_ob                     := #x_werte[5],
           Y_unt                    := DW#16#0,
           Y_ob                     := #y_werte[5],
           Z_unt                    := DW#16#0,
           Z_ob                     := #z_werte[5],
           temp_lager               := #Temp_akt2,
           X_aend_mm                := #X_aend2_mm,
           Y_aend_mm                := #Y_aend2_mm,
           Z_aend_mm                := #Z_aend2_mm);
      SPA   M001; 

M039: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[6]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber1act_tab_2; 
      SPBN  M032; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[5],
           grad_ob                  := #gradwerte[6],
           X_unt                    := #x_werte[5],
           X_ob                     := #x_werte[6],
           Y_unt                    := #y_werte[5],
           Y_ob                     := #y_werte[6],
           Z_unt                    := #z_werte[5],
           Z_ob                     := #z_werte[6],
           temp_lager               := #Temp_akt2,
           X_aend_mm                := #X_aend2_mm,
           Y_aend_mm                := #Y_aend2_mm,
           Z_aend_mm                := #Z_aend2_mm);
      SPA   M001; 
M032: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[7]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber2act_tab_2; 
      SPBN  M033; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[6],
           grad_ob                  := #gradwerte[7],
           X_unt                    := #x_werte[6],
           X_ob                     := #x_werte[7],
           Y_unt                    := #y_werte[6],
           Y_ob                     := #y_werte[7],
           Z_unt                    := #z_werte[6],
           Z_ob                     := #z_werte[7],
           temp_lager               := #Temp_akt2,
           X_aend_mm                := #X_aend2_mm,
           Y_aend_mm                := #Y_aend2_mm,
           Z_aend_mm                := #Z_aend2_mm);
      SPA   M001; 
M033: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[8]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber3act_tab_2; 
      SPBN  M034; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[7],
           grad_ob                  := #gradwerte[8],
           X_unt                    := #x_werte[7],
           X_ob                     := #x_werte[8],
           Y_unt                    := #y_werte[7],
           Y_ob                     := #y_werte[8],
           Z_unt                    := #z_werte[7],
           Z_ob                     := #z_werte[8],
           temp_lager               := #Temp_akt2,
           X_aend_mm                := #X_aend2_mm,
           Y_aend_mm                := #Y_aend2_mm,
           Z_aend_mm                := #Z_aend2_mm);
      SPA   M001; 
M034: TAK   ; 
      <R    ; 
      U(    ; 
      L     #gradwerte[9]; 
      DTR   ; 
      <=R   ; 
      )     ; 
      =     #ber4act_tab_2; 
      SPBN  M040; 
      CALL "FC_TEMP_HILFSFKT" (
           grad_unt                 := #gradwerte[8],
           grad_ob                  := #gradwerte[9],
           X_unt                    := #x_werte[8],
           X_ob                     := #x_werte[9],
           Y_unt                    := #y_werte[8],
           Y_ob                     := #y_werte[9],
           Z_unt                    := #z_werte[8],
           Z_ob                     := #z_werte[9],
           temp_lager               := #Temp_akt2,
           X_aend_mm                := #X_aend2_mm,
           Y_aend_mm                := #Y_aend2_mm,
           Z_aend_mm                := #Z_aend2_mm);
      SPA   M001; 
M040: TAK   ; 
      <R    ; 
      U(    ; 
      L     1.000000e+002; //grenze 100grd Fehler??
      <=R   ; 
      )     ; 
      =     #ber5act_tab_2; 
      SPBN  M035; 
      L     #x_werte[9]; 
      DTR   ; 
      L     1.000000e+003; 
      /R    ; 
      T     #X_aend2_mm; 
      L     #y_werte[9]; 
      DTR   ; 
      L     1.000000e+003; 
      /R    ; 
      T     #Y_aend2_mm; 
      L     #z_werte[9]; 
      DTR   ; 
      L     1.000000e+003; 
      /R    ; 
      T     #Z_aend2_mm; 
      SPA   M001; 

M035: NOP   0; 
      L     0; 
      T     #Z_aend2_mm; 
      T     #Y_aend2_mm; 
      T     #X_aend2_mm; 
      SPA   M001; 
NETWORK
TITLE =Start und Spaltenpointer festlegen

M001: O     "M_8_ter_OB1_Zyklen"; // M     52.3 INIT_OK
      O     "DB_Temp_Comp".SSPI.Temp_Komp_wert_wirksam; 
      S     #gud_les_akt; 
      R     "DB_Temp_Comp".SSPI.Temp_Komp_wert_wirksam; 

      U     "DB_FEHLERMELDUNG".MELDUNG3._700517_Tempkomp_Falsche; //falsche Eingabe
      SPB   SUM; 

      UN    "M_Ruecksetze_Fehler"; //"M_Reset_Taste"
      SPB   M008; 
      SET   ; 
      R     #FP_req_old; 
      R     #start; 
      R     #gud_les_akt; 
      L     0; 
      T     #Zeilenzahl; 
M008: NOP   0; 
      UN    "M_8_ter_OB1_Zyklen"; // M     52.3 INIT_OK
      SPB   M009; 
      L     0; 
      T     #Zeilenzahl; 
M009: NOP   0; 
      L     #index2tmp; 
      L     0; 
      <>I   ; 
      SPB   M010; 
      LAR1  P##gradwerte; 
      SPA   M011; 
M010: TAK   ; 
      L     1; 
      <>I   ; 
      SPB   M012; 
      LAR1  P##x_werte; 
      SPA   M011; 
M012: TAK   ; 
      L     2; 
      <>I   ; 
      SPB   M013; 
      LAR1  P##y_werte; 
      SPA   M011; 
M013: TAK   ; 
      L     3; 
      <>I   ; 
      SPB   M011; 
      LAR1  P##z_werte; 
M011: NOP   0; 
NETWORK
TITLE =wenn GUD lesen fertig>>> fb2 starten (setting DATA)

      UN    #gud_les_akt; 
      SPB   M014; 
      L     #Zeilenzahl; 
      SPL   M015; 
      SPA   M016; 
      SPA   M017; 
      SPA   M018; 
      SPA   M019; 
      SPA   M020; 
      SPA   M021; 
      SPA   M022; 
      SPA   M023; 
      SPA   M024; 
      SPA   M025; 
      SPA   M026; 
M015: BEA   ; //kein Schritt aktiv

M016: NOP   0; 
      L     0; 
      T     #index1tmp; 
      SPA   M014; 
M017: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#0.0]; 
      L     1; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M018: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#4.0]; 
      L     2; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M019: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#8.0]; 
      L     3; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M020: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#12.0]; 
      L     4; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M021: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#16.0]; 
      L     5; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M022: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#20.0]; 
      L     6; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------

M023: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#24.0]; 
      L     7; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M024: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#28.0]; 
      L     8; 
      T     #index1tmp; 
      SPA   M014; 
//-----------------------------
M025: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#32.0]; 
      L     9; 
      T     #index1tmp; 
      SPA   M014; 

//-----------------------------

M026: NOP   0; 
      UN    #done1; 
      SPB   M014; 
      L     #grdtmp1; 
      T     D [AR1,P#36.0]; 
      L     0; 
      T     #Zeilenzahl; 
      L     #index2tmp; 
      INC   1; 
      T     #index2tmp; 
      L     3; 
      >I    ; 
      R     #gud_les_akt; 
      U     #gud_les_akt; 
      SPB   M014; 
      L     0; 
      T     #index2tmp; 
NETWORK
TITLE =GUD lesen

M014: NOP   0; 
      U     #gud_les_akt; 
      UN    #done1; 
      UN    #Req_fb2; 
      FP    #Fp_req_old1; 
      S     #Req_fb2; 

      CALL #GUD_Lesen (
           Req                      := #Req_fb2,
           Addr                     := #daten_temp,
           Area                     := B#16#0,
           Unit                     := B#16#1,
           Index1                   := #index1tmp,
           Index2                   := #index2tmp,
           CnvtToken                := FALSE,
           Error                    := #error1,
           Done                     := #done1,
           State                    := #error_state1,
           RD                       := #grdtmp1);

      O     #done1; 
      O     "M_Reset_Taste"; // alte MSTT
      R     #Req_fb2; 

      ON    #done1; 
      SPB   M027; 
      L     #Zeilenzahl; 
      INC   1; 
      T     #Zeilenzahl; 
M027: NOP   0; 
NETWORK
TITLE =Option X

      U     "DB_PLC_MD_DB20".UDHex._18_BIT0_Spi_Tempk_X; 
      SPB   M028; 
      L     0; 
      T     #X_aend1_mm; 
      T     #X_aend2_mm; 
M028: NOP   0; 
NETWORK
TITLE =Option Y

      U     "DB_PLC_MD_DB20".UDHex._18_BIT1_Spi_Tempk_Y; 
      SPB   M029; 
      L     0; 
      T     #Y_aend1_mm; 
      T     #Y_aend2_mm; 
M029: NOP   0; 
NETWORK
TITLE =Option Z

      U     "DB_PLC_MD_DB20".UDHex._18_BIT2_Spi_Tempk_Z; 
      SPB   M030; 
      L     0; 
      T     #Z_aend1_mm; 
      T     #Z_aend2_mm; 
M030: NOP   0; 
NETWORK
TITLE =GUDs für Glättung lesen (GUD4: GLAET_SPW[0,0] & GLAET_TIME[0,0])
// 
//
SUM:  U     #gud_les_akt; 
      UN    #req_read_time_1; 
      FP    #FP_req_lesen_interv; 
      S     #req_read_time_1; 

      CALL #GUD_Lesen_Verz (
           Req                      := #req_read_time_1,
           Addr                     := #Addr_Verzoegerung,
           Area                     := B#16#0,
           Unit                     := B#16#1,
           Index1                   := 0,
           Index2                   := 0,
           CnvtToken                := FALSE,
           Error                    := #errorver,
           Done                     := #donever,
           State                    := #State_Ver,
           RD                       := #Wert_Verzoegerung);


      CALL #GUD_Lesen_Verz_2 (
           Req                      := #req_read_time_1,
           Addr                     := #Addr_Verzoegerung,
           Area                     := B#16#0,
           Unit                     := B#16#1,
           Index1                   := 0,
           Index2                   := 1,
           CnvtToken                := FALSE,
           Error                    := #errorver_1,
           Done                     := #donever_1,
           State                    := #State_Ver_1,
           RD                       := #Wert_Verzoegerung_1);

      U     #donever_1; 
      U     #donever; 
      O     "M_Reset_Taste"; // alte MSTT
      R     #req_read_time_1; 
NETWORK
TITLE =GUDs für Glättung lesen (GUD4: GLAET_SPW[0,1] & GLAET_TIME[0,1])

      U     #gud_les_akt; 
      UN    #req_read_time_2; 
      FP    #FP_req_lesen_interv; 
      S     #req_read_time_2; 

      CALL #GUD_Lesen_Interv (
           Req                      := #req_read_time_2,
           Addr                     := #Addr_Intervall,
           Area                     := B#16#0,
           Unit                     := B#16#1,
           Index1                   := 0,
           Index2                   := 0,
           CnvtToken                := FALSE,
           Error                    := #errorint,
           Done                     := #doneint,
           State                    := #State_Int,
           RD                       := #Wert_Intervall);

      CALL #GUD_Lesen_Interv_2 (
           Req                      := #req_read_time_2,
           Addr                     := #Addr_Intervall,
           Area                     := B#16#0,
           Unit                     := B#16#1,
           Index1                   := 0,
           Index2                   := 1,
           CnvtToken                := FALSE,
           Error                    := #errorint_1,
           Done                     := #doneint_1,
           State                    := #State_Int_1,
           RD                       := #Wert_Intervall_1);

      U     #doneint_1; 
      U     #doneint; 
      O     "M_Reset_Taste"; // alte MSTT
      R     #req_read_time_2; 
NETWORK
TITLE =Glättung Spindel Tabelle1  On/Off  
//Wenn im Gud4 die Sprungweite bzw Time kleiner gleich Null ist
//oder Tabelle 1 deaktiv ist wird Glättung ausgeschalten!
      U(    ; 
      U(    ; 
      L     #Wert_Verzoegerung; 
      L     0; 
      <=I   ; 
      )     ; 
      O(    ; 
      L     #Wert_Intervall; 
      L     0; 
      <=I   ; 
      )     ; 
      ON    "DB_Temp_Comp".SSPI.Tab1_act; 
      ON    "DB_SIEM_KANAL_1".E_AxesRef; 
      )     ; 
      =     #Glaettung_aus; 

      CALL #SPI_TAB1 (
           SPW_Glaet                := #Wert_Intervall,
           T_Glaettu                := #Wert_Verzoegerung,
           FP_Glaet_aktiv           := "FP_Glaet_aktiv",
           Takt                     := "m_takt_0,1sec.",
           X_Komp_2                 := #X_aend1_mm,
           Y_Komp_2                 := #Y_aend1_mm,
           Z_Komp_2                 := #Z_aend1_mm,
           Enable                   := #Glaettung_aus,
           X_Glaett                 := #aend_mm_1.X,
           Y_Glaett                 := #aend_mm_1.Y,
           Z_Glaett                 := #aend_mm_1.Z);

      U     #Glaettung_aus; 
      SPBN  a09; 
      L     #X_aend1_mm; 
      T     #aend_mm_1.X; 
      T     #X_aend1_mm; 

      L     #Y_aend1_mm; 
      T     #aend_mm_1.Y; 
      T     #Y_aend1_mm; 

      L     #Z_aend1_mm; 
      T     #aend_mm_1.Z; 
      T     #Z_aend1_mm; 
a09:  NOP   0; 
NETWORK
TITLE =Glättung Spindel Tabelle2  On/Off 
//Wenn im Gud4 die Sprungweite bzw Time kleiner gleich Null ist
//oder Tabelle 1 deaktiv ist wird Glättung ausgeschalten!
      U(    ; 
      U(    ; 
      L     #Wert_Verzoegerung_1; 
      L     0; 
      <=I   ; 
      )     ; 
      O(    ; 
      L     #Wert_Intervall_1; 
      L     0; 
      <=I   ; 
      )     ; 
      ON    "DB_Temp_Comp".SSPI.Tab2_act; 
      ON    "DB_SIEM_KANAL_1".E_AxesRef; 
      )     ; 
      =     #Glaettung_aus_1; 

      CALL #SPI_TAB2 (
           SPW_Glaet                := #Wert_Intervall_1,
           T_Glaettu                := #Wert_Verzoegerung_1,
           FP_Glaet_aktiv           := "FP_Glaet_aktiv1",
           Takt                     := "m_takt_0,1sec.",
           X_Komp_2                 := #X_aend2_mm,
           Y_Komp_2                 := #Y_aend2_mm,
           Z_Komp_2                 := #Z_aend2_mm,
           Enable                   := #Glaettung_aus_1,
           X_Glaett                 := #aend_mm_2.X,
           Y_Glaett                 := #aend_mm_2.Y,
           Z_Glaett                 := #aend_mm_2.Z);

      U     #Glaettung_aus_1; 
      SPBN  a10; 
      L     #X_aend2_mm; 
      T     #aend_mm_2.X; 
      T     #X_aend2_mm; 

      L     #Y_aend2_mm; 
      T     #aend_mm_2.Y; 
      T     #Y_aend2_mm; 

      L     #Z_aend2_mm; 
      T     #aend_mm_2.Z; 
      T     #Z_aend2_mm; 
a10:  NOP   0; 
NETWORK
TITLE =Je nach akiver Spindel jeweilige Tabelle (Wert) wirksam

      L     "DB_PLC_MD_DB20".UDInt._000_STANDORT_MASCHTYP; 
      L     3090; //  340ger Maschine
      ==I   ; 
      SPBN  x01; 

      U     "M_VSP_Aktiv"; //Bei nicht aktiver Vorsatzspindel zweite Tabelle auf Null schreiben 
      SPB   x01; 
      L     0.000000e+000; 
      T     #aend_mm_2.X; 
      T     #aend_mm_2.Y; 
      T     #aend_mm_2.Z; 

x01:  L     #aend_mm_1.X; 
      L     #aend_mm_2.X; 
      +R    ; 
      T     #aend_mm.X; 

      L     #aend_mm_1.Y; 
      L     #aend_mm_2.Y; 
      +R    ; 
      T     #aend_mm.Y; 

      L     #aend_mm_1.Z; 
      L     #aend_mm_2.Z; 
      +R    ; 
      L     "DB_SPDL_Komp".Z_Komp; 
      +R    ; 
      T     #aend_mm.Z; 
NETWORK
TITLE =Fehler falls Grenzwert Maximaler Kompensationsweg überschritten

      U     "M_Reset_Taste"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._701004_TEMP_KOMP_GRENZW; 

      L     "DB_PLC_MD_DB20".UDInt._173_Max_Tempkomp_Wert; 
      ITD   ; 
      DTR   ; 
      L     #aend_mm.Z; 
      ABS   ; 
      <R    ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._701004_TEMP_KOMP_GRENZW; 

      TAK   ; 
      L     #aend_mm.Y; 
      ABS   ; 
      <R    ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._701004_TEMP_KOMP_GRENZW; 

      TAK   ; 
      L     #aend_mm.X; 
      <R    ; 
      ABS   ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._701004_TEMP_KOMP_GRENZW; 
NETWORK
TITLE =Korrektur auf SETTING Datum Schreiben
//Korrekturdaten nicht direkt auf die Settingdaten schreiben sondern in die 
//Tempkomp
      L     #aend_mm.X; 
      T     "DB_NC_PLC".PLC_Achsen_Offset0_X; 
      L     #aend_mm.Y; 
      T     "DB_NC_PLC".PLC_Achsen_Offset1_Y; 
      L     #aend_mm.Z; 
      T     "DB_NC_PLC".PLC_Achsen_Offset2_Z; 
      L     0.000000e+000; 
      T     "DB_NC_PLC".PLC_Offset_3; 
// Korrekturdaten (Hydr. Kopf) werden direkt in Tempkomp.spf beschrieben KEE
//      T     "DB_NC_PLC".PLC_Drehzahl_Offset
//      T     "DB_NC_PLC".PLC_Offset_1
//      T     "DB_NC_PLC".PLC_Offset_2

      CALL "FC_SIEM_PLC_NCK" (// schreiben zum NCK
           Enable                   := TRUE,
           Funct                    := B#16#4,
           S7Var                    := "DI_TEMP_TAB_SPINDEL".aend_mm.X,
           IVAR1                    := 200,
           IVAR2                    := -1,
           Error                    := #dummy,
           ErrCode                  := #DmmyW);

      CALL "FC_SIEM_PLC_NCK" (// schreiben zum NCK
           Enable                   := TRUE,
           Funct                    := B#16#4,
           S7Var                    := "DI_TEMP_TAB_SPINDEL".aend_mm.Y,
           IVAR1                    := 204,
           IVAR2                    := -1,
           Error                    := #dummy,
           ErrCode                  := #DmmyW);

      CALL "FC_SIEM_PLC_NCK" (// schreiben zum NCK
           Enable                   := TRUE,
           Funct                    := B#16#4,
           S7Var                    := "DI_TEMP_TAB_SPINDEL".aend_mm.Z,
           IVAR1                    := 208,
           IVAR2                    := -1,
           Error                    := #dummy,
           ErrCode                  := #DmmyW);

NETWORK
TITLE =Bausteinende

fin:  BE    ; 

END_FUNCTION_BLOCK

