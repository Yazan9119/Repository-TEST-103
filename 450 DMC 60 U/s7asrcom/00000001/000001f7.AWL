FUNCTION_BLOCK "FB_SPH_Spannen"
TITLE =
//$Revision: 1.3 $
//$Date: 2007/08/29 15:32:50CEST $
//$Author: ami $

VERSION : 0.1


VAR_INPUT
  i_Bef_DruckA : BOOL ;	
  i_ABST_CTRL_DruckA : BOOL ;	
  i_I_CTRL_OK : BOOL ;	
  i_vonHAWE_P_OK : BOOL ;	
  i_vonHAWE_NOTAUS : BOOL ;	
  i_Zeit_CTRL_ms : DINT ;	
END_VAR
VAR_OUTPUT
  o_DruckA_OK : BOOL ;	
  o_anHAWE_Bef_Spannen : BOOL ;	
END_VAR
VAR_IN_OUT
  io_DruckA_FM_HAWE : BOOL ;	
  io_DruckA_FM_CTRL : BOOL ;	
  io_T_CTRL_Start : BOOL ;	
  io_T_CTRL_Ausgang : BOOL ;	
  io_T_CTRL_Vorgabe : DINT ;	
END_VAR
VAR
  s_Z00_DruckA_GRST : BOOL ;	
  s_Z01_DruckA_anfahren : BOOL ;	
  s_Z02_DruckA_CTRL : BOOL ;	
  s_Z03_DruckA_OK : BOOL ;	
  s_Z04_DruckA_FM : BOOL ;	
  s_HM_PI_Bef_DruckA : BOOL ;	
END_VAR
VAR_TEMP
  t_PI_Bef_DruckA_anfahren : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =PI: Befehl Spann-Ablauf Druck A
// Netzwerk-Übersicht
// ==================
// NW 1: PI: Befehl Spannen für Auslöser Spann-Ablauf
// NW 2: Z01: Zustand Spannen
// NW 3: Z02: Zustand Kontrolle "gespannt"
// NW 4: Timer für Kontrolle "gespannt" OK
// NW 5: Z03: Zustand Spannstelle ist gespannt OK
// NW 6: Z04: Zustand Spannstelle Spannen mit Fehler
// NW 7: Z00: Zustand Spannen Grundstellung
// NW 8: FM: gespannt wegen Überwachung
// NW 9: FM: gespannt wegen HAWE
// NW 10: Meldung: gespannt OK
// NW 11: Befehl an HAWE: Spannen
      U     #i_Bef_DruckA; 
      FP    #s_HM_PI_Bef_DruckA; 
      =     #t_PI_Bef_DruckA_anfahren; 
NETWORK
TITLE =Z01: Zustand Druck A anfahren

      U     #s_Z00_DruckA_GRST; 
      U     #t_PI_Bef_DruckA_anfahren; 
      S     #s_Z01_DruckA_anfahren; 
      R     #s_Z00_DruckA_GRST; 
NETWORK
TITLE =Z02: Zustand Kontrolle "DruckA"

      U     #s_Z01_DruckA_anfahren; 
      U     #i_vonHAWE_P_OK; 
      U     #i_ABST_CTRL_DruckA; 
      S     #s_Z02_DruckA_CTRL; 
      R     #s_Z01_DruckA_anfahren; 
NETWORK
TITLE =Timer für Kontrolle "DruckA" OK

      L     #i_Zeit_CTRL_ms; 
      T     #io_T_CTRL_Vorgabe; 

      U     #s_Z02_DruckA_CTRL; 
      =     #io_T_CTRL_Start; 
NETWORK
TITLE =Z03: Zustand Spannstelle DruckA ist OK

      U     #s_Z01_DruckA_anfahren; 
      U     #i_vonHAWE_P_OK; 
      UN    #i_ABST_CTRL_DruckA; 
      O     ; 
      U     #s_Z02_DruckA_CTRL; 
      U     #i_I_CTRL_OK; 
      S     #s_Z03_DruckA_OK; 
      R     #s_Z01_DruckA_anfahren; 
      R     #s_Z02_DruckA_CTRL; 
NETWORK
TITLE =Z04: Zustand Spannstelle Spannen mit Fehler

      U(    ; 
      O     #s_Z01_DruckA_anfahren; 
      O     #s_Z02_DruckA_CTRL; 
      )     ; 
      U     #i_vonHAWE_NOTAUS; 
      O     ; 
      U     #s_Z02_DruckA_CTRL; 
      U     #io_T_CTRL_Ausgang; 
      S     #s_Z04_DruckA_FM; 
      R     #s_Z01_DruckA_anfahren; 
      R     #s_Z02_DruckA_CTRL; 
NETWORK
TITLE =Z00: Zustand Spannen Grundstellung
//- Befehl-Abbruch
//- INIT: kein Zustand aktiv
      UN    #i_Bef_DruckA; 
      O     ; 
      UN    #s_Z00_DruckA_GRST; 
      UN    #s_Z01_DruckA_anfahren; 
      UN    #s_Z02_DruckA_CTRL; 
      UN    #s_Z03_DruckA_OK; 
      UN    #s_Z04_DruckA_FM; 
      S     #s_Z00_DruckA_GRST; 
      R     #s_Z01_DruckA_anfahren; 
      R     #s_Z02_DruckA_CTRL; 
      R     #s_Z03_DruckA_OK; 
      R     #s_Z04_DruckA_FM; 
NETWORK
TITLE =FM: gespannt wegen Überwachung
//Reset im Aufruf-FB: FB200
      U     #s_Z04_DruckA_FM; 
      U     #i_ABST_CTRL_DruckA; 
      UN    #i_I_CTRL_OK; 
      S     #io_DruckA_FM_CTRL; 
NETWORK
TITLE =FM: gespannt wegen HAWE
//Reset im Aufruf-FB: FB200
      U     #s_Z04_DruckA_FM; 
      U     #i_vonHAWE_NOTAUS; 
      S     #io_DruckA_FM_HAWE; 
NETWORK
TITLE =Meldung: gespannt OK

      U     #s_Z03_DruckA_OK; 
      =     #o_DruckA_OK; 
NETWORK
TITLE =Befehl an HAWE: Spannen

      O     #s_Z01_DruckA_anfahren; 
      O     #s_Z02_DruckA_CTRL; 
      =     #o_anHAWE_Bef_Spannen; 
END_FUNCTION_BLOCK

