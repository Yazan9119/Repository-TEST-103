FUNCTION "FC_ML_SPI" : VOID
TITLE =
//$Revision: 1.9 $
//$Date: 2008/09/15 07:48:27CEST $
//$Author: riedlh $
//
//Minimalschmierung Fa.Vogel
//==========================
//
//MiMaSch nur aktiv, wenn DB20.DBW=2/3 ist
//Minimalmengenschmierung der Fa. Vogel Typ UFD10-020
//
//DB20.DBW70 = 2 : Nachfüllen über Schalterpunkt S2 
//DB20.DBW70 = 3 : Nachfüllen über Schalterpunkt S3 (Standardeinstellung)
//DB20.DBW70 = 4 : ohne Autom. Nachfüllen
//DB20.DBW108    : Überwachungszeit für Druckeinbrüche in msec.
//Wenn ein ByPassventil vorhanden ist, wird es über das Steuergrät angesteuert
//
//Einstellung der DIP-Schalter 
//                +-----------------+
// DIP-Schalter   | 1 2 3 4 5 6 7 8 | 
//                +-+-+-+-+-+-+-+-+-+
//Bei Vorwahl 2 :   0 1 0 0 0 0 0 0    
//Bei Vorwahl 3 :   0 0 1 0 0 0 0 0
//Bei Vorwahl 4 :   0 0 0 0 0 0 0 0
//                  | | | | | | | |
//                  | | | | | | +-+----- eigene Systemadresse 
//                  | | | | +-+--------- Anzahl Erzeuger
//                  | | | +------------- Datenanschluss
//                  | +-+--------------- Steuerung Nachfüllen
//                  +------------------- Steuerung ByPass

AUTHOR : gs
FAMILY : Tool
NAME : FC_MiMaS
VERSION : 1.0


VAR_TEMP
  AnW_2 : BOOL ;	//UFD10 Befüllung über Schaltpunkt S2. Mit/ohne ByPassventil 
  AnW_3 : BOOL ;	//UFD10 Befüllung über Schaltpunkt S3. Mit/ohne ByPassventil 
  AnW_4 : BOOL ;	//UFD10 ohne Befüllung
  M_Fuellen : BOOL ;	//Manuelles Befüllen
  S2_Fuellen : BOOL ;	//Befüllen über Schaltpunkt S2
  S3_Fuellen : BOOL ;	//Befüllen über Schaltpunkt S3
  AnW_8 : BOOL ;	
  AnW_9 : BOOL ;	
  PRG0 : BOOL ;	//Programmnummer 0 angewählt (Befüllen Drucklos)
END_VAR
BEGIN
NETWORK
TITLE =Überwachungen / Parametrierung
// Netzwerk-Übersicht
// ==================
// NW 1: Überwachungen / Parametrierung
// NW 2: Daten von NC
// NW 3: MiMaSch - Funktionen
// NW 4: 702446 "MEL, Minimalmengenschmierung Behaelter minimum"
// NW 5: 702447 "MEL, Minimalmengenschmierung Parametrierfehler"
// NW 6: 702448 "MEL, MMS Zeitüberwachung Fuellen ueberschritten
// NW 7: 702449 "MEL, Minimalmengenschmierung Hardware Fehler"
// NW 8: 702450 "MEL, Minimalmengenschmierung Druckeinbruch beim Schmiere
// NW 9: 702451 "MEL, Minimalmengenschmierung Falsche Programmnummer"
// NW 10: 701344 "WAR, Minimalmengenschmierung Behälter leer"             
// NW 11: 700442 "VS-HALT, Minimalmengenschmierung Beaelter leer M34 aktiv
// NW 12: 702452 "MEL, Minimalmengenschmierung Behaelter uebervoll"
// NW 13: RESET aller Fehler-Meldungen

// Anwahl der Minimalmengenschmierung
      L     "DB_PLC_MD_DB20".UDInt._035_ANWAHL_MIN_SCHMIER; 
      L     2; 
      ==I   ; 
      =     #AnW_2; 
      =     #S2_Fuellen; 
      TAK   ; 
      L     3; 
      ==I   ; 
      =     #AnW_3; 
      =     #S3_Fuellen; 
      TAK   ; 
      L     4; 
      ==I   ; 
      =     #AnW_4; 
      =     #M_Fuellen; 

// UFD010 angewählt ?
      UN    #AnW_2; 
      UN    #AnW_3; 
      UN    #AnW_4; 
      S     "DB_ML_SPI".MiMaSch_iO; 
      R     "DB_ML_SPI".MiMaSch_Fe; 
      BEB   ; 
      S     "DB_ML_SPI".P_Befuellung; // Autom. Befüllung über S2/S3
      S     "DB_ML_SPI".P_ByPass; // Steuerung über Steuergerät, wenn vorhanden

      U     #AnW_4; // ohne Autom. Nachbefüllen
      R     "DB_ML_SPI".P_Befuellung; // Autom. Befüllung über S2/S3

// DIP-Schalter laden  S1..S8
      U     #AnW_2; 
      L     2#1000000; // Nachbefüllen über S2
      SPB   _AnW; 
      U     #AnW_3; 
      L     2#100000; // Nachbefüllen über S2
      SPB   _AnW; 
      U     #AnW_4; 
      L     2#0; // ohne Autom Nachbefüllen

_AnW: T     "DB_ML_SPI".P_DIP; // SchmiermerkerByte

// Überwachungszeit Druck  iO
      L     "DB_PLC_MD_DB20".UDInt._054_SONDER_1; // in msec.
      T     "DB_ML_SPI".P_UZT_Druck; 

NETWORK
TITLE =Daten von NC
//Vor Einschalten der Schmierung muss eine gültige Programmnummer anliegen
//Programmnummer  1 ..  3 Freiblasen der Leitung
//Programmnummer  4 .. 24 Schmierprogramme
// Programmnummer von NC H34= 1 .. 24
      UN    "DB_H_FUNKTION".HX_M[17]; 
      SPB   _kH; 
      R     "DB_H_FUNKTION".HX_M[17]; 
      L     "DB_H_FUNKTION".HX_Real_Wert[17]; 
      TRUNC ; 
      T     "DB_ML_SPI".MiMaSch_Prg_Nr; 
_kH:  NOP   0; 


// Befüllen manuell/Ansaugen Autom.Befuellen  ( MMS mit Prog 1-3)
// mauelles befüllen über Not-Aus
      L     "DB_ML_SPI".MiMaSch_Prg_Nr; 
      L     0; 
      ==I   ; 
      =     #PRG0; 

      U(    ; 
      U     #AnW_2; 
      U     "DB_ML_SPI"._int.Beh_unter_min; 
      O     ; 
      U     #AnW_3; 
      UN    "DB_ML_SPI"._int.Beh_voll; 
      )     ; 
      U     #PRG0; 
      =     "DB_ML_SPI".BeFuellen; 

// Anlage EIN 
      U     "M_Leistung_Steht_an"; 
      UN    "DB_ML_SPI".BeFuellen; 
      =     "DB_ML_SPI".MiMaSch_EIN; 

// Schmierung Ein
      U(    ; 
      O     "M_M7_Act"; 
      O     "M_M8_Act"; 
      )     ; 
      UN    "DB_ML_SPI".BeFuellen; 
      =     "DB_ML_SPI".Schmierung; 

NETWORK
TITLE =MiMaSch - Funktionen
//MiMaSch_EIN     : Anlage EIN  (Hauptventil EIN)
//MiMaSch_STOP    : Unterbrechen für WZW (nur bei P_mitByPass = TRUE )
//Schmierung      : Schmierung EIN gespeicherte M-Funktion
//Man_Fuellen     : Manuelles Befüllen
//MiMaSch_Prg_Nr  : Schmiernummer 1-24
//P_DIP           : Einstellungen DIP-Schalter zur Kontrolle
//P_Fuel_vorhanden: Automatischer Nachbefüllung vorhanden
//P_mitByPass     : Bypass für schnellen Druckaufbau (WZW) vorhanden
//MiMaSch_iO      : VS-Freigabe
//MiMaSch_Fe      : Sammelfehler
//Drucklos        : Anlage ist zum Man. Befüllen bereit
//Fuellstand iO   : Freigabe für M34
      L     "I_ML_SPI_1"; //PEW  340
      T     DB282.DBW   12; //Eingänge

      CALL "FB_ML_SPI" , "DB_ML_SPI" ;

      L     DB282.DBW   14; // Ausgänge
      T     "O_ML_SPI_1"; // PAW  340

NETWORK
TITLE =702446 "MEL, Minimalmengenschmierung Behaelter minimum"

      U     "DB_ML_SPI".FeMe.Me_Beh_min; 
      =     "DB_FEHLERMELDUNG".Meldung4._702446_MMS_MIN; 
NETWORK
TITLE =702447 "MEL, Minimalmengenschmierung Parametrierfehler"

//Parametrierfehler
      O     "DB_ML_SPI".FeMe.Fe_ResetbeiEIN; 
      O     "DB_ML_SPI".FeMe.Fe_P_Fuellen; 
      O     "DB_ML_SPI".FeMe.Fe_ByPass_nicht_aktiv; 
      =     "DB_FEHLERMELDUNG".Meldung4._702447_MMS_PARAM_FE; 
NETWORK
TITLE =702448 "MEL, MMS Zeitüberwachung Fuellen ueberschritten

// Systemfehler
// Zeitüberwachung Füllen
      O     "DB_ML_SPI".FeMe.Fe_TimOut_Fuel; 
      =     "DB_FEHLERMELDUNG".Meldung4._702448_MMS_ZEIT_FE; 
NETWORK
TITLE =702449 "MEL, Minimalmengenschmierung Hardware Fehler"

// Hardwarefehler
      O     "DB_ML_SPI".FeMe.Fe_Systemfehler; 
      O     "DB_ML_SPI".FeMe.Fe_SchwSch_Fe; 
      =     "DB_FEHLERMELDUNG".Meldung4._702449_MMS_HW_FE; 
NETWORK
TITLE =702450 "MEL, Minimalmengenschmierung Druckeinbruch beim Schmiere

// Druckeinbruch beim Schmieren
      U     "DB_ML_SPI".FeMe.Fe_Schmierfehler; 
      =     "DB_FEHLERMELDUNG".Meldung4._702450_MMS_DRU_0SIG; 
NETWORK
TITLE =702451 "MEL, Minimalmengenschmierung Falsche Programmnummer"

// Anwenderfehler
// Falsche Programmnummer
      O     "DB_ML_SPI".FeMe.Fe_Prg_falsch; 
      =     "DB_FEHLERMELDUNG".Meldung4._702451_PRG_NR_FE; 
NETWORK
TITLE =701344 "WAR, Minimalmengenschmierung Behälter leer"             

// Behälterfüllstand niO
// Behälter leer - 
      U     "DB_ML_SPI".FeMe.Fe_Beh_leer; 
      S     "DB_FEHLERMELDUNG".WARNUNG2._701344_MMS_BEH_LEER; 
NETWORK
TITLE =700442 "VS-HALT, Minimalmengenschmierung Beaelter leer M34 aktiv

      U     "DB_ML_SPI".FeMe.Fe_Beh_leer; 
      U     "DB_DM_M_FUNKTION".MX_M[34]; 
      S     "DB_FEHLERMELDUNG".VS_Halt._700442_MMS_leer_M34_akt; 
NETWORK
TITLE =702452 "MEL, Minimalmengenschmierung Behaelter uebervoll"

// Behälter übervoll
      U     "DB_ML_SPI".FeMe.FE_Behaelter_zuVoll; 
      =     "DB_FEHLERMELDUNG".Meldung4._702452_MMS_BEH_UEB_MAX; 
NETWORK
TITLE =RESET aller Fehler-Meldungen

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".WARNUNG2._701344_MMS_BEH_LEER; 
      R     "DB_FEHLERMELDUNG".Meldung4._702446_MMS_MIN; 
      R     "DB_FEHLERMELDUNG".Meldung4._702447_MMS_PARAM_FE; 
      R     "DB_FEHLERMELDUNG".Meldung4._702448_MMS_ZEIT_FE; 
      R     "DB_FEHLERMELDUNG".Meldung4._702449_MMS_HW_FE; 
      R     "DB_FEHLERMELDUNG".Meldung4._702450_MMS_DRU_0SIG; 
      R     "DB_FEHLERMELDUNG".Meldung4._702451_PRG_NR_FE; 
      R     "DB_FEHLERMELDUNG".Meldung4._702452_MMS_BEH_UEB_MAX; 
      R     "DB_FEHLERMELDUNG".VS_Halt._700442_MMS_leer_M34_akt; 

END_FUNCTION

