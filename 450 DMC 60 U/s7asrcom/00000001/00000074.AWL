FUNCTION "FC_SAFE_ALLG" : VOID
TITLE =
//$Revision: 1.19 $
//$Date: 2008/10/29 09:41:56CET $
//$Author: hgc $
//
//Baustein für Solutionline DMx55-125x2
AUTHOR : SCH
FAMILY : MA
NAME : LEISTUNG
VERSION : 0.1


VAR_TEMP
  T_IMN_Fehler_ASUP : BOOL ;	
  T_IMN_Fehler_ASUP1 : BOOL ;	
  T_ASUP_RM_FALSCH : BOOL ;	
  T_Einricht_OK : BOOL ;	
  T_3_BA_OK : BOOL ;	
  T_4_BA_OK : BOOL ;	
  T_KSOBA_OK : BOOL ;	
  T_Freigabe_Ablauefe : BOOL ;	
  Max_soll : REAL ;	
  Prog_soll : REAL ;	
  T_Option_PW : BOOL ;	
  T_Option_TUER_LI : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Spindel Stop C Impuls
// AB 23.04.04 Fehler Spindel trudelt aus bei Not-AUS SBR-Fehler
      U     "DB_ACHSE6_SPINDEL".E_StopC; 
      UN    "DB_ACHSE6_SPINDEL".E_n_nx; 
      FP    "DB_SAFE_ALLG1".M_SAFE_SPI_SYNC_STOPC_FM; //  "M_SAFE_SPI_SYNC_STOPC_FM"
      =     "DB_SAFE_ALLG1".M_SAFE_SPI_SYNC_STOPC_IM; //  "M_SAFE_SPI_SYNC_STOPC_IM"

NETWORK
TITLE =Kein Stop Hauptachsen

      UN    "DB_ACHSE1_X".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE1_X".E_StopC; // STOP C aktiv
      UN    "DB_ACHSE1_X".E_StopD; // STOP D aktiv

      UN    "DB_ACHSE2_Y".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE2_Y".E_StopC; // STOP C aktiv
      UN    "DB_ACHSE2_Y".E_StopD; // STOP D aktiv

      UN    "DB_ACHSE3_Z".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE3_Z".E_StopC; // STOP C aktiv
      UN    "DB_ACHSE3_Z".E_StopD; // STOP D aktiv

      =     "m_vke_uebertrag_1"; // STOP A/B aktiv
// STOP C aktiv
// STOP D aktiv

// STOP A/B aktiv
// STOP C aktiv
// STOP D aktiv

// Fehler von ER Modul Überwachung
// Kein Stop Hauptachsen
NETWORK
TITLE =Kein Stop Hauptachsen

      U     "m_vke_uebertrag_1"; 
      UN    "DB_ACHSE4".E_StopAB; 
      UN    "DB_ACHSE4".E_StopC; 
      UN    "DB_ACHSE4".E_StopD; 
      UN    "DB_ACHSE5".E_StopAB; 
      UN    "DB_ACHSE5".E_StopC; 
      UN    "DB_ACHSE5".E_StopD; 
      UN    "M_MA_Fehler_ER_Modul"; 
      =     "DB_SAFE_ALLG1".HAX_Kein_Stop; 
NETWORK
TITLE =Kein Stop Spindel

      UN    "DB_ACHSE6_SPINDEL".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE6_SPINDEL".E_StopC; // STOP C aktiv
      UN    "DB_ACHSE6_SPINDEL".E_StopD; // STOP D aktiv
      UN    "M_MA_Fehler_ER_Modul"; // Fehler von ER Modul Überwachung
      =     "DB_SAFE_ALLG1".Spi_Kein_Stop; // Kein Stop Spindel
NETWORK
TITLE =Maschine Ein verzögert

      U     "I_AL_MASCHINE_EIN"; 
      L     S5T#2S; 
      SE    "T_SAFECO_Maschine_Ein"; 
NETWORK
TITLE =Achsen: Leistung steht an 

      U     "T_SAFECO_Maschine_Ein"; 
      UN    "DB_ACHSE1_X".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE2_Y".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE3_Z".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE4".E_StopAB; // STOP A/B aktiv
      UN    "DB_ACHSE5".E_StopAB; // STOP A/B aktiv
      UN    "M_MA_Fehler_ER_Modul"; // Fehler von ER Modul Überwachung
      U     "O_AL_PLC_BTB"; 
      U     "TE_ENABLE_OUT_3_NX_NCU"; 
      O     "DB_FEHLERMELDUNG".SAFE_MELD._701501_ZD_HAX_laeuft; 
      =     "DB_SAFE_ALLG1".Leistung_steht_an; 
NETWORK
TITLE =SP-VS-Halt bei BA-Wechsel

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt._700203_Stop_BA_Wechsel; 

// Negative Flanke von den Sonder BA's Stop Hauptachsen + Spindel

      U(    ; 
      O     "DB_SPL".IFX_GEN_OM1_SEL; 
      O     "DB_SPL".IFX_GEN_OM2_SEL; 
      O     "DB_SPL".IFX_GEN_OM3_4_SEL; 
      )     ; 
      FN    "M_FMN_Sonder_BA"; 
      UN    "M_Hauptspindel_steht"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt._700203_Stop_BA_Wechsel; 
      NOP   0; 
NETWORK
TITLE =Arbeistraum Achsen SBH aktiv

      U(    ; 
      O     "O_AL_KABINE_ENTRIEGELN"; 
      ON    "I_AL_Kabine_zu"; 
      )     ; 
      =     "M_SBH_ANF_ARBEITSRAUM"; 
      U     "M_SBH_ANF_ARBEITSRAUM"; 
      U(    ; 
      U     "DB_ACHSE1_X".E_SBH; 
      U     "DB_ACHSE2_Y".E_SBH; 
      U     "DB_ACHSE3_Z".E_SBH; 
      U     "DB_ACHSE4".E_SBH; 
      U(    ; 
      O     "DB_ACHSE5".E_SBH; 
      ON    "DB_SIEM_STARTUP".ActivAxis[5]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE11".E_SBH; 
      ON    "DB_SIEM_STARTUP".ActivAxis[11]; 
      )     ; 
      U     "DB_ACHSE6_SPINDEL".E_StopAB; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      )     ; 
      =     "M_SAFETY_SBH_AKTIV"; 
NETWORK
TITLE =Meldung: Türe entriegeln nicht möglich->Signal Achsen stehen feh

      U     "M_SAFETY_SBH_AKTIV"; 
      UN    "DB_SPL".OFX_GEN_STOP_MONITOR; 
      U     "DB_SAFE_ALLG1".Leistung_steht_an; 
      =     "DB_FEHLERMELDUNG".MELDUNG6._702839_K_AX_STEHEN; 
NETWORK
TITLE =SAFETY: SBH aktiv -> Freigabe Arbeitsraumtüre entriegeln

      U     "M_SAFETY_SBH_AKTIV"; 
      =     "M_SBH_AKTIV_ARBEITSRAUM"; 
NETWORK
TITLE =Palettenwechsler Achsen SBH aktiv -> Freigabe entriegeln

      U(    ; 
      UN    "O_PW_TUER_ENTRIEGELN"; 
      U(    ; 
      O     "I_ROBOT_LOCK"; 
      ON    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_ROB_BELADUNG_RPL; 
      )     ; 
      U(    ; 
      ON    "DB_FEHLERMELDUNG".SAFE_MELD._701502_ZD_ANF_HAX; 
      ON    "DB_PLC_MD_DB20".UDHex._04_BIT7_SAFETY; 
      O     "DB_SAFE_ALLG1".ZD_Z_disable; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     "M_SBH_ANF_RÜSTPLATZ"; 
      O     "m_inbetrieb_logisch_1"; 
      )     ; 
      ON    "I_PW_Tueren_zu"; 
      )     ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      UN    "DB_PLC_MD_DB20".UDHex._32_Bit7_RPL_UEBERBR; 
      =     "M_SBH_ANF_RÜSTPLATZ"; 
      U     "M_SBH_ANF_RÜSTPLATZ"; 
      U(    ; 
      O     "DB_ACHSE8".E_SBH; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      )     ; 
      =     "M_SBH_AKTIV_RÜSTPLATZ"; 
NETWORK
TITLE =Magazin Achsen SBH aktiv -> Freigabe entriegeln

      U(    ; 
      U     "O_WM_Beladung_entr_auf"; 
      U(    ; 
      ON    "DB_FEHLERMELDUNG".SAFE_MELD._701502_ZD_ANF_HAX; 
      ON    "DB_PLC_MD_DB20".UDHex._04_BIT7_SAFETY; 
      O     "DB_SAFE_ALLG1".ZD_Z_disable; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     "M_SBH_ANF_MAGAZIN"; 
      )     ; 
      ON    "I_WM_Beladung_zu"; 
      )     ; 
      =     "M_SBH_ANF_MAGAZIN"; 
      U     "M_SBH_ANF_MAGAZIN"; 
      U(    ; 
      U(    ; 
      O     "DB_ACHSE7".E_SBH; 
      ON    "DB_SIEM_STARTUP".ActivAxis[7]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE13".E_SBH; 
      ON    "DB_SIEM_STARTUP".ActivAxis[13]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE14".E_SBH; 
      ON    "DB_SIEM_STARTUP".ActivAxis[14]; 
      )     ; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     "DB_PLC_MD_DB20".UDHex._05_BIT0_TESTRACK_IO; 
      )     ; 
      =     "M_SBH_AKTIV_MAGAZIN"; 
NETWORK
TITLE =Palettenspeicher Achsen SBH aktiv -> Freigabe entriegeln

      U(    ; 
      U     "O_PS_RPL_TUER_VORN_ENTR"; 
      U     "O_PS_RPL_TUER_HINT_ENTR"; 
      UN    "DB_SAFE_ZD".GR2_ZD.Tuere_zuhalten; 
      U(    ; 
      ON    "DB_FEHLERMELDUNG".MELDUNG6._702834_PS_ZD_ANF_AX; 
      ON    "DB_PLC_MD_DB20".UDHex._04_BIT7_SAFETY; 
      O     "DB_SAFE_ALLG1".PS_ZD_disable; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     "M_SBH_ANF_RPL_PS"; 
      )     ; 
      O     ; 
      UN    "I_PS_RPL_TUER_VORNE_ZU"; 
      UN    "I_PS_RPL_TUER_HINTEN_ZU"; 
      )     ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      =     "M_SBH_ANF_RPL_PS"; 
      U     "M_SBH_ANF_RPL_PS"; 
      U(    ; 
      U     "DB_ACHSE8".E_SBH; 
      U     "DB_ACHSE9".E_SBH; 
      U     "DB_ACHSE10".E_SBH; 
      U(    ; 
      O     "DB_ACHSE16".E_SBH; 
      ON    "DB_SIEM_STARTUP".ActivAxis[16]; 
      )     ; 
      ON    "DB_SAFE_ALLG1".Leistung_steht_an; 
      )     ; 
      =     "M_SBH_AKTIV_RPL_PS"; 
END_FUNCTION

