FUNCTION "FC_ACHS_16" : VOID
TITLE =
//$Revision: 1.15 $
//$Date: 2008/10/11 09:26:30CEST $
//$Author: hgc $
VERSION : 0.1


VAR_TEMP
  TX_Vorschub_Halt_Ax16 : BOOL ;	
  TX_Override_aktiv : BOOL ;	
  TB_Vorschub_OVR : BYTE ;	
  TINT_AX_NR : INT ;	
  HUB_Geschwindigkeit : REAL ;	
END_VAR
BEGIN
NETWORK
TITLE =16-Achse: Aktivierung Softwarenocken nachdem Achse referiert ist
//16-Achse (Hubachse Palettenspeicher)
      UN    "DB_PLC_MD_DB20".UDHex._42_BIT1_ACHSE16_MESSYST; 
      U     "DB_ACHSE16".E_RefSyn1; 
      O     ; 
      U     "DB_PLC_MD_DB20".UDHex._42_BIT1_ACHSE16_MESSYST; 
      U     "DB_ACHSE16".E_RefSyn2; 
      =     "DB_ACHSE16".A_SWCam; 
NETWORK
TITLE =Vorschub Halt

      O     "M_Vorschub_Halt_Asyn3"; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_Kanal; 
      =     #TX_Vorschub_Halt_Ax16; 
NETWORK
TITLE =Override aktivieren

      U(    ; 
      O     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      O     "DB_PLC_MD_DB20".UDHex._04_BIT0_OVR_HILFSACHSEN; 
      O     ; 
      U(    ; 
      O     "DB_OEM".OEM_PW_Rettbildaktiv; 
      O     "DB_OEM".OEM_SpeEinrbildaktiv; 
      O     "DB_OEM".OEM_SpeRettbildaktiv; // "DB_SIEM_BAG".E_JOG
      O     "DB_OEM".OEM_SpeRettbildaktiv3; 
      ON    "MX_Jobbetrieb"; 
      )     ; 
      U     "M_Handbetrieb"; 
      )     ; 
      UN    "DB_SAFE_ZD".GR2_BT.Override_auf_100; 
      =     #TX_Override_aktiv; 
NETWORK
TITLE =Achsnummer mit 0 vorbesetzen währen Bremsentest läuft

      U     "m_eins"; 
      =     L      8.0; 
      U     L      8.0; 
      U     "DB_SAFE_ALLG1".PS_AX_BT_laeuft; 
      SPBNB _001; 
      L     0; 
      T     #TINT_AX_NR; 
_001: NOP   0; 
      U     L      8.0; 
      UN    "DB_SAFE_ALLG1".PS_AX_BT_laeuft; 
      SPBNB _002; 
      L     16; 
      T     #TINT_AX_NR; 
_002: NOP   0; 
NETWORK
TITLE =Umschalten der Geschwindigkeit
//Bei 0.000000e+000 wird der Geschwindigkeitswert aus Maschinendatum aktiv
      U     "m_eins"; 
      =     L      8.0; 
      U     L      8.0; 
      U     "MX_Transport_Gst"; 
      SPBNB _003; 
      L     0.000000e+000; 
      T     #HUB_Geschwindigkeit; 
_003: NOP   0; 
      U     L      8.0; 
      UN    "MX_Transport_Gst"; 
      SPBNB _004; 
      L     2.500000e+003; 
      T     #HUB_Geschwindigkeit; 
_004: NOP   0; 
NETWORK
TITLE =Aufruf FC_ACHSE_HILF

      CALL "FC_ACHSE_HILF" (
           EX_EN                    := TRUE,
           EX_UDHex_00_IB_Bit_Achse := "DB_PLC_MD_DB20".UDHex._40_Bit1_ACHSE16_AUSBLEN,
           EX_UDHex_01_Ax_Messystem := "DB_PLC_MD_DB20".UDHex._42_BIT1_ACHSE16_MESSYST,
           EX_UDHex_RPF_Richtung    := FALSE,
           EX_Option_Safety         := FALSE,//"O_INFEED_ENABLE_NCU"    // Bei Solutionline bleibt RF und ImpFrg für 3 Sek. nach Not-Aus anstehen
           EX_Safe_Bremsentest      := FALSE,
           EX_Safe_StopC_aktiv      := FALSE,
           EX_Safe_StopA_aktiv      := "DB_ACHSE16".E_StopAB,
           EX_Safe_Teststop_laeuft  := "DB_SPL".IPLC_ZD_GR2_RUN,
           EX_Maschine_Ein          := "M_MA_Maschine_Ein",
           EX_Antrieb_Sperre        := "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_16,
           EX_Freigabe_Regler       := "DB_Achs16_Hub_PS".MX_Ax_Leistung_Aktiv,
           EX_Leistung_steht_an     := "DB_Achs16_Hub_PS".MX_Ax_Leistung_Aktiv,
           EX_VSHalt_EinzelAchse    := "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_16_Achse,
           EX_VSHalt_Gruppe         := #TX_Vorschub_Halt_Ax16,
           EX_VSHalt_Kanal          := "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_Kanal,
           EX_RefPkt_Achse          := FALSE,
           EX_Abbruch_Positionieren := "DB_Achs16_Hub_PS".MX_Abbruch_Positionieren,
           EX_Ax_KorrWirksam_1x7    := #TX_Override_aktiv,
           EX_Ax_E_RefSyn1_60x4     := "DB_ACHSE16".E_RefSyn1,
           EX_Ax_E_FahrAF_61x0      := "DB_ACHSE16".E_TravRequ,
           EX_Ax_E_Steht_61x4       := "DB_ACHSE16".E_Stat,
           EX_Ax_E_LageRegler_61x5  := "DB_ACHSE16".E_PositContr,
           EX_Ax_E_DrehzRegler_61x6 := "DB_ACHSE16".E_SpeedContr,
           EX_Ax_E_StromRegler_61x7 := "DB_ACHSE16".E_CurrentContr,
           EX_Ax_E_PLC_Achse_63x1   := "DB_ACHSE16".E_PLCCtrlAx,
           EX_Ax_E_FBMinus_64x6     := "DB_ACHSE16".E_TCMinus,
           EX_Ax_E_FBPlus_64x7      := "DB_ACHSE16".E_TCPlus,
           EX_Ax_E_ChPoss_68x5      := "DB_ACHSE16".E_ChPoss,
           EX_Ax_E_NeutrASp_68x6    := "DB_ACHSE16".E_NeutrASp,
           EX_Ax_E_MotOK_93x5       := "DB_ACHSE16".E_MotOK,
           EX_Ax_E_IntSperr_93x6    := "DB_ACHSE16".E_IntegratDisable,
           EX_Ax_E_ImpFrei_93x7     := "DB_ACHSE16".E_PulseEnable,
           EX_Ax_E_MTempWarn_94x0   := "DB_ACHSE16".E_MTempWarn,
           EX_Ax_E_KTempWarn_94x1   := "DB_ACHSE16".E_HTempWarn,
           EX_FahrAnf_Positiv       := "DB_Achs16_Hub_PS".MX_FahrAnf_Plus,
           EX_FahrAnf_Negativ       := "DB_Achs16_Hub_PS".MX_FahrAnf_Minus,
           EX_Inch_Umrechnen        := FALSE,
           EB_Achs_Typ              := B#16#3,
           EB_MSST_Vorschub_OVR     := "MSST_Vorschub_OVR",
           EB_Positierung           := B#16#0,
           EI_Teilung               := 0,
           EI_Achs_Nr               := #TINT_AX_NR,//16
           ER_SollPosition          := "DB_Achs16_Hub_PS".MR_SollPosition,
           ER_Istposition           := "DB_Achs16_Hub_PS".MR_IstPosition,
           ER_Geschwindigkeit       := #HUB_Geschwindigkeit,//0.000000e+000
           ET_Timer_Ax_Freigabe     := "Te_Achs16",
           EAX_Ax_Anforderung_Pos   := "DB_Achs16_Hub_PS".MX_Anf_Positionieren,
           EAX_Ax_Rueckmeldung_Pos  := "DB_Achs16_Hub_PS".MX_Rueck_Positionieren,
           EAX_Ax_Fehler_Pos        := "DB_Achs16_Hub_PS".MX_Fehler_Positionieren,
           EAX_Ax_Anf_Referenzieren := "DB_Achs16_Hub_PS".MX_Anf_Referieren,
           EAX_Ax_A_ASpSperre_1x3   := "DB_ACHSE16".A_ASpDisable,
           EAX_Ax_A_NachfBetr_1x4   := "DB_ACHSE16".A_Follow_upMode,
           EAX_Ax_A_LageMess1_1x5   := "DB_ACHSE16".A_PosMeas1,
           EAX_Ax_A_LageMess2_1x6   := "DB_ACHSE16".A_PosMeas2,
           EAX_Ax_A_KorrW_1x7       := "DB_ACHSE16".A_ORactive,
           EAX_Ax_A_ReglerFrei_2x1  := "DB_ACHSE16".A_ContrEnable,
           EAX_Ax_A_RestWegRe_2x2   := "DB_ACHSE16".A_DelDTGSpReset,
           EAX_Ax_A_VSSpHalt_4x3    := "DB_ACHSE16".A_FDSpStop,
           EAX_Ax_A_Minus_4x6       := "DB_ACHSE16".A_Minus,
           EAX_Ax_A_Plus_4x7        := "DB_ACHSE16".A_Plus,
           EAX_Ax_A_NCASpChA_8x0    := "DB_ACHSE16".A_NCASpChanA,
           EAX_Ax_A_NCASpStrobe_8x4 := "DB_ACHSE16".A_NCASpStrobe,
           EAX_Ax_A_PLCASp_8x7      := "DB_ACHSE16".A_PLCASp,
           EAX_Ax_A_RefVerzoeg_12x7 := "DB_ACHSE16".A_DelayRef,
           EAX_Ax_A_ImpFrei_21x7    := "DB_ACHSE16".A_PulseEnable,
           EAX_Ax_A_PLC_Achse_28x7  := "DB_ACHSE16".A_PLCAxis,
           EAX_AX_RefSync1          := "DB_Achs16_Hub_PS".MX_Achs_Referiert,
           EAX_Alarm_Regler         := "DB_FEHLERMELDUNG".Spi_VS_Halt1._510115_16_IMP_REGFRG_0S,
           EAX_Alarm_VSSP_Mot_Temp  := "DB_FEHLERMELDUNG".Spi_VS_Halt1._510116_16_TEMP_ZU_HOCH,
           EAX_Alarm_VSSP_KK_Temp   := "DB_FEHLERMELDUNG".Spi_VS_Halt1._510206_16_KK_TEMP_ZH,
           EAX_Alarm_NA_MotOK       := "DB_FEHLERMELDUNG".Spi_VS_Halt3._701147_16_DR_READY_0,
           EAX_Alarm_FehlerPos      := "DB_FEHLERMELDUNG".WARNUNG4._702325_Rueckm_Achs_16un,
           EAX_M_Bremse_oeffnen     := "M_PS_HUB_AX_Bremse_auf",
           EAX_Anf_Lesen_AxPos      := "DB_Achs16_Hub_PS".MX_Ax_Position_Lesen,
           EAX_Rdy_Lesen_AxPos      := "DB_Achs16_Hub_PS".M_Ax_Pos_Lesen_Fertig,
           EAX_Achse_Fahranf        := "DB_Achs16_Hub_PS".MX_Ax_FahrAnf,
           EAX_Achse_InPosition     := "DB_Achs16_Hub_PS".MX_Ax_InPosition,
           EAX_Achse_Fehler_Pos     := "DB_Achs16_Hub_PS".MX_Ax_Fehler_Pos,
           EAX_Achse_Positioniert   := "DB_Achs16_Hub_PS".MX_Ax_Positioniert,
           EAX_InPos_Merker         := "DB_Achs16_Hub_PS".MX_FP_Achse_in_Pos,
           EAB_Ax_VS_Korrektur_DBB0 := "DB_ACHSE16".A_FD_OR,
           EAB_SKZ_Achse            := "DB_Achs16_Hub_PS".MB_SKZ_Achse);

NETWORK
TITLE =Achse Reset Fahranforderung

      U     "DB_Achs16_Hub_PS".MX_Abbruch_Positionieren; 
      =     "DB_ACHSE16".A_AxReset; 
      R     "DB_Achs16_Hub_PS".MX_Abbruch_Positionieren; 
      R     "DB_Achs16_Hub_PS".MX_Ax_FahrAnf; 
NETWORK
TITLE =Achse fährt

      ON    "DB_ACHSE16".E_Stat; 
      O     "DB_ACHSE16".E_TCMinus; 
      O     "DB_ACHSE16".E_TCPlus; 
      =     "DB_Achs16_Hub_PS".MX_Achse_Faehrt; 
NETWORK
TITLE =Zwangsdynamisierung
// Bei Zwangsdynamisierung Reglerfreigabe wegnehmen --> arbeiten gegen Bremse
// Impulsfreigabe setzen
      U     "DB_SAFE_ZD".GR2_ZD.Reglerfreigabe; 
      SPBN  xx3; 
      R     "DB_ACHSE16".A_ContrEnable; 
      S     "DB_ACHSE16".A_PulseEnable; 
xx3:  NOP   0; 

      U     "DB_SAFE_ZD".Seco_Test.BER2_Test_Bremse_laeuft; 
      =     "DB_ACHSE16".A_SWLimit2Minus; 
      =     "DB_ACHSE16".A_SWLimit2Plus; 

NETWORK
TITLE =SINAMICS / SOLUTIONLINE: Nahtstellensignal Bremse schliessen

      U     "DB_PLC_MD_DB20".UDHex._31_Bit1_SOLUTIONLINE; 
      U(    ; 
      O     "DB_SAFE_ZD".GR2_BT.Bremse_schliessen; 
      O     "DB_SAFE_ZD".GR2_ZD.Bremse; 
      )     ; 
      =     "DB_ACHSE16".A_BrakeToClose; 
END_FUNCTION

