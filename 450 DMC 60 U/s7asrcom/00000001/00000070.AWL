FUNCTION "FC_SAFE_SPL_PLC" : VOID
TITLE =
//$Revision: 1.31 $
//$Date: 2008/11/08 07:06:15CET $
//$Author: hgc $
//
//
//
//PLC-SPL Baustein für Solutionline DMx55-125x2
//
//Unterteilt in zwei Sicherheitsbereiche:
//1. BEREICH 1 (MASCHINE) (Arbeitsraumachsen/Palettenwechslerachse/ Mgazinachsen)
//bestehend aus Achse:     X Y Z B C SPI              PW            MAG1/MAG2 GR  
//
//2. BEREICH 2 (PS)     (Palettenspeicherachsen / Palettenwechslerachse)
//bestehend aus Achse:     DREHST/TRANSP/HUB              PW
//
AUTHOR : GehAB
FAMILY : SPL
VERSION : 3.0


VAR_TEMP
  TX_ABNAHME_TEST_SBH_AKT : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =PLC-->NC: Vorbesetzungen
// Reissleine  
      SET   ; 
      =     "DB_SPL".IPLC_NEG_NOT_HALT; 
      =     "DB_SIEM_NCK".A_Disabl_Out1; 
      =     "DB_SIEM_NCK".A_OvMask_Out1; 
      =     "DB_SIEM_NCK".A_Set_Out1; 
      CLR   ; 
      =     "DB_SIEM_NCK".A_Disabl_Out2; 
      =     "DB_SIEM_NCK".A_Disabl_Out3; 
      SET   ; 
      =     "DB_SIEM_NCK".A_OvMask_Out2; 
      =     "DB_SIEM_NCK".A_OvMask_Out3; 
NETWORK
TITLE =INSEP[01] Betriebsart 1 auf Merker abbilden

      U     "DB_SPL".IFX_GEN_OM1_SEL; 
      =     "I_AL_ANWAHL_BA1"; 
NETWORK
TITLE =INSEP[02] Betriebsart 2 auf Merker abbilden

      U     "DB_SPL".IFX_GEN_OM2_SEL; 
      =     "I_AL_ANWAHL_BA2"; 
NETWORK
TITLE =INSEP[03] Betriebsart 3/4 auf Merker abbilden

      U     "DB_SPL".IFX_GEN_OM3_4_SEL; 
      =     "I_AL_ANWAHL_BA3/4"; 
NETWORK
TITLE =INSEP[04] Maschine Ein (Kein NOT-AUS) auf Merker abbilden

      U     "DB_SPL".IFX_GEN_EMSTP_N; 
      =     "I_AL_NEG_Not_Aus"; 
NETWORK
TITLE =INSEP[05] Antriebsfreigabe Werkzeugwechsler auf Merker abbilden

      U     "DB_SPL".ISX_TC_DRIVES_EN_CNC; 
      =     "I_TC_ENABLE"; 
NETWORK
TITLE =INSEP[06] Antriebsfreigabe Werkzeugmagazin auf Merker abbilden

      U     "DB_SPL".ISX_TM_DRIVES_EN_CNC; 
      =     "I_WM_Energiefreigabe"; 
NETWORK
TITLE =INSEP[07] Antriebsfreigabe Palettenwechsler auf Merker abbilden

      U     "DB_SPL".ISX_PC_DRIVES_EN_CNC; 
      =     "I_PC_ENABLE"; 
NETWORK
TITLE =INSEP[08] Antriebsfreigabe Palettenspeicher auf Merker abbilden

      U     "DB_SPL".ISX_PS_DRIVES_EN_CNC; 
      =     "I_PS_FRG_AR_PSACHS"; 
NETWORK
TITLE =INSEP[09] Antriebsfreigabe Rüstplatz auf Merker abbilden

      U     "DB_SPL".ISX_SST_DRIVES_EN_CNC; 
      =     "I_PW_RPL_FREIGABE"; 
NETWORK
TITLE =PLC-->NC: Safety Abnahme läuft
// kein NOT-AUS auslösen und nicht SBH/SG abwählen bei Fehler SG/SGH
      U     "DB_PLC_MD_DB20".UDHex._05_BIT2_SAFTY_ABNAHME; 
      =     "DB_SPL".IPLC_Abnahme; 
NETWORK
TITLE =PLC-->NC: Zwangsdynamisierung laeuft
//BEREICH 1 (MASCHINE)
//kein Not-AUS auslösen
      U(    ; 
      U(    ; 
      O     "DB_SAFE_ZD".Seco_Test.Test_HW_laueft; 
      O     "DB_SAFE_ZD".Seco_Test.Test_ABSP_laeuft; 
      O     "DB_SAFE_ZD".Seco_Test.Test_extStop_laeuft; 
      O     "DB_SAFE_ZD".Seco_Test.Test_Bremse_laeuft; 
      )     ; 
      S     "DB_SAFE_ALLG1".M_IMP_Test_HW_laueft; 
      U(    ; 
      U     "DB_SAFE_ZD".Seco_Test.Test_extStop_laeuft; 
      FN    "DB_SAFE_ALLG1".M_FP_Test_HW_laueft; 
      O     "M_Ruecksetze_Fehler"; 
      )     ; 
      R     "DB_SAFE_ALLG1".M_IMP_Test_HW_laueft; 
      U     "DB_SAFE_ALLG1".M_IMP_Test_HW_laueft; 
      )     ; 
      L     S5T#1S; 
      SA    "T_SA_HW_Test_laeuft"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      U     "T_SA_HW_Test_laeuft"; 
      =     "DB_SPL".IPLC_ZD_GR1_RUN; 
NETWORK
TITLE =PLC-->NC: ARBEITSRAUM: SBH Anforderung durch Anwahl Entriegeln
//BEREICH 1 (MASCHINE)
      U(    ; 
      O     "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     ; 
      U     "T_SAFECO_Maschine_Ein"; 
      U     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      U     "M_SBH_ANF_ARBEITSRAUM"; 
      U(    ; 
      U     "DB_ACHSE1_X".E_n_nx; 
      U     "DB_ACHSE2_Y".E_n_nx; 
      U     "DB_ACHSE3_Z".E_n_nx; 
      U     "DB_ACHSE4".E_n_nx; 
      U(    ; 
      O     "DB_ACHSE5".E_n_nx; 
      ON    "DB_SIEM_STARTUP".ActivAxis[5]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE11".E_n_nx; 
      ON    "DB_SIEM_STARTUP".ActivAxis[11]; 
      )     ; 
      O     "DB_SPL".IPLC_CAB_UNLOCK; 
      O     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      =     "DB_SPL".IPLC_CAB_UNLOCK; 
NETWORK
TITLE =PLC-->NC: PW: SBH Anforderung durch Anwahl Entriegeln
//BEREICH 1 (MASCHINE)
//071011: Silgener "DB_SPL".IPLC_PC_UNLOCK bei Rundspeicher (>=RS4) und 
//eine Rundspeicherttür entriegeln >> SBH Anforderung PC
      U(    ; 
      O     "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     ; 
      U     "T_SAFECO_Maschine_Ein"; 
      U     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      U(    ; 
      O     "M_SBH_ANF_RÜSTPLATZ"; 
      O     "M_SBH_ANF_RPL_PS"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE8".E_n_nx; 
      O     "DB_SPL".IPLC_PC_UNLOCK; 
      O     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      =     "DB_SPL".IPLC_PC_UNLOCK; 
NETWORK
TITLE =PLC-->NC: MAGAZIN: SBH Anforderung durch Anwahl Entriegeln
//BEREICH 1 (MASCHINE)
      U(    ; 
      O     "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     ; 
      U     "T_SAFECO_Maschine_Ein"; 
      U     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      U(    ; 
      U     "M_SBH_ANF_MAGAZIN"; 
      UN    "I_WM_ZUSTIMMTASTE"; 
      U(    ; 
      U     "DB_ACHSE7".E_n_nx; 
      U(    ; 
      O     "DB_ACHSE13".E_n_nx; 
      ON    "DB_SIEM_STARTUP".ActivAxis[13]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE14".E_n_nx; 
      ON    "DB_SIEM_STARTUP".ActivAxis[14]; 
      )     ; 
      O     "DB_SPL".IPLC_TC_TM_UNLOCK; 
      )     ; 

      O     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      =     "DB_SPL".IPLC_TC_TM_UNLOCK; 
NETWORK
TITLE =Tetstrack aktiv

      U     "DB_PLC_MD_DB20".UDHex._05_BIT0_TESTRACK_IO; 
      SPB   T_R1; 
NETWORK
TITLE =Achsen kein Stop A/B aktiv
//BEREICH 1 (MASCHINE)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      UN    "DB_ACHSE1_X".E_StopAB; 
      UN    "DB_ACHSE2_Y".E_StopAB; 
      UN    "DB_ACHSE3_Z".E_StopAB; 
      UN    "DB_ACHSE4".E_StopAB; 
      UN    "DB_ACHSE5".E_StopAB; 
      UN    "DB_ACHSE7".E_StopAB; 
      UN    "DB_ACHSE8".E_StopAB; 
      UN    "DB_ACHSE11".E_StopAB; 
      UN    "DB_ACHSE13".E_StopAB; 
      UN    "DB_ACHSE14".E_StopAB; 
      UN    "DB_ACHSE15".E_StopAB; 
      =     "DB_SPL".II_HAX_KSTOPAB_AKTIV; 
NETWORK
TITLE =Spindel Stop A/B aktiv
//BEREICH 1 (MASCHINE)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      U     "DB_ACHSE6_SPINDEL".E_StopAB; 
      =     "DB_SPL".II_SPI_STOPAB_AKTIV; 
NETWORK
TITLE =Achsen kein Stop C aktiv
//BEREICH 1 (MASCHINE)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      UN    "DB_ACHSE1_X".E_StopC; 
      UN    "DB_ACHSE2_Y".E_StopC; 
      UN    "DB_ACHSE3_Z".E_StopC; 
      UN    "DB_ACHSE4".E_StopC; 
      UN    "DB_ACHSE5".E_StopC; 
      UN    "DB_ACHSE7".E_StopC; 
      UN    "DB_ACHSE8".E_StopC; 
      UN    "DB_ACHSE11".E_StopC; 
      UN    "DB_ACHSE13".E_StopC; 
      UN    "DB_ACHSE14".E_StopC; 
      UN    "DB_ACHSE15".E_StopC; 
      =     "DB_SPL".II_HAX_KSTOPC_AKTIV; 
NETWORK
TITLE =Spindel Stop C aktiv
//BEREICH 1 (MASCHINE)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      U     "DB_ACHSE6_SPINDEL".E_StopC; 
      =     "DB_SPL".II_SPI_STOPC_AKTIV; 
NETWORK
TITLE =Achsen kein Stop D aktiv
//BEREICH 1 (MASCHINE)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      UN    "DB_ACHSE1_X".E_StopD; 
      UN    "DB_ACHSE2_Y".E_StopD; 
      UN    "DB_ACHSE3_Z".E_StopD; 
      UN    "DB_ACHSE4".E_StopD; 
      UN    "DB_ACHSE5".E_StopD; 
      UN    "DB_ACHSE7".E_StopD; 
      UN    "DB_ACHSE8".E_StopD; 
      UN    "DB_ACHSE11".E_StopD; 
      UN    "DB_ACHSE13".E_StopD; 
      UN    "DB_ACHSE14".E_StopD; 
      UN    "DB_ACHSE15".E_StopD; 
      =     "DB_SPL".II_HAX_KSTOPD_AKTIV; 
NETWORK
TITLE =Spindel Stop D aktiv
//BEREICH 1 (MASCHINE)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      U     "DB_ACHSE6_SPINDEL".E_StopD; 
      =     "DB_SPL".II_SPI_STOPD_AKTIV; 
NETWORK
TITLE =Achsen stehen (n<nx)
//BEREICH 1 (MASCHINE)
//
      U(    ; 
      O     "DB_ACHSE1_X".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX1; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE2_Y".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX2; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE3_Z".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX3; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE4".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX4; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE5".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX5; 
      ON    "DB_SIEM_STARTUP".ActivAxis[5]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE7".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX7; 
      ON    "M_7Achse_existiert"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE8".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX8; 
      ON    "M_8Achse_existiert"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE11".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX11; 
      ON    "DB_SIEM_STARTUP".ActivAxis[11]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE13".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX13; 
      ON    "DB_SIEM_STARTUP".ActivAxis[13]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE14".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX14; 
      ON    "DB_SIEM_STARTUP".ActivAxis[14]; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE15".E_n_nx; 
      ON    "DB_SIEM_STARTUP".ActivAxis[15]; 
      )     ; 
      U     "DB_SPL".OPLC_HAX_Stehen; 
      =     "DB_SPL".II_HAX_N_KL_NX; 
NETWORK
TITLE =Spindel steht (n<nx)
//BEREICH 1 (MASCHINE)
      O     "DB_ACHSE6_SPINDEL".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX6; 
      U     "DB_SPL".OPLC_Spindel_Steht; 
      =     "DB_SPL".II_SPI_N_KL_NX; 
NETWORK
TITLE =SPL-Logik INSEP/INSIP -> OUTSIP abbilden: Schutzbereich Maschine
//BEREICH 1 (MASCHINE)
//vgl. SAFE.SPF
//$A_MARKERSIP[1]...$A_MARKERSIP[64] : DB18.DBX70.0 ... DB18.DBX77.7
//$A_OUTSIP[1]......$A_OUTSIP[64]    : DB18.DBX62.0 ... DB18.DBX69.7
//
//--- Anforderung ST öffnen 
//--- PLC- Eingaenge --> an NC 
//--- NOT-HALT
// MK_NEG_NOT_HALT = (IX_AL_Drehzahl_Max==1) OR (IX_AL_BA2==1) OR (IX_AL_BA34==1)
T_R1: NOP   0; 
// merker werden in NCK-SPL um 20 ms gegenüber dem Eingang verzögert
// weil das PNOZ-multi die Signale mit 200 us taktet

      U     "DB_SPL".IFX_GEN_OM1_SEL; 
      =     "DB_SPL".MX_HAX_VERZ; 

      U     "DB_SPL".IFX_GEN_OM2_SEL; 
      =     "DB_SPL".MARKERSIP12; 

      U     "DB_SPL".IFX_GEN_OM3_4_SEL; 
      =     "DB_SPL".MARKERSIP13; 

      U     "DB_SPL".IFX_GEN_EMSTP_N; 
      =     "DB_SPL".MARKERSIP14; 

//-------------------------------------------------------------------

      O     "DB_SPL".IFX_GEN_OM1_SEL; 
      O     "DB_SPL".IFX_GEN_OM2_SEL; 
      O     "DB_SPL".IFX_GEN_OM3_4_SEL; 
      U     "DB_SPL".IFX_GEN_EMSTP_N; 
      U     "DB_SPL".IPLC_NEG_NOT_HALT; 
      =     "DB_SPL".MK_NEG_NOT_HALT; 

      U     "DB_SPL".IFX_GEN_EMSTP_N; 
      =     "DB_SPL".MK_NEG_NOT_TAST; 

// SBH-Abwahl------------------------------------------------------------
//N1020 IDS=55 DO OI_SBH_ABW_HAX = ((MK_NEG_NOT_HALT AND (NOT IPLC_CAB_UNLOCK)) OR IFX_GEN_OM3_4_SEL OR IFX_GEN_OM2_SEL)  OI_SBH_ABW_SPI = ((MK_NEG_NOT_HALT AND (NOT IPLC_CAB_UNLOCK)) OR (IFX_GEN_OM3_4_SEL) OR (IFX_GEN_OM2_SEL))   
//; SBH-Abwahl Toolchanger (Werkzeugwechsler)  --> wenn SBH überschritten wird wird intern Stop C / Stop A ausgelöst 
//N1025 IDS=56 DO OI_SBH_ABW_TC = (MK_NEG_NOT_HALT AND ISX_TC_DRIVES_EN_CNC AND NOT IPLC_TC_TM_UNLOCK AND NOT IPLC_CAB_UNLOCK AND NOT IFX_GEN_OM3_4_SEL AND NOT IFX_GEN_OM2_SEL)
//; SBH-Abwahl Toolmagazin (Werkzeugmagazin)   --> wenn SBH überschritten wird wird intern Stop C / Stop A ausgelöst 
//N1030 IDS=57 DO OI_SBH_ABW_TM = NOT IPLC_TC_TM_UNLOCK 
//; SBH-Abwahl Paletchanger (Palettenwechsler) --> wenn SBH überschritten wird wird intern Stop C / Stop A ausgelöst 
//N1035 IDS=58 DO OI_SBH_ABW_PC = MK_NEG_NOT_HALT AND ISX_PC_DRIVES_EN_CNC AND (NOT IPLC_PC_UNLOCK) 

      U     "I_AL_ANWAHL_BA3/4"; 
      O     "I_AL_ANWAHL_BA2"; 
      O(    ; 
      U     "DB_SPL".MK_NEG_NOT_HALT; 
      UN    "DB_SPL".IPLC_CAB_UNLOCK; 
      )     ; 
      =     "DB_SPL".OI_SBH_ABW_HAX; 
      =     "DB_SPL".OI_SBH_ABW_SPI; 


      U     "DB_SPL".MK_NEG_NOT_HALT; 
      U     "DB_SPL".ISX_TC_DRIVES_EN_CNC; 
      UN    "DB_SPL".IPLC_TC_TM_UNLOCK; 
      UN    "DB_SPL".IPLC_CAB_UNLOCK; 
      UN    "I_AL_ANWAHL_BA3/4"; 
      UN    "I_AL_ANWAHL_BA2"; 
      =     "DB_SPL".OI_SBH_ABW_TC; 

      UN    "DB_SPL".IPLC_TC_TM_UNLOCK; 
      =     "DB_SPL".OI_SBH_ABW_TM; 

      U     "DB_SPL".ISX_PC_DRIVES_EN_CNC; 
      UN    "DB_SPL".IPLC_PC_UNLOCK; 
      =     "DB_SPL".OI_SBH_ABW_PC; 

// SBH-Abwahl Rüstplatzachse abfallverzögert wegen Zweihandbedienung
      U     "DB_SPL".ISX_SST_DRIVES_EN_CNC; 
      L     S5T#1S; 
      SA    "T_SAFECO_SST_DRIV_EN_CNC"; 
      U     "T_SAFECO_SST_DRIV_EN_CNC"; 
      =     "DB_SPL".OI_SBH_ABW_SST; 

// SBH/SG-Abwahl---------------------------------------------------------
//N1030 IDS=59 DO OI_SBH_SG_ABW_HAX = (NOT MK_NEG_NOT_TAST) OR (NOT II_HAX_KSTOPAB_AKTIV AND NOT IPLC_Abnahme) OI_SBH_SG_ABW_SPI = ((NOT MK_NEG_NOT_HALT) OR (II_SPI_STOPAB_AKTIV)); AND (NOT MIX_AL_BA34) 

      ON    "DB_SPL".MK_NEG_NOT_TAST; 
      =     "DB_SPL".OI_SBH_SG_ABW_HAX; 

      O     "DB_SPL".II_SPI_STOPAB_AKTIV; 
      ON    "DB_SPL".MK_NEG_NOT_HALT; 
      =     "DB_SPL".OI_SBH_SG_ABW_SPI; 

// SG-Auswahl------------------------------------------------------------
//N1040 IDS=60 DO OI_SG_AUSW_24_HAX = MIX_AL_BA2 AND (NOT MIX_AL_BA34) AND (NOT MIX_AL_Drehzahl_Max)
//N1045 IDS=61 DO OI_SG_AUSW_24_MAGAX = OI_SG_AUSW_24_HAX AND IPLC_Abnahme
//N1050 IDS=62 DO OI_SG_AUSW_34_HAX = (MIX_AL_BA34 AND (NOT MIX_AL_Drehzahl_Max)) OR IPLC_TEST_BA4 
//N1055 IDS=63 DO OI_SG_AUSW_34_MAGAX = OI_SG_AUSW_34_HAX AND IPLC_Abnahme

      U     "DB_SPL".IFX_GEN_OM2_SEL; // BA 2    
      UN    "DB_SPL".IFX_GEN_OM3_4_SEL; 
      UN    "DB_SPL".IFX_GEN_OM1_SEL; 
      =     "DB_SPL".OI_SG_AUSW_24_HAX; 

      U     "DB_SPL".OI_SG_AUSW_24_HAX; // BA 2 währende SI-Abnahme
      U     "DB_SPL".IPLC_Abnahme; 
      =     "DB_SPL".OI_SG_AUSW_24_MAGAX; 



      U     "DB_SPL".IFX_GEN_OM3_4_SEL; // BA 34    
      UN    "DB_SPL".IFX_GEN_OM1_SEL; 
      O     "DB_SPL".IPLC_Test_BA4; 
      =     "DB_SPL".OI_SG_AUSW_34_HAX; 

      U     "DB_SPL".OI_SG_AUSW_34_HAX; // BA 34 währende SI-Abnahme
      U     "DB_SPL".IPLC_Abnahme; 
      =     "DB_SPL".OI_SG_AUSW_34_MAGAX; 

//--- Externer Stopp A- Abwahl : Spindel
//--- Wenn keine Betriebsart mehr aktiv ist dann Spindel stoppen

      U     "DB_SPL".MK_NEG_NOT_HALT; // Not-Halt gesamt
      L     S5T#5S; 
      SA    "T_SAFECO_STOPA_SPI"; 
      U     "T_SAFECO_STOPA_SPI"; 
      =     "DB_SPL".MK_STOPA_NH_SPI; // HM STOP A von Not-Halt Hauptachsen

      U     "DB_SPL".OI_SBH_ABW_HAX; // Abwahl STOP A Hauptachsen
      S     "DB_SPL".MK_KSTOPA_NX_SPI; 
      U     "DB_SPL".II_SPI_N_KL_NX; 
      R     "DB_SPL".MK_KSTOPA_NX_SPI; 

      U     "DB_SPL".MK_STOPA_NH_SPI; // HM STOP A von Not-Halt Hauptachsen
      UN    "DB_SAFE_ZD".GR1_ZD.req_stopa_plc; // Zwangsdyn. STOP A (PLC)
      U(    ; 
      O     "DB_SPL".OI_SBH_ABW_HAX; 
      O     "DB_SPL".MK_KSTOPA_NX_SPI; // HM STOP A von Not-Halt 
      )     ; 
      =     "DB_SPL".OI_STOPA_ABW_SPI; // Abwahl STOP A 

//--- Externer Stopp A- Abwahl : Hauptachsen

      L     "DB_PLC_MD_DB20".UDInt._005_ANWAHL_SONDER_SPDL; //Option Drehbetrieb wenn Hirthverzahnung
      L     2; 
      ==I   ; 
      SPBN  n4a; 
      U     "DB_SPL".MK_NEG_NOT_TAST; // Not-Halt gesamt
      L     S5T#8S; 
      SA    "T_NH_STOPA_AX"; 
      U     "T_NH_STOPA_AX"; 
      =     "DB_SPL".MK_STOPA_NH_AX; // HM STOP A von Not-Halt Hauptachsen
      SPA   n4b; 

n4a:  U     "DB_SPL".MK_NEG_NOT_TAST; // Not-Halt gesamt
      L     S5T#1S; 
      SA    "T_NH_STOPA_AX"; 
      U     "T_NH_STOPA_AX"; 
      =     "DB_SPL".MK_STOPA_NH_AX; // HM STOP A von Not-Halt Hauptachsen

n4b:  U     "DB_SPL".MK_STOPA_NH_AX; // HM STOP A von Not-Halt Hauptachsen
      UN    "DB_SAFE_ZD".GR1_ZD.req_stopa_plc; // Zwangsdyn. STOP A (PLC)
      =     "DB_SPL".OI_STOPA_ABW_HAX; // Abwahl STOP A 

//--- Externer Stopp D- Abwahl : Hauptachsen

      UN    "DB_SAFE_ZD".GR1_ZD.req_stopd_plc; // Zwangsdyn. STOP D (PLC)
      =     "DB_SPL".OI_STOPD_ABW_HAX; 

//--- Externer Stopp D- Abwahl : Palettenwechslerachse

      O(    ; 
      U     "DB_SPL".OI_STOPD_ABW_HAX; 
      U     "DB_SPL".MK_NEG_NOT_TAST; 
      UN    "DB_SPL".IPLC_ZD_GR1_RUN; 
      )     ; 
      O(    ; 
      U     "DB_SPL".OI_STOPC_ABW_HAX; 
      U     "DB_SPL".IPLC_ZD_GR1_RUN; 
      )     ; 
      =     "DB_SPL".OI_STOPD_ABW_PC; 

//--- Externer Stopp C - Abwahl : Hauptachsen

      U     "DB_SPL".MK_NEG_NOT_TAST; 
      UN    "DB_SAFE_ZD".GR1_ZD.req_stopc_plc; // Zwangsdyn. STOP C (PLC)
      =     "DB_SPL".OI_STOPC_ABW_HAX; 

//--- Externer Stopp C- Abwahl : Palettenwechslerachse

      UN    "DB_SPL".IPLC_ZD_GR1_RUN; 
      O(    ; 
      U     "DB_SPL".OI_STOPC_ABW_HAX; 
      U     "DB_SPL".IPLC_ZD_GR1_RUN; 
      )     ; 
      =     "DB_SPL".OI_STOPC_ABW_PC; 

//--- OX_AL_NEG_NOT_HALT-------------------------------------------------
// Bei Zwangsdynamisierung wird kein NOT-HALT ausgegeben 

      ON    "DB_SPL".II_HAX_KSTOPC_AKTIV; 
      O     "DB_SPL".II_SPI_STOPC_AKTIV; 
      ON    "DB_SPL".II_HAX_KSTOPD_AKTIV; 
      O     "DB_SPL".II_SPI_STOPD_AKTIV; 
      ON    "DB_SPL".II_HAX_KSTOPAB_AKTIV; 
      L     S5T#1S; 
      SV    "T_DREHZAHL_OK"; 

      U     "T_DREHZAHL_OK"; 
      UN    "DB_SPL".IPLC_ZD_GR1_RUN; 
      UN    "DB_SPL".IPLC_Abnahme; 
      =     "DB_SPL".MX_AL_DREHZAHL_OK; 

//-----------------------------------------------------------------------

      UN    "DB_SPL".MX_AL_DREHZAHL_OK; 
      =     "DB_SPL".OFX_GEN_EMSTP_N; 

// OX_AL_HAX_stehen------------------------------------------------------  
// Takt für das Signal alle 300 ms 
// Ergebnis an NC mitteilen
// in NC Takt-Merker überwachen

      UN    "T_HAX_Stehen"; 
      =     "DB_SPL".IPLC_Takt_Stehen; //IPLC_Takt
      L     S5T#300MS; 
      SV    "T_HAX_Stehen"; 

      UN    "DB_SPL".IPLC_Takt_Stehen; 
      SPB   xx1; 
      U     "DB_SPL".II_HAX_N_KL_NX; 
      =     "DB_SPL".IPLC_HAX_Stehen; // an NC
      U     "DB_SPL".II_SPI_N_KL_NX; 
      =     "DB_SPL".IPLC_Spindel_Steht; 

      U     "DB_SPL".IPLC_HAX_Stehen; // von PLC
      U     "DB_SPL".OPLC_HAX_Stehen; // von NC aus letztem Takt
      U     "DB_SPL".IPLC_Spindel_Steht; // von PLC
      U     "DB_SPL".OPLC_Spindel_Steht; // von NC aus letztem Takt
      =     "DB_SPL".OFX_GEN_STOP_MONITOR; 
//-----------------------------------------------------------------------

xx1:  NOP   0; 

NETWORK
TITLE =Ausgang "O_AL_NEG_NOTAUS"
//Ext.Ausgänge (OUTSEP) an Peripherie ausgeben
//Info:
//Ausgang wird bei Solutionline nicht gebildet. Signal wird mit Profisafemodul 
//direkt vom DB18.DBX46.0 abgegriffen
      U     "DB_SPL".OFX_GEN_EMSTP_N; 
      =     "O_AL_NEG_NOTAUS"; 
NETWORK
TITLE =Ausgang "O_AL_STILLSTAND"
//Ext.Ausgänge (OUTSEP) an Peripherie ausgeben
//Info:
//Ausgang wird bei Solutionline nicht gebildet. Signal wird mit Profisafemodul 
//direkt vom DB18.DBX46.1 abgegriffen
      U     "DB_SPL".OFX_GEN_STOP_MONITOR; 
      =     "O_AL_STILLSTAND"; 
NETWORK
TITLE =Achsen Abwahl Stop A
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_STOPA_ABW_HAX; 
      =     "DB_ACHSE1_X".A_extStopA; 
      =     "DB_ACHSE2_Y".A_extStopA; 
      =     "DB_ACHSE3_Z".A_extStopA; 
      =     "DB_ACHSE4".A_extStopA; 
      =     "DB_ACHSE5".A_extStopA; 
      =     "DB_ACHSE7".A_extStopA; 
      =     "DB_ACHSE8".A_extStopA; 
      =     "DB_ACHSE11".A_extStopA; 
      =     "DB_ACHSE13".A_extStopA; 
      =     "DB_ACHSE14".A_extStopA; 
      =     "DB_ACHSE15".A_extStopA; 
NETWORK
TITLE =Spindel Abwahl Stop A
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_STOPA_ABW_SPI; 
      =     "DB_ACHSE6_SPINDEL".A_extStopA; 
NETWORK
TITLE =Achsen + Spindel Abwahl Stop C
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_STOPC_ABW_HAX; 
      =     "DB_ACHSE1_X".A_extStopC; 
      =     "DB_ACHSE2_Y".A_extStopC; 
      =     "DB_ACHSE3_Z".A_extStopC; 
      =     "DB_ACHSE4".A_extStopC; 
      =     "DB_ACHSE5".A_extStopC; 
      =     "DB_ACHSE7".A_extStopC; 
      =     "DB_ACHSE11".A_extStopC; 
      =     "DB_ACHSE13".A_extStopC; 
      =     "DB_ACHSE14".A_extStopC; 

      =     "DB_ACHSE15".A_extStopC; 
      =     "DB_ACHSE6_SPINDEL".A_extStopC; 
NETWORK
TITLE =Achsen Abwahl Stop D
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_STOPD_ABW_HAX; 
      =     "DB_ACHSE1_X".A_extStopD; 
      =     "DB_ACHSE2_Y".A_extStopD; 
      =     "DB_ACHSE3_Z".A_extStopD; 
      =     "DB_ACHSE4".A_extStopD; 
      =     "DB_ACHSE5".A_extStopD; 
      =     "DB_ACHSE6_SPINDEL".A_extStopD; 
      =     "DB_ACHSE7".A_extStopD; 
      =     "DB_ACHSE11".A_extStopD; 
      =     "DB_ACHSE13".A_extStopD; 

      =     "DB_ACHSE14".A_extStopD; 
      =     "DB_ACHSE15".A_extStopD; 
NETWORK
TITLE =Achsen Abwahl Stop E
//BEREICH 1 (MASCHINE)
//Stop E wird gemeinsam mit Stop D abgewählt da kein ESR
      U     "DB_SPL".OI_STOPD_ABW_HAX; 
      =     "DB_ACHSE1_X".A_extStopE; 
      =     "DB_ACHSE2_Y".A_extStopE; 
      =     "DB_ACHSE3_Z".A_extStopE; 
      =     "DB_ACHSE4".A_extStopE; 
      =     "DB_ACHSE5".A_extStopE; 
      =     "DB_ACHSE6_SPINDEL".A_extStopE; 
      =     "DB_ACHSE7".A_extStopE; 
      =     "DB_ACHSE11".A_extStopE; 
      =     "DB_ACHSE13".A_extStopE; 

      =     "DB_ACHSE14".A_extStopE; 
      =     "DB_ACHSE15".A_extStopE; 
NETWORK
TITLE =Zwangsdynamisierung aktiv ?
//BEREICH 1 (MASCHINE)
      UN    "DB_SPL".IPLC_ZD_GR1_RUN; 
      SPB   KZD; 
NETWORK
TITLE =Palettenwechsler Abwahl Stop C D E (bei Zwangsdynamisierung)
//BEREICH 1 (MASCHINE)
      U     "m_eins"; 
      =     L      1.0; 
      U     L      1.0; 
      U     "DB_SPL".OI_STOPD_ABW_HAX; 
      =     "DB_ACHSE8".A_extStopD; 
      U     L      1.0; 
      U     "DB_SPL".OI_STOPD_ABW_HAX; 
      =     "DB_ACHSE8".A_extStopE; 
      U     L      1.0; 
      U     "DB_SPL".OI_STOPC_ABW_HAX; 
      =     "DB_ACHSE8".A_extStopC; 
NETWORK
TITLE =Sprung zu Abwahl SBH Arbeitsraum (bei Zwangsdynamisierung)
//BEREICH 1 (MASCHINE)
      SPA   SBH; 

NETWORK
TITLE =Palettenwechsler Abwahl Stop C D E (bei NOT-AUS)
//BEREICH 1 (MASCHINE)
KZD:  U     "m_eins"; 
      =     L      1.0; 
      U     L      1.0; 
      U     "DB_SPL".OI_STOPC_ABW_HAX; 
      =     "DB_ACHSE8".A_extStopD; 
      =     "DB_ACHSE8".A_extStopE; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_ACHSE8".A_extStopC; 
NETWORK
TITLE =Abwahl SBH Arbeitsraum
//BEREICH 1 (MASCHINE)
SBH:  U     "DB_SPL".OI_SBH_ABW_HAX; 
      =     "DB_ACHSE1_X".A_SBH; 
      =     "DB_ACHSE2_Y".A_SBH; 
      =     "DB_ACHSE3_Z".A_SBH; 
      =     "DB_ACHSE4".A_SBH; 
      =     "DB_ACHSE5".A_SBH; 
      =     "DB_ACHSE11".A_SBH; 
      =     "DB_ACHSE6_SPINDEL".A_SBH; 
NETWORK
TITLE =Abwahl SBH Magazin
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SBH_ABW_TM; 
      =     "DB_ACHSE7".A_SBH; 
      =     "DB_ACHSE13".A_SBH; 
NETWORK
TITLE =Abwahl SBH Palettenwechsler
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SBH_ABW_PC; 
      =     "DB_ACHSE8".A_SBH; 

NETWORK
TITLE =Abwahl SBH Rüstplatzachse
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SBH_ABW_SST; 
      =     "DB_ACHSE15".A_SBH; 
NETWORK
TITLE =Abwahl SBH Werkzeugwechsler
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SBH_ABW_TC; 
      =     "DB_ACHSE14".A_SBH; 
NETWORK
TITLE =Achsen SBH/SG-Abwahl
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SBH_SG_ABW_HAX; 
      =     "DB_ACHSE1_X".A_SG_SBH; 
      =     "DB_ACHSE2_Y".A_SG_SBH; 
      =     "DB_ACHSE3_Z".A_SG_SBH; 
      =     "DB_ACHSE4".A_SG_SBH; 
      =     "DB_ACHSE5".A_SG_SBH; 
      =     "DB_ACHSE7".A_SG_SBH; 
      =     "DB_ACHSE8".A_SG_SBH; 
      =     "DB_ACHSE11".A_SG_SBH; 
      =     "DB_ACHSE13".A_SG_SBH; 
      =     "DB_ACHSE14".A_SG_SBH; 
NETWORK
TITLE =Spindel SBH/SG-Abwahl
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SBH_SG_ABW_SPI; 
      =     "DB_ACHSE6_SPINDEL".A_SG_SBH; 
NETWORK
TITLE =SG 0 -Auswahl
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SG_AUSW_24_HAX; 
      =     "DB_ACHSE1_X".A_SGselect0; 
      =     "DB_ACHSE2_Y".A_SGselect0; 
      =     "DB_ACHSE3_Z".A_SGselect0; 
      =     "DB_ACHSE4".A_SGselect0; 
      =     "DB_ACHSE5".A_SGselect0; 
      =     "DB_ACHSE6_SPINDEL".A_SGselect0; 
      =     "DB_ACHSE8".A_SGselect0; 
      =     "DB_ACHSE11".A_SGselect0; 
      =     "DB_ACHSE15".A_SGselect0; 
NETWORK
TITLE =SG 0 -Auswahl Werkzeugwechsler / Magazin
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SG_AUSW_24_MAGAX; 
      =     "DB_ACHSE7".A_SGselect0; 
      =     "DB_ACHSE13".A_SGselect0; 
      =     "DB_ACHSE14".A_SGselect0; 
NETWORK
TITLE =SG 1 -Auswahl
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SG_AUSW_34_HAX; 
      =     "DB_ACHSE1_X".A_SGselect1; 
      =     "DB_ACHSE2_Y".A_SGselect1; 
      =     "DB_ACHSE3_Z".A_SGselect1; 
      =     "DB_ACHSE4".A_SGselect1; 
      =     "DB_ACHSE5".A_SGselect1; 
      =     "DB_ACHSE6_SPINDEL".A_SGselect1; 
      =     "DB_ACHSE8".A_SGselect1; 
      =     "DB_ACHSE11".A_SGselect1; 
      =     "DB_ACHSE15".A_SGselect1; 
NETWORK
TITLE =SG 1 -Auswahl Werkzeugwechsler / Magazin
//BEREICH 1 (MASCHINE)
      U     "DB_SPL".OI_SG_AUSW_34_MAGAX; 
      =     "DB_ACHSE7".A_SGselect1; 
      =     "DB_ACHSE13".A_SGselect1; 
      =     "DB_ACHSE14".A_SGselect1; 
NETWORK
TITLE =*** AB HIER BEGINNT DER BEREICH 2 (ZD PALETTENSPEICHER) ***

      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS4_AKTIV; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; 
      SPB   KRS; 
NETWORK
TITLE =Reset "DB_SAFE_ZD".GR2_ZD.PS_Test_Tuere_verriegeln bei Neustart
//Bit kann zum verriegeln der Rüstplatztüre während der Safetyabnahme durch 
//PLC-Status gesetzt werden. Bei Neustart wird es abgelöscht.
      U     "m_neustart"; 
      R     "DB_SAFE_ZD".GR2_ZD.PS_Test_Tuere_verriegeln; 
NETWORK
TITLE =PLC-->NC: Zwangsdynamisierung laeuft
//BEREICH 2 (PALETTENSPEICHER)
//kein Not-AUS auslösen
      U(    ; 
      U(    ; 
      O     "DB_SAFE_ZD".Seco_Test.BER2_Test_HW_laueft; 
      O     "DB_SAFE_ZD".Seco_Test.BER2_Test_ABSP_laeuft; 
      O     "DB_SAFE_ZD".Seco_Test.BER2_Test_extStop_laeuft; 
      O     "DB_SAFE_ZD".Seco_Test.BER2_Test_Bremse_laeuft; 
      O     "DB_SAFE_ZD_INST".BER2_FM_Schritt[1]; 
      )     ; 
      S     "DB_SAFE_ALLG1".M_PS_IMP_Test_HW_laueft; 
      U(    ; 
      U     "DB_SAFE_ZD".Seco_Test.BER2_Test_extStop_laeuft; 
      FN    "DB_SAFE_ALLG1".M_PS_FN_Test_HW_laueft; 
      O     "M_Ruecksetze_Fehler"; 
      )     ; 
      R     "DB_SAFE_ALLG1".M_PS_IMP_Test_HW_laueft; 
      U     "DB_SAFE_ALLG1".M_PS_IMP_Test_HW_laueft; 
      )     ; 
      L     S5T#1S; 
      SA    "T_SA_GR2ZD_HWTest_laeuft"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      U     "T_SA_GR2ZD_HWTest_laeuft"; 
      =     "DB_SPL".IPLC_ZD_GR2_RUN; 
NETWORK
TITLE =PLC-->NC: PS: SBH Anforderung durch Anwahl Entriegeln
//BEREICH 2 (PALETTENSPEICHER)
//
      U(    ; 
      O     "DB_SAFE_ALLG1".Leistung_steht_an; 
      O     ; 
      U     "T_SAFECO_Maschine_Ein"; 
      U     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      U     "M_SBH_ANF_RPL_PS"; 
      U(    ; 
      U     "DB_ACHSE9".E_n_nx; 
      U     "DB_ACHSE10".E_n_nx; 
      U(    ; 
      O     "DB_ACHSE16".E_n_nx; 
      ON    "DB_SIEM_STARTUP".ActivAxis[16]; 
      )     ; 
      O     "DB_SPL".IPLC_Abnahme; 
      )     ; 
      =     "DB_SPL".IPLC_PS_UNLOCK; 
NETWORK
TITLE =Tetstrack aktiv

      U     "DB_PLC_MD_DB20".UDHex._05_BIT0_TESTRACK_IO; 
      SPB   T_R2; 
NETWORK
TITLE =Achsen kein Stop A/B aktiv
//BEREICH 2 (PALETTENSPEICHER)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      UN    "DB_ACHSE9".E_StopAB; 
      UN    "DB_ACHSE10".E_StopAB; 
      UN    "DB_ACHSE16".E_StopAB; 
      =     "DB_SPL".II_PS_STOPAB_AKTIV; 
NETWORK
TITLE =Achsen kein Stop C aktiv
//BEREICH 2 (PALETTENSPEICHER)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      UN    "DB_ACHSE9".E_StopC; 
      UN    "DB_ACHSE10".E_StopC; 
      UN    "DB_ACHSE16".E_StopC; 
      =     "DB_SPL".II_PS_STOPC_AKTIV; 
NETWORK
TITLE =Achsen kein Stop D aktiv
//BEREICH 2 (PALETTENSPEICHER)
//Status-Signale von SI (SGA) -> INSIP auf interne Eingänge abbilden
      UN    "DB_ACHSE9".E_StopD; 
      UN    "DB_ACHSE10".E_StopD; 
      UN    "DB_ACHSE16".E_StopD; 
      =     "DB_SPL".II_PS_STOPD_AKTIV; 
NETWORK
TITLE =Achsen stehen (n<nx)
//BEREICH 2 (PALETTENSPEICHER)
//
      U(    ; 
      O     "DB_ACHSE9".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX9; 
      ON    "M_9Achse_existiert"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE10".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX10; 
      ON    "M_10Achse_existiert"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE16".E_n_nx; 
      O     "DB_SPL".OPLC_SIMU_AX16; 
      ON    "M_16Achse_existiert"; 
      )     ; 
      =     "DB_SPL".II_PS_N_KL_NX; 
NETWORK
TITLE =PNOZ Bereichsüberwachung mit sicherem Endschalter hat ausgelöst
//BEREICH 2 (PALETTENSPEICHER)
//Die Drehachse wird Bereichsüberwacht mit sicherem Endschalter, verdrahtet auf 
//PNOZ-Multi.
//Dieser Schalter hat 1-Signal im Bereich der Speicherplätze bis zum 
//Maschinenübergabeplatz.
//Ist die RP-Türe hinten nicht geschlossen und der sicherer Schalter wird 
//überfahren  
//meldet "DB_SPL".ISX_PS_DRIVES_EN_CNC 0 Signal. Es erfolgt Anwahl STOP C um 
//die Drehachse abzubremsen.
//(Der Antrieb wird durch Drehzahlsollwertvorgabe = 0 an der Stromgrenze 
//abgebremst)
//
      U     "DB_SPL".ISX_PS_DRIVES_EN_CNC; 
      R     "DB_SPL".IPLC_PS_PNOZ_AREA_STOPC; 
      U     "DB_SPL".MK_NEG_NOT_TAST; 
      UN    "DB_SPL".IPLC_PS_UNLOCK; 
      U(    ; 
      ON    "DB_SPL".ISX_PS_DRIVES_EN_CNC; 
      ON    "M_PS_DRIVES_EN_PERIPH_E"; 
      )     ; 
      U(    ; 
      O     "DB_ACHSE9".E_TCMinus; 
      O     "DB_ACHSE9".E_TCPlus; 
      ON    "DB_ACHSE9".E_n_nx; 
      )     ; 
      S     "DB_SPL".IPLC_PS_PNOZ_AREA_STOPC; 
      U     "DB_SPL".IPLC_PS_PNOZ_AREA_STOPC; 
      =     "DB_FEHLERMELDUNG".MELDUNG6._702841_PNOZ_BER_UEBERW; 
NETWORK
TITLE =SPL-Logik INSEP/INSIP -> OUTSIP abbilden: Schutzbereich PS
//BEREICH 2 (PALETTENSPEICHER)
//
//
//--- SBH- Abwahl : Rundspeicherachsen
T_R2: U     "DB_SPL".ISX_PS_DRIVES_EN_CNC; 
      UN    "DB_SPL".IPLC_PS_UNLOCK; 
      UN    "DB_SPL".IPLC_Abnahme; 
      =     "DB_SPL".OI_SBH_ABW_PS; 

//--- SBH/SG- Abwahl : Rundspeicherachsen
      ON    "DB_SPL".MK_NEG_NOT_TAST; 
      =     "DB_SPL".OI_SBH_SG_ABW_PS; 

//--- Externer Stopp A- Abwahl : Rundspeicherachsen
      U     "DB_SPL".MK_STOPA_NH_AX; // HM STOP A von Not-Halt Hauptachsen
      UN    "DB_SAFE_ZD".GR2_ZD.req_stopa_plc; // Zwangsdyn. STOP A (PLC)
      =     "DB_SPL".OI_STOPA_ABW_PS; // Abwahl STOP A 

//--- Externer Stopp D- Abwahl : Rundspeicherachsen 
      U     "DB_SPL".MK_NEG_NOT_TAST; 
      UN    "DB_SAFE_ZD".GR2_ZD.req_stopd_plc; // Zwangsdyn. STOP D (PLC)
      =     "DB_SPL".OI_STOPD_ABW_PS; 

//--- Externer Stopp C - Abwahl : Rundspeicherachsen
      UN    "DB_SPL".IPLC_PS_PNOZ_AREA_STOPC; 
      UN    "DB_SAFE_ZD".GR2_ZD.req_stopc_plc; // Zwangsdyn. STOP C (PLC)
      =     "DB_SPL".OI_STOPC_ABW_PS; 
NETWORK
TITLE =Palettenspeicher Abwahl Stop A (bei Zwangsdynamisierung)
//BEREICH 2 (PALETTENSPEICHER)
      U     "DB_SPL".OI_STOPA_ABW_PS; 
      =     "DB_ACHSE9".A_extStopA; 
      =     "DB_ACHSE10".A_extStopA; 
      =     "DB_ACHSE16".A_extStopA; 
NETWORK
TITLE =Zwangsdynamisierung aktiv ?
//BEREICH 2 (PALETTENSPEICHER)
      UN    "DB_SPL".IPLC_ZD_GR2_RUN; 
      SPB   KZDP; 
NETWORK
TITLE =Palettenspeicher Abwahl Stop C (bei Zwangsdynamisierung)
//BEREICH 2 (PALETTENSPEICHER)
      U     "DB_SPL".OI_STOPC_ABW_PS; 
      =     "DB_ACHSE9".A_extStopC; 
      =     "DB_ACHSE10".A_extStopC; 
      =     "DB_ACHSE16".A_extStopC; 

NETWORK
TITLE =Palettenspeicher Abwahl Stop D (bei Zwangsdynamisierung)
//BEREICH 2 (PALETTENSPEICHER)
      U     "DB_SPL".OI_STOPD_ABW_PS; 
      =     "DB_ACHSE9".A_extStopD; 
      =     "DB_ACHSE10".A_extStopD; 
      =     "DB_ACHSE16".A_extStopD; 

NETWORK
TITLE =Palettenspeicher Abwahl Stop E (bei Zwangsdynamisierung)
//BEREICH 2 (PALETTENSPEICHER)
//Stop E wird gemeinsam mit Stop D abgewählt da kein ESR
      U     "DB_SPL".OI_STOPD_ABW_PS; 
      =     "DB_ACHSE9".A_extStopE; 
      =     "DB_ACHSE10".A_extStopE; 
      =     "DB_ACHSE16".A_extStopE; 

NETWORK
TITLE =Sprung zu Abwahl SBH Arbeitsraum (bei Zwangsdynamisierung)
//BEREICH 2 (PALETTENSPEICHER)
      SPA   SBHP; 

NETWORK
TITLE =Palettenspeicher Abwahl Stop C D E (bei NOT-AUS)
//BEREICH 2 (PALETTENSPEICHER)
KZDP: U     "m_eins"; 
      =     L      1.0; 
      U     L      1.0; 
      U     "DB_SPL".OI_STOPD_ABW_PS; 
      =     "DB_ACHSE9".A_extStopD; 
      =     "DB_ACHSE9".A_extStopE; 
      =     "DB_ACHSE10".A_extStopD; 
      =     "DB_ACHSE10".A_extStopE; 
      =     "DB_ACHSE16".A_extStopD; 
      =     "DB_ACHSE16".A_extStopE; 
      U     L      1.0; 
      U     "DB_SPL".OI_STOPC_ABW_PS; 
      =     "DB_ACHSE9".A_extStopC; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_ACHSE10".A_extStopC; 
      U     L      1.0; 
      BLD   102; 
      =     "DB_ACHSE16".A_extStopC; 
NETWORK
TITLE =Abwahl SBH Palettenspeicher
//BEREICH 2 (PALETTENSPEICHER)
SBHP: U     "DB_SPL".OI_SBH_ABW_PS; 
      =     "DB_ACHSE9".A_SBH; 
      =     "DB_ACHSE10".A_SBH; 
      =     "DB_ACHSE16".A_SBH; 

NETWORK
TITLE =Abwahl SBH/SG Palettenspeicher
//BEREICH 2 (PALETTENSPEICHER)
      U     "DB_SPL".OI_SBH_SG_ABW_PS; 
      =     "DB_ACHSE9".A_SG_SBH; 
      =     "DB_ACHSE10".A_SG_SBH; 
      =     "DB_ACHSE16".A_SG_SBH; 
NETWORK
TITLE =SG 0 -Auswahl Palettenspeicher
//BEREICH 2 (PALETTENSPEICHER)
      U     "m_null"; 
      =     "DB_ACHSE9".A_SGselect0; 
      =     "DB_ACHSE10".A_SGselect0; 
      =     "DB_ACHSE16".A_SGselect0; 
NETWORK
TITLE =SG 1 -Auswahl Palettenspeicher
//BEREICH 2 (PALETTENSPEICHER)
      U     "m_null"; 
      =     "DB_ACHSE9".A_SGselect1; 
      =     "DB_ACHSE10".A_SGselect1; 
      =     "DB_ACHSE16".A_SGselect1; 
NETWORK
TITLE =Spindel aktuelle Getriebstufe kopieren --> Anw. Meisel

KRS:  U     "DB_ACHSE6_SPINDEL".A_ActGearA; 
      =     "DB_ACHSE6_SPINDEL".A_Transmiss0; 

      U     "DB_ACHSE6_SPINDEL".A_ActGearB; 
      =     "DB_ACHSE6_SPINDEL".A_Transmiss1; 

      U     "DB_ACHSE6_SPINDEL".A_ActGearC; 
      =     "DB_ACHSE6_SPINDEL".A_Transmiss2; 


NETWORK
TITLE =Meldung: Fahrbefehl an Achse mit aktivem SBH steht an

      U(    ; 
      UN    "DB_ACHSE1_X".E_TCMinus; 
      UN    "DB_ACHSE1_X".E_TCPlus; 
      UN    "DB_ACHSE1_X".A_Minus; 
      UN    "DB_ACHSE1_X".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE1_X".A_SBH; 
      O     ; 
      UN    "DB_ACHSE2_Y".E_TCMinus; 
      UN    "DB_ACHSE2_Y".E_TCPlus; 
      UN    "DB_ACHSE2_Y".A_Minus; 
      UN    "DB_ACHSE2_Y".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE2_Y".A_SBH; 
      O     ; 
      UN    "DB_ACHSE3_Z".E_TCMinus; 
      UN    "DB_ACHSE3_Z".E_TCPlus; 
      UN    "DB_ACHSE3_Z".A_Minus; 
      UN    "DB_ACHSE3_Z".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE3_Z".A_SBH; 
      O     ; 
      UN    "DB_ACHSE4".E_TCMinus; 
      UN    "DB_ACHSE4".E_TCPlus; 
      UN    "DB_ACHSE4".A_Minus; 
      UN    "DB_ACHSE4".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE4".A_SBH; 
      O     ; 
      UN    "DB_ACHSE5".E_TCMinus; 
      UN    "DB_ACHSE5".E_TCPlus; 
      UN    "DB_ACHSE5".A_Minus; 
      UN    "DB_ACHSE5".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE5".A_SBH; 
      O     ; 
      UN    "DB_ACHSE11".E_TCMinus; 
      UN    "DB_ACHSE11".E_TCPlus; 
      UN    "DB_ACHSE11".A_Minus; 
      UN    "DB_ACHSE11".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE11".A_SBH; 
      O     ; 
      UN    "DB_ACHSE7".E_TCMinus; 
      UN    "DB_ACHSE7".E_TCPlus; 
      UN    "DB_ACHSE7".A_Minus; 
      UN    "DB_ACHSE7".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE7".A_SBH; 
      O     ; 
      UN    "DB_ACHSE13".E_TCMinus; 
      UN    "DB_ACHSE13".E_TCPlus; 
      UN    "DB_ACHSE13".A_Minus; 
      UN    "DB_ACHSE13".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE13".A_SBH; 
      O     ; 
      UN    "DB_ACHSE8".E_TCMinus; 
      UN    "DB_ACHSE8".E_TCPlus; 
      UN    "DB_ACHSE8".A_Minus; 
      UN    "DB_ACHSE8".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE8".A_SBH; 
      O     ; 
      UN    "DB_ACHSE14".E_TCMinus; 
      UN    "DB_ACHSE14".E_TCPlus; 
      UN    "DB_ACHSE14".A_Minus; 
      UN    "DB_ACHSE14".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE14".A_SBH; 
      O     ; 
      UN    "DB_ACHSE9".E_TCMinus; 
      UN    "DB_ACHSE9".E_TCPlus; 
      UN    "DB_ACHSE9".A_Minus; 
      UN    "DB_ACHSE9".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE9".A_SBH; 
      O     ; 
      UN    "DB_ACHSE10".E_TCMinus; 
      UN    "DB_ACHSE10".E_TCPlus; 
      UN    "DB_ACHSE10".A_Minus; 
      UN    "DB_ACHSE10".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE10".A_SBH; 
      O     ; 
      UN    "DB_ACHSE15".E_TCMinus; 
      UN    "DB_ACHSE15".E_TCPlus; 
      UN    "DB_ACHSE15".A_Minus; 
      UN    "DB_ACHSE15".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE15".A_SBH; 
      O     ; 
      UN    "DB_ACHSE16".E_TCMinus; 
      UN    "DB_ACHSE16".E_TCPlus; 
      UN    "DB_ACHSE16".A_Minus; 
      UN    "DB_ACHSE16".A_Plus; 
      NOT   ; 
      UN    "DB_ACHSE16".A_SBH; 
      )     ; 
      UN    "DB_PLC_MD_DB20".UDHex._05_BIT0_TESTRACK_IO; 
      =     "DB_FEHLERMELDUNG".MELDUNG6._702840_FAHRBEF_BEI_SBH; 
NETWORK
TITLE =Schutz der PLC-SPL

      UN    "DB_PLC_MD_DB20".UDHex._17_Bit0_Safe_SPL_Schutz; 
      =     "DB_SPL".SPL_SCHUTZ; 
END_FUNCTION

