FUNCTION "FC_PRG_Anwahl_Main" : VOID
TITLE =
//$Revision: 1.62 $
//$Date: 2008/08/25 10:06:38CEST $
//$Author: sth $
AUTHOR : SJA
NAME : PW
VERSION : 0.1


VAR_TEMP
  t_b_dummy : BOOL ;	
  t_b_dummy3 : BOOL ;	
  FB3_Req : BOOL ;	
  FB3_Done : BOOL ;	
  FB3_Error : BOOL ;	
  FB4_Req_ext_NV : BOOL ;	
  Pal_Speicher : BOOL ;	
  t_str_dummy : STRING  [28 ];	
  t_b_dummy2 : BOOL ;	
  t_int_dummy : INT ;	
  tx_BAG_auto : BOOL ;	
  tx_Kein_Job : BOOL ;	
  tx_Job_in_Masch : BOOL ;	
  tx_Spannhyd : BOOL ;	
  BytesIdent : BOOL ;	
  t_Prg_Maschine_Laeuft : BOOL ;	
  tb_TW_Stellung : INT ;	
  t_int : INT ;	
  FB3_State : WORD ;	
  Wert_externe_NV_B : REAL ;	
  T_ANZ_SPEICHERPLAETZE : INT ;	
  T_ANZAHL_JOBS : INT ;	//Anzahl der Jobs
  F_leere_Auftragsliste : BOOL ;	
  F_Steuerdatei_fehlt : BOOL ;	
  F_Nr_Steuerdatei : BOOL ;	
  Quitt_bei_Fehler : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Bausteinende bei Anwahl 3 (Fastems)
// Netzwerk-Übersicht
// ==================
// NW 1: Bausteinende bei Anwahl 3 (Fastems)
// NW 2: Auswertung Anzahl der Speicherplätze
// NW 3: Auswertung Anzahl der Jobs
// NW 4: Jobbetrieb
// NW 5: Palettenwechselmakro bei Automatikabarbeitung
// NW 6: Automatikbetrieb
// NW 7: M-EINGANG: Rohteil Setzen
// NW 8: Verzögerung Jobbetrieb
// NW 9: Verzögerung Programm läuft nicht
// NW 10: 702009 "MEL, Maschine nicht in Betriebsart Automatik"
// NW 11: MEL: Palettenspeicher ist nicht in Grd/Transportwagen belegt
// NW 12: Programmanwahl
// NW 13: Programm von Maschine anwählen
// NW 14: Programm Palettenwechsel anwaehlen
// NW 15: PRG: Paletenwechsler Makro NC-Start
// NW 16: Job aktiv setzen
// NW 17: Status in Arbeit setzen
// NW 18: M67 Palette bearbeitet
// NW 19: bei Stückzahl grösser gleich Losgroesse Priorität auf 0 setzen 
// NW 20: aktuelle Zeit mitzählen, bei aktivem Job 
// NW 21: 702059 "MEL, kein Job gefunden od. kein Rohteil vorhanden"
// NW 22: Rohteil setzen mittels Hardkey Palettenwechslermaschine
// NW 23: Anforderung Anwahl externe Nullpunktverschiebung
// NW 24: externe Nullpunktverschiebung an NC
// NW 25: Funktion Werkstück entladen laden
// NW 26: Fehlerauswertung Werkstück laden
// NW 27: Quittieren bei Fehler
// NW 28: Start Entladen
// NW 29: Entladen beendet
// NW 30: Start Beladen
// NW 31: Laden beendet
// NW 32: Reset Werkstück laden
//
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASREMS_V3X; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      BEB   ; 
NETWORK
TITLE =Palettenspeicher vorhanden

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      =     #Pal_Speicher; 
NETWORK
TITLE =Auswertung Anzahl der Speicherplätze
//
//
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      SPBN  X001; 
      L     5; 
      SPA   X003; 

X001: U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; 
      SPBN  X002; 
      L     10; 
      SPA   X003; 

X002: L     4; 
X003: T     #T_ANZ_SPEICHERPLAETZE; 
NETWORK
TITLE =Auswertung Anzahl der Jobs

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      SPBN  J001; 
      L     12; 
      SPA   J003; 

J001: U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; 
      SPBN  J002; 
      L     12; 
      SPA   J003; 

J002: L     10; 
J003: T     #T_ANZAHL_JOBS; 

NETWORK
TITLE =Jobbetrieb

      U     "DB_OEMDB2".RS4.Jobbetrieb; 
      U     "M_Satzfolge_aktiv"; 
      =     "MX_Jobbetrieb"; 
NETWORK
TITLE =Palettenwechselmakro bei Automatikabarbeitung

      L     "DB_PLC_MD_DB20".UDInt._041_ANWAHL_PALVERWAL; // oder keine Palettenverwaltung
      L     1; 
      ==I   ; 
      SPBN  PV1; 

      UN    "I_PW_Tueren_zu"; 
      =     "DB_OEMDB2".RS4.Eingaenge.RP_editierbar; 

PV1:  U     "DB_LSSchnittstelle".OX_LS_Auftrag_abbrechen; 
      O     "MX_POS_FL_SKR"; 
      R     "MX_Jobbetrieb"; 
      R     "DB_OEMDB2".RS4.Jobbetrieb; 

      U     "MX_Pal_auf_Stoerung"; 
      O     "Mx_Pal_Stoerung_WZBruch"; 
      O     "Mx_Pal_Defekt_H67"; 
      SPBN  NW1; 

      L     5; 
      T     "DB_PAL_NR".Platz[0].Status; // setze Maschinenpalette auf Stoerung

      U     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700863_WZB_TOL_UEBER; // und Fehler Bruch 
      O     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700863_WZB_TOL_UEBER; 
      O     "DB_FEHLERMELDUNG".Warnung._701900_WZB_2MW_GR_1MW; 
      O     "DB_FEHLERMELDUNG".Warnung._701901_WZB_TOL_UEBER; 
      U     "Mx_Pal_Stoerung_WZBruch"; 
      U     "MX_Jobbetrieb"; 
      S     "Mx_Verlasse_Prg"; 

NW1:  U     "MX_Pal_auf_Stoerung"; 
      O     "Mx_Pal_Stoerung_WZBruch"; 
      UN    "DB_SIEM_KANAL_1".E_ProgRunn; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; // und Kanal Reset
      R     "MX_Pal_auf_Stoerung"; // Ruecksetbxze Merker Palette auf Stoerung
      R     "Mx_Pal_Stoerung_WZBruch"; 
      R     "Mx_Verlasse_Prg"; 
NETWORK
TITLE =Automatikbetrieb

      U     "M_Automatik_Betrieb"; 
      UN    "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      O     ; 
      U     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      U     "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      O     "DB_SIEM_KANAL_1".E_ProgStop; 
      O     "DB_SIEM_KANAL_1".E_ProgInterrupt; 
      =     #tx_BAG_auto; 
NETWORK
TITLE =M-EINGANG: Rohteil Setzen

      O     "I_MSST_PW_Freigabe"; 
      O     "I_PW_QUIT_Freigabe"; 
      =     "I_RP_Rohteil_setzen"; 
NETWORK
TITLE =Verzögerung Jobbetrieb

      U     "MX_Jobbetrieb"; 
      L     S5T#4S; 
      SE    "T_verz_PW_JOB"; 
NETWORK
TITLE =Verzögerung Programm läuft nicht

      U     "DB_SIEM_KANAL_1".E_ChanReset; 
//      UN    "DB_SIEM_KANAL_1".E_ProgRunn
      L     S5T#4S; 
      SE    "TE_Prg_nicht_aktiv"; 
NETWORK
TITLE =702009 "MEL, Maschine nicht in Betriebsart Automatik"

      U(    ; 
      O     "MX_Jobbetrieb"; 
      O     "DB_OEMDB2".RS4.Jobbetrieb; 
      )     ; 
      UN    #tx_BAG_auto; 
      UN    "DB_FEHLERMELDUNG".Meldung._702022_Ref_Punkt; 
      =     "DB_FEHLERMELDUNG".Meldung._702009_fals_BA_fuer_Job; 
NETWORK
TITLE =MEL: Palettenspeicher ist nicht in Grd/Transportwagen belegt

      U     "m_eins"; 
      =     L     53.0; 
      U     L     53.0; 
      U(    ; 
      O     "M_Ruecksetze_Fehler"; 
      ON    "DB_LSSchnittstelle".IX_Transportwagen_belegt; 
      )     ; 
      R     "DB_FEHLERMELDUNG".Meldung._702104_RS4_ni_Grdst; 
      U     L     53.0; 
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".NC_Startsperre._700109_K_START_BEI_EINR; 
      U     L     53.0; 
      U     "MX_Jobbetrieb"; 
      FP    "M_JOPBETRIEB_FP"; 
      =     L     53.1; 
      U     L     53.1; 
      U     "DB_LSSchnittstelle".IX_Transportwagen_belegt; 
      S     "DB_FEHLERMELDUNG".Meldung._702104_RS4_ni_Grdst; 
      R     "MX_Jobbetrieb"; 
      R     "DB_OEMDB2".RS4.Jobbetrieb; 
      U     L     53.1; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; 
      )     ; 
      U     "M_Automatik_Betrieb"; 
      U     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      S     "DB_FEHLERMELDUNG".NC_Startsperre._700109_K_START_BEI_EINR; 
      R     "MX_Jobbetrieb"; 
      R     "DB_OEMDB2".RS4.Jobbetrieb; 
NETWORK
TITLE =Programmanwahl

      U     "MX_Jobbetrieb"; // Speicher in Auto Betrieb
      U(    ; 
      U(    ; 
      L     5; 
      L     "DB_PAL_NR".Platz[0].Status; //  Palette Maschine auf Stoerung 
      ==I   ; 
      )     ; 
      UN    "DB_PLC_MD_DB20".UDHex._09_BIT7_P_STOER_WECHSEL; // keine erneute Prg anwahl, wenn auf Stoerung
      )     ; 
      O     "DB_LSSchnittstelle".IX_Holen_abgebrochen; 
      O     "DB_LSSchnittstelle".IX_Bringen_abgebrochen; 
      R     "MX_Jobbetrieb"; 
      R     "DB_OEMDB2".RS4.Jobbetrieb; 

// nur in AUTO, kein Prg läuft (Kanal im Reset) --> Prg anwählen

      U     "MX_Jobbetrieb"; // Speicher in Auto Betrieb
      U     #tx_BAG_auto; // "BAG".E_AUTO
      UN    "DB_STRINGPW".RequestPW; 
      UN    "M_PV_akt_Prg_lesen"; 
      UN    "DB_FEHLERMELDUNG".Einlesesperre._700553_Progr_pruefen; 
      UN    "DB_FAMILIEN_MODUL".Gesamt.A_Einlesesperre; 
      UN    "DB_FAMILIEN_MODUL".Gesamt.A_NC_Start_Sperre; 
      UN    "DB_SIEM_KANAL_1".A_NCStartDisabl; 
      U     "T_verz_PW_JOB"; 
      UN    "M_Req_externe_NV"; 
      U(    ; 
      U     "DB_NC_PLC".Zusatz.NC_an_PLC.PROG_EVENT_ENDE; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      O     "TE_Prg_nicht_aktiv"; 
      )     ; 
      SPBN  PREN; 

NETWORK
TITLE =Programm von Maschine anwählen
//wenn unbearbeitete Palette in Maschine --> diese anwählen , sonst neue anwählen
      SET   ; 
      S     #t_b_dummy2; 
      R     #t_b_dummy; 

      CALL "FC_Prg_Anwahl_Check" (
           i_Platz                  := 0,
           iAnzahlJobs              := #T_ANZAHL_JOBS,
           b_PalinJob               := #t_b_dummy);

      U(    ; 
      L     "DB_PAL_NR".Platz[0].Pal_Nr; 
      L     0; // Palette in Maschine
      <>I   ; 
      )     ; 
      U(    ; 
      L     "DB_PAL_NR".Platz[0].Status; 
      L     2; 
      ==I   ; // und Rohteil  
      )     ; 
      U     #t_b_dummy; // und Maschinenplatz in Jobliste
      U(    ; 
      UN    "DB_SIEM_KANAL_1".A_SBL; 
      UN    "DB_SIEM_KANAL_1".E_ProgRunn; 
      O     "DB_SIEM_KANAL_1".E_ChanReset; 
      )     ; 
      S     "MX_Prg_von_Maschine"; 
      R     "M_neue_Pal_anwaehlen"; 

      SPBN  NEPA; 

      CLR   ; 
      =     #t_b_dummy; 

      CALL "FC_Prg_Anwahl_1" (
           i_bPalwechsel            := #t_b_dummy,
           i_iPWart                 := 0,
           iAnzahlJobs              := #T_ANZAHL_JOBS,
           o_strPrg                 := "DB_STRINGPW".File,
           o_strPfad                := "DB_STRINGPW".Pfad,
           o_test                   := #t_str_dummy,
           retWert                  := #t_b_dummy2);

      U     #t_b_dummy2; 
//      =     "DB_STRINGPW".RequestPW
      S     "DB_OEMDB2".WST_Laden.Request; 

      SPA   PREN; 

//in Auto-Betrieb, wenn kein Prg läuft Programm anwählen

// neue Palette anwaehlen

// 0:Maschine,1:Überpl1,2:Überpl2,3:Transportw,4:Ruepl,5-xx:Speicherpl 1 -- xx 
NEPA: U     "DB_LSSchnittstelle".IX_Freigabe_PRG_Start; 
      UN(   ; 
      L     "DB_SPEICHERTRANSPORT".holen_von; 
      L     1; 
      ==I   ; 
      )     ; 
      UN(   ; 
      L     "DB_SPEICHERTRANSPORT".bringen_zu; 
      L     1; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_LSSchnittstelle".IX_Ruestplatz_Tuer_Zu; 
      O     "DB_LSSchnittstelle".IX_Ruestplatz_Tuer_Auf; 
      )     ; 

      O(    ; 
      UN    #Pal_Speicher; 
      U     "DB_PW_DATA".In.IX_RPT_zu; 
      U(    ; 
      ON    "DB_HR".ABST.SP_FD; 
      ON    "DB_PV_SpH".SpH[1].Stelle[1].KONF_mitSPH; 
      O     "DB_PV_SpH".SpH[1].Stelle[1].STAT_gespannt; 
      O     "DB_NC_PLC".PW_SPH_MA_entladen; 
      )     ; 
      )     ; 
      SPBN  PREN; 

      CLR   ; 
      =     #t_b_dummy; 

      CLR   ; 
      =     "MX_Kein_JOB_Vorhanden"; 
      =     "MX_Job_In_Maschine"; 

      CALL "FC_Prg_Job_Suche" (
           iAnzahlSpPlaetze         := #T_ANZ_SPEICHERPLAETZE,
           iAnzahlJobs              := #T_ANZAHL_JOBS);

      U     "MX_Kein_JOB_Vorhanden"; 
      R     "MX_Jobbetrieb"; 
      R     "DB_OEMDB2".RS4.Speicher_in_Auto; 

NETWORK
TITLE =Programm Palettenwechsel anwaehlen

      U(    ; 
      O(    ; 
      L     "DB_PAL_NR".Platz[0].Status; 
      L     0; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PAL_NR".Platz[0].Status; 
      L     1; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PAL_NR".Platz[0].Status; 
      L     4; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PAL_NR".Platz[0].Status; 
      L     5; 
      ==I   ; 
      )     ; 
      )     ; 
      U(    ; 
      L     "DB_PAL_NR".Platz[1].Pal_Nr; 
      L     0; 
      <>I   ; 
      )     ; 
      U(    ; 
      O(    ; 
      L     "DB_PAL_NR".Platz[1].Status; 
      L     2; 
      ==I   ; 
      )     ; 
      O     ; 
      U(    ; 
      L     "DB_PAL_NR".Platz[1].Status; 
      L     1; 
      ==I   ; 
      )     ; 
      U(    ; 
      O     "DB_ROB_IF".STAT.Roboter.Automation_ein; 
      O     ; 
      UN    "DB_ROB_IF".STAT.Roboter.Automation_ein; 
      U     "MX_1Job_Maschine"; 
      U(    ; 
      L     "DB_PAL_NR".Platz[0].Status; 
      L     1; 
      <>I   ; 
      )     ; 
      )     ; 
      )     ; 
      SPBN  PREN; 

      SET   ; 
      R     "MX_Prg_von_Maschine"; 
      S     "M_neue_Pal_anwaehlen"; 

      SET   ; 
      =     #t_b_dummy; 
      R     #t_b_dummy2; 

      CALL "FC_Prg_Anwahl_1" (
           i_bPalwechsel            := #t_b_dummy,
           i_iPWart                 := 1,
           iAnzahlJobs              := #T_ANZAHL_JOBS,
           o_strPrg                 := "DB_STRINGPW".File,
           o_strPfad                := "DB_STRINGPW".Pfad,
           o_test                   := #t_str_dummy,
           retWert                  := #t_b_dummy2);

      UN    #t_b_dummy2; 
      SPB   PREN; 

      SET   ; 
      =     "DB_STRINGPW".RequestPW; 

      SPA   PREN; 

NETWORK
TITLE =PRG: Paletenwechsler Makro NC-Start

PREN: U     "M_Prg_Maschine_anwaehl"; 
      O     "DB_STRINGPW".DonePW; 
      R     "DB_STRINGPW".RequestPW; 
      R     "DB_OEMDB2".WST_Laden.Request; 

      U(    ; 
      U     "DB_STRINGPW".ErrorPW; 
      O     "DB_STRINGPW".DonePW; 
      U     "DB_STRINGPW".RequestPW; 
      )     ; 
      R     "DB_STRINGPW".RequestPW; 
      R     "DB_OEMDB2".WST_Laden.Request; 


      CALL "FB_SIEM_PI" , "DB_PI_Service" (
           Req                      := "DB_STRINGPW".RequestPW,
           PIService                := "DB_SIEM_PI".SELECT,
           Unit                     := 1,
           Addr1                    := "DB_STRINGPW".Pfad,
           Addr2                    := "DB_STRINGPW".File,
           Error                    := "DB_STRINGPW".ErrorPW,
           Done                     := "DB_STRINGPW".DonePW,
           State                    := "DB_STRINGPW".StatePW);




      U     "DB_STRINGPW".DonePW; 
      FP    "M_FP_NCStart_RS4_PW"; 
      S     "M_PWMakro_NCStart"; 

      UN    "MX_Jobbetrieb"; 
      ON    #tx_BAG_auto; // "BAG".E_AUTO
      R     "M_PWMakro_NCStart"; 

      U     "M_PWMakro_NCStart"; 
      U     #tx_BAG_auto; // "BAG".E_AUTO
      U     "MX_Jobbetrieb"; 
      UN    "DB_STRINGPW".RequestPW; 
      R     "M_PWMakro_NCStart"; 
      SPBN  ncst; 

      UN    "M_NC_Start_Prg_Anwahl"; 
      =     "M_NC_Start_Prg_Anwahl"; //NC-Start

// Wenn keine Seite aktiv, oder wenn alle aktiven Seiten unbearbeitet sind Zeit auf 0 setzen

      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[1].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      )     ; 
      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[2].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      )     ; 
      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[3].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      )     ; 
      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[4].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      )     ; 
      SPBN  ncst; 

      L     0; 
      T     "DB_JOB".Job[1].Zeit; 
      T     "DB_JOB".Job[2].Zeit; 
      T     "DB_JOB".Job[3].Zeit; 
      T     "DB_JOB".Job[4].Zeit; 
      T     "DB_JOB".Job[5].Zeit; 
      T     "DB_JOB".Job[6].Zeit; 
      T     "DB_JOB".Job[7].Zeit; 
      T     "DB_JOB".Job[8].Zeit; 
      T     "DB_JOB".Job[9].Zeit; 
      T     "DB_JOB".Job[10].Zeit; 
      T     "DB_JOB".Job[11].Zeit; 
      T     "DB_JOB".Job[12].Zeit; 

ncst: UN    "MX_Jobbetrieb"; // Speicher in Auto Betrieb
      O     "DB_SIEM_KANAL_1".E_ProgRunn; // Programm läuft
      ON    #tx_BAG_auto; // "BAG".E_AUTO
      R     "DB_STRINGPW".RequestPW; 
      R     "M_neue_Pal_anwaehlen"; 
      R     "M_Prg_Maschine_anwaehl"; 
      R     "M_NC_Start_Prg_Anwahl"; 
      R     "DB_STRINGPW".DonePW; 
      R     "DB_OEMDB2".WST_Laden.Request; 
      NOP   0; 

NETWORK
TITLE =Job aktiv setzen
//      U     "MX_Jobbetrieb"; 
//      =     "DB_NCPlc_DB70".Jobbetrieb; 
      UN    "DB_SIEM_KANAL_1".E_ChanReset; // bei Kanal Reset
      U     "MX_Prg_von_Maschine"; 
      =     #t_Prg_Maschine_Laeuft; 
      SPB   n12a; 


n12a: CLR   ; 
      =     "DB_JOB".Job[1].aktiv; 
      =     "DB_JOB".Job[2].aktiv; 
      =     "DB_JOB".Job[3].aktiv; 
      =     "DB_JOB".Job[4].aktiv; 
      =     "DB_JOB".Job[5].aktiv; 
      =     "DB_JOB".Job[6].aktiv; 
      =     "DB_JOB".Job[7].aktiv; 
      =     "DB_JOB".Job[8].aktiv; 
      =     "DB_JOB".Job[9].aktiv; 
      =     "DB_JOB".Job[10].aktiv; 
      =     "DB_JOB".Job[11].aktiv; 
      =     "DB_JOB".Job[12].aktiv; 


// schauen, welcher Job gerade läuft
      L     "DB_JOB".Job_aktiv; 
      SPL   kjob; 
      SPA   jb00; 
      SPA   jb01; 
      SPA   jb02; 
      SPA   jb03; 
      SPA   jb04; 
      SPA   jb05; 
      SPA   jb06; 
      SPA   jb07; 
      SPA   jb08; 
      SPA   jb09; 
      SPA   jb10; 
      SPA   jb11; 
      SPA   jb12; 


kjob: SPA   n12e; 

jb00: SPA   n12e; 

jb01: SET   ; 
      =     "DB_JOB".Job[1].aktiv; 
      SPA   n12e; 
jb02: SET   ; 
      =     "DB_JOB".Job[2].aktiv; 
      SPA   n12e; 
jb03: SET   ; 
      =     "DB_JOB".Job[3].aktiv; 
      SPA   n12e; 
jb04: SET   ; 
      =     "DB_JOB".Job[4].aktiv; 
      SPA   n12e; 
jb05: SET   ; 
      =     "DB_JOB".Job[5].aktiv; 
      SPA   n12e; 
jb06: SET   ; 
      =     "DB_JOB".Job[6].aktiv; 
      SPA   n12e; 
jb07: SET   ; 
      =     "DB_JOB".Job[7].aktiv; 
      SPA   n12e; 
jb08: SET   ; 
      =     "DB_JOB".Job[8].aktiv; 
      SPA   n12e; 
jb09: SET   ; 
      =     "DB_JOB".Job[9].aktiv; 
      SPA   n12e; 
jb10: SET   ; 
      =     "DB_JOB".Job[10].aktiv; 
      SPA   n12e; 
jb11: SET   ; 
      =     "DB_JOB".Job[11].aktiv; 
      SPA   n12e; 
jb12: SET   ; 
      =     "DB_JOB".Job[12].aktiv; 
      SPA   n12e; 

n12e: NOP   0; 

NETWORK
TITLE =Status in Arbeit setzen

      O(    ; 
      U(    ; 
      U(    ; 
      U     #t_Prg_Maschine_Laeuft; 
      SPBNB _001; 
      CALL "FC_UT_STRING_VGL" (
           String_1                 := "DB_PrgAnwahl".PR_NC,
           String_2                 := "DB_PrgAnwahl".PR_VGL,
           Identisch                := #BytesIdent);
_001: U     BIE; 
      )     ; 
      U     #BytesIdent; 
      SPBNB _002; 
      CALL "FC_UT_STRING_VGL" (
           String_1                 := "DB_PrgAnwahl".PATH_NC,
           String_2                 := "DB_PrgAnwahl".PATH_VGL,
           Identisch                := #BytesIdent);
_002: U     BIE; 
      )     ; 
      U     #BytesIdent; 
      SPBNB _003; 
      L     3; 
      T     "DB_PAL_NR".Platz[0].Status; 
      SET   ; 
      SAVE  ; 
      CLR   ; 
_003: U     BIE; 
      )     ; 
      ON    "MX_Jobbetrieb"; 
      ON    #tx_BAG_auto; 
      R     "MX_Prg_von_Maschine"; 
NETWORK
TITLE =M67 Palette bearbeitet
//Palette in Maschine auf Fertigteil setzen,
//Jobliste Stückzahlzähler um 1 erhöhen. 
      UN    "DB_DM_M_FUNKTION".MX_M[67]; // PI: M167
      UN    "Mx_Pal_Fertigteil_H67"; 
      SPB   M401; 

// aktive Seite auf bearbeitet setzen

      U     "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[1].bearbeitet; 
      S     "DB_PRG_LISTE".Platz[0].Seite[1].bearbeitet; 
      SPB   E4SB; 

      U     "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[2].bearbeitet; 
      S     "DB_PRG_LISTE".Platz[0].Seite[2].bearbeitet; 
      SPB   E4SB; 

      U     "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[3].bearbeitet; 
      S     "DB_PRG_LISTE".Platz[0].Seite[3].bearbeitet; 
      SPB   E4SB; 

      U     "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[4].bearbeitet; 
      S     "DB_PRG_LISTE".Platz[0].Seite[4].bearbeitet; 
      SPB   E4SB; 

E4SB: NOP   0; 

// Wenn eine aktive unbearbeite Seite vorhanden ist, Status Rohteil setzen

      U     "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[1].bearbeitet; 
      O     ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[2].bearbeitet; 
      O     ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[3].bearbeitet; 
      O     ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[4].bearbeitet; 
      SPBN  ESTR; 

      L     2; 
      T     "DB_PAL_NR".Platz[0].Status; 

ESTR: NOP   0; 


// Wenn die aktiven Seiten alle bearbeitet sind, oder keine Seite aktiv ist Status Fertigteil setzen

      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      U     "DB_PRG_LISTE".Platz[0].Seite[1].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      )     ; 
      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      U     "DB_PRG_LISTE".Platz[0].Seite[2].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      )     ; 
      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      U     "DB_PRG_LISTE".Platz[0].Seite[3].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      )     ; 
      U(    ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      U     "DB_PRG_LISTE".Platz[0].Seite[4].bearbeitet; 
      ON    "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      )     ; 
      O     "Mx_Pal_Fertigteil_H67"; 
      SPBN  M401; 

      L     4; 
      T     "DB_PAL_NR".Platz[0].Status; 

      NOP   0; 

// bei M67 wird die Stückzahl vom gerade aktiven Job erhöht
      U     "DB_JOB".Job[1].aktiv; 
      SPB   js01; 
      U     "DB_JOB".Job[2].aktiv; 
      SPB   js02; 
      U     "DB_JOB".Job[3].aktiv; 
      SPB   js03; 
      U     "DB_JOB".Job[4].aktiv; 
      SPB   js04; 
      U     "DB_JOB".Job[5].aktiv; 
      SPB   js05; 
      U     "DB_JOB".Job[6].aktiv; 
      SPB   js06; 
      U     "DB_JOB".Job[7].aktiv; 
      SPB   js07; 
      U     "DB_JOB".Job[8].aktiv; 
      SPB   js08; 
      U     "DB_JOB".Job[9].aktiv; 
      SPB   js09; 
      U     "DB_JOB".Job[10].aktiv; 
      SPB   js10; 
      U     "DB_JOB".Job[11].aktiv; 
      SPB   js11; 
      U     "DB_JOB".Job[12].aktiv; 
      SPB   js12; 

      SPA   M401; 

js01: L     "DB_JOB".Job[1].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[1].Stueckzahl; 

      L     "DB_JOB".Job[1].Zeit; 
      T     "DB_JOB".Job[1].letzteZeit; 

      L     "DB_JOB".Job[1].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[1].gesJobZeit; 
      SPA   M401; 

js02: L     "DB_JOB".Job[2].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[2].Stueckzahl; 

      L     "DB_JOB".Job[2].Zeit; 
      T     "DB_JOB".Job[2].letzteZeit; 

      L     "DB_JOB".Job[2].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[2].gesJobZeit; 
      SPA   M401; 

js03: L     "DB_JOB".Job[3].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[3].Stueckzahl; 

      L     "DB_JOB".Job[3].Zeit; 
      T     "DB_JOB".Job[3].letzteZeit; 

      L     "DB_JOB".Job[3].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[3].gesJobZeit; 
      SPA   M401; 

js04: L     "DB_JOB".Job[4].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[4].Stueckzahl; 

      L     "DB_JOB".Job[4].Zeit; 
      T     "DB_JOB".Job[4].letzteZeit; 

      L     "DB_JOB".Job[4].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[4].gesJobZeit; 
      SPA   M401; 

js05: L     "DB_JOB".Job[5].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[5].Stueckzahl; 

      L     "DB_JOB".Job[5].Zeit; 
      T     "DB_JOB".Job[5].letzteZeit; 

      L     "DB_JOB".Job[5].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[5].gesJobZeit; 
      SPA   M401; 

js06: L     "DB_JOB".Job[6].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[6].Stueckzahl; 

      L     "DB_JOB".Job[6].Zeit; 
      T     "DB_JOB".Job[6].letzteZeit; 

      L     "DB_JOB".Job[6].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[6].gesJobZeit; 
      SPA   M401; 

js07: L     "DB_JOB".Job[7].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[7].Stueckzahl; 

      L     "DB_JOB".Job[7].Zeit; 
      T     "DB_JOB".Job[7].letzteZeit; 

      L     "DB_JOB".Job[7].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[7].gesJobZeit; 
      SPA   M401; 

js08: L     "DB_JOB".Job[8].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[8].Stueckzahl; 

      L     "DB_JOB".Job[8].Zeit; 
      T     "DB_JOB".Job[8].letzteZeit; 

      L     "DB_JOB".Job[8].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[8].gesJobZeit; 
      SPA   M401; 

js09: L     "DB_JOB".Job[9].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[9].Stueckzahl; 

      L     "DB_JOB".Job[9].Zeit; 
      T     "DB_JOB".Job[9].letzteZeit; 

      L     "DB_JOB".Job[9].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[9].gesJobZeit; 
      SPA   M401; 

js10: L     "DB_JOB".Job[10].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[10].Stueckzahl; 

      L     "DB_JOB".Job[10].Zeit; 
      T     "DB_JOB".Job[10].letzteZeit; 

      L     "DB_JOB".Job[10].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[10].gesJobZeit; 
      SPA   M401; 

js11: L     "DB_JOB".Job[11].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[11].Stueckzahl; 

      L     "DB_JOB".Job[11].Zeit; 
      T     "DB_JOB".Job[11].letzteZeit; 

      L     "DB_JOB".Job[11].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[11].gesJobZeit; 
      SPA   M401; 

js12: L     "DB_JOB".Job[12].Stueckzahl; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[12].Stueckzahl; 

      L     "DB_JOB".Job[12].Zeit; 
      T     "DB_JOB".Job[12].letzteZeit; 

      L     "DB_JOB".Job[12].gesJobZeit; 
      +D    ; 
      T     "DB_JOB".Job[12].gesJobZeit; 
      SPA   M401; 

M401: NOP   0; 


NETWORK
TITLE =bei Stückzahl grösser gleich Losgroesse Priorität auf 0 setzen 
//Priorität erst 0 Setzen, wenn die Palette auf dem Rüstplatz ist.
//Bei Speicher wenn die Palette auf dem Rüstplatz die Losgrösse erreicht hat.
//Ohne Speicher wenn die Palette auf dem Übergabeplatz die Losgrösse erreicht hat.
//
      CLR   ; 
      =     #t_b_dummy; 

      U     #Pal_Speicher; 
      SPBN  OSP; 
      L     "DB_JOB".Job_Ruestplatz; 
      SPA   RPRI; 
OSP:  L     "DB_JOB".Job_Uebergabeplatz_1; 
RPRI: L     0; 
      >I    ; 
      SPBN  n5en; // beenden wenn kein Job Rüstplatz oder Übergabeplatz
      TAK   ; 
      L     1; 
      -I    ; 
      L     92; // Offset Job
      *I    ; 
      SLD   3; 
      LAR1  ; 
      AUF   "DB_JOB"; 
      L     DBW [AR1,P#2.0]; // Stückzahl
      L     DBW [AR1,P#72.0]; // Losgrösse
      >=I   ; 
      SPBN  n5en; 
      S     #t_b_dummy; 
      L     0; 
      T     DBB [AR1,P#75.0]; 
      SPA   n5en; 

n5en: NOP   0; 
NETWORK
TITLE =aktuelle Zeit mitzählen, bei aktivem Job 

      U     "M_Takt_1s"; 
      U     "MX_Jobbetrieb"; // Speicher in Auto Betrieb
      U(    ; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; // Programm läuft
      O     "DB_SIEM_KANAL_1".E_ProgWait; 
      O     "DB_SIEM_KANAL_1".E_ProgStop; // z.B. M0
      O     "DB_SIEM_KANAL_1".E_ProgInterrupt; 
      O     "DB_SIEM_KANAL_1".E_ChanActive; 
      O     "DB_SIEM_KANAL_1".E_ChanInterrupt; // bei M0
      )     ; 
      U     #tx_BAG_auto; // "BAG".E_AUTO
      SPBN  n6kj; 

      L     "DB_JOB".Job_aktiv; 
      SPL   n6jj; 

      SPA   n600; 
      SPA   n601; 
      SPA   n602; 
      SPA   n603; 
      SPA   n604; 
      SPA   n605; 
      SPA   n606; 
      SPA   n607; 
      SPA   n608; 
      SPA   n609; 
      SPA   n610; 
      SPA   n611; 
      SPA   n612; 


n6jj: SPA   n6kj; 

n600: SPA   n6kj; 

n601: L     "DB_JOB".Job[1].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[1].Zeit; 
      SPA   n6kj; 

n602: L     "DB_JOB".Job[2].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[2].Zeit; 
      SPA   n6kj; 

n603: L     "DB_JOB".Job[3].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[3].Zeit; 
      SPA   n6kj; 

n604: L     "DB_JOB".Job[4].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[4].Zeit; 
      SPA   n6kj; 

n605: L     "DB_JOB".Job[5].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[5].Zeit; 
      SPA   n6kj; 

n606: L     "DB_JOB".Job[6].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[6].Zeit; 
      SPA   n6kj; 

n607: L     "DB_JOB".Job[7].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[7].Zeit; 
      SPA   n6kj; 

n608: L     "DB_JOB".Job[8].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[8].Zeit; 
      SPA   n6kj; 

n609: L     "DB_JOB".Job[9].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[9].Zeit; 
      SPA   n6kj; 

n610: L     "DB_JOB".Job[10].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[10].Zeit; 
      SPA   n6kj; 

n611: L     "DB_JOB".Job[11].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[11].Zeit; 
      SPA   n6kj; 

n612: L     "DB_JOB".Job[12].Zeit; 
      L     1; 
      +D    ; 
      T     "DB_JOB".Job[12].Zeit; 
      SPA   n6kj; 

n6kj: NOP   0; 

NETWORK
TITLE =Meldung Jobliste verzögert

      U     "MX_Jobbetrieb"; // Speicher in Autobetrieb
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_STRINGPW".RequestPW; 
      L     S5T#5S; 
      SE    "Te_Meldung_Jobliste"; 
NETWORK
TITLE =702059 "MEL, kein Job gefunden od. kein Rohteil vorhanden"

      O     "Te_Meldung_Jobliste"; 
      O     "MX_Kein_JOB_Vorhanden"; 
      =     "DB_FEHLERMELDUNG".Meldung._702059_Jobli_nicht_akti; 
NETWORK
TITLE =Rohteil setzen mittels Hardkey Palettenwechslermaschine

      U(    ; 
      L     "DB_PAL_NR".Platz[1].Status; 
      L     1; 
      <>I   ; 
      U     "DB_HR".RPT.RP_PWFG_oRSP_aktiv; 
      UN    "DB_ROB_IF".STAT.Roboter.Automation_ein; 
      )     ; 
      FP    "M_FP_PW_FREIGABE"; 

      O     "I_DM_Ta_Rohteil"; 
      O     "DB_PLC_MD_DB20".UDHex._09_BIT4_Folgebearbeitun; 
      UN    #Pal_Speicher; 
      SPBN  y01; 

      L     2; 
      T     "DB_PAL_NR".Platz[1].Status; 

      SET   ; 
      R     "DB_PRG_LISTE".Platz[1].Seite[1].bearbeitet; 
      R     "DB_PRG_LISTE".Platz[1].Seite[2].bearbeitet; 
      R     "DB_PRG_LISTE".Platz[1].Seite[3].bearbeitet; 
      R     "DB_PRG_LISTE".Platz[1].Seite[4].bearbeitet; 

y01:  NOP   0; 

NETWORK
TITLE =Anforderung Anwahl externe Nullpunktverschiebung
//      U     "I_AL_MASCHINE_EIN"
//      U     "M_Satzfolge_aktiv"
//      FP    "DB_HR".Allgemein.HM_PI_NPV_aktivieren
//      O     "DB_DM_M_FUNKTION".MX_M[67] // PI: M167
//      O     "M_Reset_Taste"
      O     "M_NC_Start_Prg_Anwahl"; 
      O     "DB_NC_PLC".PW_NC_Aktiv; 
      FP    "M_FP_Req_externe_NV"; 
      S     "M_Req_externe_NV"; 

NETWORK
TITLE =externe Nullpunktverschiebung an NC

      U     "M_Req_externe_NV"; 
      SPBN  EENV; 

// Wertermittlung für externe Nullpunktverschiebung
      U     "DB_NC_PLC".PW_NC_Aktiv; 
      O     ; 
      U     "DB_PRG_LISTE".Platz[0].Seite[1].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[1].bearbeitet; 
      SPB   S_0; 

      U     "DB_PRG_LISTE".Platz[0].Seite[2].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[2].bearbeitet; 
      SPBN  S_3; 

      L     9.000000e+001; 
      S     "DB_NC_PLC".PW_NPV_aktiv; 
      SPA   TENV; 

S_3:  U     "DB_PRG_LISTE".Platz[0].Seite[3].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[3].bearbeitet; 
      SPBN  S_4; 

      L     1.800000e+002; 
      S     "DB_NC_PLC".PW_NPV_aktiv; 
      SPA   TENV; 

S_4:  U     "DB_PRG_LISTE".Platz[0].Seite[4].aktiv; 
      UN    "DB_PRG_LISTE".Platz[0].Seite[4].bearbeitet; 
      SPBN  S_0; 

      L     2.700000e+002; 
      S     "DB_NC_PLC".PW_NPV_aktiv; 
      SPA   TENV; 

S_0:  L     0.000000e+000; 

TENV: T     "DB_REMANENT".Externe_NV_Tisch; 

EENV: L     0.000000e+000; 
      L     "DB_REMANENT".Externe_NV_Tisch; 
      ==R   ; 
      U     "DB_ACHSE4".A_ExtZO; 
      R     "DB_NC_PLC".PW_NPV_aktiv; 

      U     "DB_ACHSE4".A_ExtZO; 
      R     "DB_ACHSE4".A_ExtZO; 
      R     "M_Req_externe_NV"; 

      U     "M_Req_externe_NV"; 
      UN    "M_Error_externe_NV"; 
      UN    "TMSpindleIF".IFNo[1]; 
      =     #FB3_Req; 

      CALL "FB_SIEM_PUT" , "DI_FB3_EXTERNE_NV" (
           Req                      := #FB3_Req,
           NumVar                   := 1,
           Addr1                    := "DB_NC_VAR".C1_FE_linShift4_1,
           Error                    := "M_Error_externe_NV",
           Done                     := #FB3_Done,
           State                    := #FB3_State,
           SD1                      := "DB_REMANENT".Externe_NV_Tisch);

      U     #FB3_Done; 
      S     "DB_ACHSE4".A_ExtZO; 

NETWORK
TITLE =Funktion Werkstück entladen laden
//Vorraussetzung:
//Steuerdatei PLC_IN_OUT_xxx.TEA im Verzeichnis Anzeigemaschinendaten bd.dir
//Jobliste im entsprechenden Werkstück im Werkstückverzeichnis
//xxx = Palettennummer, WS001 = Werkstückname
//Eintrag in Steuerdatei: /WKS.DIR/WS001.WPD/JOB.JOB
//Eintrag in Jobliste: LOAD *.*
NETWORK
TITLE =Fehlerauswertung Werkstück laden
//"DB_SIEM_MMC".E_PPError
//• 0 = kein Fehler
//• 1 = ungültige Nummer für Steuer-Datei
//(Wert im DB19.DBB16<127 oder ungültig)
//• 2 = DB19 DBB17 konnte nicht gelesen werden
//• 3 = Steuer-Datei /BD.DIR/PLC_IN_OUT_xxx.TEA nicht gefunden
//(Wert im DB19.DBB16 ungültig)
//• 4 = Ungültiger Index in Steuer-Datei
//(Wert im DB19.DBB17 falsch)
//• 5 = Ausgewählte Jobliste in der Steuerdatei konnte nicht geöffnet werden
//• 6 = Fehler in der Jobliste. (Joblisten-Interpreter meldet Fehler)
//• 7 = Joblisten-Interpreter meldet leere Auftragsliste
//• 8 = Fehler während der Datenübertragung
//Der Fehlertext wird in das DIENSTE PROTOKOLL eingetragen.
//• 9 = Fehler beim Ausführen der Jobliste
//
      L     "DB_SIEM_MMC".E_PPError; 
      L     7; 
      ==I   ; 
      =     #F_leere_Auftragsliste; 

      L     "DB_SIEM_MMC".E_PPError; 
      L     3; 
      ==I   ; 
      =     #F_Steuerdatei_fehlt; 

      L     "DB_SIEM_MMC".E_PPError; 
      L     1; 
      ==I   ; 
      =     #F_Nr_Steuerdatei; 

      L     "DB_SIEM_MMC".E_PPError; 
      L     0; 
      >I    ; 
      =     "DB_FEHLERMELDUNG".MELDUNG6._702842_FEHLER_WST_LADEN; 

NETWORK
TITLE =Quittieren bei Fehler

      U     "DB_SIEM_MMC".E_PPErr; 
      U(    ; 
      O     #F_leere_Auftragsliste; 
      O     #F_Steuerdatei_fehlt; 
      O     #F_Nr_Steuerdatei; 
      O     "DB_FEHLERMELDUNG".MELDUNG6._702842_FEHLER_WST_LADEN; 
      )     ; 
      =     #Quitt_bei_Fehler; 
NETWORK
TITLE =Start Entladen

      L     DB19.DBB   26; 
      L     1; 
      <=I   ; 
      U     "DB_OEMDB2".WST_Laden.Request; 
      UN    "DB_OEMDB2".WST_Laden.Entladen; 
      S     "DB_SIEM_MMC".A_PPdeload; 
      S     "DB_OEMDB2".WST_Laden.Entladen; 
      SPBN  EPLD; 

      L     128; 
      L     "DB_PAL_NR".Platz[1].Pal_Nr; 
      +I    ; 
      T     "DB_SIEM_MMC".A_V24NoFile1; // Steuerdatei PLC_IN_OUT_xxx.TEA xxx=Palettennummer

      L     1; 
      T     "DB_SIEM_MMC".A_V24NoFile2; // Zeile in Steuerdatei PLC_IN_OUT_xxx.TEA

EPLD: NOP   0; 


NETWORK
TITLE =Entladen beendet

      U(    ; 
      O     #Quitt_bei_Fehler; 
      O     "DB_SIEM_MMC".E_PPOK; 
      )     ; 
      U     "DB_SIEM_MMC".A_PPdeload; 
      U     "DB_OEMDB2".WST_Laden.Entladen; 
      R     "DB_SIEM_MMC".A_PPdeload; 

NETWORK
TITLE =Start Beladen

      L     DB19.DBB   26; 
      L     1; 
      <=I   ; 
      U     "DB_OEMDB2".WST_Laden.Request; 
      UN    "DB_OEMDB2".WST_Laden.Beladen; 
      UN    "DB_SIEM_MMC".A_PPdeload; 
      S     "DB_SIEM_MMC".A_PPload; 
      S     "DB_OEMDB2".WST_Laden.Beladen; 
      SPBN  EPUL; 

      L     128; 
      L     "DB_PAL_NR".Platz[0].Pal_Nr; 
      +I    ; 
      T     "DB_SIEM_MMC".A_V24NoFile1; // Steuerdatei PLC_IN_OUT_xxx.TEA xxx=Palettennummer

      L     1; 
      T     "DB_SIEM_MMC".A_V24NoFile2; // Zeile in Steuerdatei PLC_IN_OUT_xxx.TEA

EPUL: NOP   0; 


NETWORK
TITLE =Laden beendet

      U(    ; 
      O     #Quitt_bei_Fehler; 
      O     "DB_SIEM_MMC".E_PPOK; 
      )     ; 
      U     "DB_SIEM_MMC".A_PPload; 
      U     "DB_OEMDB2".WST_Laden.Beladen; 
      R     "DB_SIEM_MMC".A_PPload; 
      S     "DB_STRINGPW".RequestPW; 
      R     "DB_OEMDB2".WST_Laden.Beladen; 

      R     "DB_OEMDB2".WST_Laden.Request; 
NETWORK
TITLE =Reset Werkstück laden

      UN    "DB_OEMDB2".WST_Laden.Request; 
      =     L     53.0; 
      U     L     53.0; 
      BLD   102; 
      R     "DB_SIEM_MMC".A_PPdeload; 

      U     L     53.0; 
      BLD   102; 
      R     "DB_SIEM_MMC".A_PPload; 
      U     L     53.0; 
      BLD   102; 
      R     "DB_OEMDB2".WST_Laden.Entladen; 
      U     L     53.0; 
      BLD   102; 
      R     "DB_OEMDB2".WST_Laden.Beladen; 
      U     L     53.0; 
      SPBNB _004; 
      L     0; 
      T     DB19.DBB   26; 
_004: NOP   0; 
END_FUNCTION

