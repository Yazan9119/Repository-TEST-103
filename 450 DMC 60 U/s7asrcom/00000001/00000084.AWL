FUNCTION "FC_PS_Transp_Pos" : VOID
TITLE =
//$Revision: 1.8 $
//$Date: 2007/10/19 07:15:10CEST $
//$Author: hgc $
//
//Anordnung der Plätze
//
//1 = Position 1 (Grundstellung)
//2 = Position Rüstplatz
//3 = Position Maschinenplatz
//4 = Position Speicherplatz 1
//.
//.
//13 = Position Speicherplatz 10
AUTHOR : SCH
NAME : WZM160
VERSION : 0.1


VAR_INPUT
  Eb_Position : BYTE ;	//Anforderung Platz
  EX_Fahrbefehl : BOOL ;	//Anforderung fahren 
END_VAR
VAR_IN_OUT
  EAX_Fahrbefehl_Fertig : BOOL ;	//Rückmeldung Fahrbefehl abgeschlossen
END_VAR
VAR_TEMP
  T_X_Pos : REAL ;	
  T_Z_Pos : REAL ;	
  T_Typ : BYTE ;	
  T_Platz : INT ;	
  T_MAX_POSITIONEN : BYTE ;	
END_VAR
BEGIN
NETWORK
TITLE =Auswertung Anzahl Positionen
//1 = Position 1 (Grundstellung)
//2 = Position Rüstplatz
//3 = Position Maschinenplatz
//4 = Position Speicherplatz 1
//.
//.
//13 = Position Speicherplatz 10
//
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      SPBN  X001; 
      L     8; 
      SPA   X003; 

X001: U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; 
      SPBN  X002; 
      L     13; 
      SPA   X003; 

X002: L     7; 
X003: T     #T_MAX_POSITIONEN; 
NETWORK
TITLE =Schrittkette Positionieren
//
//
      UN    #EX_Fahrbefehl; 
      SPBN  STRT; 
      L     0; 
      T     "MB_SKZ_Transport"; 
      SET   ; 
      R     "DB_Achs10_Transport".MX_Anf_Positionieren; 

STRT: L     "MB_SKZ_Transport"; 
      SPL   SK99; 
      SPA   SK00; 
      SPA   SK01; 
      SPA   SK02; 
      SPA   SK03; 

SK99: SPA   MEND; 

// -- Schritt 0 ----------------------------------------------------------------

SK00: UN    #EAX_Fahrbefehl_Fertig; 
      U     #EX_Fahrbefehl; 
      UN    "DB_Achs10_Transport".MX_Achse_Faehrt; 

      U(    ; 
      L     #Eb_Position; 
      L     0; 
      >I    ; 
      )     ; 
      SPBN  MEND; 

      U     "DB_LS_POS".Daten_OK; 
      SPB   E001; 
      SET   ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async4._701806_Pos_Ungueltig; 
      SPA   MEND; 

E001: L     1; 
      T     "MB_SKZ_Transport"; 

// -- Schritt 1 ----------------------------------------------------------------
// Platz Ausrechnen

// Transport Grundstellung
SK01: L     #Eb_Position; 
      L     1; 
      ==I   ; 
      SPBN  E011; 
      L     "DB_LS_POS".Transport_Gst; 
      T     "DB_Achs10_Transport".MR_SollPosition; 
      SPA   E012; 

// Positionen ausgefahren
E011: L     #Eb_Position; 
      L     2; 
      ==I   ; 
      U(    ; 
      L     "DB_LSSchnittstelle".IB_TW_Stellung; 
      L     2; 
      >=I   ; 
      TAK   ; 
      U(    ; 
      L     #T_MAX_POSITIONEN; 
      <=I   ; 
      )     ; 
      )     ; 
      SPBN  MEND; 

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS10_AKTIV; // Bei RS10 Abfrage ob unten oder oben
      U(    ; 
      L     "DB_LSSchnittstelle".IB_TW_Stellung; 
      L     9; 
      >=I   ; 
      )     ; 
      SPB   OBEN; 

// *** Auswertung der unteren Ebene (RS4+RS5 komplett, RS10 unten) ***
      L     "DB_LSSchnittstelle".IB_TW_Stellung; 
      DEC   2; 
      L     4; 
      *I    ; 
      SLD   3; 
      LAR1  ; 
      L     14; 
      SLD   3; 
      +AR1  ; 

      AUF   "DB_LS_POS"; 

      L     DBD [AR1,P#0.0]; // X Position
      T     "DB_Achs10_Transport".MR_SollPosition; 
      SPA   E012; 

// *** Auswertung der Speicherpositionen 5-10 (RS10 oben) ***
OBEN: L     "DB_LSSchnittstelle".IB_TW_Stellung; 
      DEC   9; 
      L     4; 
      *I    ; 
      SLD   3; 
      LAR1  ; 
      L     80; 
      SLD   3; 
      +AR1  ; 

      AUF   "DB_LS_POS"; 

      L     DBD [AR1,P#0.0]; // X Position
      T     "DB_Achs10_Transport".MR_SollPosition; 


E012: SET   ; 
      S     "DB_Achs10_Transport".MX_Anf_Positionieren; 

      L     2; 
      T     "MB_SKZ_Transport"; 

// -- Schritt 2 ----------------------------------------------------------------
// Warten bis Positioniert

SK02: U     "DB_Achs10_Transport".MX_Rueck_Positionieren; 
      UN    "DB_Achs10_Transport".MX_Achse_Faehrt; 
      SPBN  MEND; 

      L     3; 
      T     "MB_SKZ_Transport"; 

// -- Schritt 3 ----------------------------------------------------------------
// Quittierung setzen

SK03: SET   ; 
      S     #EAX_Fahrbefehl_Fertig; 
      R     "DB_Achs10_Transport".MX_Rueck_Positionieren; 

      L     0; 
      T     "MB_SKZ_Transport"; 

MEND: NOP   0; 

END_FUNCTION

