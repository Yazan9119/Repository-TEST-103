FUNCTION "FC_CONFIG" : VOID
TITLE =
//$Revision: 1.24 $
//$Date: 2009/01/29 08:31:53CET $
//$Author: riedlh $
{ S7_language := '7(1) Deutsch (Deutschland)  07.05.2008  14:15:41' }
VERSION : 0.1


VAR_TEMP
  t_INT26_DEKADE_1_2 : WORD ;	//INT26: SpannHydraulik Dekade 1+2
  dummy : BOOL ;	
  t_FEST_ext_NPSPS : BOOL ;	//INT26: 1xx ext.Nullpunktspannsystem angewählt
END_VAR
BEGIN
NETWORK
TITLE =ABST: Ausbaustufen
// Netzwerk-Übersicht
// ==================
// NW 1: ABST: Ausbaustufen
// NW 2: Auswertung des angewählten Maschinentyps
// NW 3: Auswertung des angewählten Handlingsystems
// NW 4: ABST: SpannHydraulik HAWE-Box
// NW 5: ABST: SpannHydraulik mit Kuppeln/Entkuppeln im Maschinenraum
// NW 6: ABST: SpannHydraulik nur am Rüstplatz
// NW 7: ABST: SpannHydraulik vorbereitet (ohne HAWEbox und Aggregat)
// NW 8: ABST: HAWEbox-ProfiBus im "FC_CIO_IO_RANGIER" unterdrücken
// NW 9: ABST: SpannHydraulik für Maschinen OHNE PW (offene Tür zulässig)
// NW 10: ABST: SpannHydraulik für Maschinen OHNE PW (offene Tür NICHT zul
// NW 11: ABST: SpannHydraulik für FD-Maschinen bei offener RP-Tuer
// NW 12: ABST: SpannHydraulik bei offener RPT zulässig
// NW 13: ABST: Rüstplatz mit Achse
// NW 14: ABST: automatische RüstPlatzTür
// NW 15: ABST: SpannHydraulik ohne PROP-Ventil (alte SpH)
// NW 16: ABST: Spannhydraulik für SA-MTU
// NW 17: INT26: 1xx ext.Nullpunktspannsystem angewählt
// NW 18: ABST: externes Nullpunktspannsystem (SONDER-Abteilung)
// NW 19: ABST fuer RS: Palette am RPL mit Klemmung & Kupplung
// NW 20: Anwahl FASTEMS
// NW 21: Aufsatzplatte auf Ruestplatz vorhanden
// NW 22: Absoluter Sprung
// NW 23: Rüstplatz: Palette mit Nullpunktspannsystem
// NW 24: Anwahl Palettenspeicher aktiv
// NW 25: Roboter-Beladung OHNE Programm-Kontrolle
// NW 26: Roboter-Beladung mit Programm-Übergabe, wenn Roboter PW-FG auslö
      U     "m_neustart"; 
      SPBN  ABSE; 
NETWORK
TITLE =Auswertung des angewählten Maschinentyps

      U     "m_eins"; 
      =     L      3.0; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._000_STANDORT_MASCHTYP; 
      L     99; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_99_AKTIV; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._000_STANDORT_MASCHTYP; 
      L     3060; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3060_AKTIV; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._000_STANDORT_MASCHTYP; 
      L     3070; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._000_STANDORT_MASCHTYP; 
      L     3071; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
NETWORK
TITLE =Auswertung des angewählten Handlingsystems

      U     "m_eins"; 
      =     L      3.0; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     1; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_3R_OHNE_RPC; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     2; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_3R_MIT_RPC; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     3; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     4; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_VORBER; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     5; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_OHNE_RPC; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     6; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_EROWA_MIT_RPC; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     7; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_ROB_BELADUNG_RPL; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     0; 
      <>I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_VORHANDEN; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     8; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASREMS_V3X; 
      U     L      3.0; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     9; 
      ==I   ; 
      )     ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
NETWORK
TITLE =ABST: SpannHydraulik HAWE-Box

      O     "DB_PLC_MD_DB20".UDHex._33_Bit0_SPH_RPL; 
      O     "DB_PLC_MD_DB20".UDHex._33_Bit1_SPH_MPL; 
      O     "DB_PLC_MD_DB20".UDHex._33_Bit3_SPH_FD; 
      =     "DB_HR".ABST.SpannHydraulik; 
NETWORK
TITLE =ABST: SpannHydraulik mit Kuppeln/Entkuppeln im Maschinenraum

      U     "DB_PLC_MD_DB20".UDHex._27_Bit4_MPLmitKupplung; 
      =     "DB_HR".ABST.SP_MPL_Kuppeln; 
NETWORK
TITLE =ABST: SpannHydraulik nur am Rüstplatz

      U     "DB_PLC_MD_DB20".UDHex._33_Bit0_SPH_RPL; 
      UN    "DB_PLC_MD_DB20".UDHex._33_Bit1_SPH_MPL; 
      =     "DB_HR".ABST.SP_nur_RPL; 
NETWORK
TITLE =ABST: SpannHydraulik vorbereitet (ohne HAWEbox und Aggregat)

      U     "DB_PLC_MD_DB20".UDHex._33_Bit4_SPH_vorb; 
      =     "DB_CONFIG".ABST.SPH_VORBEREITET; 
NETWORK
TITLE =ABST: HAWEbox-ProfiBus im "FC_CIO_IO_RANGIER" unterdrücken

      SET   ; 
      =     "DB_HR".ABST.SP_ProfiBus_aus; 
NETWORK
TITLE =ABST: SpannHydraulik für Maschinen OHNE PW (offene Tür zulässig)

      U     "DB_PLC_MD_DB20".UDHex._33_Bit2_SPH_oPW; 
      U     "DB_PLC_MD_DB20".UDHex._27_Bit7_SPH_FKT_oRPLT; 
      =     "DB_HR".ABST.SP_ohnePW_OFFEN_Tuer; 
NETWORK
TITLE =ABST: SpannHydraulik für Maschinen OHNE PW (offene Tür NICHT zul

      U     "DB_PLC_MD_DB20".UDHex._33_Bit2_SPH_oPW; 
      UN    "DB_PLC_MD_DB20".UDHex._27_Bit7_SPH_FKT_oRPLT; 
      =     "DB_HR".ABST.SP_ohnePW_ZU_Tuer; 
NETWORK
TITLE =ABST: SpannHydraulik für FD-Maschinen bei offener RP-Tuer

      U     "DB_PLC_MD_DB20".UDHex._33_Bit3_SPH_FD; 
      =     "DB_HR".ABST.SP_FD; 
NETWORK
TITLE =ABST: SpannHydraulik bei offener RPT zulässig

      U     "DB_PLC_MD_DB20".UDHex._27_Bit7_SPH_FKT_oRPLT; 
      =     "DB_HR".ABST.SP_bei_offener_RPT; 
NETWORK
TITLE =ABST: Rüstplatz mit Achse
//1: Rüstplatz mit NC-Drehachse
//0: Rüstplatz ohne NC-Drehachse
      U     "DB_PLC_MD_DB20".UDHex._32_Bit0_RPL_Endlos; 
      =     "DB_HR".ABST.RP_mit_Achse; 
NETWORK
TITLE =ABST: automatische RüstPlatzTür

      U     "DB_PLC_MD_DB20".UDHex._27_Bit6_AUTO_RPLT; 
      =     "DB_HR".ABST.Automatische_RPT; 
NETWORK
TITLE =ABST: SpannHydraulik ohne PROP-Ventil (alte SpH)
//INT26: SpannHydraulik Dekade 1+2  = 1
      L     "DB_PLC_MD_DB20".UDInt._026_ANWAHL_SPANNVORR; 
      ITB   ; 
      SLW   8; 
      SRW   8; 
      BTD   ; 
      L     1; 
      ==I   ; 
      =     "DB_HR".ABST.SP_ohnePROP; 
NETWORK
TITLE =ABST: Spannhydraulik für SA-MTU
//3.Dekade von INT_26: 0 = kein Nullpunktspannsystem
//                     1 = Nullpunktspannsystem
//                     2 = Spannhydraulik für FD-Maschinen bei offener RPT
//                     9 = SA-MTU
      L     "DB_PLC_MD_DB20".UDInt._026_ANWAHL_SPANNVORR; 
      ITB   ; 
      SRW   8; 
      L     9; 
      ==I   ; 
      =     "DB_HR".ABST.SP_SA_MTU; 
NETWORK
TITLE =INT26: 1xx ext.Nullpunktspannsystem angewählt
//3.Dekade von INT_26: 0 = kein Nullpunktspannsystem
//                     1 = Nullpunktspannsystem
//                     2 = Spannhydraulik für FD-Maschinen bei offener RPT
//                     9 = SA-MTU
ABSE: L     "DB_PLC_MD_DB20".UDInt._026_ANWAHL_SPANNVORR; 
      ITB   ; 
      SRW   8; 
      L     1; 
      ==I   ; 
      =     #t_FEST_ext_NPSPS; 
NETWORK
TITLE =ABST: externes Nullpunktspannsystem (SONDER-Abteilung)
//INT26=1xx : bedeutet, dass alle Paletten am Rüstplatz immer ein 
//            Ext.Nullpunktspannsystem haben.
//
//INT26<>1xx :Die ABST wird über die Paletten-Codierung variabel aktiviert.
      U(    ; 
      U     "DB_PV_SpH".SpH_Erw[1].Allgemein.KONF_ext_NPSPS; 
      UN    "MX_ABST_Rundspeicher"; 
      O     ; 
      U     "DB_PV_SpH".SpH_Erw[4].Allgemein.KONF_ext_NPSPS; 
      U     "MX_ABST_Rundspeicher"; 
      )     ; 
      UN    #t_FEST_ext_NPSPS; 
      O     #t_FEST_ext_NPSPS; 
      =     "DB_HR".ABST.SP_ext_Nullpunktspannsys; 
NETWORK
TITLE =ABST fuer RS: Palette am RPL mit Klemmung & Kupplung
//Rüstplatz muss ankuppeln und/oder klemmen nach Palette bringen
//Rüstplatz muss abkuppeln und/oder lösen vor Palette holen
      U(    ; 
      O     "DB_HR".ABST.SpannHydraulik; 
      O     "DB_HR".ABST.SP_ext_Nullpunktspannsys; 
      )     ; 
      U     "MX_ABST_Rundspeicher"; 
      U(    ; 
      O     "DB_PLC_MD_DB20".UDHex._17_BIT7_RP_HYD_KLEMMEN; 
      O     "DB_PLC_MD_DB20".UDHex._27_Bit3_RPLmitKupplung; 
      )     ; 
      =     "DB_HR".RPT.extSpannsystem_vorhanden; 
NETWORK
TITLE =Anwahl FASTEMS

      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_VORBER; //"DB_IN".EMX_RPL_Pal_mit_NPSpS; 
      SPBN  FAST; 
NETWORK
TITLE =Aufsatzplatte auf Ruestplatz vorhanden

      U     "I_PC_FIXTURE_EXIST"; 
      =     "DB_IN".EMX_FIXTURE_ON_SUS; 
NETWORK
TITLE =Absoluter Sprung

      SPA   TEMS; 

NETWORK
TITLE =Rüstplatz: Palette mit Nullpunktspannsystem

FAST: U     "DB_HR".ABST.SP_ext_Nullpunktspannsys; 
      =     "DB_IN".EMX_FIXTURE_ON_SUS; 
TEMS: NOP   0; 
NETWORK
TITLE =Anwahl Palettenspeicher aktiv

      L     "DB_PLC_MD_DB20".UDInt._016_ANWAHL_PALSPEICHER; 
      L     0; 
      <>I   ; 
      =     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 


NETWORK
TITLE =Roboter-Beladung OHNE Programm-Kontrolle

      U     "DB_PLC_MD_DB20".UDHex._52_Bit0_ROB_OhnePrgCTRL; 
      =     "DB_CONFIG".ABST.ROB_keine_PRG_CTRL; 
NETWORK
TITLE =Roboter-Beladung mit Programm-Übergabe, wenn Roboter PW-FG auslö

      U     "DB_PLC_MD_DB20".UDHex._52_Bit1_ROB_PrgUebern; 
      =     "DB_CONFIG".ABST.ROB_mit_PRG_UEBERGABE; 
END_FUNCTION

