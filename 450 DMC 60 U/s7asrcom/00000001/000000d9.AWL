FUNCTION "FC_HANDLINGSYSTEM_3R" : VOID
TITLE =
//$Revision: 1.24 $
//$Date: 2008/03/26 09:53:18CET $
//$Author: rieds $
VERSION : 0.1


VAR_TEMP
  t_frg_rettungsmenue : BOOL ;	
  t_frg_tuer_oeffnen : BOOL ;	
  t_frg_tuer_schliessen : BOOL ;	
  t_not_o_robot_tcp1 : BOOL ;	
  t_not_o_robot_cop1 : BOOL ;	
  t_frg_achsen_fahren : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Anwahl Handlingsystem SYSTEM 3R
//1. Anwahl Handlingsystem SYSTEM 3R ohne Leitrechner
//2. Anwahl Handlingsystem SYSTEM 3R mit Leitrechner
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_3R_OHNE_RPC; 
      SPB   _ORP; 

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_3R_MIT_RPC; 
      SPBN  _ABB; 

NETWORK
TITLE =RPC - Schnittstelle
//Werkstückstatus an DB12 ( RPC - Schnittstelle ) melden
//NC-Start o. Rettung neues Werkstück
//H67=1 o. Rettung Werkstück fertig
//H67=2 o. Rettung Werkstück mit fehler beendet
      U     "DB_SINRPC".NC_Kommunikation.NC_Start; 
      FP    "M_FP_RPC_NC-START"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_104_Plus; 
      S     DB12.DBX   21.0; //neues WP
      R     DB12.DBX   21.5; //WP fertig
      R     DB12.DBX   21.6; //WP abgebrochen

      SET   ; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[67]; 

      U     "DB_H_FUNKTION".HX_M[67]; 
      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     1.000000e+000; 
      ==R   ; 
      )     ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_105_Plus; 
      R     DB12.DBX   21.0; //neues WP
      S     DB12.DBX   21.5; //WP fertig
      R     DB12.DBX   21.6; //WP abgebrochen
      R     "DB_DM_M_FUNKTION".MX_M[67]; 

      U     "DB_H_FUNKTION".HX_M[67]; 
      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     2.000000e+000; 
      ==R   ; 
      )     ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_106_Plus; 
      R     DB12.DBX   21.0; //neues WP
      R     DB12.DBX   21.5; //WP fertig
      S     DB12.DBX   21.6; //WP abgebrochen

      U     "DB_H_FUNKTION".HX_M[67]; 
      =     "DB_H_FUNKTION".HX_Quitt[67]; 
      R     "DB_H_FUNKTION".HX_M[67]; 

NETWORK
TITLE =RPC - Statusänderung
//Wenn sich RPC Mode ändern, Meldung senden
      U     "M_8_ter_OB1_Zyklen"; 
      SPBN  _ne1; 

      L     1; 
      T     "DB_SINRPC".Trigger; 

_ne1: L     "DB_SINRPC".MODE_SinCOM; 
      T     "MB_RPC_MODE"; 

      L     "MB_RPC_MODE"; 
      L     "MB_RPC_MODE_OLD"; 
      ==I   ; 
      SPB   _ne2; 
      U     DB12.DBX   14.5; 
      SPBN  _ne3; 
      R     DB12.DBX   14.5; 
      SPA   _ne4; 
_ne3: S     DB12.DBX   14.5; 
_ne4: L     "MB_RPC_MODE"; 
      T     "MB_RPC_MODE_OLD"; 
_ne2: NOP   0; 

NETWORK
TITLE =System 3R Reset Sperre
//1. Wenn im Unbemannten jobmode die NC-reset Taste betätigt wird ist für 3R reset
//gesperrt
//2. Wenn im Unbemannten jobmode die NC-Stop Taste betätigt wird ist für 3R reset 
//gesperrt 
      U     "MSSTstReset"; 
      UN    "DB_SINRPC".NC_Kommunikation.Reset; 
      U     DB12.DBX    4.0; 
      U(    ; 
      U     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      FP    "M_FP_MSST_RESET"; 
      S     "M_IMP_MSST_RESET"; 

      UN    DB12.DBX    4.0; 
      ON(   ; 
      U     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      O     "MSSTst_NC_Start"; 
      R     "M_IMP_MSST_RESET"; 

      UN    "MSSTst NC-Stop *"; 
      U     DB12.DBX    4.0; 
      U(    ; 
      U     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      FP    "M_FP_NC_STOP_MSST"; 
      S     "M_IMP_NC_STOP_MSST"; 

      U     "MSSTstReset"; 
      ON    DB12.DBX    4.0; 
      ON(   ; 
      U     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      O     "MSSTst_NC_Start"; 
      R     "M_IMP_NC_STOP_MSST"; 

NETWORK
TITLE =RPC NC Start/Stop/Reset
//NC Start/Stop/Reset aus DB12 an entsprechende Merker
      U     DB12.DBX   15.0; 
      UN    "M_IMP_MSST_RESET"; 
      UN    "M_IMP_NC_STOP_MSST"; 
      SPBN  _nf1; 

      U     "DB_SINRPC".NC_Kommunikation.NC_Start; 
      FP    "M_FP_NC_START_RPC_3R"; 
      =     "M_NC_START_RPC_3R"; 

      U     "DB_SINRPC".NC_Kommunikation.NC_Stop; 
      FP    "M_FP_NC_STOP_RPC_3R"; 
      =     "M_NC_STOP_RPC_3R"; 

      U     "DB_SINRPC".NC_Kommunikation.Reset; 
      FP    "M_FP_RESET_RPC_3R"; 
      =     "M_RESET_RPC_3R"; 

      SPA   _nf2; 

_nf1: CLR   ; 
      =     "M_NC_START_RPC_3R"; 
      =     "M_NC_STOP_RPC_3R"; 
      =     "M_RESET_RPC_3R"; 
      =     "DB_SINRPC".NC_Kommunikation.NC_Start; 
      =     "DB_SINRPC".NC_Kommunikation.NC_Stop; 
      =     "DB_SINRPC".NC_Kommunikation.Reset; 
_nf2: NOP   0; 


NETWORK
TITLE =Maschinenzustandsänderung
//Wenn sich der Maschinenzustandändert, Meldung senden
      L     DB12.DBW   14; 
      T     "MW_STATUS_MASCH"; 

      L     "MW_STATUS_MASCH"; 
      L     "MW_STATUSMASCH_OLD"; 
      ==D   ; 
      SPB   _ng1; 

      SET   ; 
      =     DB12.DBX    0.2; 

      L     "DB_SINRPC".Trigger; 
      L     128; 
      ==I   ; 
      SPBN  _ng2; 

      L     1; 
      T     "DB_SINRPC".Trigger; 

      L     "MW_STATUS_MASCH"; 
      T     "MW_STATUSMASCH_OLD"; 

      SPA   _ng1; 

_ng2: L     "DB_SINRPC".Trigger; 
      SLW   1; 
      T     "DB_SINRPC".Trigger; 

      L     "MW_STATUS_MASCH"; 
      T     "MW_STATUSMASCH_OLD"; 

_ng1: NOP   0; 

NETWORK
TITLE =RPC Status
//BAG u. Kanal Status an RPC
      U     "M_Handbetrieb"; 
      =     DB12.DBX   15.2; 

      U     "DB_SIEM_BAG".E_MDA; 
      O     "MX_MDA"; 
      =     DB12.DBX   15.1; 

      U     "DB_SIEM_BAG".E_AUTO; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      =     DB12.DBX   15.0; 

      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      =     DB12.DBX   14.3; 

      U     "DB_SIEM_KANAL_1".E_ChanInterrupt; 
      =     DB12.DBX   14.1; 

      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      =     DB12.DBX   14.0; 

      U     "M_8_ter_OB1_Zyklen"; 
      FP    "M_FP_8_ter_OB1_Zyklus"; 
      =     DB12.DBX   14.2; 
      R     "DB_SINRPC".NC_Kommunikation.NC_Start; 

      L     1; 
      T     "DB_SINRPC".DockPosCount; 

NETWORK
TITLE =Security Bit
//Security Bit 1, wenn im unbemannt mode Reset oder NC-Stop betätigt wurde
      O     "M_IMP_NC_STOP_MSST"; 
      O     "M_IMP_MSST_RESET"; 
      =     "DB_SINRPC".Security_Bit; 
      =     DB12.DBX   14.6; 
NETWORK
TITLE =Fehler Ruecksetzen
//Meldungen mittels Quitt Taste Rücksetzen
_ORP: U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510226_BTB_Roboter; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510309_TuerRobot_geschl; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 

NETWORK
TITLE =Reset
//Bei Reset Taste Fehler, M-Fkt. u. Merker rücksetzen
      U     "M_Reset_Taste"; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510226_BTB_Roboter; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510308_TimeOut_Robot; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700928_ROBOT_FALSCHE_PA; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700632_Spannvor_Gesp_0S; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt1._510117_Robot_Tuer_offen; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510309_TuerRobot_geschl; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 
      R     "DB_DM_M_FUNKTION".MX_M[65]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[65]; 
      R     "DB_H_FUNKTION".HX_M[65]; 
      S     "DB_H_FUNKTION".HX_Quitt[65]; 
      R     "DB_H_FUNKTION".HX_M[66]; 
      S     "DB_H_FUNKTION".HX_Quitt[66]; 
      R     "M_SERIELL_ADRESS_AKTIV"; 
      R     "M_HANDLINGSYSTEM_AKTIV"; 
      R     "O_ROBOT_CLOCK"; 
      R     "M_ROBOT_SYNC"; 
      R     "O_ROBOT_SYNC"; 
      R     "O_ROBOT_DATA"; 
      R     "O_ROBOT_START"; 
      R     "O_ROBOT_TCP1"; 
      R     "O_ROBOT_COP1"; 
      R     "M_POS_TAKT"; 
      R     "M_NEG_TAKT"; 
      R     "T_SE_TAKT_POS"; 
      R     "T_SE_TAKT_NEG"; 
      R     "T_SE_Roboter_Sync"; 
      R     "T_SE_Robot_Verzug"; 
      R     "T_SE_WERKSTUECK_W_OK"; 
      R     "M_CHECK_DATA_IN"; 
      SPBN  _BEG; 
      L     0; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
//      =     "DB_NC_PLC".Zusatz.PLC_an_NC.Roboter_Auftrag_fertig

NETWORK
TITLE =Robot nicht in Automatik
//Fehler bei Roboter nicht im Automatikbetrieb u. keine Abarbeitung der 
//Schrittkette
_BEG: UN    "I_ROBOT_AUTO"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510226_BTB_Roboter; 
      SPB   ENDE; 

NETWORK
TITLE =H65 Chuck Adresse
//Chuck Adresse aus Makro DM_M65 PAR1
      SET   ; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[65]; 

      U     "DB_H_FUNKTION".HX_M[65]; 
      SPBN  _EN6; 

      L     "DB_H_FUNKTION".HX_Real_Wert[65]; 
      RND   ; 
      T     "DB_HANDLINGSYSTEM_3R".CHUCK_ADRESSE; 

NETWORK
TITLE =Ueberpruefe Chuck Adresse
//Adresse nur Gültig wenn: 0 < Adresse < 3
      L     "DB_HANDLINGSYSTEM_3R".CHUCK_ADRESSE; 
      L     0; 
      ==I   ; 

      SPBN  _KL3; 

      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 

      SPA   _EN6; 

_KL3: L     "DB_HANDLINGSYSTEM_3R".CHUCK_ADRESSE; 
      L     3; 
      >=I   ; 

      SPBN  _EN6; 

      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 

_EN6: NOP   0; 

NETWORK
TITLE =H66 Werkstueck Adresse
//Werkstückadresse aus Makro DM_M65 PAR2
      SET   ; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[66]; 

      U     "DB_H_FUNKTION".HX_M[66]; 
      SPBN  _EN7; 

      L     "DB_H_FUNKTION".HX_Real_Wert[66]; 
      RND   ; 
      T     "MW_WERKSTUECK_ADRESSE"; 
      T     "DB_HANDLINGSYSTEM_3R".WERKSTUECK_ADRESSE; 

_EN7: NOP   0; 

NETWORK
TITLE =Serielle Adresse bilden
//Chuck- u. Werkstück- Adresse zu einer einzigen Adresse vereinen
      U(    ; 
      O     "DB_H_FUNKTION".HX_M[65]; 
      O     "DB_H_FUNKTION".HX_M[66]; 
      )     ; 
      UN    "M_SERIELL_ADRESS_AKTIV"; 
      SPBN  _EN9; 

      L     "MW_WERKSTUECK_ADRESSE"; 
      SLW   2; 
      L     "DB_HANDLINGSYSTEM_3R".CHUCK_ADRESSE; 
      OW    ; 
      T     "DB_HANDLINGSYSTEM_3R".SERIELLE_ADRESSE; 
      T     "MW_SERIELLE_ADRESSE"; 

NETWORK
TITLE =Ruecksetzen der H-Fkt
//Bestätigung H-Fkt.
      SET   ; 
      R     "DB_H_FUNKTION".HX_M[65]; 
      S     "DB_H_FUNKTION".HX_Quitt[65]; 
      R     "DB_H_FUNKTION".HX_M[66]; 
      S     "DB_H_FUNKTION".HX_Quitt[66]; 

_EN9: NOP   0; 

NETWORK
TITLE =M65 Start Werkstueckwechsel
//M65 Befehl startet Schrittkette u. setzt die Merker für die Adressübertragung 
//und den Wechsel. Die Beladetür wird geöffnet. Nur möglich, wenn Roboter in 
//Neutralstellung ist
      SET   ; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[65]; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[393]; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[394]; 
      CLR   ; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[65]; 

      U     "DB_DM_M_FUNKTION".MX_M[65]; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_NEUTRAL"; 
      SPBN  _E10; 
      S     "M_SERIELL_ADRESS_AKTIV"; 
      S     "M_HANDLINGSYSTEM_AKTIV"; 
      R     "M_ROBOT_DOOR_CLOSE"; 
      S     "M_ROBOT_DOOR_OPEN"; 
//      R     "DB_NC_PLC".Zusatz.PLC_an_NC.Roboter_Auftrag_fertig
      L     1; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E10: U     "DB_DM_M_FUNKTION".MX_M[65]; 
      UN    "I_ROBOT_NEUTRAL"; 
      S     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 

      U     "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  ENDE; 

NETWORK
TITLE =TIMER 
//T_SE_Roboter_Sync = Zeit des Synchronisationsimpuls
//T_SE_Roboter_Verzug = Zeit für den Verzug zwischen Sync. und Adressübertragung
//T_SE_TAKT_POS = Zeit der Positiven Taktflanke
//T_SE_TAKT_NEG = Zeit der Negativen Taktflanke
      U     "O_ROBOT_SYNC"; 
      L     S5T#50MS; 
      SE    "T_SE_Roboter_Sync"; 

      U     "M_ROBOT_SYNC"; 
      L     S5T#50MS; 
      SE    "T_SE_Robot_Verzug"; 

      U     "M_POS_TAKT"; 
      L     S5T#50MS; 
      SE    "T_SE_TAKT_POS"; 

      U     "M_NEG_TAKT"; 
      L     S5T#50MS; 
      SE    "T_SE_TAKT_NEG"; 

NETWORK
TITLE =Syncronisation
//Wenn die Beladetür geöffnet ist wird für 50msec der Synchronisationsimpuls 
//ausgegeben
      U     "M_SERIELL_ADRESS_AKTIV"; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_DOOR_OPENED"; 
      UN    "I_ROBOT_DOOR_CLOSED"; 
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     1; 
      ==I   ; 
      )     ; 
      SPBN  _E12; 

      SET   ; 
      S     "O_ROBOT_SYNC"; 

      U     "T_SE_Roboter_Sync"; 
      SPBN  _E12; 

      R     "O_ROBOT_SYNC"; 
      R     "T_SE_Roboter_Sync"; 
      L     2; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E12: U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     1; 
      ==I   ; 
      )     ; 
      UN    "I_ROBOT_DOOR_OPENED"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510309_TuerRobot_geschl; 

NETWORK
TITLE =Verzugszeit bis Adressübertragung startet
//Verzugszeit zwischen dem Synchronisationsimpuls und dem ersten Takt der 
//Adressübertragung
      U     "M_SERIELL_ADRESS_AKTIV"; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     2; 
      ==I   ; 
      )     ; 
      SPBN  _E13; 

      SET   ; 
      S     "M_ROBOT_SYNC"; 

      U     "T_SE_Robot_Verzug"; 
      SPBN  _E13; 

      R     "M_ROBOT_SYNC"; 
      R     "T_SE_Robot_Verzug"; 
      L     3; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E13: NOP   0; 

NETWORK
TITLE =Übertragung der Adresse
//Generierung des Takt mit 50msec 1:1. Im positiven Taktzustand wird ein 
//Adressbit 
//ausgegeben im negativen geschieht nichts. Mit Abschluss des negativen wird die 
//Adresse geschoben und die Schrittkette erhöht
      UN(   ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     3; 
      <I    ; 
      )     ; 
      UN(   ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     18; 
      >I    ; 
      )     ; 
      U     "M_SERIELL_ADRESS_AKTIV"; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  _E14; 

      UN    "T_SE_TAKT_POS"; 
      SPBN  _NEG; 

      S     "O_ROBOT_CLOCK"; 
      S     "M_POS_TAKT"; 
      U     "M_ROBOT_DATA"; 
      S     "O_ROBOT_DATA"; 

      U     "T_SE_TAKT_POS"; 
      SPBN  _E14; 

_NEG: SET   ; 
      R     "O_ROBOT_CLOCK"; 
      R     "O_ROBOT_DATA"; 
      S     "M_NEG_TAKT"; 

      U     "T_SE_TAKT_NEG"; 
      SPBN  _E14; 

      R     "T_SE_TAKT_POS"; 
      R     "T_SE_TAKT_NEG"; 
      R     "M_POS_TAKT"; 
      R     "M_NEG_TAKT"; 

      L     1; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      +I    ; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

      L     "MW_SERIELLE_ADRESSE"; 
      SRW   1; 
      T     "MW_SERIELLE_ADRESSE"; 

      U     "M_ROBOT_DATA"; 
      S     "O_ROBOT_DATA"; 

_E14: NOP   0; 

NETWORK
TITLE =Aufnehmen der zurückgesandten Adresse
//Mit einer neg. Flanke des Takt wird die Adressrückmeldung vom System 3R 
//ausgewertet
      UN(   ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     3; 
      <I    ; 
      )     ; 
      UN(   ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     17; 
      >I    ; 
      )     ; 
      U     "M_SERIELL_ADRESS_AKTIV"; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  _E15; 

      U     "O_ROBOT_CLOCK"; 
      FN    "M_FN_ROBOT_CLOCK"; 
      S     "M_CHECK_DATA_IN"; 

      U     "M_CHECK_DATA_IN"; 
      SPBN  _E15; 

      U     "I_ROBOT_DATA_IN"; 
      =     "M_ROBOT_DATA_IN"; 

      SET   ; 
      L     "MW_ADRESSE_KONTR"; 
      SRW   1; 
      T     "MW_ADRESSE_KONTR"; 
      T     "DB_HANDLINGSYSTEM_3R".RUECK_ADRESSE; 

      R     "M_CHECK_DATA_IN"; 

_E15: NOP   0; 

NETWORK
TITLE =Vergleich der Adressen
//Mit Abschluss der Adressübertragung wird die gesendete mit der empfangenen 
//Adresse verglichen
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     19; 
      ==I   ; 
      )     ; 
      U     "M_SERIELL_ADRESS_AKTIV"; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  _E16; 

      R     "M_SERIELL_ADRESS_AKTIV"; 

      UN(   ; 
      L     "DB_HANDLINGSYSTEM_3R".SERIELLE_ADRESSE; 
      L     "DB_HANDLINGSYSTEM_3R".RUECK_ADRESSE; 
      ==I   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700928_ROBOT_FALSCHE_PA; 

      L     20; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E16: NOP   0; 

NETWORK
TITLE =Start Robot
//Ist die Adressübertragung abgeschlossen, erfolgt der Werkstückwechsel durch 
//System 3R
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     20; 
      ==I   ; 
      )     ; 
      UN    "M_SERIELL_ADRESS_AKTIV"; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      SPBN  _E17; 

      UN    "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700928_ROBOT_FALSCHE_PA; 
      S     "O_ROBOT_START"; 
      S     "O_ROBOT_TCP1"; 
      S     "O_ROBOT_COP1"; 
      L     21; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E17: NOP   0; 

NETWORK
TITLE =Beladen fertig
//Am Ende des Beladevorgang wird Beladen Ende oder Fehler für min. 200msec 
//erwartet
      O     "I_ROBOT_BELADEN_ENDE"; 
      O     "I_ROBOT_ERROR"; 
      O     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700928_ROBOT_FALSCHE_PA; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     21; 
      ==I   ; 
      )     ; 
      L     S5T#200MS; 
      SE    "T_SE_BELADEN_ENDE"; 
      U     "T_SE_BELADEN_ENDE"; 
      SPBN  _E18; 

      R     "O_ROBOT_START"; 
      R     "O_ROBOT_TCP1"; 
      R     "O_ROBOT_COP1"; 
      L     22; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E18: NOP   0; 

NETWORK
TITLE =Beladetür schliessen
//Beladetür wird geschlossen, wenn der Roboter seine Neutralstellung meldet
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     22; 
      ==I   ; 
      )     ; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      U     "I_ROBOT_NEUTRAL"; 
      SPBN  _E19; 

      S     "M_ROBOT_DOOR_CLOSE"; 
      R     "M_ROBOT_DOOR_OPEN"; 
      L     23; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

_E19: U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     22; 
      ==I   ; 
      )     ; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      UN    "I_ROBOT_NEUTRAL"; 
      S     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 

NETWORK
TITLE =Überwachung Beladetür
//Es wird gewartet, bis die Beladetür geschlossen ist
      U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     23; 
      ==I   ; 
      )     ; 
      U     "M_HANDLINGSYSTEM_AKTIV"; 
      UN    "I_ROBOT_DOOR_OPENED"; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      SPBN  _E20; 

      R     "M_HANDLINGSYSTEM_AKTIV"; 
      CLR   ; 
      =     "DB_DM_M_FUNKTION".MX_M[65]; 
      SET   ; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[65]; 

      L     0; 
      T     "DB_HANDLINGSYSTEM_3R".Schrittkette; 

      UN    "I_ROBOT_DOOR_CLOSED"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 

_E20: U(    ; 
      L     "DB_HANDLINGSYSTEM_3R".Schrittkette; 
      L     23; 
      ==I   ; 
      )     ; 
      UN    "I_ROBOT_DOOR_CLOSED"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510310_TuerRobot_offen; 

NETWORK
TITLE =Wechsel mit Fehler beendet
//Roboter meldet Fehler
      O     "I_ROBOT_ERROR"; 
      O     ; 
      U     "I_ROBOT_BELADEN_ENDE"; 
      U     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700928_ROBOT_FALSCHE_PA; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700927_ROBOT_WECHSEL_FE; 
NETWORK
TITLE =Überwachungszeit
//Zeit wird in Makro DM_M65 von GUD8 in Schnittstelle DB70 geschrieben, hier 
//überwachung ob Werkstückwechseln innerhalb der Zeit beendet wurde
      L     "DB_NC_PLC".Zusatz.NC_an_PLC.Zeit_Roboter; 
      SRW   8; 
      ITB   ; 
      L     8192; 
      OW    ; 
      T     "DB_HANDLINGSYSTEM_3R".ZEIT_BELADEN; 

      U     "O_ROBOT_START"; 
      L     "DB_HANDLINGSYSTEM_3R".ZEIT_BELADEN; 
      SE    "T_SE_WERKSTUECK_W_OK"; 

      U     "T_SE_WERKSTUECK_W_OK"; 
      S     "DB_FEHLERMELDUNG".MELDUNG5._510308_TimeOut_Robot; 

      U     "DB_SIEM_KANAL_1".E_ChanActive; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510308_TimeOut_Robot; 

NETWORK
TITLE =Fehler Spannfutter
//Überwachung des Spannfutter
ENDE: UN    "M_HANDLINGSYSTEM_AKTIV"; 
      UN    "I_ROBOT_CHUCKS_CLOSED"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700632_Spannvor_Gesp_0S; 
NETWORK
TITLE =Vorschub Halt Beladetür offen
//Beladetür offen --> Achssperre X,Y,Z,Spindel
      UN    "I_ROBOT_DOOR_CLOSED"; 
      =     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
NETWORK
TITLE =Ende an warten in Makro DM_M65.SPF
//Beenden des Makro DM_M65
//      UN    "M_HANDLINGSYSTEM_AKTIV"
//      =     "DB_NC_PLC".Zusatz.PLC_an_NC.Roboter_Auftrag_fertig  
NETWORK
TITLE =NC-Stop
//
//
      O     "T_SE_WERKSTUECK_W_OK"; 
      O     "M_NC_STOP_RPC_3R"; 
      =     "DB_SIEM_KANAL_1".A_NCStop; 
NETWORK
TITLE =Rettungsmenü: Anzeige
//OEM Anzeigen in DECKEL MAHO II Optionen-System3R
      U     "M_MA_Maschine_Ein"; 
      U     "DB_SIEM_BAG".E_JOG; 
      =     #t_frg_rettungsmenue; 
      =     #t_frg_tuer_oeffnen; 

      U     #t_frg_rettungsmenue; 
      U     "I_ROBOT_NEUTRAL"; 
      =     #t_frg_tuer_schliessen; 

      U     "DB_OEM".Handlingsystem_Aktiv; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN    "I_ROBOT_NEUTRAL"; 
      S     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 

      U     "I_ROBOT_NEUTRAL"; 
      R     "DB_FEHLERMELDUNG".MELDUNG5._510227_Lader_im_Stoerb; 

      CALL "FB_VENTIL_2E2A" , "DI_VENTIL_2E2A_HASYST" (
           EX_Init                  := "m_null",
           Zeittakt                 := "m_takt_0,1sec.",
           OutputMode               := "m_null",
           Endlage_AST              := "I_ROBOT_DOOR_OPENED",
           Endlage_GST              := "I_ROBOT_DOOR_CLOSED",
           Automatik                := "M_HANDLINGSYSTEM_AKTIV",
           Hand                     := #t_frg_rettungsmenue,
           Freig_Ventil_AST_Auto    := "M_ROBOT_DOOR_OPEN",
           Freig_Ventil_GST_Auto    := "M_ROBOT_DOOR_CLOSE",
           Taste_AST_Hand           := "DB_EINRICHTFUNKTION".Bewegung_103_Minus,
           Taste_GST_Hand           := "DB_EINRICHTFUNKTION".Bewegung_103_Plus,
           Freigabe_AST_Hand        := #t_frg_tuer_oeffnen,
           Freigabe_GST_Hand        := #t_frg_tuer_schliessen,
           Freig_Ventil_Hardware    := "m_eins",
           SST_Anlage_Aus           := "m_null",
           QUIT                     := "M_Ruecksetze_Fehler",
           Laufzeit_AST             := 50,
           Laufzeit_GST             := 50,
           Wartezeit_AST            := 0,
           Wartezeit_GST            := 0,
           Einrichten_Aktiv         := TRUE,
           Bewegungsnummer          := 103,
           Ventil_AST               := "O_ROBOT_DOOR_OPEN",
           Ventil_GST               := "O_ROBOT_DOOR_CLOSE");

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #t_frg_rettungsmenue,
           Freigabe_GST_Hand        := #t_frg_rettungsmenue,
           Endlage_AST              := FALSE,
           Endlage_GST              := DB12.DBX   21.0,
           Ventil_AST               := FALSE,
           Ventil_GST               := FALSE,
           Bewegungsnummer          := 104);

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #t_frg_rettungsmenue,
           Freigabe_GST_Hand        := #t_frg_rettungsmenue,
           Endlage_AST              := FALSE,
           Endlage_GST              := DB12.DBX   21.5,
           Ventil_AST               := FALSE,
           Ventil_GST               := FALSE,
           Bewegungsnummer          := 105);

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #t_frg_rettungsmenue,
           Freigabe_GST_Hand        := #t_frg_rettungsmenue,
           Endlage_AST              := FALSE,
           Endlage_GST              := DB12.DBX   21.6,
           Ventil_AST               := FALSE,
           Ventil_GST               := FALSE,
           Bewegungsnummer          := 106);


      U     "DB_EINRICHTFUNKTION".Bewegung_107_Plus; 
      O     ; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      ON    "DB_OEM".Handlingsystem_Aktiv; 
      ON    "DB_SIEM_BAG".E_JOG; 
      )     ; 
      R     "O_ROBOT_COP1"; 


      U     "DB_EINRICHTFUNKTION".Bewegung_107_Minus; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      S     "O_ROBOT_COP1"; 

      UN    "O_ROBOT_COP1"; 
      =     #t_not_o_robot_cop1; 

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #t_frg_rettungsmenue,
           Freigabe_GST_Hand        := #t_frg_rettungsmenue,
           Endlage_AST              := "O_ROBOT_COP1",
           Endlage_GST              := #t_not_o_robot_cop1,
           Ventil_AST               := "O_ROBOT_COP1",
           Ventil_GST               := #t_not_o_robot_cop1,
           Bewegungsnummer          := 107);

      U     "DB_EINRICHTFUNKTION".Bewegung_108_Plus; 
      O     ; 
      UN    "M_HANDLINGSYSTEM_AKTIV"; 
      U(    ; 
      ON    "DB_OEM".Handlingsystem_Aktiv; 
      ON    "DB_SIEM_BAG".E_JOG; 
      )     ; 
      R     "O_ROBOT_TCP1"; 

      U     "DB_EINRICHTFUNKTION".Bewegung_108_Minus; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      U     "I_ROBOT_DOOR_OPENED"; 
      S     "O_ROBOT_TCP1"; 

      UN    "O_ROBOT_TCP1"; 
      =     #t_not_o_robot_tcp1; 

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #t_frg_rettungsmenue,
           Freigabe_GST_Hand        := #t_frg_rettungsmenue,
           Endlage_AST              := "O_ROBOT_TCP1",
           Endlage_GST              := #t_not_o_robot_tcp1,
           Ventil_AST               := "O_ROBOT_TCP1",
           Ventil_GST               := #t_not_o_robot_tcp1,
           Bewegungsnummer          := 108);

NETWORK
TITLE =Fahren auf Wechselposition
//Rettungsmenü fahren auf Wechselposition
      U     "DB_EINRICHTFUNKTION".Bewegung_109_Plus; 
      =     "M_ROBOT_FAHREN_AUF_WEPOS"; 
NETWORK
TITLE =Fahren auf Spülposition
//Rettungsmenü fahren auf Spülposition
      U     "DB_EINRICHTFUNKTION".Bewegung_110_Plus; 
      =     "M_ROBOT_FAHREN_AUF_SPPOS"; 
NETWORK
TITLE =Wechselposition ASUP starten
//Starten des ASUP MACHINE 
      U     "M_ROBOT_FAHREN_AUF_WEPOS"; 
      U     "M_Leistung_Steht_an"; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      FP    "M_FP_ASUP_WEPOS"; 
      SPBN  Asu2; 
NETWORK
TITLE =ASUP-fc201 2 Palettenwechslerwechsler Rettung (Einrichten)
//Start ASUP
      U     "m_eins"; 
      S     "Start_ASUP_Machine_Nr.5"; 
NETWORK
TITLE =Rettungs Funktion Nummer

      L     1; 
      T     "DB_NC_PLC".Rettung_FktNr; 
      SPA   EnAs; 
NETWORK
TITLE =Spülposition ASUP starten
//Starten des ASUP MACHINE 
Asu2: U     "M_ROBOT_FAHREN_AUF_SPPOS"; 
      U     "M_Leistung_Steht_an"; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      FP    "M_FP_ASUP_SPPOS"; 
      SPBN  EnAs; 
NETWORK
TITLE =ASUP-fc201 2 Palettenwechslerwechsler Rettung (Einrichten)
//Start ASUP
      U     "m_eins"; 
      S     "Start_ASUP_Machine_Nr.5"; 
NETWORK
TITLE =Rettungs Funktion Nummer

      L     2; 
      T     "DB_NC_PLC".Rettung_FktNr; 
NETWORK
TITLE =Rücksetzten des ASUP
//Wird die Rettungstaste losgelassen, so wird ASUP gestoppt
EnAs: U(    ; 
      U(    ; 
      L     109; 
      L     "DB_OEM".Bewegungsnummer; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_109_Plus; 
      O     ; 
      U(    ; 
      L     110; 
      L     "DB_OEM".Bewegungsnummer; 
      ==I   ; 
      )     ; 
      UN    "DB_EINRICHTFUNKTION".Bewegung_110_Plus; 
      )     ; 
      U     "DB_OEM".Handlingsystem_2_Aktiv; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      SPBN  KAsu; 
NETWORK
TITLE =WZW: Reset auslösen bei Abbruch Asup durch loslassen der Rettung
//Reset und ASUP rücksetzen
      U     "m_eins"; 
      =     "M_RESET_RPC_3R"; 
      R     "Start_ASUP_Machine_Nr.5"; 
NETWORK
TITLE =Freigabe fahren der Achsen
//Freigabe zum fahren der Achsen über Rettungsmenü
KAsu: U     #t_frg_rettungsmenue; 
      U     "I_ROBOT_DOOR_CLOSED"; 
      =     #t_frg_achsen_fahren; 
NETWORK
TITLE =Auf Position
//M393 meldet auf Wechsel position
      U     "DB_DM_M_FUNKTION".MX_M[393]; 
      S     "M_AUF_WE_POS"; 
      U(    ; 
      ON    "DB_ACHSE1_X".E_Stat; 
      ON    "DB_ACHSE2_Y".E_Stat; 
      ON    "DB_ACHSE3_Z".E_Stat; 
      ON    "DB_ACHSE4".E_Stat; 
      O     ; 
      UN    "DB_ACHSE5".E_Stat; 
      U     "DB_ACHSE5".E_AxisReady; 
      )     ; 
      R     "M_AUF_WE_POS"; 
      U     "M_AUF_WE_POS"; 
      R     "DB_DM_M_FUNKTION".MX_M[393]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[393]; 
NETWORK
TITLE =Auf Position
//M394 meldet auf Spül position
      U     "DB_DM_M_FUNKTION".MX_M[394]; 
      S     "M_AUF_SP_POS"; 
      U(    ; 
      ON    "DB_ACHSE1_X".E_Stat; 
      ON    "DB_ACHSE2_Y".E_Stat; 
      ON    "DB_ACHSE3_Z".E_Stat; 
      ON    "DB_ACHSE4".E_Stat; 
      O     ; 
      UN    "DB_ACHSE5".E_Stat; 
      U     "DB_ACHSE5".E_AxisReady; 
      )     ; 
      R     "M_AUF_SP_POS"; 
      U     "M_AUF_SP_POS"; 
      R     "DB_DM_M_FUNKTION".MX_M[394]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[394]; 
NETWORK
TITLE =Anzeige Rettungsmenü - Achse auf Wechselposition
//Anzeige der Endlagen und Ventile im Rettungsmenü
//
      U     #t_frg_achsen_fahren; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_achsen_fahren; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     "M_AUF_WE_POS"; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "M_ROBOT_FAHREN_AUF_WEPOS"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 109);
      NOP   0; 
NETWORK
TITLE =Anzeige Rettungsmenü - Achse auf Spuelposition
//Anzeige der Endlagen und Ventile im Rettungsmenü
//
      U     #t_frg_achsen_fahren; 
      =     L      1.0; 
      BLD   103; 
      U     #t_frg_achsen_fahren; 
      =     L      1.1; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.2; 
      BLD   103; 
      U     "M_AUF_SP_POS"; 
      =     L      1.3; 
      BLD   103; 
      U     "m_null"; 
      =     L      1.4; 
      BLD   103; 
      U     "M_ROBOT_FAHREN_AUF_SPPOS"; 
      =     L      1.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      1.0,
           Freigabe_GST_Hand        := L      1.1,
           Endlage_AST              := L      1.2,
           Endlage_GST              := L      1.3,
           Ventil_AST               := L      1.4,
           Ventil_GST               := L      1.5,
           Bewegungsnummer          := 110);
      NOP   0; 
NETWORK
TITLE =Rettungsmenü Grundstellung

      U     "I_ROBOT_DOOR_CLOSED"; 
      UN    "O_ROBOT_TCP1"; 
      UN    "O_ROBOT_COP1"; 
      =     "DB_OEM".Handlingsystem_GrdSt; 
NETWORK
TITLE =Ende
//Bausteinende
_ABB: BEA   ; 

END_FUNCTION

