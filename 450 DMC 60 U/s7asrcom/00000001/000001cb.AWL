FUNCTION "FC_FMS_MC_IF" : VOID
TITLE =
//$Revision: 1.8 $
//$Date: 2008/09/08 13:52:43CEST $
//$Author: sth $
VERSION : 0.1


VAR_INPUT
  IW_Mag_Nummer : WORD ;	
  IW_Platznummer : WORD ;	
  IW_T_Nummer : WORD ;	
  I_req_wz_melden : BOOL ;	
END_VAR
VAR_OUTPUT
  O_Ready_WZ_melden : BOOL ;	
END_VAR
VAR_TEMP
  FrgMeldHost : BOOL ;	
  QuitBeEntladen : BOOL ;	
  StartTelWzg : BOOL ;	
  EA_Pal_Nr_MA_1 : BOOL ;	
  ZS_DT : ARRAY  [1 .. 12 ] OF BYTE ;	
  ZS_Zustand : BYTE ;	
  ZS_PLCMode : BYTE ;	
  ZS_BA : BYTE ;	
END_VAR
BEGIN
NETWORK
TITLE =mit request, command type auskodieren 

      U     "DI_FB_IF_FASTEMS".FMS_MC_Read_data_command; 
      UN    "DI_FB_IF_FASTEMS".m_read_program_selection; 
      UN    "DI_FB_IF_FASTEMS".m_read_pallet_number; 
      UN    "DI_FB_IF_FASTEMS".m_read_b_axis_angle; 
      UN    "DI_FB_IF_FASTEMS".m_read_hydraulik_info; 
      FP    "DI_FB_IF_FASTEMS".fp_read_data_command; 
      SPBN  a01; 

      L     "DI_FB_IF_FASTEMS".FMS_MC_Read_data_comtype; 
      L     10; 
      ==I   ; 
      S     "DI_FB_IF_FASTEMS".m_read_program_selection; 

      L     "DI_FB_IF_FASTEMS".FMS_MC_Read_data_comtype; 
      L     20; 
      ==I   ; 
      S     "DI_FB_IF_FASTEMS".m_read_pallet_number; 

      L     "DI_FB_IF_FASTEMS".FMS_MC_Read_data_comtype; 
      L     30; 
      ==I   ; 
      S     "DI_FB_IF_FASTEMS".m_read_b_axis_angle; 

      L     "DI_FB_IF_FASTEMS".FMS_MC_Read_data_comtype; 
      L     40; 
      ==I   ; 
      S     "DI_FB_IF_FASTEMS".m_read_hydraulik_info; 


a01:  NOP   0; 

NETWORK
TITLE =programm selection

      CALL "FB_SIEM_PI" , "DI_FB_SIEM_PI_Fastems" (
           Req                      := "DI_FB_IF_FASTEMS".prog_selec.Request,
           PIService                := "DB_SIEM_PI".SELECT,
           Unit                     := 1,
           Addr1                    := "DI_FB_IF_FASTEMS".prog_selec.Pfad_Programm,
           Addr2                    := "DI_FB_IF_FASTEMS".FMS_MC_data_bus_string24,
           Error                    := "DI_FB_IF_FASTEMS".prog_selec.Error,
           Done                     := "DI_FB_IF_FASTEMS".prog_selec.Done,
           State                    := "DI_FB_IF_FASTEMS".prog_selec.State);

      U     "DI_FB_IF_FASTEMS".m_read_program_selection; 
      FP    "DI_FB_IF_FASTEMS".prog_selec.fp_prog_select; 
      SPBN  nw2; 
      R     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
      L     0; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      S     "DI_FB_IF_FASTEMS".prog_selec.Request; 

nw2:  U     "DI_FB_IF_FASTEMS".prog_selec.Error; 
      FP    "DI_FB_IF_FASTEMS".prog_selec.fp_error; 
      SPBN  nw2a; 
      L     12; 
      R     "DI_FB_IF_FASTEMS".prog_selec.Request; 
      SPA   nw2b; 

nw2a: U     "DI_FB_IF_FASTEMS".prog_selec.Done; 
      FP    "DI_FB_IF_FASTEMS".prog_selec.fp_done; 
      SPBN  enw2; 
      L     11; 
      R     "DI_FB_IF_FASTEMS".prog_selec.Request; 

nw2b: T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      S     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
enw2: NOP   0; 
NETWORK
TITLE =pallet number
//Übergabe der Palettennummer
      U     "DI_FB_IF_FASTEMS".m_read_pallet_number; 
      FP    "M_FP_read_pallet_number"; 
      SPBN  enw3; 
      R     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
      L     0; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 

      CALL "STRNG_I" (
           S                        := "DI_FB_IF_FASTEMS".FMS_MC_data_bus_string24,
           RET_VAL                  := "DB_PAL_NR".Platz[1].Pal_Nr);//Rüstplatz

//Abfrage Palettennummer empfangen
      UN    BIE; //BIE ist Null wenn STRNG_I Wandlung ungültig
      O(    ; 
      L     "DB_PAL_NR".Platz[1].Pal_Nr; 
      L     9999; 
      >I    ; 
      )     ; 
      SPB   nw3a; 

      L     0; 
      L     "DB_PAL_NR".Platz[1].Pal_Nr; 
      ==I   ; 
      SPB   nw3a; 
      L     21; //APC pallet number received
      SPA   nw3b; 
nw3a: L     0; 
      T     "DB_PAL_NR".Platz[1].Pal_Nr; 
      L     22; //APC pallet number not accepted
nw3b: T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      S     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
enw3: NOP   0; 
NETWORK
TITLE =b-axis angle
//GUD  _DM_4AXIS_ANGLE im GUD9 schreiben
//GUD schreiben
      CALL "FB_SIEM_PUT" , "DI_FB_SIEM_PUT_Fastems" (
           Req                      := "DI_FB_IF_FASTEMS".gud_write.gud_write_request,
           NumVar                   := 1,
           Addr1                    := "DI_FB_IF_FASTEMS".gud_write.GUDVar1Token,
           Unit1                    := B#16#0,
           Column1                  := W#16#0,
           Line1                    := W#16#0,
           Error                    := "DI_FB_IF_FASTEMS".gud_write.gud_write_error,
           Done                     := "DI_FB_IF_FASTEMS".gud_write.gud_write_done,
           State                    := "DI_FB_IF_FASTEMS".gud_write.gud_write_state,
           SD1                      := "DI_FB_IF_FASTEMS".gud_write.MD_b_axis_angle_value);

// Adresse der Gud Variable lesen 
//------------------------------------------------------------------
      U     "M_8_ter_OB1_Zyklen"; 
      S     "DI_FB_IF_FASTEMS".gud_write.read_token_request; 

      U     "DI_FB_IF_FASTEMS".gud_write.read_token_error; 
      U     "M_Reset_Taste"; 
      O     "DI_FB_IF_FASTEMS".gud_write.read_token_done; 
      R     "DI_FB_IF_FASTEMS".gud_write.read_token_request; 

      CALL "FB_SIEM_GETGUD" , "DI_FB_SIEM_GETGUD_Fastem" (
           Req                      := "DI_FB_IF_FASTEMS".gud_write.read_token_request,
           Addr                     := "DI_FB_IF_FASTEMS".gud_write.read_token_Variable,
           Area                     := B#16#0,// 0=NCK, 2=CHAN
           Unit                     := B#16#1,// Kanal-Nr.
           Index1                   := 0,// Feldindex 1
           Index2                   := 0,// Feldindex
           CnvtToken                := TRUE,
           VarToken                 := "DI_FB_IF_FASTEMS".gud_write.GUDVar1Token,
           Error                    := "DI_FB_IF_FASTEMS".gud_write.read_token_error,
           Done                     := "DI_FB_IF_FASTEMS".gud_write.read_token_done,
           State                    := "DI_FB_IF_FASTEMS".gud_write.read_token_state,
           RD                       := "DI_FB_IF_FASTEMS".gud_write.read_token_Data);
//------------------------------------------------------------------

// Request von Fastems
      U     "DI_FB_IF_FASTEMS".m_read_b_axis_angle; 
      FP    "DI_FB_IF_FASTEMS".gud_write.fp_m_read_b_axis_angle; 
      SPBN  nw4; 
      R     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
      L     0; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 

//Request für FB3
      UN    "DI_FB_IF_FASTEMS".gud_write.read_token_request; 
      S     "DI_FB_IF_FASTEMS".gud_write.gud_write_request; 
//String wandel nach Int
      CALL "STRNG_I" (
           S                        := "DI_FB_IF_FASTEMS".FMS_MC_data_bus_string24,
           RET_VAL                  := "DI_FB_IF_FASTEMS".gud_write.MW_b_axis_angle_value);

      U     BIE; 
      U(    ; 
      L     "DI_FB_IF_FASTEMS".gud_write.MW_b_axis_angle_value; 
      L     0; 
      >=I   ; 
      )     ; 
      U(    ; 
      L     "DI_FB_IF_FASTEMS".gud_write.MW_b_axis_angle_value; 
      L     360; 
      <I    ; 
      )     ; 
      SPBN  nw4b; 

//Int in Dword für FB3
nw4:  L     "DI_FB_IF_FASTEMS".gud_write.MW_b_axis_angle_value; 
      T     "DI_FB_IF_FASTEMS".gud_write.MD_b_axis_angle_value; 

      O     "DI_FB_IF_FASTEMS".gud_write.gud_write_done; 
      O     "DI_FB_IF_FASTEMS".gud_write.gud_write_error; 
      U     "DI_FB_IF_FASTEMS".m_read_b_axis_angle; 
      SPBN  enw4; 

      U     "DI_FB_IF_FASTEMS".gud_write.gud_write_done; 
      R     "DI_FB_IF_FASTEMS".gud_write.gud_write_request; 
      L     31; 
      SPB   nw4a; 

      U     "DI_FB_IF_FASTEMS".gud_write.gud_write_error; 
nw4b: R     "DI_FB_IF_FASTEMS".gud_write.gud_write_request; 
      L     32; 
nw4a: T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      S     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
enw4: NOP   0; 
NETWORK
TITLE =hydraulik pallet info
//Information ob die neue Palette mit oder ohne Spannhydraulik ist.
//Hier wird auch der Spanndruck vorgegeben!!!
      U     "DI_FB_IF_FASTEMS".m_read_hydraulik_info; 
      FP    "M_FP_FMS_HYD_INFO"; 
      SPBN  enw5; 
      R     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
      L     0; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; 

      CALL "STRNG_I" (
           S                        := "DI_FB_IF_FASTEMS".FMS_MC_data_bus_string24,
           RET_VAL                  := "DI_FB_IF_FASTEMS".mw_hydraulik_info);

//Abfrage Hydraulikinfo empfangen   
      UN    BIE; //Wandlung erfolgreich?
      O(    ; //nicht Null?
      L     0; 
      L     "DI_FB_IF_FASTEMS".mw_hydraulik_info; 
      ==I   ; 
      )     ; 
      O(    ; //nicht größer 2?
      L     "DI_FB_IF_FASTEMS".mw_hydraulik_info; 
      L     2; 
      >I    ; 
      )     ; 
      SPB   nw5a; 

//auskodieren ob Hydraulikpalette oder normal
      L     "DI_FB_IF_FASTEMS".mw_hydraulik_info; //1=hydraulikpalette
      L     1; 
      ==I   ; 
      SPBN  nw5c; 
      L     1; //Palette mit Spannhydraulik
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; //(1=Vorrichtung; 2=Spannfutter)
      L     30; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_P_Vorgabe; // Vorgabe Spanndruck (niedrigst möglicher Druck)
      L     41; 
      SPA   nw5b; 
nw5c: L     0; //Palette ohne Spannhydraulik
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; //(1=Vorrichtung; 2=Spannfutter)
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_P_Vorgabe; // Vorgabe Spanndruck (niedrigst möglicher Druck)

      L     41; //hydraulik pallet info received
      SPA   nw5b; 
nw5a: L     0; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_P_Vorgabe; 
      L     42; //hydraulik pallet info not accepted
nw5b: T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      S     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
enw5: NOP   0; 
NETWORK
TITLE =Rücksetzten des Read data result code/response
//Read data result code/response werden zurückgesetzt wenn Read data cmd von FMS 
//wieder auf Null gesetzt wird.
      UN    "DI_FB_IF_FASTEMS".FMS_MC_Read_data_command; 
      U(    ; 
      U     "DI_FB_IF_FASTEMS".m_read_program_selection; 
      O     "DI_FB_IF_FASTEMS".m_read_pallet_number; 
      O     "DI_FB_IF_FASTEMS".m_read_b_axis_angle; 
      O     "DI_FB_IF_FASTEMS".m_read_hydraulik_info; 
      )     ; 
      FP    "DI_FB_IF_FASTEMS".fp_response_reset; 
      SPBN  nw6a; 
      L     0; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_result; 
      R     "DI_FB_IF_FASTEMS".MC_FMS_Read_data_respons; 
      R     "DI_FB_IF_FASTEMS".m_read_program_selection; 
      R     "DI_FB_IF_FASTEMS".m_read_pallet_number; 
      R     "DI_FB_IF_FASTEMS".m_read_b_axis_angle; 
      R     "DI_FB_IF_FASTEMS".m_read_hydraulik_info; 
nw6a: NOP   0; 
NETWORK
TITLE =Ablöschen der palettendaten auf dem Rüstplatz
// immer wenn Eingang "I_PW_UPL_PAL_VORH" auf null und PW in Grundstellung 
//   --> pallet number und hydraulik info auf null schreiben 
      UN    "DI_FB_IF_FASTEMS".MC_FMS_plc_Pallet_on_APC; 
      UN    "M_PW_Aktiv"; 
      FP    "M_FP_Keine _Pal_vorhande"; 
      SPBN  n7a; 
      L     0; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_Element; 
      T     "DB_PV_SpH".SpH[1].Stelle[1].KONF_P_Vorgabe; 
      T     "DB_PAL_NR".Platz[1].Pal_Nr; 
n7a:  NOP   0; 
NETWORK
TITLE =H67
//
//
      SET   ; 
      =     "DB_H_FUNKTION".HX_QuitPflicht[67]; 

      U     "DB_H_FUNKTION".HX_M[67]; 
      SPBN  H67; 

      SET   ; 
      S     "DI_FB_IF_FASTEMS".m_save_H67; 

      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      RND   ; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Program_result_co; 

      U     "DB_H_FUNKTION".HX_M[67]; 
      =     "DB_H_FUNKTION".HX_Quitt[67]; 
H67:  NOP   0; 
NETWORK
TITLE =Prog End bei Wz Bruch mit Bruchüberwachung

      U     "Mx_Pal_Stoerung_WZBruch"; 
      FP    "M_FP_FMS_WZ_BRUCH"; 
      SPBN  enw9; 
      S     "DI_FB_IF_FASTEMS".m_save_H67; 
      L     2; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Program_result_co; 
      S     "DI_FB_IF_FASTEMS".MC_FMS_Program_end; 
enw9: NOP   0; 
NETWORK
TITLE =1-signal if Program end and H67 has been executed before. Stays 

      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      O     "DB_SIEM_KANAL_1".E_M01; // Wenn WZBruch erkannt mit Blum
      U     "DI_FB_IF_FASTEMS".m_save_H67; 
      S     "DI_FB_IF_FASTEMS".MC_FMS_Program_end; 

      U     "DI_FB_IF_FASTEMS".FMS_MC_cycle_start; 
      O     "M_NC_Start_Taste"; 
//   O     "DB_SIEM_KANAL_1".E_ProgRunn
      O(    ; 
      U     "M_Reset_Taste"; 
      UN    "DI_FB_IF_FASTEMS".FMS_MC_External_reset; 
      )     ; 
      FP    "M_FP_Reset_FMS_Prog_END"; 
      SPBN  enw8; 
      R     "DI_FB_IF_FASTEMS".m_save_H67; 
      R     "DI_FB_IF_FASTEMS".MC_FMS_Program_end; 
      L     0; 
      T     "DI_FB_IF_FASTEMS".MC_FMS_Program_result_co; 
enw8: NOP   0; 
NETWORK
TITLE =Übergabeplatz gesperrt

      U     "DI_FB_IF_FASTEMS".FMS_MC_APC_movement_ok; 
      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     4.000000e+001; 
      ==R   ; 
      )     ; 
      S     "Mx_APC_Disable_H67"; 
      U(    ; 
      O(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[67]; 
      L     4.100000e+001; 
      ==R   ; 
      )     ; 
      O     "M_Reset_Taste"; 
      O     "DB_SIEM_KANAL_1".E_ChanReset; 
      )     ; 
      R     "Mx_APC_Disable_H67"; 
      NOP   0; 
NETWORK
TITLE =701524 "MEL, Übergabeplatz gesperrt"

      U     "Mx_APC_Disable_H67"; 
      =     "DB_FEHLERMELDUNG".SAFE_MELD._701524; 
NETWORK
TITLE =Palettennummer in der Maschine muss immer an NC übergeben werden
//Palettennummer für Nullpunktverschiebung 
//Gibt nur 50 Nullpunktverschiebungen
//wird im WRITEPD auf 50 begrenzt.
      L     255; 
      L     "DB_PAL_NR".Platz[0].Pal_Nr; 
      <I    ; 
      SPB   nw11; 
      SPA   n11e; 
nw11: L     0; 
n11e: T     "DB_NC_PLC".PW_Pal_Nummer; 
NETWORK
TITLE =Heartbeat acknowledge response the Heartbeat request from FMS 

      U     "DI_FB_IF_FASTEMS".FMS_MC_Heartbeat_rquest; 
      =     "DI_FB_IF_FASTEMS".MC_FMS_Heartbeat_acknow; 
NETWORK
TITLE =Beginn RPC 
//     Anwahl RPC & V3.x
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      SPBN  krpc; 
NETWORK
TITLE =Anforderung WZ melden 
//Umspeichern der Anforderung in die RPC Schnittstelle
      U     "m_eins"; 
      R     #O_Ready_WZ_melden; 
NETWORK
TITLE =Werkzeug melden an RPC

      U     #I_req_wz_melden; 
      FP    "Mx_FP_wz_melden"; 
      SPBN  n1e; 
NETWORK
TITLE =Anforderung WZ melden 
//Umspeichern der Anforderung in die RPC Schnittstelle
      L     #IW_Mag_Nummer; 
      T     "DB_SINRPC".PLC_TEL.WZ_Mag_Nr; 

      L     #IW_Platznummer; 
      T     "DB_SINRPC".PLC_TEL.WZ_Platz_Nr; 

      L     #IW_T_Nummer; 
      T     "DB_SINRPC".PLC_TEL.WZ_TNr; 

      S     "DB_SINRPC".PLC_TEL.Werkzeug; 
n1e:  NOP   0; 
NETWORK
TITLE =Auswertung SinCOM-Modus und Anforderung SinCOM

      SET   ; 
      R     "DB_SINRPC".Mode.FLR_unbemannt; 
      R     "DB_SINRPC".Mode.FLR_bemannt; 
      R     "DB_SINRPC".Mode.FLR_manuell; 
      R     "DB_SINRPC".Mode.FLR_Sonder; 
      R     "DB_SINRPC".Mode.FLR_1_offline; 
      R     "DB_SINRPC".Mode.FLR_2_offline; 
      R     "DB_SINRPC".Mode.Synch_aktiv; 
      R     "DB_SINRPC".Mode.M_Aus; 
      R     "DB_SINRPC".Mode.WPC_W_enable; 

// Auswertung SinCOM-Modus

      L     "DB_SINRPC".MODE_SinCOM; 
      SRW   1; 
      SPZ   NB_1; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_unbemannt; 

NB_1: SRW   1; 
      SPZ   NB_2; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_bemannt; 

NB_2: SRW   1; 
      SPZ   NB_3; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_manuell; 

NB_3: SRW   1; 
      SPZ   NB_4; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_Sonder; 

NB_4: SRW   1; 
      SPZ   NB_5; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_1_offline; 

NB_5: SRW   1; 
      SPZ   NB5b; 

      SET   ; 
      S     "DB_SINRPC".Mode.FLR_2_offline; 

NB5b: NOP   0; 

// Anforderung SinCOM

      L     "DB_SINRPC".SCReq; 
      SRW   1; 
      SPZ   NB_6; 

      SET   ; 
      S     "DB_SINRPC".Mode.Synch_aktiv; 

NB_6: SRW   1; 
      SPZ   NB_7; 

      SET   ; 
      S     "DB_SINRPC".Mode.M_Aus; 

NB_7: SRW   2; 
      SPZ   NB_8; 

      SET   ; 
      S     "DB_SINRPC".Mode.WPC_W_enable; 

NB_8: NOP   0; 
NETWORK
TITLE =SINRPC: Maschine min einmal ein

      U     "M_MA_Maschine_Ein"; 
      S     "Mx_Maschine_min_1mal_ein"; 
NETWORK
TITLE =Zustandsänderung
//      Auswertung Maschinenzustand
      L     0; 

      U     "DB_SIEM_KANAL_1".E_ProgRunn; // Programm läuft
      O     "DB_SIEM_KANAL_1".E_ProgStop; 
      UN    "DI_FB_IF_FASTEMS".MC_FMS_plc_Alarm; // Störung
      U     "Mx_Maschine_min_1mal_ein"; //"M__26.1"  // Erster Paketdurchlauf *
      SPBN  ZUS1; 

      L     1; 
      SPA   TZUS; 

ZUS1: U     "DI_FB_IF_FASTEMS".MC_FMS_plc_Alarm; // "A_Stoerung"  // Störung
      U     "Mx_Maschine_min_1mal_ein"; //"M__26.1"  // Erster Paketdurchlauf *
      SPBN  ZUS2; 

      L     2; 
      SPA   TZUS; 

ZUS2: UN    "Mx_Maschine_min_1mal_ein"; // m_7ter_ob1zyklus              // "M__26.1"  // Erster Paketdurchlauf *
      SPBN  TZUS; 

      L     4; 
      SPA   TZUS; 

TZUS: T     #ZS_Zustand; 

// Auswertung NC-Betriebsarten

      L     0; 

      U     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO        // AUTOMATIK
      SPBN  BA_1; 

      L     1; 
      SPA   T_BA; 

BA_1: UN    "DB_SIEM_BAG".E_TEACHIN; // TEACH IN
      U     "MX_MDA"; //"DB_SIEM_BAG".E_MDA         // MDA
      SPBN  BA_2; 

      L     2; 
      SPA   T_BA; 

BA_2: U     "M_Handbetrieb"; //"DB_SIEM_BAG".E_JOG         // JOG
      SPBN  BA_3; 

      L     4; 
      SPA   T_BA; 


BA_3: U     "MX_MDA"; //"DB_SIEM_BAG".E_MDA         // TEACH IN
      SPBN  T_BA; 

      L     8; 
      SPA   T_BA; 


T_BA: T     #ZS_BA; 

// Auswertung Maschinenmodus

      L     0; 

      UN    "M_Leistung_Steht_an"; // Antriebe eingeschaltet
      SPBN  TMOD; 

      L     1; 
      SPA   TMOD; 

TMOD: T     #ZS_PLCMode; 

// Start Telegramm

      L     "DB_SINRPC".MODE_SinCOM; 
      L     "DB_SINRPC".PLC_TEL.MODE_SinCOM; 
      <>I   ; 
      O(    ; 
      L     #ZS_Zustand; 
      L     "DB_SINRPC".PLC_TEL.MachineStatus; 
      <>I   ; 
      )     ; 
      O(    ; 
      L     #ZS_BA; 
      L     "DB_SINRPC".PLC_TEL.MachineMode; 
      <>I   ; 
      )     ; 
      O(    ; 
      L     #ZS_PLCMode; 
      L     "DB_SINRPC".PLC_TEL.MODE_PLC; 
      <>I   ; 
      )     ; 
      S     "DB_SINRPC".PLC_TEL.Zustand; 
      SPBN  EZUS; 

      L     "DB_SINRPC".MODE_SinCOM; 
      T     "DB_SINRPC".PLC_TEL.MODE_SinCOM; 

      L     #ZS_Zustand; 
      T     "DB_SINRPC".PLC_TEL.MachineStatus; 

      L     #ZS_BA; 
      T     "DB_SINRPC".PLC_TEL.MachineMode; 

      L     #ZS_PLCMode; 
      T     "DB_SINRPC".PLC_TEL.MODE_PLC; 

EZUS: NOP   0; 

NETWORK
TITLE =Rücksetzen Telegramm bei Offline

      U     "DB_SINRPC".Mode.FLR_1_offline; 
      U     "DB_SINRPC".Mode.FLR_2_offline; 
      R     "DB_SINRPC".PLC_TEL.WPC; 
      R     "DB_SINRPC".PLC_TEL.Werkzeug; 
      R     "DB_SINRPC".PLC_TEL.Zustand; 
      SPBN  ERES; 

      L     0; 
      T     "DB_SINRPC".MagNum; 
      T     "DB_SINRPC".PlaceNum; 
      T     "DB_SINRPC".PLCReq; 

ERES: NOP   0; 
NETWORK
TITLE =Freigabe Telegramm an Host

      U(    ; 
      L     "DB_SINRPC".PLCReq; 
      L     0; 
      ==I   ; 
      )     ; 
      U(    ; 
      ON    "DB_SINRPC".Mode.FLR_1_offline; 
      ON    "DB_SINRPC".Mode.FLR_2_offline; 
      )     ; 
      =     #FrgMeldHost; 
NETWORK
TITLE =Telegramm Werkzeug melden

      U     #FrgMeldHost; 
      U     "DB_SINRPC".PLC_TEL.Werkzeug; 
      R     "DB_SINRPC".PLC_TEL.Werkzeug; 
      SPBN  TE1a; 

      L     "DB_SINRPC".PLC_TEL.WZ_Mag_Nr; 
      T     "DB_SINRPC".MagNum; 

      L     "DB_SINRPC".PLC_TEL.WZ_Platz_Nr; 
      T     "DB_SINRPC".PlaceNum; 

      L     "DB_SINRPC".PLC_TEL.WZ_TNr; 
      T     "DB_SINRPC".TNum; 

      U     "DI_FB_IF_FASTEMS".req_tool_unload; 
      SPBN  n23a; 
      L     22; 
      SPA   n23b; 
n23a: L     21; 
n23b: T     "DB_SINRPC".DataType; // 21 alle Daten, 22, 23

      L     2; 
      T     "DB_SINRPC".PLCReq; 
      S     #O_Ready_WZ_melden; 
      S     "M_READY_WZ_MELDEN"; 
      SPA   TELO; 
NETWORK
TITLE =Telegramm Zustandsänderung

TE1a: U     #FrgMeldHost; 
      U     "DB_SINRPC".PLC_TEL.Zustand; 
      R     "DB_SINRPC".PLC_TEL.Zustand; 
      SPBN  EANH; 

      L     "DB_SINRPC".PLC_TEL.MachineMode; 
      T     "DB_SINRPC".MachineMode; 

      L     "DB_SINRPC".PLC_TEL.MachineStatus; 
      T     "DB_SINRPC".MachineStatus; 

      L     "DB_SINRPC".PLC_TEL.MODE_PLC; 
      T     "DB_SINRPC".MODE_PLC; 

      L     4; 
      T     "DB_SINRPC".PLCReq; 

      SPA   TELO; 

NETWORK
TITLE =Telegramm Ausgabe

TELO: U(    ; 
      L     0; 
      L     "DB_SINRPC".Trigger; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     128; 
      >=I   ; 
      )     ; 
      SPBN  ESTR; 

      L     1; 
      SPA   TRIG; 

ESTR: TAK   ; 
      SLW   1; 

TRIG: T     "DB_SINRPC".Trigger; 

EANH: NOP   0; 

NETWORK
TITLE =Aufruf von Baustein für Fastems Remote Tool Handling


      CALL "FB_FMS_TOOL_HANDLING" , "DI_FMS_TOOL_HANDLING" ;
krpc: NOP   0; 
NETWORK
TITLE = SinCOM-Modus Fertigungsleitrechner 1 offline

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      SPB   nw28; 

      S     "DB_SINRPC".Mode.FLR_1_offline; 
      S     "DB_SINRPC".Mode.FLR_2_offline; 

nw28: NOP   0; 
NETWORK
TITLE =700035 "NOT-AUS, "

      U     "M_Reset_Taste"; 
      R     "DB_FEHLERMELDUNG".NotAus._700035_Ext_NOT_AUS_HASY; 

      U     "I_FMS_AUTO_POWER_OFF"; 
      S     "DB_FEHLERMELDUNG".NotAus._700035_Ext_NOT_AUS_HASY; 
END_FUNCTION

