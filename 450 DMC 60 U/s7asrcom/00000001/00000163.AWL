FUNCTION_BLOCK "FB_FMS_Tool_handling"
TITLE =
//Program to create or delete tools
//Command is given from FMS-Control using RPC-Call
//
//Create command: Tool with defined number of additional edges is created.
//
//Delete command: Tool with all edges is deleted
//
//When command is executed, command response is sended to FMS-Control with 
//RPC-Call.
//
//Hot-link for sending is defined in SCVARSET.INI
AUTHOR : ER
NAME : ToolCrea
VERSION : 0.1


VAR
  TNR : INT ;	//Internal T-number, which is created
  EdgeCount : INT ;	//Number of tool edges
  Unit : BYTE ;	//Tool offset area or Channel number
  Command : BYTE ;	//Creation=1/Deletion=2
  CommandResp : BYTE ;	//Command response: 1-creation OK, 2-deletion OK, 11- errors
  Cnt : BYTE ;	//Counter for creation of additional edges
  TNr_PI : WORD ;	//Internal T-number for PI-service
  Unit_PI : INT ;	//Unit for PI-service
  CreateT : BOOL ;	
  DeleteT : BOOL ;	
  CreateEdge : BOOL ;	
  CreateEdgeReq : BOOL ;	//Request to create 2nd.. additional edge
  Err1 : BOOL ;	//PI-service error
  Err2 : BOOL ;	
  Err3 : BOOL ;	
  Done1 : BOOL ;	//PI-service executed without errors
  Done2 : BOOL ;	
  Done3 : BOOL ;	
  FB_SIEM_PI_CREATO : "FB_SIEM_PI";	
  FB_SIEM_PI_DELETO : "FB_SIEM_PI";	
  FB_SIEM_PI_CREACE : "FB_SIEM_PI";	
END_VAR
VAR_TEMP
  State1 : WORD ;	//Error code from FB4 call
  State2 : WORD ;	
  State3 : WORD ;	
END_VAR
BEGIN
NETWORK
TITLE =CREATO/DELETO
//Create 4 instance-DB for FB4 and 1 instance-DB for FB198
//If PI-services CREATO,DELETO or CREACE are used in other places of PLC-program, 
//modification may be required.
      UN    "M_1_ter_OB1_Zyklus"; 
      UN    "DB_SIEM_KANAL_1".A_Reset; 
      SPB   A1; 
      L     0; 
      T     #Command; 
      T     #CommandResp; 

A1:   L     #Command; 
      SPL   CMDX; 
      SPA   CMD0; 
      SPA   CMD1; 
      SPA   CMD2; 
      SPA   CMDX; 
// illegal command
CMDX: L     99; 
      T     #CommandResp; 
      L     0; 
      T     #Command; 
      SPA   CMD0; 
// create tool
CMD1: SET   ; 
      S     #CreateT; 
      L     #EdgeCount; 
      L     -1; 
      +I    ; 
      SPM   CMDX; 
      T     #Cnt; 
      SPA   A3; 
// delete tool
CMD2: SET   ; 
      S     #DeleteT; 
      SPA   A3; 
A3:   L     #TNR; 
      T     #TNr_PI; 
      L     #Unit; 
      T     #Unit_PI; 
      L     0; 
      T     #CommandResp; 
      T     #Command; 
      T     #Unit; 
      T     #EdgeCount; 
      T     #TNR; 
// ----------------
// PI-SERVICE - create tool
// ----------------
CMD0: CALL #FB_SIEM_PI_CREATO (
           Req                      := #CreateT,
           PIService                := "DB_SIEM_PI".CREATO,
           Unit                     := #Unit_PI,
           WVar1                    := #TNr_PI,
           Error                    := #Err1,
           Done                     := #Done1,
           State                    := #State1);


      UN    #Done1; 
      SPB   B1; 
      R     #CreateT; 
      L     0; 
      L     #Cnt; 
      ==I   ; 
      SPB   B2; 
// Create additional edges
      S     #CreateEdge; 
      +     -1; 
      T     #Cnt; 
      SPA   B1; 
// Creation OK
B2:   L     1; 
      T     #CommandResp; 
B1:   UN    #Err1; 
      SPB   B3; 
// Error on tool creation
      R     #CreateT; 
      L     #State1; 
      L     10; 
      +I    ; 
      T     #CommandResp; 
// ----------------
//  PI-SERVICE - delete tool
// ----------------
B3:   CALL #FB_SIEM_PI_DELETO (
           Req                      := #DeleteT,
           PIService                := "DB_SIEM_PI".DELETO,
           Unit                     := #Unit_PI,
           WVar1                    := #TNr_PI,
           Error                    := #Err2,
           Done                     := #Done2,
           State                    := #State2);

      UN    #Done2; 
      SPB   C1; 
// Deletion OK
      R     #DeleteT; 
      L     2; 
      T     #CommandResp; 
      SPA   C2; 
C1:   UN    #Err2; 
      SPB   C2; 
// Error on tool deletion
      R     #DeleteT; 
      L     #State2; 
      L     30; 
      +I    ; 
      T     #CommandResp; 
// ----------------
//  PI-SERVICE - create additional edges
// ----------------
C2:   CALL #FB_SIEM_PI_CREACE (
           Req                      := #CreateEdge,
           PIService                := "DB_SIEM_PI".CREACE,
           Unit                     := #Unit_PI,
           WVar1                    := #TNr_PI,
           Error                    := #Err3,
           Done                     := #Done3,
           State                    := #State3);
      UN    #Done3; 
      SPB   D1; 
      R     #CreateEdge; 
      L     0; 
      L     #Cnt; 
      ==I   ; 
      SPB   D2; 
// Create more additional edges
      S     #CreateEdgeReq; 
      +     -1; 
      T     #Cnt; 
      SPA   D1; 
// Creation OK
D2:   L     1; 
      T     #CommandResp; 
D1:   UN    #Err3; 
      SPB   D3; 
// Error on additional edge creation
      R     #CreateEdge; 
      L     #State3; 
      L     20; 
      +I    ; 
      T     #CommandResp; 
// next edge creation is started, when Done3=FALSE
D3:   UN    #Err3; 
      UN    #Done3; 
      U     #CreateEdgeReq; 
      UN    #CreateEdge; 
      S     #CreateEdge; 
      R     #CreateEdgeReq; 


END_FUNCTION_BLOCK

