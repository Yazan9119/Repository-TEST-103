FUNCTION "FC_MASCHTYP_MCP" : VOID
TITLE =
//$Revision: 1.53 $
//$Date: 2008/10/10 11:07:34CEST $
//$Author: hgc $
//
//27.03.04
//sja --> wegen MKS aus MSST/Achskreuz Baustein (FC112) rasugenommen, da dieser 
//allgemein verwaltet wird
//--> hier Maschinetypabhängige Tastenbehnadlung Reset Taste/Override/Achsanwahl 
//etc...
AUTHOR : sja
VERSION : 0.1


VAR_IN_OUT
  EAX_MSST_NC_Start : BOOL ;	
  EAX_MSST_NC_Stop : BOOL ;	
  EAX_MSST_Feed_Start : BOOL ;	
  EAX_MSST_Reset : BOOL ;	
  EAX_MSST_SP_Overr_100 : BOOL ;	
  EAX_MSST_WZAX_Rett3070 : BOOL ;	//Taste Werkzeugachse Rettung für Maschinentyp 3070
  EAX_MSST_WZAX_Rett3071 : BOOL ;	//Taste Werkzeugachse Rettung für Maschinentyp 3071
  EAB_MSST_Eilgang_Overr : BYTE ;	
  EAB_MSST_Vorschub_Overr : BYTE ;	
END_VAR
VAR_TEMP
  ovr_temp2 : WORD ;	
  IM_Achsanwahl : BOOL ;	
  dummy : BOOL ;	
  t_IX_MSST_Ta_3_7 : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =MSST: Taste NC-Start

      U(    ; 
      U     "M_NC_Start_Prg_Anwahl"; // NC-Start Palverwaltung
      U     "MX_Jobbetrieb"; 
      O     ; 
      U     "DI_FB_IF_FASTEMS".FMS_MC_cycle_start; // NC-Start Fastems 
      U     "Mx_FMS_OM1_DISABLED"; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASREMS_V3X; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      )     ; 
      O     ; 
      U     #EAX_MSST_NC_Start; 
      U(    ; 
      O     "M_Einrichtbetrieb"; 
      O     "M_3Te_Betriebsart"; 
      O     "M_4Te_Betriebsart"; 
      O     "I_AL_Kabine_Verriegelt"; 
      )     ; 
      O     ; 
      U     "M_NC_Start_Warmlpr"; 
      U(    ; 
      ON    #EAX_MSST_NC_Start; 
      ON    "DB_SIEM_BAG".E_REF; 
      ON    "DB_SIEM_KANAL_1".E_AxesRef; 
      ON    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      )     ; 
      )     ; 
      U(    ; 
      O     "TE_ENABLE_OUT_3_NX_NCU"; 
      ON    "DB_PLC_MD_DB20".UDHex._31_Bit1_SOLUTIONLINE; 
      )     ; 
      =     #EAX_MSST_NC_Start; 
NETWORK
TITLE =MSST: Taste Vorschubfreigabe bei Maschine Ein setzen

      U     "M_MA_FP_Maschine_Ein"; 
      S     #EAX_MSST_Feed_Start; 
NETWORK
TITLE =MSST: Taste Spindel Stop bei ShopMill

      U     "DB_CMM_PLC".CMM_IN.spindle_stop; 
      R     #EAX_MSST_NC_Stop; 
NETWORK
TITLE =MSST: Taste Eilgang verriegeln 

      U     "M_Eilgang_verriegeln"; 
      R     "I_DM_Ta Eilgang"; 
NETWORK
TITLE =Verriegelung der Taste Reset bei Initialisierung der ASUP`s
//// Reset Taste nicht betätigen bei Initialiesierungs-ASUPS
//// 07.12.00 AB
//
      U     "DB_FEHLERMELDUNG".Meldung._702031_INITIALISIEREN; 
      L     S5T#4S; 
      SI    "Ti_Init_Maschine"; 
NETWORK
TITLE =MSST: Taste Reset verlängert
//// ************  RESET  E3.7 beschreiben 
//**************************************************************
      U     "Ti_Init_Maschine"; 
      R     #EAX_MSST_Reset; 
      U(    ; // Reset-Taste auslösen bei Maschine Ein und Aus
      O     "Tv_Maschine_ein_aus"; // Reset-Taste verlängern auf min. 500ms
      O     "Tv_Reset_Impuls"; 
      )     ; 
      S     #EAX_MSST_Reset; 
      NOP   0; 
NETWORK
TITLE =Reset für Werkzeugverwaltung
////--------------------------------------------------------------
//// Meldung bei WZW bei Reset unterdrücken 
//// zuerst WZV neg. quittieren dann NC Reset
//
      U     #EAX_MSST_Reset; 
      =     "M_RESET_WZV"; 
NETWORK
TITLE =Taste Reset unterdrücken

      O     "Tv_ResetTaste_WZV_verr"; 
      O     "Tv_ResetTaste_WZV_verl"; 
      R     #EAX_MSST_Reset; 
NETWORK
TITLE =Resettaste verzögern bei aktivem WZV Auftrag

      O(    ; 
      O     "TMSpindleIF".IF[1].Perform; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_RAD_MIT_ACHSE; 
      )     ; 
      O     "TMSpindleIF".IF[1].Prepare; 
      U     #EAX_MSST_Reset; 
      L     S5T#300MS; 
      SV    "Tv_ResetTaste_WZV_verr"; 

NETWORK
TITLE =Resettaste verlängern nach aktivem WZV Auftrag

      UN    "Tv_ResetTaste_WZV_verr"; 
      L     S5T#300MS; 
      SV    "Tv_ResetTaste_WZV_verl"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
NETWORK
TITLE =Taste Reset unterdrücken bei aktivem WZV Auftrag

      U     "Tv_ResetTaste_WZV_verr"; 
      R     #EAX_MSST_Reset; 
      =     "M_RESET_DISABLE"; 
NETWORK
TITLE =Reset bei WZ-Bruch

      U(    ; 
      O     "MX_Pal_auf_Stoerung"; 
      O     "Mx_Pal_Stoerung_WZBruch"; 
      )     ; 
      U     "DB_PLC_MD_DB20".UDHex._09_BIT7_P_STOER_WECHSEL; 
      UN    "M_WZW_Aktiv"; 
      U(    ; 
      O     "MX_Jobbetrieb"; 
      O     ; 
      U     "DB_SINRPC".Mode.FLR_unbemannt; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      )     ; 
      =     "M_RESET_DURCH_RS_HS"; 
NETWORK
TITLE =MSST: Taste Reset

      O     "Tv_ResetTaste_WZV_verl"; // Reset Taste
      O     "MX_BHG_Reset"; // Reset auslösen von MPI BHG
      O     "m_WZWAsupAbbrechen"; // Reset auslösen bei Abbruch Asup`s durch loslassen der Rettungs-Taste
      O     "M_WIKO_Asup_abbrechen"; 
      O     "M_PW_Asup_abbrechen"; 
      O     "M_RESET_DURCH_RS_HS"; 
      S     #EAX_MSST_Reset; 
NETWORK
TITLE =Rücksetzen der ASUP Resetmerker
//**************************************************************
      U     "m_eins"; 
      R     "MX_BHG_Reset"; 
      R     "m_WZWAsupAbbrechen"; 
      R     "M_WIKO_Asup_abbrechen"; 
      R     "M_PW_Asup_abbrechen"; 
NETWORK
TITLE =M147 Override auf 100% 

      U     "DB_DM_M_FUNKTION".MX_IM[147]; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT0_OVR_HILFSACHSEN; 
      S     "MX_Override100"; 
      R     "DB_DM_M_FUNKTION".MX_M[148]; 
NETWORK
TITLE =M148 Abwahl Override auf 100% 

      O     "DB_DM_M_FUNKTION".MX_IM[148]; 
      O     "M_Reset_Taste"; 
      O     "DB_SIEM_KANAL_1".E_ChanReset; 
      R     "MX_Override100"; 
      R     "DB_DM_M_FUNKTION".MX_M[147]; 
      R     "DB_H_FUNKTION".HX_M[47]; 
      R     "Mx_H47_angefordert"; 
NETWORK
TITLE =Eilgangoverride
////--------------------------------------------------------------
//// Eilgangoverride  in BA JOG  30.04.02 AB
//// in JOG wird der override für Eilgang nur aus dem Achs-DB verwendet
//// --> Poti < 1% wird auf 1% gestellt
////-Vorschub auf 100 % 13.05.02 AB ------
      CALL "FC_TEMP_FUNKTIONEN" (
           int_funktion             := 1,
           int_mp                   := "DB_PLC_MD_DB20".UDInt._055_SONDER_2,
           int_dekade               := 2,
           int_wert                 := 1,
           boo_ergebnis             := "DB_ANWENDER_SOFTKEY".Eiloverride_Vorwahl.Angewaehlt);


      U     "DB_ANWENDER_SOFTKEY".Eiloverride_Vorwahl.Angewaehlt; 
      UN    "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      SPBN  vsen; 

//--- Eilgang auf 10 %-----------------
      U     "DB_ANWENDER_SOFTKEY".Eiloverride_Vorwahl.Zehn_Protzent; 
      SPBN  vsfu; 
      L     #EAB_MSST_Eilgang_Overr; 
      SRW   5; 
      SLW   5; 
      L     W#16#B; 
      OW    ; 
      T     #EAB_MSST_Eilgang_Overr; 
      SPA   vsen; 

//--- Eilgang auf 50 %-----------------
vsfu: U     "DB_ANWENDER_SOFTKEY".Eiloverride_Vorwahl.Fuenfzig_Protzent; 
      SPBN  vshu; 
      L     #EAB_MSST_Eilgang_Overr; 
      SRW   5; 
      SLW   5; 
      L     W#16#19; 
      OW    ; 
      T     #EAB_MSST_Eilgang_Overr; 
      SPA   vsen; 

//--- Eilgang auf 100 %-----------------
vshu: U     "DB_ANWENDER_SOFTKEY".Eiloverride_Vorwahl.Hundert_Protzent; 
      SPBN  vsen; 
      L     #EAB_MSST_Eilgang_Overr; 
      SRW   5; 
      SLW   5; 
      L     W#16#1C; 
      OW    ; 
      T     #EAB_MSST_Eilgang_Overr; 

vsen: NOP   0; 

NETWORK
TITLE =H47 Override Beeinflussung
////--------------------------------------------------------------
//// Über H-Funktion Poti mittels H-Funktion: H147=1 --> nur Vorschub auf 100%
////                                               2 --> nur Spindel --> auf 100%
////                                               3 --> Vorschub und Spindel --> 
//auf 100%
////                                               M148 Abwahl --> Potis gelten 
//wieder
//
      U     "DB_H_FUNKTION".HX_M[47]; 
      FP    "Mx_Imp_H47"; 
      S     "Mx_H47_angefordert"; 

      U     "Mx_H47_angefordert"; 
      SPBN  a01; 

      L     "DB_H_FUNKTION".HX_Real_Wert[47]; 
      L     1.000000e+000; 
      ==R   ; 
      SPBN  a02; 
      L     #EAB_MSST_Vorschub_Overr; 
      L     1; 
      >I    ; 
      SPBN  a02; 
      L     W#16#15; 
      OW    ; 
      T     #EAB_MSST_Vorschub_Overr; 

a02:  L     "DB_H_FUNKTION".HX_Real_Wert[47]; 
      L     2.000000e+000; 
      ==R   ; 
      SPBN  a03; 
      S     #EAX_MSST_SP_Overr_100; // "I_DM_Ta_Spindel_over100"

a03:  L     "DB_H_FUNKTION".HX_Real_Wert[47]; 
      L     3.000000e+000; 
      ==R   ; 
      SPBN  a01; 
      S     #EAX_MSST_SP_Overr_100; // "I_DM_Ta_Spindel_over100"
      L     #EAB_MSST_Vorschub_Overr; 
      L     1; 
      >I    ; 
      SPBN  a01; 
      L     W#16#15; 
      OW    ; 
      T     #EAB_MSST_Vorschub_Overr; 


a01:  U     "Mx_H47_angefordert"; 
      R     "DB_H_FUNKTION".HX_M[47]; 
NETWORK
TITLE =Vorschub auf 100 % / Eilgang auf 100 %
//--------------------------------------------------------------
      U     "MX_Override100"; 
      SPBN  vspo; 

//--- Vorschub auf 100 %-----------------

      L     "MSST_Vorschub_OVR"; 
      L     1; 
      >I    ; 
      SPBN  vsnu; 
      L     "MSST_Vorschub_OVR"; 
      SRW   5; 
      SLW   5; 
      L     W#16#15; 
      OW    ; 
      T     "MSST_Vorschub_OVR"; 

//--- Eilgang auf 100 %-----------------

vsnu: L     #EAB_MSST_Eilgang_Overr; 
      L     1; 
      >I    ; 
      SPBN  vspo; 
      L     #EAB_MSST_Eilgang_Overr; 
      SRW   5; 
      SLW   5; 
      L     W#16#1C; 
      OW    ; 
      T     #EAB_MSST_Eilgang_Overr; 

vspo: NOP   0; 
NETWORK
TITLE =Vorschuboverride
//// Bei Positionierachsen wirkt der Vorschuboverride im Achs-DB
//// Deshalb muss bei Eilgang der Eilgangoveride in den Achs-DB geschrieben werden
//
      U     "I_DM_Ta Eilgang"; 
      U     "M_Handbetrieb"; 
      UN    "DB_PLC_MD_DB20".UDHex._14_BIT7_BP_SIEMENS_B; 
      SPBN  M001; 

      L     #EAB_MSST_Eilgang_Overr; 
      L     W#16#1F; 
      UW    ; 
      SPZ   EDEC; 
      L     1; 
      -D    ; 
EDEC: L     2; 
      *D    ; 
      SLW   3; 
      LAR1  ; 
      AUF   "DB_OVR_WANDLER"; 
      L     DBW [AR1,P#80.0]; 
      T     #ovr_temp2; 
      L     #EAB_MSST_Vorschub_Overr; 
      SRW   5; 
      SLW   5; 
      L     #ovr_temp2; 
      OW    ; 
      T     #EAB_MSST_Vorschub_Overr; 
M001: NOP   0; 
      U(    ; 
      L     0; 
      L     #EAB_MSST_Vorschub_Overr; 
      L     W#16#1F; 
      UW    ; 
      L     19; 
      ==I   ; 
      O(    ; 
      TAK   ; 
      L     18; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     22; 
      ==I   ; 
      )     ; 
      O(    ; 
      TAK   ; 
      L     23; 
      ==I   ; 
      )     ; 
      )     ; 
      =     #dummy; 

      L     #EAB_MSST_Vorschub_Overr; 
      L     W#16#FFE0; 
      UW    ; 
      L     21; 
      O     "M_Einrichtbetrieb"; 
      O     "M_3Te_Betriebsart"; 
      O     "M_4Te_Betriebsart"; 
      U     #dummy; 
      SPBN  M002; 

      OW    ; 
      T     #EAB_MSST_Vorschub_Overr; 

M002: U     "MX_MsSt_Vorschub_0"; 
      SPBN  M003; 

      L     #EAB_MSST_Vorschub_Overr; 
      L     W#16#FFE0; 
      UW    ; 
      T     #EAB_MSST_Vorschub_Overr; 

M003: NOP   0; 

NETWORK
TITLE = Bei Bremsentest den Override auf 100 % stellen

      U     "DB_SAFE_ZD".GR1_BT.Override_auf_100; 
      U     "DB_PLC_MD_DB20".UDHex._04_BIT7_SAFETY; 
      SPBN  M007; 
      L     W#16#1A; 
      T     #EAB_MSST_Vorschub_Overr; 
M007: NOP   0; 
NETWORK
TITLE =In BA2 Spindel nur drehen wenn auch Taste gedrückt

      U     "DB_PLC_MD_DB20".UDHex._16_BIT7_SPI_STOP_BA2; 
      U     "M_Einrichtbetrieb"; 
      SPBN  x01; 
      U     "I_DM_Ta Spindel rechts"; 
      O     "I_DM_Ta_Spindel links"; 
      FN    "M_FN_SPI_re.li."; 
      R     "I_DM_Ta_Spindel HALT"; 
x01:  NOP   0; 
NETWORK
TITLE =Sicherer Rückzug (mit L_Frei)  
//Freifahren ohne Referenzpunkt bei 5.Achs-Maschine
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      U     "DB_PLC_MD_DB20".UDHex._05_BIT7_Freif_o_RFP; 
      SPBN  NW3; 

      U     #EAX_MSST_WZAX_Rett3070; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      FN    "Mx_FN_Taste"; 
      =     "Mx_FN_Taste_WZ_Achse_RET"; 

      U     #EAX_MSST_WZAX_Rett3070; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      FP    "Mx_FP_aus_Loch"; 
      =     "Mx_FP_Taste_WZ_Achse_RET"; 
      SPBN  n20z; 


      U     "M_Leistung_Steht_an"; 
      U     "DB_SIEM_KANAL_1".E_AxesRef; 
      U     "DB_ANWENDER_SOFTKEY".Freifahren_ohne_RFP_akt; 

      S     "Start_ASUP_Machine_Nr.1"; 
      L     99; 
      T     "DB_NC_PLC".Rettung_FktNr; 

n20z: NOP   0; 

      U     "Mx_FN_Taste_WZ_Achse_RET"; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      SPBN  n20y; 

      SET   ; 
      =     #EAX_MSST_Reset; 

// Umschaltung Messsystem bei MAschine Aus
n20y: UN    "DB_ANWENDER_SOFTKEY".Freifahren_ohne_RFP_akt; 
      UN    "M_Leistung_Steht_an"; 
      UN    "M_MA_Maschine_Ein"; 
      SPBN  n20w; 

      UN    "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 
      U     "M_4Ax_Messystem_direkt"; 
      S     "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 

      UN    "DB_PLC_MD_DB20".UDHex._02_BIT4_ACHSE5_MESSYST; 
      U     "M_5Ax_Messystem_direkt"; 
      S     "DB_PLC_MD_DB20".UDHex._02_BIT4_ACHSE5_MESSYST; 
      SPB   n20x; 

n20w: U     "DB_ANWENDER_SOFTKEY".Freifahren_ohne_RFP_akt; 
      UN    "M_Leistung_Steht_an"; 
      UN    "M_MA_Maschine_Ein"; 
      UN    "DB_SIEM_KANAL_1".E_AxesRef; 
      R     "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 
      R     "DB_PLC_MD_DB20".UDHex._02_BIT4_ACHSE5_MESSYST; 


n20x: UN    "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 
      U     "M_4Ax_Messystem_direkt"; 
      U     "M_4Achse_existiert"; 
      O     ; 
      UN    "DB_PLC_MD_DB20".UDHex._02_BIT4_ACHSE5_MESSYST; 
      U     "M_5Ax_Messystem_direkt"; 
      U     "M_5Achse_existiert"; 
      =     "DB_FEHLERMELDUNG".MELDUNG3._700518_Motor_Messsytem; 

NETWORK
TITLE =Sicherer Rückzug (mit DM_RUECKZUG)  
//Freifahren ohne Referenzpunkt bei 4.Achs-Maschine
NW3:  U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      U     "DB_PLC_MD_DB20".UDHex._05_BIT7_Freif_o_RFP; 
      SPBN  E_SR; 

      U     #EAX_MSST_WZAX_Rett3071; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      FN    "Mx_FN_Taste"; 
      =     "Mx_FN_Taste_WZ_Achse_RET"; 

      U     #EAX_MSST_WZAX_Rett3071; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      FP    "Mx_FP_aus_Loch"; 
      =     "Mx_FP_Taste_WZ_Achse_RET"; 
      SPBN  n30z; 


      U     "M_Leistung_Steht_an"; 
      S     "Start_ASUP_Machine_Nr.1"; 
      L     98; 
      T     "DB_NC_PLC".Rettung_FktNr; 

n30z: NOP   0; 

      U     "Mx_FN_Taste_WZ_Achse_RET"; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      SPBN  E_SR; 

      SET   ; 
      =     #EAX_MSST_Reset; 

NETWORK
TITLE =Wiegekopfrettung

E_SR: U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      SPBN  E_WK; 

      U     #EAX_MSST_WZAX_Rett3070; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      FN    "Mx_FN_Taste"; 
      =     "Mx_FN_Taste_WZ_Achse_RET"; 

      U     #EAX_MSST_WZAX_Rett3070; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      FP    "Mx_FP_aus_Loch"; 
      =     "Mx_FP_Taste_WZ_Achse_RET"; 
      SPBN  n5z; 


      U     "M_Leistung_Steht_an"; 
      U     "DB_SIEM_KANAL_1".E_AxesRef; 
      U     "DB_ANWENDER_SOFTKEY".Wiko_freifahren; 

      S     "Start_ASUP_Machine_Nr.1"; 
      L     97; 
      T     "DB_NC_PLC".Rettung_FktNr; 

n5z:  NOP   0; 

      U     "Mx_FN_Taste_WZ_Achse_RET"; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      SPBN  n5y; 

      SET   ; 
      =     #EAX_MSST_Reset; 

// Umschaltung Messsystem bei MAschine Aus
n5y:  UN    "DB_ANWENDER_SOFTKEY".Wiko_freifahren; 
      UN    "M_Leistung_Steht_an"; 
      UN    "M_MA_Maschine_Ein"; 
      SPBN  n5w; 

      UN    "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 
      U     "M_4Ax_Messystem_direkt"; 
      S     "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 
      SPA   n5x; 

n5w:  U     "DB_ANWENDER_SOFTKEY".Wiko_freifahren; 
      UN    "M_Leistung_Steht_an"; 
      UN    "M_MA_Maschine_Ein"; 
      UN    "DB_SIEM_KANAL_1".E_AxesRef; 
      R     "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 


n5x:  UN    "DB_PLC_MD_DB20".UDHex._02_BIT3_ACHSE4_MESSYST; 
      U     "M_4Ax_Messystem_direkt"; 
      U     "M_4Achse_existiert"; 
      O     "DB_ANWENDER_SOFTKEY".Wiko_freifahren; 
      =     "DB_FEHLERMELDUNG".MELDUNG3._700518_Motor_Messsytem; 
NETWORK
TITLE =Vorbesetzen Achsanwahl bei Hochlauf

E_WK: U     "DB_PLC_MD_DB20".UDHex._07_BIT4_5_6_AX_N_HOCHLA; 
      U     "M_8_ter_OB1_Zyklen"; 
      SPBN  M005; 

// ---- Teilapperat aktivieren nach Hochlauf ----
      L     "DB_PLC_MD_DB20".UDInt._043_ANWAHL_TISCH; // Teilapperat vorhanden ?
      L     1; 
      ==I   ; 
      SPBN  NEX1; 
      L     2; 
      T     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      SPA   M006; 

// ---- Komet U-Achswerkzeug aktivieren nach Hochlauf ----
NEX1: U     "M_12Achse_existiert"; // Komet U-Achswerkzeug vorhanden?
      U     "m_UiSpi"; 
      SPBN  NEX2; 
      L     3; 
      T     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      SPA   M006; 

// ---- 5te Achse aktivieren nach Hochlauf ----
NEX2: U     "DB_SIEM_STARTUP".ActivAxis[5]; // 5te Achse vorhanden ?
      SPBN  NEX3; 
      L     0; 
      T     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      SPA   M006; 

// ---- Spindel aktivieren nach Hochlauf ----
M005: UN    "DB_PLC_MD_DB20".UDHex._07_BIT4_5_6_AX_N_HOCHLA; 
      U     "M_8_ter_OB1_Zyklen"; 
      SPBN  M006; 
NEX3: L     1; 
      T     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
M006: NOP   0; 
END_FUNCTION

