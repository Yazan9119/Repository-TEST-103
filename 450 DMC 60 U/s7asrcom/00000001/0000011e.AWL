FUNCTION "FC_ACHSE_SPINDEL" : VOID
TITLE =
//$Revision: 1.23 $
//$Date: 2008/10/16 09:26:44CEST $
//$Author: hgc $
//
//
//Funktionsbeschreibung:
//    Abfrage: Achse vorhanden ? 
//    Aktivierung Meßsystem
//    Vorschubkorrekturschalter / Eilgang-Überlagerung aktivieren 
//
//    Achse: Nachführbetrieb 
//    Umschaltung OVR für Eilgang
//    Achse: "Bremse lösen" Übergabe-Signal bilden
//    Verzögerung bilden: Reglerfreigabe nach Bremse lüften
//    
//    Achse: Impulsfreigabe
//    Achse: Reglerfreigabe
//Logbuch:
///V1.1 /18.05.2005/Scheuenstuhl; parkende Achse, neu:EX_AxPark, Abwahl Meßsysteme
//
///V1.0 /23-05-00/ AB Schnittstellen geändert
//
///V0.11/8-Mai-00/ BAG
// o Eilgang-Überlagerung in Jog mit Eilgang-Override veränderbar aktiviert
//
///V0.10/23-02-00/ BAG
// o Schnittstelle erweitert: => Vorschubfreigabe für Antriebsoptimierungs-Tests 
//    E_Taste_Brems_Auf_Messen  // für Antriebsoptimierungs-Tests 
//
///V0.9/17-02-00/ BAG
// o Schnittstelle erweitert: E_Kanal1_E_Transform => Vorschubfreigabe
//
///V0.8/15.12.99/ BAG
//- NST "Achsen_Halt_von_aussen" ergänzt
//- Vorschubkorrekturschalter / Eilgang-Überlagerung aktivieren 
//- Achse: Nachführbetrieb 
//
//V0.6 /22.10.99/ BAG
//- Modul Schnittstelle eingerichtet 
//
//22.12.99 / AB
//Fehler mit Quittiertaste rückgesetzt 
//
//17.10.01 M5 setzt M3 und M4 zurück
AUTHOR : BAG
FAMILY : DM_Basis
NAME : ACHSEN
VERSION : 1.1


VAR_INPUT
  EX_EN : BOOL ;	//Modul-Freigabe
  EX_UDHex_00_IB_Bit_Achse : BOOL ;	//PLC_MD_Achsen-Abwahl 1.Achse: 0=ein, 1=aus
  EX_UDHex_01_Ax_Messystem : BOOL ;	//PLC_MD_Anwahl Messystem 1.Achse: 0=indirekt, 1=direkt
  EX_Spindel_Aktiv : BOOL ;	//Spindel_Aktiv
  EX_Antrieb_Sperre : BOOL ;	//Antrieb gesperrt
  EX_Freigabe_Regler : BOOL ;	//Freigabe Regler
  EX_Option_Safety : BOOL ;	//Maschine mit Safety 
  EX_Safe_StopA_abgewaehlt : BOOL ;	//Stop A abgewählt
  EX_Safe_Teststop_laeuft : BOOL ;	//Teststop läuft
  EX_Leistung_steht_an : BOOL ;	//Leistung steht an in Betriebsarten
  EX_GewSchn_Aktiv : BOOL ;	//Gewindeschneide Gesamt
  EX_VSHalt_EinzelAchse : BOOL ;	//Achs-Vorschubsperre von aussen angefordert
  EX_VSHalt_Gruppe : BOOL ;	//Vorschub Halt Hauptachsen
  EX_VSHalt_Kanal : BOOL ;	//Vorschub Halt Kanal
  EX_RefPkt_Achse : BOOL ;	//Anforderung: Referenzpunkt Achse
  EX_Spdl_KorrWirksam : BOOL ;	//Anforderung: Vorschub Korrektur wirksam setzen
  EX_Spdl_Neu_Referenz : BOOL ;	//Anforderung: Neu Referieren
  EX_Spdl_Bei_Pos_NeuRef : BOOL ;	//Anforderung: bei Positionieren neu Referieren
  EX_Spdl_RestwegLoeschen : BOOL ;	//Anforderung: Restweg löschen
  EX_Spdl_E_FahrAF_61x0 : BOOL ;	//Fahranforderung für Antriebsoptimierungs-Tests 
  EX_Spdl_E_Steht_61x4 : BOOL ;	//Achse Steht
  EX_Spdl_E_DrehzRegl_61x6 : BOOL ;	//Drehzahlregler aktiv
  EX_Spdl_E_Handrad_64x0 : BOOL ;	//Handrad Aktiv
  EX_Spdl_E_FBMinus_64x6 : BOOL ;	//Fahrbefehl Minus
  EX_Spdl_E_FBPlus_64x7 : BOOL ;	//Fahrbefehl Plus
  EX_Spdl_E_PLCASp_68x7 : BOOL ;	//PLC Positionierbetrieb
  EX_Spdl_E_GetrUmsch_82x3 : BOOL ;	//Getriebe schalten 
  EX_Spdl_E_DrehGrenz_83x0 : BOOL ;	//Drehzahlgrenze
  EX_Spdl_E_SollDZbeg_83x1 : BOOL ;	//Solldrehzahl begrenzt
  EX_Spdl_E_SollBer_83x5 : BOOL ;	//Sollbereich
  EX_Spdl_E_GewBohren_84x3 : BOOL ;	//Gewindebohnren
  EX_Spdl_E_PosBetr_84x5 : BOOL ;	//Positionierbetrieb Spindel aktiv
  EX_Spdl_E_PendBetr_84x6 : BOOL ;	//Pendelbetrieb
  EX_Spdl_E_MotOK_93x5 : BOOL ;	//Achse Drive-Ready
  EX_Spdl_E_ImpFrei_93x7 : BOOL ;	//Impulse freigegeben
  EX_Spdl_E_MTempWarn_94x0 : BOOL ;	//Temperaturvorwarnung Motor
  EX_Spdl_E_KTempWarn_94x1 : BOOL ;	//Temperaturvorwarnung Kühlkörper
  EX_Spdl_E_RUCompl_94x2 : BOOL ;	//Hochlauf beendet
  EX_Spdl_E_Meldung_94x7 : BOOL ;	//Variable Meldefunktion (Vorschubsperre bei 90%)
  EX_Fehler_Quit : BOOL ;	//Taste Fehlerquittierung
  EX_AxPark : BOOL ;	//Achse parken (=Abwahl Meßsystem1+2)
  ES5T_RegFr_SA : S5TIME ;	//Zeit Reglerfreigabe abschalten
  ET_Timer_RegFSA : TIMER ;	//Timer Reglerfreigabe abschalten
  ET_Timer_RegFSE : TIMER ;	//Timer Reglerfreigabe einschalten
  ET_Timer_Reg_Drive_ready : TIMER ;	//Timer Abfrage Achse bereit
  EDW_Spdl_SFunkt : REAL ;	//S-Funktion
  EDW_Spdl_MFunkt : INT ;	//M-Funktion
  EB_A_SpOR : BYTE ;	//Spindel Override
END_VAR
VAR_IN_OUT
  EAX_Spdl_A_ASpSperre_1x3 : BOOL ;	//Achsen-/Spindelsperre
  EAX_Spdl_A_NachfBetr_1x4 : BOOL ;	//Nachführbetrieb
  EAX_Spdl_A_LageMess1_1x5 : BOOL ;	//Lagemeßsystem 1
  EAX_Spdl_A_LageMess2_1x6 : BOOL ;	//Lagemeßsystem 2
  EAX_Spdl_A_KorrW_1x7 : BOOL ;	//Korrektur wirksam
  EAX_Spdl_A_ReglFrei_2x1 : BOOL ;	//Reglerfreigabe
  EAX_Spdl_A_RWLSpRes_2x2 : BOOL ;	//Restweg löschen
  EAX_Spdl_A_GeschDz_3x6 : BOOL ;	//Geschwindigkeit DZ Begrenzung
  EAX_Spdl_A_VSSpHalt_4x3 : BOOL ;	//Vorschub/Spindel-Halt
  EAX_Spdl_A_Para_A_9x0 : BOOL ;	//Parametersatz A
  EAX_Spdl_A_Sp1Syn_16x4 : BOOL ;	//Spindel neu referieren
  EAX_Spdl_A_Sp1SynP_17x4 : BOOL ;	//Spindel bei nächster Pos. neu referieren
  EAX_Spdl_A_ImpFrei_21x7 : BOOL ;	//Impulsfreigabe
  EAX_Spdl_GetrUmsch_82x3 : BOOL ;	
  EAX_Spdl_PosBetr_84x5 : BOOL ;	//Positionierbetrieb
  EAX_Spdl_DrehGrenz_83x0 : BOOL ;	//Drehzahlgrenze
  EAX_Spdl_GewBohren_84x3 : BOOL ;	//Gewindebohren Aktiv
  EAX_Spdl_SollBer_83x5 : BOOL ;	//Spindel im Sollbereich
  EAX_Spdl_PendBetr_84x4 : BOOL ;	//Pendelbetrieb
  EAX_Spdl_E_RUCompl_94x2 : BOOL ;	//Hochlauf beendet
  EAX_Spdl_E_Message_94x7 : BOOL ;	//Variable Meldefunktion
  EAX_Spdl_A_RefVerz_12x7 : BOOL ;	//Referenzverzögerung bei Motormessystem
  EAX_Alarm_Regler : BOOL ;	//Alarm Reglerfreigabe fehlt
  EAX_Alarm_VSSP_Mot_Temp : BOOL ;	//Alarm Motortemperatur zu hoch
  EAX_Alarm_VSSP_KK_Temp : BOOL ;	//Alarm Kühlkörpertemperatur zu hoch
  EAX_Alarm_NA_MotOK : BOOL ;	//Alarm Achse nicht bereit
  EAX_Spdl_M3 : BOOL ;	
  EAX_Spdl_M4 : BOOL ;	
  EAX_Spdl_M5 : BOOL ;	
  EAX_Spdl_M3_M4 : BOOL ;	
  EAX_Spdl_IMP_M3 : BOOL ;	
  EAX_Spdl_IMP_M4 : BOOL ;	
  EAX_Spdl_IMP_M5 : BOOL ;	
  EAX_Spdl_IMP_M3_M4 : BOOL ;	
  EAX_Spdl_Reset_M3_4 : BOOL ;	
  EAX_Spdl_M3_Aktiv : BOOL ;	
  EAX_Spdl_M4_Aktiv : BOOL ;	
  EAX_Spdl_FP_M4 : BOOL ;	
  EAX_Spdl_FP_M3 : BOOL ;	
  EAX_Spdl_FP_M5 : BOOL ;	
  EAB_Spdl_A_SpOR : BYTE ;	//Spindel Override
  EAB_Spdl_SFunkt : WORD ;	
END_VAR
VAR_TEMP
  TR_Sp_ovr_Dez : REAL ;	
  TW_SP_ovr_dual : WORD ;	
  TI_Sp_ovr : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =Modul-Freigabe

      UN    #EX_EN; 
      BEB   ; 
NETWORK
TITLE =Aktivierung Meßsystem 2

      U     #EX_UDHex_01_Ax_Messystem; // PLC_MD_Anwahl Messystem 1.Achse: 0=indirekt, 1=direkt
      UN    #EX_UDHex_00_IB_Bit_Achse; 
      UN    #EX_AxPark; 
      =     #EAX_Spdl_A_LageMess2_1x6; // Meßsystem 2 aktiv
NETWORK
TITLE =Aktivierung Meßsystem 1
//   Direktes Messystem   
      UN    #EX_UDHex_01_Ax_Messystem; // PLC_MD_Anwahl Messystem 1.Achse: 0=indirekt, 1=direkt
      UN    #EX_UDHex_00_IB_Bit_Achse; 
      UN    #EX_AxPark; 
      =     #EAX_Spdl_A_LageMess1_1x5; // Meßsystem 1 aktiv
NETWORK
TITLE =Fehler quittieren

      U     #EX_Fehler_Quit; 
      R     #EAX_Alarm_NA_MotOK; // NOT-AUS, Antrieb Achse nicht bereit
      R     #EAX_Alarm_Regler; // NOT-AUS, Regler nicht bereit
      R     #EAX_Alarm_VSSP_Mot_Temp; // Motortemperatur zu hoch
      R     #EAX_Alarm_VSSP_KK_Temp; 
NETWORK
TITLE =Sprung wenn keine Spindel aktiv

      UN    #EX_Spindel_Aktiv; // nur wenn Spindel aktiv ist
      SPB   REG; 
NETWORK
TITLE =Alarm: Achse nicht bereit

      U(    ; 
      U     #EX_Leistung_steht_an; 
      UN    #EX_Antrieb_Sperre; 
      UN    #EX_UDHex_00_IB_Bit_Achse; 
      U     #EX_Safe_StopA_abgewaehlt; 
      UN    #EX_Safe_Teststop_laeuft; 
      U(    ; 
      O     #EAX_Spdl_A_LageMess1_1x5; 
      O     #EAX_Spdl_A_LageMess2_1x6; 
      )     ; 
      L     S5T#2S; 
      SE    #ET_Timer_Reg_Drive_ready; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      U     #ET_Timer_Reg_Drive_ready; 
      )     ; 
      UN    #EX_Spdl_E_MotOK_93x5; 
      S     #EAX_Alarm_NA_MotOK; 
NETWORK
TITLE =Timer Reglerfreigabe ein

      U     #EX_Leistung_steht_an; // Antriebe ein
      UN    #EX_UDHex_00_IB_Bit_Achse; // Achse ausblenden
      U     #EAX_Spdl_A_ReglFrei_2x1; // und Achse ist ein
      UN    #EX_Spdl_E_FahrAF_61x0; // und keine Achseoptimierung
      UN    #EAX_Spdl_A_VSSpHalt_4x3; // "Achse".A_VSSpHalt 
      U(    ; 
      ON    #EX_Spdl_E_DrehzRegl_61x6; // Keine Impulsfreigabe
      ON    #EX_Spdl_E_ImpFrei_93x7; // DBX93.7 Impulse freigegeben
      )     ; 
      UN    #EX_Antrieb_Sperre; // keine Antriebssperre
      U     #EAX_Spdl_M3_M4; 
      L     S5T#2S; 
      SE    #ET_Timer_RegFSE; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      NOP   0; 

NETWORK
TITLE =Alarm: Reglerfreigabe fehlt

      U     #ET_Timer_RegFSE; 
      UN    #EX_Antrieb_Sperre; // keine Antriebssperre
      S     #EAX_Alarm_Regler; 
NETWORK
TITLE =Alarm: Motortemperatur zu hoch / Kühlkörpertemperatur zu hoch
//
//
      U     #EX_Leistung_steht_an; // Antriebe EIN
      U(    ; 
      O     #EX_Spdl_E_FBMinus_64x6; // DBX64.6 Minus-Fahrbefehl 
      O     #EX_Spdl_E_FBPlus_64x7; // DBX64.7 Plus-Fahrbefehl 
      O     #EX_GewSchn_Aktiv; 
      )     ; 
      =     L      8.0; 
      U     L      8.0; // Temperaturvorwarnung Kühlkörper
      U     #EX_Spdl_E_MTempWarn_94x0; // Temperaturvorwarnung Motor
      S     #EAX_Alarm_VSSP_Mot_Temp; 
      U     L      8.0; // Motortemperatur zu hoch
      U     #EX_Spdl_E_KTempWarn_94x1; 
      S     #EAX_Alarm_VSSP_KK_Temp; 
NETWORK
TITLE =Handrad aktiv -> Servo-Parametersatz umschalten 

      U     #EX_Spdl_E_Handrad_64x0; 
      =     #EAX_Spdl_A_Para_A_9x0; 

NETWORK
TITLE =Refernzverzögerung bei Motormessystem

      U     #EX_RefPkt_Achse; 
      =     #EAX_Spdl_A_RefVerz_12x7; 
NETWORK
TITLE =Spindel neu referieren

      U     #EX_Spdl_Neu_Referenz; 
      =     #EAX_Spdl_A_Sp1Syn_16x4; 
NETWORK
TITLE =Spindel bei nächster Positionierung neu referieren

      U     #EX_Spdl_Bei_Pos_NeuRef; 
      =     #EAX_Spdl_A_Sp1SynP_17x4; 
NETWORK
TITLE =Restweg löschen

      U     #EX_Spdl_RestwegLoeschen; 
      =     #EAX_Spdl_A_RWLSpRes_2x2; 
NETWORK
TITLE =Spindel Override
//
//
      L     #EB_A_SpOR; 
      T     #EAB_Spdl_A_SpOR; 
NETWORK
TITLE =Auswertung M-Funktion
//M3 oder M4
      L     3; 
      L     #EDW_Spdl_MFunkt; //Impuls !! 
      ==I   ; 
      S     #EAX_Spdl_M3; 
      FP    #EAX_Spdl_FP_M3; 
      =     #EAX_Spdl_IMP_M3; // IPM: M03

      L     4; 
      ==I   ; 
      S     #EAX_Spdl_M4; 
      FP    #EAX_Spdl_FP_M4; 
      =     #EAX_Spdl_IMP_M4; // IPM: M04

      U     #EAX_Spdl_IMP_M3; 
      O     #EAX_Spdl_IMP_M4; 
      =     #EAX_Spdl_IMP_M3_M4; // IPM: M03 oder M04
      R     #EAX_Spdl_M5; 

      L     #EDW_Spdl_MFunkt; 
      L     5; 
      ==I   ; 
      FP    #EAX_Spdl_FP_M5; 
      =     #EAX_Spdl_IMP_M5; 
      S     #EAX_Spdl_M5; 

      O     #EX_Spdl_E_Steht_61x4; //Spindel steht
      R     #EAX_Spdl_M5; 

      O     #EX_Spdl_E_PosBetr_84x5; 
      O     #EAX_Spdl_M5; //M05
      O     "M_Reset_Taste"; //Taste Reset
      O     #EX_Spdl_E_PLCASp_68x7; 
      =     #EAX_Spdl_Reset_M3_4; //Reset M3/M4

//Rücksetzen von M3

      U     #EAX_Spdl_Reset_M3_4; //Reset M3/M4
      O     #EAX_Spdl_IMP_M4; //+IPM: M04
      O     #EX_Spdl_E_Steht_61x4; //Spindel steht
      R     #EAX_Spdl_M3; //M3

//Rücksetzen von M4

      U     #EAX_Spdl_Reset_M3_4; //Reset M3/M4
      O     #EAX_Spdl_IMP_M3; //+IPM: M03
      O     #EX_Spdl_E_Steht_61x4; //Spindel steht
      R     #EAX_Spdl_M4; //M4

      U     #EAX_Spdl_M3; //M3
      O     #EAX_Spdl_M4; //M4
      O     #EX_Spdl_E_FBMinus_64x6; // Fahrbefehl Spindel -
      O     #EX_Spdl_E_FBPlus_64x7; // Fahrbefehl Spindel +
      =     #EAX_Spdl_M3_M4; //M3 oder M4

      U     #EAX_Spdl_M3; //M3
      S     #EAX_Spdl_M3_Aktiv; 
      R     #EAX_Spdl_M4_Aktiv; 

      U     #EAX_Spdl_M4; //M4
      S     #EAX_Spdl_M4_Aktiv; 
      R     #EAX_Spdl_M3_Aktiv; 

      O     "M_Reset_Taste"; 
      O     #EAX_Spdl_M5; //17.10.01 AB
      O     #EX_Spdl_E_PosBetr_84x5; 
      R     #EAX_Spdl_M4_Aktiv; 
      R     #EAX_Spdl_M3_Aktiv; 
NETWORK
TITLE =Vorschubkorrekturschalter / Eilgang-Überlagerung aktivieren 
//
// Achsen-Korrektur auf BP-Tasten 
//
      SET   ; 
      =     #EAX_Spdl_A_KorrW_1x7; 

NETWORK
TITLE =Drehzahlgrenze

      U     #EX_Spdl_E_DrehGrenz_83x0; 
      =     #EAX_Spdl_DrehGrenz_83x0; 
NETWORK
TITLE =im Sollbereich

      U     #EX_Spdl_E_SollBer_83x5; 
      =     #EAX_Spdl_SollBer_83x5; 
NETWORK
TITLE =Positionierbetrieb aktiv

      U     #EX_Spdl_E_PosBetr_84x5; 
      =     #EAX_Spdl_PosBetr_84x5; 
NETWORK
TITLE =Getriebe schalten

      U     #EX_Spdl_E_GetrUmsch_82x3; 
      =     #EAX_Spdl_GetrUmsch_82x3; 
NETWORK
TITLE =Pendelbetrieb

      U     #EX_Spdl_E_PendBetr_84x6; 
      =     #EAX_Spdl_PendBetr_84x4; 
NETWORK
TITLE =Hochlauf beendet

      U     #EX_Spdl_E_RUCompl_94x2; 
      =     #EAX_Spdl_E_RUCompl_94x2; 
NETWORK
TITLE =Variable Meldefunktion

      U     #EX_Spdl_E_Meldung_94x7; 
      =     #EAX_Spdl_E_Message_94x7; 
NETWORK
TITLE =Override Real

      L     #EB_A_SpOR; //Spindeloveride Gray-Code
      PUSH  ; //AKKU 1 in AKKU 2
      SRW   1; 
      XOW   ; 
      SRW   1; 
      XOW   ; 
      SRW   1; 
      XOW   ; 
      SRW   1; 
      XOW   ; 
      L     1; 
      -I    ; //DEC 1
      T     #TW_SP_ovr_dual; //Spindeloveride dual
      DTR   ; 
      L     5.000000e-002; //* 0,05 (5%) Raster
      *R    ; 
      L     5.000000e-001; //+ 0,5 (50%) Anfangswert
      +R    ; 
      T     #TR_Sp_ovr_Dez; //Spindeloveride Dezimal 0,5 .. 1,2 (50..120%)
NETWORK
TITLE =S Funktion

      L     #EDW_Spdl_SFunkt; //aktuelle Spindeldrehzahl
      *R    ; 
      TRUNC ; //Ganzzahl
      T     #EAB_Spdl_SFunkt; //Spindeldrehzahl incl Overide
NETWORK
TITLE =Ausgabe Gewindeschneiden aktiv

      U     #EX_Spdl_E_GewBohren_84x3; // Übergabe Gewindeschneide aktiv
      =     #EAX_Spdl_GewBohren_84x3; 

NETWORK
TITLE = Impulsfreigabe, Reglerfreigabe 

REG:  O     #EAX_Spdl_A_ReglFrei_2x1; // Reglerfreigabe 
      ON    #EX_Spdl_E_Steht_61x4; // oder die Spindel dreht
      O     #EX_Option_Safety; 
      =     #EAX_Spdl_A_ImpFrei_21x7; // Impulsfreigabe setzen 
NETWORK
TITLE =Timer Reglerfreigabe

      U     #EX_Leistung_steht_an; // Leistung steht an
      L     #ES5T_RegFr_SA; 
      SA    #ET_Timer_RegFSA; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      NOP   0; 

NETWORK
TITLE =Reglerfreigabe

      U     #EX_Freigabe_Regler; // und Freigabe Regler  
      UN    #EX_UDHex_00_IB_Bit_Achse; // Achse ausblenden (IB-Bit) = 0
      UN    #EX_Antrieb_Sperre; // Keine Sperre vom Familien Modul
      U(    ; 
      U     #ET_Timer_RegFSA; 
      UN(   ; 
      U     "M_Hauptspindel_steht"; //"Spindel".E_Steht
      UN    #EX_Leistung_steht_an; 
      )     ; 
      )     ; 
      U     #EX_Spdl_E_MotOK_93x5; // und der Motor OK
      UN    #EX_Option_Safety; 
      O(    ; 
      U     #EX_Option_Safety; // Sobald Safey vorhanden immer Reglerfreigabe schalten
      U     #EX_Freigabe_Regler; // und Freigabe Regler  
      UN    #EX_UDHex_00_IB_Bit_Achse; // Achse ausblenden (IB-Bit) = 0
      UN    #EX_Antrieb_Sperre; // Keine Sperre vom Familien Modul
      )     ; 
      =     #EAX_Spdl_A_ReglFrei_2x1; // Reglerfreigabe
NETWORK
TITLE =Achse: Vorschub Halt  DBX 4.3

      ON    #EAX_Spdl_A_ReglFrei_2x1; // "Achse".A_ReglerFrei   
      O     #EX_VSHalt_EinzelAchse; 
      O     #EX_VSHalt_Gruppe; 
      O     #EX_VSHalt_Kanal; 
//      ON    #EX_Spdl_E_MotOK_93x5       // und der Motor OK
//      ON    #EX_Spdl_E_ImpFrei_93x7
      =     #EAX_Spdl_A_VSSpHalt_4x3; // "Achse".A_VSSpHalt 

NETWORK
TITLE =Achse: Nachführbetrieb
//
// Nachführbetrieb 
//
      ON    #EAX_Spdl_A_ReglFrei_2x1; 
      ON    #EX_Spindel_Aktiv; 
      O     #EX_UDHex_00_IB_Bit_Achse; // Achse ausblenden (IB-Bit)             
      =     #EAX_Spdl_A_ASpSperre_1x3; // Achsensperre
      =     #EAX_Spdl_A_NachfBetr_1x4; // Nachführbetrieb

NETWORK
TITLE =Spindel Drehzahlbegrenzung

      U     #EX_Spdl_KorrWirksam; 
      =     #EAX_Spdl_A_GeschDz_3x6; 

END_FUNCTION

