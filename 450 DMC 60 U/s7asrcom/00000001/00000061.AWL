FUNCTION "FC_SK_Ablauf" : VOID
TITLE =
//$Revision: 1.10 $
//$Date: 2008/03/10 06:22:05CET $
//$Author: rieds $
//
//Logbuch:
//
//V0.1 / 18.01.2000 / SCH
//
//- Modul Erstellt
//
VERSION : 0.1


VAR_INPUT
  E_Enable : BOOL ;	//Modulfreigabe
END_VAR
VAR_TEMP
  TX_Freigabe_schw : BOOL ;	
  TX_Freigabe_kl : BOOL ;	
  TX_FRG_FAHRB : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =

      UN    #E_Enable; // Freigabe Modul
      BEB   ; // Nein => zum Ende

      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; // SK nicht angewaehlt oder
      L     1; // Modul nicht angewaehlt
      ==I   ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     2; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     4; 
      ==I   ; 
      )     ; 
      =     "DB_NC_PLC".SK_Anwahl; // Uebergabe SK angewaehlt
      SPB   STRT; // wenn nicht ans Ende und Ausgänge rücksetzen
      CLR   ; 
      =     "O_SK_HOR_schwenken"; 
      =     "O_SK_VER_schwenken"; 
      =     "O_SK_Klemmung_zu"; 
      =     "O_SK_Klemmung_loesen"; 
// sja 24.08.01
// wenn manueller Schwenkkopf -> Überwachung des Eingangs Vertikal an DB_SIEM_NCK-Programm

      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     3; // manueller Schwenkkopf
      ==I   ; 
      )     ; 
      U     "I_SK_VER_Ebene"; // und Stellung Vertikal
      =     "DB_NC_PLC".SK_Kopf_Vertikal; 

      BEA   ; 

NETWORK
TITLE =
//Endlagen an DB_SIEM_NCK Übergeben
STRT: U     "DB_NC_PLC".SK_SK_Typ; 
      SPB   BAX; 
      U     "I_SK_VER_Ebene"; 
      UN    "I_SK_HOR_Ebene"; // Kopf Vertikal
      UN    "I_SK_Klemmung_geloest"; // Klemmung geklemmt
      U     "I_SK_Klemmung_geklemmt"; 
      =     "DB_NC_PLC".SK_Kopf_Vertikal; // Stellung Vertikal setzen

      U     "I_SK_HOR_Ebene"; // Kopf Horizontal
      UN    "I_SK_VER_Ebene"; 
      UN    "I_SK_Klemmung_geloest"; // Klemmung geklemmt
      U     "I_SK_Klemmung_geklemmt"; 
      =     "DB_NC_PLC".SK_Kopf_Horizontal; // Stellung Horizontal setzen
      SPA   ANW; 

BAX:  UN    "DB_SIEM_NCK".E_SWCamMinus[0]; // Beide Nocken 0-Signal
      UN    "DB_SIEM_NCK".E_SWCamPlus[0]; // 
      U     "DB_ACHSE5".E_SWCam; // und Nocken Aktiv
      =     "DB_NC_PLC".SK_Kopf_Vertikal; 

      UN    "DB_SIEM_NCK".E_SWCamMinus[1]; // Beide Nocken 0-Signal
      UN    "DB_SIEM_NCK".E_SWCamPlus[1]; // 
      U     "DB_ACHSE5".E_SWCam; // und Nocken Aktiv
      =     "DB_NC_PLC".SK_Kopf_Horizontal; 

NETWORK
TITLE =

ANW:  L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; // B-Achse angewählt
      L     2; // an DB_SIEM_NCK Übergeben
      ==I   ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; // A-Achse angewählt
      L     4; // an DB_SIEM_NCK Übergeben
      ==I   ; 
      )     ; 
      =     "DB_NC_PLC".SK_SK_Typ; 


NETWORK
TITLE =Schwenkfräskopf Klemmung klemmen
//Klemmung
      U     "DB_OEM".OEM_SKRettaktiv; // nur wenn SKR Aktiv
      SPBN  SK00; 

      U(    ; 
      U     "I_SK_HOR_Ebene"; 
      O     "I_SK_VER_Ebene"; 
      )     ; 
      =     #TX_Freigabe_kl; 

// Klemmung Auf

      U     "DB_EINRICHTFUNKTION".Bewegung_19_Minus; // SKR Klemmung Auf
//      U     "M_pos_Trigg_SKR_Taste"
      U     #TX_Freigabe_kl; 
      R     "O_SK_Klemmung_zu"; 
      S     "O_SK_Klemmung_loesen"; 

// Klemmung Zu

      U     "DB_EINRICHTFUNKTION".Bewegung_19_Plus; // oder SKR Klemmung schliessen
//      U     "M_pos_Trigg_SKR_Taste"
      U     #TX_Freigabe_kl; 
      S     "O_SK_Klemmung_zu"; 
      R     "O_SK_Klemmung_loesen"; 


      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #TX_Freigabe_kl,
           Freigabe_GST_Hand        := #TX_Freigabe_kl,
           Endlage_AST              := "I_SK_Klemmung_geloest",
           Endlage_GST              := "I_SK_Klemmung_geklemmt",
           Ventil_AST               := "O_SK_Klemmung_loesen",
           Ventil_GST               := "O_SK_Klemmung_zu",
           Bewegungsnummer          := 19);



NETWORK
TITLE =Schwenkfräskopf schwenken senkrecht (M54)
//SFK Stellung
//
// Kopf Vertikal
      U     "I_SK_Klemmung_geloest"; 
      U     "O_SK_Klemmung_loesen"; 
      UN    "I_SK_Klemmung_geklemmt"; 
      UN    "O_SK_Klemmung_zu"; 
      =     #TX_Freigabe_schw; 


      U     "DB_EINRICHTFUNKTION".Bewegung_20_Plus; // SKR  Kopf Vertikal
//      U     "M_pos_Trigg_SKR_Taste"
      U     #TX_Freigabe_schw; 
      S     "O_SK_VER_schwenken"; 
      R     "O_SK_HOR_schwenken"; 

//      U     "DB_EINRICHTFUNKTION".Bewegung_20_Plus    // SKR und die Endlage nicht
//U     "M_neg_Trigg_SKR_Taste"     // erreicht =>Ausgang ruecksetzen
      U     "DB_NC_PLC".SK_Kopf_Vertikal; 
      R     "O_SK_VER_schwenken"; 

// Kopf Horizontal

      U     "DB_EINRICHTFUNKTION".Bewegung_20_Minus; // SKR  Kopf Vertikal
//      U     "M_pos_Trigg_SKR_Taste"
      U     #TX_Freigabe_schw; 
      R     "O_SK_VER_schwenken"; 
      S     "O_SK_HOR_schwenken"; 

//      U     "DB_EINRICHTFUNKTION".Bewegung_20_Minus    // SKR und die Endlage nicht
//U    L "M_neg_Trigg_SKR_Taste"     // erreicht =>Ausgang ruecksetzen
      U     "DB_NC_PLC".SK_Kopf_Horizontal; 
      R     "O_SK_HOR_schwenken"; 


      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #TX_Freigabe_schw,
           Freigabe_GST_Hand        := #TX_Freigabe_schw,
           Endlage_AST              := "I_SK_HOR_Ebene",
           Endlage_GST              := "I_SK_VER_Ebene",
           Ventil_AST               := "O_SK_HOR_schwenken",
           Ventil_GST               := "O_SK_VER_schwenken",
           Bewegungsnummer          := 20);


NETWORK
TITLE =


      SET   ; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[260]; 
      =     "DB_DM_M_FUNKTION".MX_QuitPflicht[261]; 


NETWORK
TITLE =M-AUSGANG: Schwenkfraeskopf senkrecht M54

SK00: O     "M_SK_Stat"; // Status oder
      O     "M_SK_ERR"; // Fehlerunterbrechung 
      SPB   MEND; // dann zum Ende

      U     "M_SK_Aktiv"; // Startueberpruefung wenn M53/54 aktiv wird
      SPBN  ST01; 

      U     "DB_NC_PLC".SK_PLC_Freigabe; // Startueberpruefung gemacht 
      SPB   ST01; // weiter

      U     "M_4Te_Betriebsart"; 
      U     "DB_PLC_MD_DB20".UDHex._37_Bit5_LA_AUSF_BA4; 
      =     #TX_FRG_FAHRB; 

      U     "I_SK_Freigabe"; // SFK Freigabe 1-Signal
//    U     "M_SK_Fahrbereich_gesamt"   // und gesamter Fahrbereich
      UN    "M_3Te_Betriebsart"; // und keine Sonder BA's
      UN    "M_4Te_Betriebsart"; 
      UN    "M_Einrichtbetrieb"; 
      U     "M_Leistung_Steht_an"; 
      UN    "MX_WZB_Ov_0"; // und es steht auch keine Messung von WZB an
      O     #TX_FRG_FAHRB; 
      =     "DB_NC_PLC".SK_PLC_Freigabe; // Alle Bedingungen ok Startsignal an DB_SIEM_NCK 


ST01: U     "DB_NC_PLC".SK_SK_Typ; // Schwenkkopf B-Achse
      SPB   MEND; // Ausgänge nicht auswerten

// -- Schritt 0 ----------------------------------------------------------------

      L     "B_SK_Ablauf"; // Schritt 0 ?
      L     0; 
      ==I   ; 
      SPBN  SK01; // Nein => Schrtitt 1
//
// -----------------------------------------------------------------------------
//
//      U     "DB_DM_M_FUNKTION".MX_M[260]
//      U     "DB_NC_PLC".SK_Kopf_Vertikal
//      R     "DB_DM_M_FUNKTION".MX_M[260]

//      U     "DB_DM_M_FUNKTION".MX_M[261]
//      U     "DB_NC_PLC".SK_Kopf_Horizontal
//      R     "DB_DM_M_FUNKTION".MX_M[261]
//
// -----------------------------------------------------------------------------
      U(    ; 
      U     "DB_DM_M_FUNKTION".MX_M[260]; 
      UN    "DB_NC_PLC".SK_Kopf_Vertikal; 
      )     ; 
      X(    ; 
      U     "DB_DM_M_FUNKTION".MX_M[261]; 
      UN    "DB_NC_PLC".SK_Kopf_Horizontal; 
      )     ; 
      SPBN  MEND; 

      L     1; 
      T     "B_SK_Ablauf"; 

// -- Schritt 1 ----------------------------------------------------------------
//
SK01: L     "B_SK_Ablauf"; // Schritt 1 ?
      L     1; 
      ==I   ; 
      SPBN  SK02; // Nein => Schrtitt 2
//
// -----------------------------------------------------------------------------
//

// -----------------------------------------------------------------------------
//
      U     "I_SK_Freigabe"; // SFK Freigabe 1-Signal
      SPBN  MEND; 

      L     2; 
      T     "B_SK_Ablauf"; 
      SPA   MEND; 
//
// -- Schritt 2 ----------------------------------------------------------------
//
SK02: L     "B_SK_Ablauf"; // Schritt 2 ?
      L     2; 
      ==I   ; 
      SPBN  SK03; // Nein => Schrtitt 3
//
// -----------------------------------------------------------------------------
//
      U     "I_SK_VER_Ebene"; 
      =     "O_SK_VER_schwenken"; // letzte Endlage
      U     "I_SK_HOR_Ebene"; // ansteuern
      =     "O_SK_HOR_schwenken"; 

      SET   ; 
      R     "O_SK_Klemmung_zu"; 
      S     "O_SK_Klemmung_loesen"; 
      S     "M_SK_Klemm_Bewegung"; 
      S     "M_SK_Aktiv"; 
// -----------------------------------------------------------------------------
//
      U     "I_SK_Klemmung_geloest"; // Klemmung geklemmt
      UN    "I_SK_Klemmung_geklemmt"; 
      SPBN  MEND; 

      R     "M_SK_Klemm_Bewegung"; 

      L     3; // dann weiter zu Step 3
      T     "B_SK_Ablauf"; 
//
// -- Schritt 3 ----------------------------------------------------------------

SK03: L     "B_SK_Ablauf"; // Schritt 3 ?
      L     3; 
      ==I   ; 
      SPBN  SK04; // Nein => Schrtitt 4
//
// -----------------------------------------------------------------------------

      SET   ; 
      S     "M_SK_Kopf_Bewegung"; 

      U     "DB_DM_M_FUNKTION".MX_M[260]; 
      UN    "DB_DM_M_FUNKTION".MX_M[261]; 
      =     "O_SK_VER_schwenken"; 

      U     "DB_DM_M_FUNKTION".MX_M[261]; 
      UN    "DB_DM_M_FUNKTION".MX_M[260]; 
      =     "O_SK_HOR_schwenken"; 
//
// -----------------------------------------------------------------------------
//
      U     "I_SK_VER_Ebene"; 
      UN    "I_SK_HOR_Ebene"; // Kopf Vertikal
      U     "DB_DM_M_FUNKTION".MX_M[260]; 
      O     ; 
      U     "I_SK_HOR_Ebene"; // Kopf Vertikal
      UN    "I_SK_VER_Ebene"; 
      U     "DB_DM_M_FUNKTION".MX_M[261]; 
      SPBN  MEND; 

      R     "M_SK_Kopf_Bewegung"; 

      L     S5T#500MS; 
      SET   ; 
      SV    "Te_SK_Ablauf"; 
      CLR   ; 
      SV    "Te_SK_Ablauf"; 

      L     4; // dann weiter zu Step 4
      T     "B_SK_Ablauf"; 
      SPA   MEND; 
//
// -- Schritt 4 ----------------------------------------------------------------
//
SK04: L     "B_SK_Ablauf"; // Schritt 4 ?
      L     4; 
      ==I   ; 
      SPBN  SK05; // Nein => Ende
//
// -----------------------------------------------------------------------------
//
//
// -----------------------------------------------------------------------------
//
      U     "Te_SK_Ablauf"; 
      SPB   MEND; 

      L     5; 
      T     "B_SK_Ablauf"; 
//
// -- Schritt 5 ----------------------------------------------------------------
//
SK05: L     "B_SK_Ablauf"; // Schritt 5 ?
      L     5; 
      ==I   ; 
      SPBN  SK06; // Nein => Schrtitt 6
//
// -----------------------------------------------------------------------------
//
      SET   ; 
      S     "O_SK_Klemmung_zu"; 
      R     "O_SK_Klemmung_loesen"; 
      S     "M_SK_Klemm_Bewegung"; 
//
// -----------------------------------------------------------------------------
//
      UN    "I_SK_Klemmung_geloest"; // Klemmung geklemmt
      U     "I_SK_Klemmung_geklemmt"; 
      SPBN  MEND; 

      R     "M_SK_Klemm_Bewegung"; 

      L     6; 
      T     "B_SK_Ablauf"; 
//
// -- Schritt 6 ----------------------------------------------------------------
//
SK06: L     "B_SK_Ablauf"; // Schritt 6 ?
      L     6; 
      ==I   ; 
      SPBN  MEND; // Nein => Ende
//
// -----------------------------------------------------------------------------

      CLR   ; 
//     =     "DB_DM_M_FUNKTION".MX_M[260]
//     =     "DB_DM_M_FUNKTION".MX_M[261]
      =     "O_SK_VER_schwenken"; 
      =     "O_SK_HOR_schwenken"; 


// -----------------------------------------------------------------------------
//
      L     0; 
      T     "B_SK_Ablauf"; 
      SPA   MEND; 

MEND: NOP   0; 
NETWORK
TITLE =


      U     "DB_DM_M_FUNKTION".MX_M[260]; 
      U     "DB_NC_PLC".SK_Kopf_Vertikal; 
      U     "I_SK_Klemmung_geklemmt"; 
      O     "M_Reset_Taste"; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[260]; 
      R     "DB_DM_M_FUNKTION".MX_M[260]; 

      U     "DB_DM_M_FUNKTION".MX_M[261]; 
      U     "DB_NC_PLC".SK_Kopf_Horizontal; 
      U     "I_SK_Klemmung_geklemmt"; 
      O     "M_Reset_Taste"; 
      =     "DB_DM_M_FUNKTION".MX_Quitt[261]; 
      R     "DB_DM_M_FUNKTION".MX_M[261]; 

END_FUNCTION

