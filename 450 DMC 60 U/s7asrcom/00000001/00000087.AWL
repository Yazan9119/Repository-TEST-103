FUNCTION "FC_PS_RS4_5_SKR" : VOID
TITLE =
//$Revision: 1.42 $
//$Date: 2008/09/25 06:50:30CEST $
//$Author: hgc $
//
//Rundspeicher RS4 / RS5 Rettungsfunktion
//=============================
AUTHOR : SCH
NAME : WZM160
VERSION : 0.1


VAR_TEMP
  TX_Fahrbefehl_Fertig : BOOL ;	
  TX_Abzieh_Pos : BOOL ;	
  Tx_Frg_Dst : BOOL ;	
  Tx_Frg_Trans : BOOL ;	
  Tx_Frg_Hub : BOOL ;	
  Tx_Jog_Freig : BOOL ;	
  Tb_Platz : BYTE ;	
  Ventil_GST : BOOL ;	
  Ventil_AST : BOOL ;	
  FB_Achse_9 : BOOL ;	
  Tx_Frg_Trans_GST : BOOL ;	
  Tx_Frg_Trans_AST : BOOL ;	
  Tx_Frg_Klm : BOOL ;	
  Tx_Frg_Klp : BOOL ;	
  Tx_Frg_RPL_ankuppeln : BOOL ;	
  tx_dummy : BOOL ;	
  bringen : BOOL ;	
  holen : BOOL ;	
  t_Freigabe_AST_Hand : BOOL ;	
  t_Freigabe_GST_Hand : BOOL ;	
  TX_MAX_SPEICHERPLATZ : BYTE ;	
  TX_DREHST_POS_GUELTIG : BOOL ;	
  TX_DREHST_VORW_SPPL_1 : BOOL ;	
  TX_DREHST_VORW_SPPL_2 : BOOL ;	
  TX_DREHST_VORW_SPPL_3 : BOOL ;	
  TX_DREHST_VORW_SPPL_4 : BOOL ;	
  TX_DREHST_VORW_SPPL_5 : BOOL ;	
  TX_DREHST_IN_SPEICH_POS : BOOL ;	
  TX_DREHST_FRG_SPEICH_POS : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =RS4 / RS5 vorhanden

      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS4_AKTIV; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      SPBN  MEND; 
NETWORK
TITLE =Anzahl maximaler Plätze RS4 / RS5

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS4_AKTIV; 
      SPBN  X001; 
      L     7; 
      SPA   X002; 
X001: L     8; 
X002: T     #TX_MAX_SPEICHERPLATZ; 
NETWORK
TITLE =Freigabe Drehstation
//RS5 kann auch oben drehen
      U(    ; 
      U     "I_PS_DREHST_HUB_UNTEN"; 
      UN    "I_PS_DREHST_HUB_OBEN"; 
      O     ; // "DB_SIEM_BAG".E_JOG
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      U     "I_PS_DREHST_HUB_OBEN"; 
      UN    "I_PS_DREHST_HUB_UNTEN"; 
      )     ; 
      U     "MX_Transport_Gst"; 
      U     "M_Handbetrieb"; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      =     #Tx_Frg_Dst; 
NETWORK
TITLE =Freigabe Hub

      U(    ; 
      U(    ; 
      U     "MX_Transport_Ausgef"; 
      U(    ; 
      O     "M_DS_Pos_2"; 
      O     "M_DS_Pos_3"; 
      O     "M_DS_Pos_4"; 
      O     "M_DS_Pos_5"; 
      O     "M_DS_Pos_6"; 
      O     "M_DS_Pos_7"; 
      O     "M_DS_Pos_8"; 
      )     ; 
      O     "M_RS_EINRICHTEN_AKTIV"; 
      )     ; 
      U(    ; 
      U     "I_PW_RP_GELOEST"; 
      UN    "DB_PLC_MD_DB20".UDHex._17_BIT7_RP_HYD_KLEMMEN; 
      O     ; 
      U     "I_PW_RPL_PAL_GELOEST"; 
      U     "DB_PLC_MD_DB20".UDHex._17_BIT7_RP_HYD_KLEMMEN; 
      O     "DB_PLC_MD_DB20".UDHex._33_Bit7_RPL_keine_Klemm; 
      O     "M_DS_Pos_3"; 
      O     "M_DS_Pos_4"; 
      O     "M_DS_Pos_5"; 
      O     "M_DS_Pos_6"; 
      O     "M_DS_Pos_7"; 
      O     "M_DS_Pos_8"; 
      )     ; 
      O     "MX_Transport_Gst"; 
      )     ; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      U     "I_PS_RPL_TUER_VORNE_ZU"; 
      UN    "I_PS_RPL_TUER_HINTEN_ZU"; 
      U     "DB_LSSchnittstelle".IX_Ruestplatz_UebergabeP; 
      =     #Tx_Frg_Hub; 
NETWORK
TITLE =Freigabe Transport

      U(    ; 
      O     "M_DS_Pos_2"; 
      O     ; 
      U     "M_DS_Pos_3"; 
      U     "DB_PW_DATA".In.IX_PW_Hub_unten; 
      U(    ; 
      O     "DB_PW_DATA".bPW_0Grad; // nur bei RS5
      O     "DB_PW_DATA".bPW_180Grad; 
      )     ; 
      O     "M_DS_Pos_4"; // "DB_SIEM_BAG".E_JOG
      O     "M_DS_Pos_5"; 
      O     "M_DS_Pos_6"; 
      O     "M_DS_Pos_7"; 
      O     ; 
      U     "M_DS_Pos_8"; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      O     "M_RS_EINRICHTEN_AKTIV"; 
      )     ; 
      U(    ; 
      U     "I_PS_DREHST_HUB_OBEN"; 
      U(    ; 
      ON    "I_PS_DREHST_PAL_VORH"; 
      ON    "MX_Transport_Gst"; 
      )     ; 
      O     ; 
      U     "I_PS_DREHST_HUB_UNTEN"; 
      UN    "I_PS_DREHST_PAL_GST"; 
      )     ; 
      U     "M_Handbetrieb"; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      U     "I_PS_RPL_TUER_VORNE_ZU"; 
      UN    "I_PS_RPL_TUER_HINTEN_ZU"; 
      U     "DB_LSSchnittstelle".IX_Ruestplatz_UebergabeP; 
      =     #Tx_Frg_Trans; 
NETWORK
TITLE =Freigabe Transport Arbeitsstellung

      U     #Tx_Frg_Trans; 
      U(    ; 
      ON    "MX_Transport_Gst"; 
      ON    "I_PS_DREHST_PAL_VORH"; 
      ON    "I_PS_DREHST_PAL_GST"; 
      )     ; 
      U(    ; 
      ON    "I_PS_DREHST_HUB_OBEN"; 
      ON    "MX_Transport_Gst"; 
      ON    "I_PS_DREHST_PAL_VORH"; 
      )     ; 
      U(    ; 
      ON    "I_PS_DREHST_HUB_UNTEN"; 
      ON    "MX_Transport_Gst"; 
      ON    "I_PS_DREHST_PAL_GST"; 
      )     ; 
      =     #Tx_Frg_Trans_AST; 
NETWORK
TITLE =Freigabe Transport Grundstellung

      U     #Tx_Frg_Trans; 
      =     #Tx_Frg_Trans_GST; 
NETWORK
TITLE =Jog Freigabe

      U(    ; 
      O     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; // "DB_SIEM_BAG".E_JOG
      O     ; 
      U     "DB_PLC_MD_DB20".UDHex._05_BIT2_SAFTY_ABNAHME; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      )     ; 
      U     "M_Handbetrieb"; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      U     "M_LS_Ax_10_11_Freig"; 
      =     #Tx_Jog_Freig; 
NETWORK
TITLE =Freigabe Klemmung

      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      O     "I_PS_RPL_TUER_HINTEN_ZU"; 
      =     #Tx_Frg_Klm; 
      =     #Tx_Frg_Klp; 

NETWORK
TITLE =Drehstation Achse Jog minus

      U     "DB_EINRICHTFUNKTION".Bewegung_37_Minus; 
      U     #Tx_Jog_Freig; 
      =     "DB_Achs9_DrehSt".MX_FahrAnf_Minus; 
NETWORK
TITLE =Drehstation Achse Jog plus

      U     "DB_EINRICHTFUNKTION".Bewegung_37_Plus; 
      U     #Tx_Jog_Freig; 
      =     "DB_Achs9_DrehSt".MX_FahrAnf_Plus; 
NETWORK
TITLE =Transport Achse Jog minus

      U     "DB_EINRICHTFUNKTION".Bewegung_38_Minus; 
      U     #Tx_Jog_Freig; 
      =     "DB_Achs10_Transport".MX_FahrAnf_Minus; 
NETWORK
TITLE =Transport Achse Jog plus

      U     "DB_EINRICHTFUNKTION".Bewegung_38_Plus; 
      U     #Tx_Jog_Freig; 
      =     "DB_Achs10_Transport".MX_FahrAnf_Plus; 
NETWORK
TITLE =Reset
//
//
      U(    ; 
      O     "M_RS_RETTUNG_AKTIV"; 
      FN    "M_RS_SKR_NEG"; 
      O(    ; 
      U     "M_RS_RETTUNG_AKTIV"; 
      FP    "MX_SKR_FP_RS"; 
      )     ; 
      )     ; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      SPBN  SKR; 

      SET   ; 
      R     "DB_Achs9_DrehSt".MX_FahrAnf_Plus; 
      R     "DB_Achs9_DrehSt".MX_FahrAnf_Minus; 
      R     "DB_Achs10_Transport".MX_FahrAnf_Plus; 
      R     "DB_Achs10_Transport".MX_FahrAnf_Minus; 
      R     "MX_SKR_DSt_Req"; 

      L     0; 
      T     "MB_SKZ_Transport"; 
      T     "MB_SKZ_Drehstation"; 
      T     "DB_Achs9_DrehSt".MB_SKZ_Achse; 
      T     "DB_Achs10_Transport".MB_SKZ_Achse; 

      U     "M_RS_RETTUNG_AKTIV"; // nur wenn SKR Aktiv
      U     "M_LS_Ax_10_11_Freig"; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      SPBN  MEND; 

      SET   ; 
      =     "DB_LSSchnittstelle".IX_Holen_abgebrochen; 
      =     "DB_LSSchnittstelle".IX_Bringen_abgebrochen; 

      L     0; 
      T     "DB_LS_POS".SKZ_Transport_von; 
      T     "DB_LS_POS".SKZ_Transport_nach; 

SKR:  U     "M_RS_RETTUNG_AKTIV"; 
      UN    "DB_LSSchnittstelle".IX_Bringen_laeuft; 
      UN    "DB_LSSchnittstelle".IX_Holen_laeuft; 
      SPBN  MEND; 
NETWORK
TITLE =Alarm Rüstplatztüre nicht geschlossen

      UN    "I_PS_RPL_TUER_VORNE_ZU"; 
      UN    "I_PS_RPL_TUER_HINTEN_ZU"; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async3._701722_RP_Tuer_offen; 
NETWORK
TITLE =Hub Bewegung

      O     "DB_EINRICHTFUNKTION".Bewegung_36_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_36_Plus; 
      FN    "M_RS_FP_Oem"; 
      SPBN  HB01; 
      U     "I_PS_DREHST_HUB_OBEN"; 
      =     "O_PS_DREHST_HUB_AUF"; 
      U     "I_PS_DREHST_HUB_UNTEN"; 
      =     "O_PS_DREHST_HUB_AB"; 
      S     "DB_LS_POS".Hub_Unten; 

HB01: U     "DB_EINRICHTFUNKTION".Bewegung_36_Minus; 
      U     #Tx_Frg_Hub; 
      S     "O_PS_DREHST_HUB_AUF"; 
      R     "O_PS_DREHST_HUB_AB"; 
      R     "DB_LS_POS".Hub_Unten; 

      U     "DB_EINRICHTFUNKTION".Bewegung_36_Plus; 
      U     #Tx_Frg_Hub; 
      S     "O_PS_DREHST_HUB_AB"; 
      R     "O_PS_DREHST_HUB_AUF"; 
NETWORK
TITLE =Drehstation RS4
//Bei RS 4 werden die 4 Speicherpositionen über Zeile 33 und 34 angefahren
//
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS4_AKTIV; 
      SPBN  Y001; 

      O     "DB_EINRICHTFUNKTION".Bewegung_32_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_32_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_33_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_33_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_34_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_34_Plus; 
      FN    "M_RS_FP_Oem2"; 
      O(    ; 
      U     "M_LS_Ax_10_11_Freig"; 
      FN    "M_RS_FN_Oem3"; 
      )     ; 
      O     "M_Reset_Taste"; 
      S     "DB_Achs9_DrehSt".MX_Abbruch_Positionieren; 


// 0:Maschine,1:Überpl1,2:Überpl2,3:Transportw,4:Ruepl,5-xx:Speicherpl 1 -- xx 
      U     "DB_EINRICHTFUNKTION".Bewegung_32_Minus; 
      SPBN  DR01; 
      L     4; 
      SPA   DR10; 

DR01: U     "DB_EINRICHTFUNKTION".Bewegung_32_Plus; 
      SPBN  DR02; 
      L     1; 
      SPA   DR10; 

DR02: U     "DB_EINRICHTFUNKTION".Bewegung_33_Minus; 
      SPBN  DR03; 
      L     5; 
      SPA   DR10; 

DR03: U     "DB_EINRICHTFUNKTION".Bewegung_33_Plus; 
      SPBN  DR04; 
      L     6; 
      SPA   DR10; 

DR04: U     "DB_EINRICHTFUNKTION".Bewegung_34_Minus; 
      SPBN  DR05; 
      L     7; 
      SPA   DR10; 

DR05: U     "DB_EINRICHTFUNKTION".Bewegung_34_Plus; 
      SPBN  DR06; 
      L     8; 
      SPA   DR10; 

DR06: L     0; 
      T     #Tb_Platz; 
      SET   ; 
      R     "MX_SKR_DSt_Req"; 
      SPA   DR11; 

DR10: T     #Tb_Platz; 
      U     #Tx_Frg_Dst; 
      S     "MX_SKR_DSt_Req"; 

DR11: NOP   0; 

      CALL "FC_PS_DrehSt_Pos" (
           EW_Platz                 := #Tb_Platz,
           EX_Fahrbefehl            := "MX_SKR_DSt_Req",
           EAX_Fahrbefehl_Fertig    := "MX_RS_DrehSt_Fertig");

      U     "MX_RS_DrehSt_Fertig"; 
      R     "MX_RS_DrehSt_Fertig"; 

      SPA   Y002; 
NETWORK
TITLE =Drehstation RS5
//Bei RS5 werden die 5 Speicherpositionen über Anwahl im Eingabefeld in Zeile 31  
//angefahren
//Drehstation Vorwahl Speicherposition gültig
Y001: U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     4; 
      >=I   ; 
      )     ; 
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     8; 
      <=I   ; 
      )     ; 
      =     "M_RS_DREHST_POS_GUELTIG"; 

//Drehstation Eintrag 0 bei ungültiger Speicherplatzvorwahl
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     0; 
      <>I   ; 
      )     ; 
      UN    "M_RS_DREHST_POS_GUELTIG"; 
      SPBNB _001; 
      L     0; 
      T     "DB_OEM".PS_DREHST_ZUR_POSITION; 
_001: NOP   0; 

// Drehstation Speicherplatzvorwahl
      U     "M_RS_DREHST_POS_GUELTIG"; 
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     4; 
      ==I   ; 
      )     ; 
      =     #TX_DREHST_VORW_SPPL_1; 

      U     "M_RS_DREHST_POS_GUELTIG"; 
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     5; 
      ==I   ; 
      )     ; 
      =     #TX_DREHST_VORW_SPPL_2; 

      U     "M_RS_DREHST_POS_GUELTIG"; 
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     6; 
      ==I   ; 
      )     ; 
      =     #TX_DREHST_VORW_SPPL_3; 

      U     "M_RS_DREHST_POS_GUELTIG"; 
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     7; 
      ==I   ; 
      )     ; 
      =     #TX_DREHST_VORW_SPPL_4; 

      U     "M_RS_DREHST_POS_GUELTIG"; 
      U(    ; 
      L     "DB_OEM".PS_DREHST_ZUR_POSITION; 
      L     8; 
      ==I   ; 
      )     ; 
      =     #TX_DREHST_VORW_SPPL_5; 

// Drehstation Bewegung
      O     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_32_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_32_Plus; 
      FN    "M_RS_FP_Oem2"; 
      O(    ; 
      U     "M_LS_Ax_10_11_Freig"; 
      FN    "M_RS_FN_Oem3"; 
      )     ; 
      O     "M_Reset_Taste"; 
      S     "DB_Achs9_DrehSt".MX_Abbruch_Positionieren; 


// 0:Maschine,1:Überpl1,2:Überpl2,3:Transportw,4:Ruepl,5-xx:Speicherpl 1 -- xx 
      U     "DB_EINRICHTFUNKTION".Bewegung_32_Minus; 
      UN    "I_PS_RPL_TUER_HINTEN_ZU"; 
      SPBN  M003; 
      L     4; 
      SPA   M004; 

M003: U     "DB_EINRICHTFUNKTION".Bewegung_32_Plus; 
      SPBN  SP01; 
      L     1; 
      SPA   M004; 

SP01: U     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      U     #TX_DREHST_VORW_SPPL_1; 
      SPBN  SP02; 
      L     5; 
      SPA   M004; 

SP02: U     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      U     #TX_DREHST_VORW_SPPL_2; 
      SPBN  SP03; 
      L     6; 
      SPA   M004; 

SP03: U     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      U     #TX_DREHST_VORW_SPPL_3; 
      SPBN  SP04; 
      L     7; 
      SPA   M004; 

SP04: U     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      U     #TX_DREHST_VORW_SPPL_4; 
      SPBN  SP05; 
      L     8; 
      SPA   M004; 

SP05: U     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      U     #TX_DREHST_VORW_SPPL_5; 
      SPBN  M009; 
      L     9; 
      SPA   M004; 

M009: L     0; 
      T     #Tb_Platz; 
      SET   ; 
      R     "MX_SKR_DSt_Req"; 
      SPA   M010; 

M004: T     #Tb_Platz; 

      U     #Tx_Frg_Dst; 
      U(    ; 
      U     "M_RS_DREHST_POS_GUELTIG"; 
      O     "DB_EINRICHTFUNKTION".Bewegung_32_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_32_Plus; 
      )     ; 
      S     "MX_SKR_DSt_Req"; 

M010: NOP   0; 

      CALL "FC_PS_DrehSt_Pos" (
           EW_Platz                 := #Tb_Platz,
           EX_Fahrbefehl            := "MX_SKR_DSt_Req",
           EAX_Fahrbefehl_Fertig    := "MX_RS_DrehSt_Fertig");

      U     "MX_RS_DrehSt_Fertig"; 
      R     "MX_RS_DrehSt_Fertig"; 
NETWORK
TITLE =Transport

Y002: O     "DB_EINRICHTFUNKTION".Bewegung_35_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_35_Plus; 
      FN    "M_RS_FP_Oem4"; 
      O(    ; 
      U     "M_LS_Ax_10_11_Freig"; 
      FN    "M_RS_FN_Oem5"; 
      )     ; 
      O     "M_Reset_Taste"; 
      S     "DB_Achs10_Transport".MX_Abbruch_Positionieren; 

      U     "DB_EINRICHTFUNKTION".Bewegung_35_Minus; 

      U     #Tx_Frg_Trans_AST; 
      SPBN  TP01; 
      L     2; 
      SPA   TP10; 

TP01: U     "DB_EINRICHTFUNKTION".Bewegung_35_Plus; 

      U     #Tx_Frg_Trans_GST; 
      SPBN  TP06; 
      L     1; 
      SPA   TP10; 

TP06: L     0; 
      T     #Tb_Platz; 
      SET   ; 
      R     "MX_SKR_Trp_Req"; 
      SPA   TP11; 

TP10: T     #Tb_Platz; 
      U     #Tx_Frg_Trans; 
      S     "MX_SKR_Trp_Req"; 

TP11: NOP   0; 

      CALL "FC_PS_Transp_Pos" (
           Eb_Position              := #Tb_Platz,
           EX_Fahrbefehl            := "MX_SKR_Trp_Req",
           EAX_Fahrbefehl_Fertig    := "MX_RS_Tansport_Fertig");

      U     "MX_RS_Tansport_Fertig"; 
      R     "MX_RS_Tansport_Fertig"; 


NETWORK
TITLE =Freigabe RPL Palettenklemmung loesen/klemmen in Bild Anwender

      U(    ; 
      O     "I_SP_RPL_ENTKUPPELT"; 
      ON    "DB_PLC_MD_DB20".UDHex._27_Bit3_RPLmitKupplung; 
      )     ; 
      U     "DB_OEM".OEM_ANWENDER; 
      U     "I_PS_RPL_TUER_HINTEN_ZU"; 
      =     #t_Freigabe_AST_Hand; 
      =     #t_Freigabe_GST_Hand; 
NETWORK
TITLE =Ruestplatz Palettenklemmung loesen
// Klemmung Rüstplatz
      O     "DB_EINRICHTFUNKTION".Bewegung_39_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_39_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_211_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_211_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_139_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_139_Plus; 
      FN    "HM_NI_Bew_39_211"; 
      SPBN  MD01; 
      U     "I_PW_RPL_PAL_GELOEST"; 
      =     "O_PW_RPL_PAL_LOESEN"; 
      U     "I_PW_RPL_PAL_GEKLEMMT"; 
      =     "O_PW_RPL_PAL_KLEMMEN"; 

MD01: U(    ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_39_Minus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_211_Minus; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_139_Minus; 
      U     #t_Freigabe_AST_Hand; 
      )     ; 
      U     #Tx_Frg_Klp; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_211_Minus; 
      U     "I_SP_RPL_ENTKUPPELT"; 
      S     "O_PW_RPL_PAL_LOESEN"; 
      R     "O_PW_RPL_PAL_KLEMMEN"; 

      U(    ; 
      O     "DB_EINRICHTFUNKTION".Bewegung_39_Plus; 
      O     "DB_EINRICHTFUNKTION".Bewegung_211_Plus; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_139_Plus; 
      U     #t_Freigabe_GST_Hand; 
      )     ; 
      U     #Tx_Frg_Klp; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_211_Plus; 
      U     "I_SP_RPL_ENTKUPPELT"; 
      R     "O_PW_RPL_PAL_LOESEN"; 
      S     "O_PW_RPL_PAL_KLEMMEN"; 

MEND: NOP   0; 
NETWORK
TITLE =OEM Einrichten aktiv

      U     "M_RS_RETTUNG_AKTIV"; 
      SPBN  ENDE; 
NETWORK
TITLE =Fahrbefehl Achse 9

      O     "DB_ACHSE9".E_TCMinus; 
      O     "DB_ACHSE9".E_TCPlus; 
      =     #FB_Achse_9; 

NETWORK
TITLE =RS5 Anzeige für Drehstation Speicherpositionen
//Bei RS5 werden die 5 Speicherpositionen über Anwahl im Eingabefeld in Zeile 31  
//angefahren
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      SPBN  PO23; 

//Drehstation am vorgewählten Speicherplatz
      U     "M_RS_DREHST_POS_GUELTIG"; 
      U(    ; 
      U     #TX_DREHST_VORW_SPPL_1; 
      U     "M_DS_Pos_4"; 
      O     ; 
      U     #TX_DREHST_VORW_SPPL_2; 
      U     "M_DS_Pos_5"; 
      O     ; 
      U     #TX_DREHST_VORW_SPPL_3; 
      U     "M_DS_Pos_6"; 
      O     ; 
      U     #TX_DREHST_VORW_SPPL_4; 
      U     "M_DS_Pos_7"; 
      O     ; 
      U     #TX_DREHST_VORW_SPPL_5; 
      U     "M_DS_Pos_8"; 
      )     ; 
      =     #TX_DREHST_IN_SPEICH_POS; 

//Fahrbefehl Drehachse aktiv
      O     "DB_ACHSE9".E_TCMinus; 
      O     "DB_ACHSE9".E_TCPlus; 
      =     #FB_Achse_9; 

//Drehstation fährt zur Speicherpositionen
      U     "DB_EINRICHTFUNKTION".Bewegung_31_Plus; 
      U     #FB_Achse_9; 
      =     #Ventil_GST; 

//Drehstation Speicherpositionen Freigabe
      U     "M_RS_DREHST_POS_GUELTIG"; 
      U     #Tx_Frg_Dst; 
      =     #TX_DREHST_FRG_SPEICH_POS; 

//Anzeige für Drehstation Speicherpositionen
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := "m_null",
           Freigabe_GST_Hand        := #TX_DREHST_FRG_SPEICH_POS,
           Endlage_AST              := "m_null",
           Endlage_GST              := #TX_DREHST_IN_SPEICH_POS,
           Ventil_AST               := "m_null",
           Ventil_GST               := #Ventil_GST,
           Bewegungsnummer          := 31);

NETWORK
TITLE =Anzeige für Drehstation Position 2 (Rüstplatz) und 3 (Übergabe)

PO23: U     "DB_EINRICHTFUNKTION".Bewegung_32_Minus; 
      U     #FB_Achse_9; 
      =     #Ventil_AST; 

      U     "DB_EINRICHTFUNKTION".Bewegung_32_Plus; 
      U     #FB_Achse_9; 
      =     #Ventil_GST; 

// Freigabe zum Rüstplatz
      U     #Tx_Frg_Dst; 
      U(    ; 
      UN    "I_PS_RPL_TUER_HINTEN_ZU"; 
      ON    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS5_AKTIV; 
      )     ; 
      =     "m_vke_uebertrag_1"; 

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := "m_vke_uebertrag_1",
           Freigabe_GST_Hand        := #Tx_Frg_Dst,
           Endlage_AST              := "M_DS_Pos_2",
           Endlage_GST              := "M_DS_Pos_3",
           Ventil_AST               := #Ventil_AST,
           Ventil_GST               := #Ventil_GST,
           Bewegungsnummer          := 32);

NETWORK
TITLE =RS4 Anzeige für Drehstation Position 4 und 5
//Bei RS 4 werden die 4 Speicherpositionen über Zeile 33 und 34 angefahren
//
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.RS4_AKTIV; 
      SPBN  TRSP; 

      U     "DB_EINRICHTFUNKTION".Bewegung_33_Minus; 
      U     #FB_Achse_9; 
      =     #Ventil_AST; 

      U     "DB_EINRICHTFUNKTION".Bewegung_33_Plus; 
      U     #FB_Achse_9; 
      =     #Ventil_GST; 

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Frg_Dst,
           Freigabe_GST_Hand        := #Tx_Frg_Dst,
           Endlage_AST              := "M_DS_Pos_4",
           Endlage_GST              := "M_DS_Pos_5",
           Ventil_AST               := #Ventil_AST,
           Ventil_GST               := #Ventil_GST,
           Bewegungsnummer          := 33);

NETWORK
TITLE =RS4 Anzeige für Drehstation Position 6 und 7
//Bei RS 4 werden die 4 Speicherpositionen über Zeile 33 und 34 angefahren
//
      U     "DB_EINRICHTFUNKTION".Bewegung_34_Minus; 
      U     #FB_Achse_9; 
      =     #Ventil_AST; 

      U     "DB_EINRICHTFUNKTION".Bewegung_34_Plus; 
      U     #FB_Achse_9; 
      =     #Ventil_GST; 

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Frg_Dst,
           Freigabe_GST_Hand        := #Tx_Frg_Dst,
           Endlage_AST              := "M_DS_Pos_6",
           Endlage_GST              := "M_DS_Pos_7",
           Ventil_AST               := #Ventil_AST,
           Ventil_GST               := #Ventil_GST,
           Bewegungsnummer          := 34);

NETWORK
TITLE =Anzeige für Transport-Achse Position Grundstellung und Ausgef.

TRSP: CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Frg_Trans_AST,
           Freigabe_GST_Hand        := #Tx_Frg_Trans_GST,
           Endlage_AST              := "MX_Transport_Ausgef",
           Endlage_GST              := "MX_Transport_Gst",
           Ventil_AST               := "DB_ACHSE10".E_TCPlus,
           Ventil_GST               := "DB_ACHSE10".E_TCMinus,
           Bewegungsnummer          := 35);
      NOP   0; 

NETWORK
TITLE =Hub Speicher

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Frg_Hub,
           Freigabe_GST_Hand        := #Tx_Frg_Hub,
           Endlage_AST              := "I_PS_DREHST_HUB_OBEN",
           Endlage_GST              := "I_PS_DREHST_HUB_UNTEN",
           Ventil_AST               := "O_PS_DREHST_HUB_AUF",
           Ventil_GST               := "O_PS_DREHST_HUB_AB",
           Bewegungsnummer          := 36);

NETWORK
TITLE =Anzeige für Drehstation +-

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Jog_Freig,
           Freigabe_GST_Hand        := #Tx_Jog_Freig,
           Endlage_AST              := "m_null",
           Endlage_GST              := "m_null",
           Ventil_AST               := "DB_ACHSE9".E_TCMinus,
           Ventil_GST               := "DB_ACHSE9".E_TCPlus,
           Bewegungsnummer          := 37);

NETWORK
TITLE =Anzeige für Transport-Achse +-

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Jog_Freig,
           Freigabe_GST_Hand        := #Tx_Jog_Freig,
           Endlage_AST              := "m_null",
           Endlage_GST              := "m_null",
           Ventil_AST               := "DB_ACHSE10".E_TCMinus,
           Ventil_GST               := "DB_ACHSE10".E_TCPlus,
           Bewegungsnummer          := 38);

NETWORK
TITLE =Ruestplatz Palettenklemmung loesen/klemmen in Bild Speicher 2

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Frg_Klm,
           Freigabe_GST_Hand        := #Tx_Frg_Klm,
           Endlage_AST              := "I_PW_RPL_PAL_GELOEST",
           Endlage_GST              := "I_PW_RPL_PAL_GEKLEMMT",
           Ventil_AST               := "O_PW_RPL_PAL_LOESEN",
           Ventil_GST               := "O_PW_RPL_PAL_KLEMMEN",
           Bewegungsnummer          := 39);


NETWORK
TITLE =Ruestplatz Palettenklemmung loesen/klemmen in Bild Anwender
//Bild-Nr: 24
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #t_Freigabe_AST_Hand,
           Freigabe_GST_Hand        := #t_Freigabe_GST_Hand,
           Endlage_AST              := "I_PW_RPL_PAL_GELOEST",
           Endlage_GST              := "I_PW_RPL_PAL_GEKLEMMT",
           Ventil_AST               := "O_PW_RPL_PAL_LOESEN",
           Ventil_GST               := "O_PW_RPL_PAL_KLEMMEN",
           Bewegungsnummer          := 139);

NETWORK
TITLE =Ruestplatz Palettenklemmung loesen/klemmen in Bild SpH-RPL

      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := #Tx_Frg_Klm,
           Freigabe_GST_Hand        := #Tx_Frg_Klm,
           Endlage_AST              := "I_PW_RPL_PAL_GELOEST",
           Endlage_GST              := "I_PW_RPL_PAL_GEKLEMMT",
           Ventil_AST               := "O_PW_RPL_PAL_LOESEN",
           Ventil_GST               := "O_PW_RPL_PAL_KLEMMEN",
           Bewegungsnummer          := 211);

NETWORK
TITLE =Ruestplatz an-/ab-kuppeln im Bild SPEICHER_2
//      CALL  "FC_HMI_IO_TO_TLINE"
//       Freigabe_AST_Hand:=#Tx_Frg_RPL_ankuppeln
//       Freigabe_GST_Hand:=#Tx_Frg_Klp
//       Endlage_AST      :="I_SP_RPL_GEKUPPELT"
//       Endlage_GST      :="I_SP_RPL_ENTKUPPELT"
//       Ventil_AST       :="O_SP_RPL_KUPPELN"
//       Ventil_GST       :="O_SP_RPL_ENTKUPPELN"
//       Bewegungsnummer  :=40
NETWORK
TITLE =Anzeige Grundstellung

      U     "MX_Transport_Gst"; 
      U     "I_PS_DREHST_HUB_UNTEN"; 
      =     "DB_OEM".Speicher_GrdST; 
NETWORK
TITLE =

ENDE: NOP   0; 

END_FUNCTION

