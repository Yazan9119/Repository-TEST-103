FUNCTION_BLOCK "FB_TEMP_ANA_IN_PT100"
TITLE =
//$Revision: 1.7.1.1 $
//$Date: 2008/01/14 10:05:45CET $
//$Author: sth $

//
VERSION : 0.1


VAR_INPUT
  enable : BOOL ;
  MSST_Reset : BOOL ;	
  adress_int : INT ;	
END_VAR
VAR_OUTPUT
  Fehler_Auswertebaugruppe : BOOL ;	
  Leitungsbruch : BOOL ;	
  oberer_Grenzwert : BOOL ;	
  unterer_Grenzwert : BOOL ;	
  interner_Modulfehler : BOOL ;	
  Parametrierfehler : BOOL ;	
  Fehler_analog_Kanal_1 : BOOL ;	
  Fehler_analog_Kanal_2 : BOOL ;	
END_VAR
VAR_TEMP
  retval : INT ;	
  busy : BOOL ;	
  diag_record : ARRAY  [0 .. 63 ] OF BYTE ;	
  adress_hex : WORD ;	
END_VAR
BEGIN
NETWORK
TITLE =

      SET   ; 
      R     #Fehler_Auswertebaugruppe; 
      R     #Leitungsbruch; 
      R     #oberer_Grenzwert; 
      R     #unterer_Grenzwert; 
      R     #interner_Modulfehler; 
      R     #Parametrierfehler; 
      R     #Fehler_analog_Kanal_1; 
      R     #Fehler_analog_Kanal_2; 

      U     #enable; 
      UN    #MSST_Reset; 
      SPB   dg1; 
      BEA   ; 

NETWORK
TITLE =Auswertung Datensatz

dg1:  L     #adress_int; 
      T     #adress_hex; 

      CALL "DPNRM_DG" (
           REQ                      := #enable,
           LADDR                    := #adress_hex,//Diagnoseadresse 
           RET_VAL                  := #retval,//Fehler oder Datensatzlänge
           RECORD                   := #diag_record,//Datenablagefeld
           BUSY                     := #busy);
      U     #busy; 

      BEB   ; 

      U(    ; 
      L     #retval; //Übertragene Daten
      L     35; //keine Fehlerdaten
      ==I   ; 
      )     ; 
      O(    ; 
      L     #diag_record[35]; //Modulnummer
      L     W#16#9A; // Nr. 27
      <>I   ; 
      )     ; 
      BEB   ; //wenn kein Fehler im Modul 27 vorliegt

//Auswertung Datensatz

      SET   ; 
      S     #Fehler_Auswertebaugruppe; 

      L     #diag_record[36]; 
      L     2#1000000; //Fehler im Kanal 1
      ==I   ; 
      S     #Fehler_analog_Kanal_1; 
      TAK   ; 
      L     2#1000001; //Fehler im Kanal 2
      ==I   ; 
      S     #Fehler_analog_Kanal_2; 

      L     #diag_record[37]; 
      SLW   11; 
      SRW   11; 
      L     2#110; //Leitungsbruch
      ==I   ; 
      =     #Leitungsbruch; 
      TAK   ; 
      L     2#111; //oberer Grenzwert überschritten
      ==I   ; 
      =     #oberer_Grenzwert; 
      TAK   ; 
      L     2#1000; //unterer Grenzwert unterschritten
      ==I   ; 
      =     #unterer_Grenzwert; 
      TAK   ; 
      L     2#1001; //interner Modulfehler
      ==I   ; 
      =     #interner_Modulfehler; 
      TAK   ; 
      L     2#10000; //Parametrierfehler
      ==I   ; 
      =     #Parametrierfehler; 

      BE    ; 
END_FUNCTION_BLOCK

