FUNCTION "FC_OP_Main" : VOID
TITLE =
//$Revision: 1.14 $
//$Date: 2008/09/08 12:24:33CEST $
//$Author: hgc $
//
//
//Funktionesbeschreibung:
//Logbuch:
//V0.1 /30.01.00/ HS - Modul angelegt
//
//V0.2 /18.05.00/ SJA -Aufruf FC243 (Spaenefoerdere, Spulepistole)
//V0.3 /25.05.00/ SJA -Aufruf FC244 (M20)
//V0.4 /30.05.00/ SJA -Aufruf FC245 (M80-M89)  
// 
//KNOW_HOW_PROTECT 
// (falls angegeben, kann im generierten Block keine Änderungen mehr vorgenommen 
//werden)
{ S7_language := '7(1) Deutsch (Deutschland)  20.02.2008  14:02:12' }
AUTHOR : HS
FAMILY : CHAN
NAME : UT_Main
VERSION : 0.1


VAR_INPUT
  E_EN : BOOL ;	//Enable / Modul-Freigabe
END_VAR
VAR_TEMP
  Anwahl_TK : BOOL ;	
  t_Feed_on : BOOL ;	
  t_PLC_Fehler : BOOL ;	
  t_oper_or_apc_door : BOOL ;	
  t_Crane_Movement_allowed : BOOL ;	
  tx_RPL_Tuer_offen_links : BOOL ;	//Rüstplatz-Tür links offen
  tx_RPL_Tuer_offen_rechts : BOOL ;	//Rüstplatz-Tür rechts offen
  tb_Poti_Vorschub : BYTE ;	
  tb_Poti_Eilgang : BYTE ;	
  tx_Poti_grosser_75 : BOOL ;	
  tb_Spi_Override : BYTE ;	
END_VAR
BEGIN
NETWORK
TITLE =Modul Freigabe
// Netzwerk-Übersicht
// ==================
// NW 1: Modul Freigabe
// NW 2: Spaenefoerderer, Spuelpistole, 
// NW 3: M190 Modul
// NW 4: Freigabe NC_START/ PW_FREIGABE
// NW 5: Betriebsart Automatik gesperrt Palettenspeichersystem
// NW 6: Handlingsystem Ausbaustufe FASTEMS
// NW 7: Ausbaustufe FASTEMS vorhanden ?
// NW 8: FASTEMS Start
// NW 9: FASTEMS Movement ok
// NW 10: Temporär #t_Feed_on
// NW 11: Temporär #t_PLC_Fehler
// NW 12: Temporär #t_oper_or_apc_door
// NW 13: Temporär #t_Crane_Movement_allowed
// NW 14: Leitrechner gesteuerter NC_START 
// NW 15: Interface to FASTEMS 
// NW 16: PF Werkzeugwechsel für RPC
// NW 17: T Nummer von Spindel umkopieren
// NW 18: "DB_CTS".Magazinnummer 
// NW 19: "DB_CTS".Platznummer
// NW 20: Anforderung Werkzeug melden an Fertigungsleitrechner
// NW 21: CALL  "FC_SINRPC"
// NW 22: Anforderung Werkzeug melden an Fertigungsleitrechner
// NW 23: Handlingssystem: RP-Tuer links offen
// NW 24: Handlingssystem: RP-Tuer rechts offen
// NW 25: Handlingssystem: Roboter-Beladung mit Rüstplatz
// NW 26: Ende
      UN    #E_EN; 
      SPB   MEnd; 
NETWORK
TITLE =M-AUSGANG: Betriebsstundenzaehler Programm laeuft

      UN    "M_Hauptachsen_stehen"; 
      UN    "M_Hauptspindel_steht"; 
      O     "DB_SIEM_KANAL_1".E_ProgRunn; 
      =     "O_GEN_CNC_PGM_ACTIVE"; 
NETWORK
TITLE =

NETWORK
TITLE =Spaenefoerderer, Spuelpistole, 

      U     "m_eins"; 
      =     L      5.0; 
      BLD   103; 
      CALL "FC_OP_SPAE_SPUE" (
           E_Enabled                := L      5.0);
      NOP   0; 
NETWORK
TITLE =M190 Modul

      CALL "FC_OP_M20" ;
      NOP   0; 
NETWORK
TITLE =Freigabe NC_START/ PW_FREIGABE

      UN    "M_Einrichtbetrieb"; 
      UN    "M_3Te_Betriebsart"; 
      UN    "M_4Te_Betriebsart"; 
      UN    "I_FMS_OM_DISABLED"; 
      ON    "DB_PLC_MD_DB20".UDHex._50_Bit0_AKT_FMS; 
      =     "Mx_FMS_OM1_DISABLED"; 
NETWORK
TITLE =Betriebsart Automatik gesperrt Palettenspeichersystem

      O     "M_Einrichtbetrieb"; 
      O     "M_3Te_Betriebsart"; 
      O     "M_4Te_Betriebsart"; 
      O     "I_FMS_OM_DISABLED"; 
      =     "O_FMS_OM1_DISABLED"; 
NETWORK
TITLE =Handlingsystem Ausbaustufe FASTEMS
//3 = Fastems Palettenspeicher
//1x = CTS Vorbereitung für Fastems aus Projekt Volvo
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._027_ANWAHL_HANDLINGSYST; 
      L     13; 
      ==I   ; 
      )     ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASREMS_V3X; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      =     "DB_HR".FASTEMS.ABST_FASTEMS; 
NETWORK
TITLE =Ausbaustufe FASTEMS vorhanden ?

      UN    "DB_HR".FASTEMS.ABST_FASTEMS; 
      SPB   FEnd; 
NETWORK
TITLE =FASTEMS Start
//    U     "IX_FMS_CNC_START"
//    =     "I_FMS_NC_START"
      CALL "FC_UT_GRAY_TO_BIN" (
           Greycode                 := "MSST_Vorschub_OVR",
           Binaerwert               := #tb_Poti_Vorschub);

      CALL "FC_UT_GRAY_TO_BIN" (
           Greycode                 := "MSST_Eilgang_OVR",
           Binaerwert               := #tb_Poti_Eilgang);


      CALL "FC_UT_GRAY_TO_BIN" (
           Greycode                 := "DB_ACHSE6_SPINDEL".A_SpOR,
           Binaerwert               := #tb_Spi_Override);

NETWORK
TITLE =FASTEMS Movement ok
//      U     "IX_FMS_APC_MOVEMENT_OK"
//     =     "I_FMS_APC_MOVEMENT_OK"
      CALL "FC_OVR_PROZENT" (
           EB_Vorschub_Poti         := #tb_Poti_Vorschub,
           EB_Eilgang_Poti          := #tb_Poti_Eilgang,
           EB_Spindel_Override      := #tb_Spi_Override,
           AB_Vorschub_Prozent      := "DB_IN".EMB_FEEDRATE,
           AB_Eilgang_Prozent       := "DB_IN".EMB_RAPID_VELOCITY,
           AB_SpindelOver_Prozent   := "DB_IN".ImbSpindleOverride);


      L     75; 
      L     "DB_IN".EMB_FEEDRATE; 
      >I    ; 
      =     #tx_Poti_grosser_75; 

NETWORK
TITLE =Temporär #t_Feed_on

      UN    "M_Hauptachsen_stehen"; 
      U(    ; 
      UN    "DB_ACHSE6_SPINDEL".E_Stat; 
      U     "DB_ACHSE6_SPINDEL".E_SpNA; 
      O     ; 
      UN    "DB_ACHSE4".E_Stat; 
      U     "DB_ACHSE4".E_SpNA; 
      )     ; 
      U(    ; 
      ON    DB20.DBX   37.2; 
      O     #tx_Poti_grosser_75; 
      )     ; 
      =     #t_Feed_on; 
NETWORK
TITLE =Temporär #t_PLC_Fehler

      O     "DB_FAMILIEN_MODUL".E_Fehler.Not_Aus; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.NC_Start_Sperre; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.NC_Start_Sperre; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_Kanal; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Spindel_Halt_Kanal; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_Haupt_Achs; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Einlesesperre; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Werkzeugwechsel; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Palettenwechsel; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Kopf_schwenken; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Sonderaktion; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess1; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess2; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess3; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess4; 
      O     ; 
      U     "O_AL_LM_RD"; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_mit_Wiko; 
      =     #t_PLC_Fehler; 
NETWORK
TITLE =Temporär #t_oper_or_apc_door

      O     "M_PW_GRDST"; 
      O     "I_AL_Kabine_Verriegelt"; 
      =     #t_oper_or_apc_door; 
NETWORK
TITLE =Temporär #t_Crane_Movement_allowed

      U     "M_PW_GRDST"; 
      UN    "Mx_APC_Disable_H67"; 
      =     #t_Crane_Movement_allowed; 
NETWORK
TITLE =Leitrechner gesteuerter NC_START 
//Programmstart von Leitrechner in BA2/BA3/BA4/Deaktivierung Schluesselschalter 
//nicht moeglich.
      O     "M_Einrichtbetrieb"; 
      O     "M_3Te_Betriebsart"; 
      O     "M_4Te_Betriebsart"; 
      O     "I_FMS_OM_DISABLED"; 
      =     "O_FMS_OM1_DISABLED"; 
NETWORK
TITLE =Interface to FASTEMS 

      U     "m_null"; 
      =     L      5.0; 
      BLD   103; 
      U     "I_PW_UPL_PAL_VORH"; 
      =     L      5.1; 
      BLD   103; 
      U     #t_Crane_Movement_allowed; 
      =     L      5.2; 
      BLD   103; 
      U     "DB_PW_DATA".bPW_0Grad; 
      =     L      5.3; 
      BLD   103; 
      U     "DB_PW_DATA".bPW_180Grad; 
      =     L      5.4; 
      BLD   103; 
      U     "M_PW_Aktiv"; 
      =     L      5.5; 
      BLD   103; 
      U     #t_oper_or_apc_door; 
      =     L      5.6; 
      BLD   103; 
      U     "M_Automatik_Betrieb"; 
      U     "Mx_FMS_OM1_DISABLED"; 
      =     L      5.7; 
      BLD   103; 
      U     "DB_SIEM_KANAL_1".E_ProgRunn; 
      =     L      6.0; 
      BLD   103; 
      U     #t_Feed_on; 
      =     L      6.1; 
      BLD   103; 
      U     #t_PLC_Fehler; 
      =     L      6.2; 
      BLD   103; 
      U     "MX_FIXTURE_ON_APC"; 
      =     L      6.3; 
      BLD   103; 
      U     "MX_FIXTURE_RELEASED"; 
      =     L      6.4; 
      BLD   103; 
      U     "MX_Fixture_Locked"; 
      =     L      6.5; 
      BLD   103; 
      U     "M_MA_Maschine_Ein"; 
      =     L      7.3; 
      BLD   103; 
      U     "DB_PLC_MD_DB20".UDHex._50_Bit1_FMS_PW_one_Pal; 
      =     L      7.5; 
      BLD   103; 
      U     "I_FMS_APC_MOVEMENT_OK"; 
      =     L      8.1; 
      BLD   103; 
      U     "I_FMS_NC_START"; 
      =     L      8.2; 
      BLD   103; 
      U     "I_FMS_AUTO_POWER_OFF"; 
      =     L      8.3; 
      BLD   103; 
      U     "I_FMS_FIXTURE_RELEASE"; 
      =     L      8.4; 
      BLD   103; 
      CALL "FB_IF_FASTEMS" , "DI_FB_IF_FASTEMS" (
           MC_FMS_Reserved          := L      5.0,
           MC_FMS_plc_Pallet_on_APC := L      5.1,
           MC_FMS_plc_APC_home_pos  := L      5.2,
           MC_FMS_plc_APC_ASide_out := L      5.3,
           MC_FMS_plc_APC_BSide_out := L      5.4,
           MC_FMS_plc_PC_act        := L      5.5,
           MC_FMS_plc_oper_apc_door := L      5.6,
           MC_FMS_plc_On_line       := L      5.7,
           MC_FMS_plc_Cycle_on      := L      6.0,
           MC_FMS_plc_Feed_on       := L      6.1,
           MC_FMS_plc_Alarm         := L      6.2,
           MC_FMS_FixtureonAPC      := L      6.3,
           MC_FMS_FixtureReleased   := L      6.4,
           MC_FMS_FixtureLocked     := L      6.5,
           MC_FMS_Emergency_stop_ok := L      7.3,
           MC_FMS_APC_on_pc_rot_ok  := L      7.5,
           FMS_MC_APC_movement_ok   := L      8.1,
           FMS_MC_cycle_start       := L      8.2,
           FMS_MC_AutoPowerOffToMC  := L      8.3,
           FMS_MC_Release_Fixture   := L      8.4);

      NOP   0; 
NETWORK
TITLE =Request für WZ melden
//      
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      SPBN  n18a; 

      O     "DI_FB_IF_FASTEMS".req_tool_to_TPU_or_load; 
      O     "DI_FB_IF_FASTEMS".req_tool_from_TPU; 
      O     "DI_FB_IF_FASTEMS".req_tool_unload; 
      O     "DI_FB_IF_FASTEMS".New_Tool; 
      =     "MX_WZW_RPC_START"; 
NETWORK
TITLE =Request für WZ melden von mag in tpu oder beladen

      U     "DI_FB_IF_FASTEMS".req_tool_to_TPU_or_load; 
      U     "MX_WZW_RPC_START"; 
      UN    "DB_CTS".Req_WZ_melden; 
      SPBN  n16a; 
      L     "DI_FB_IF_FASTEMS".magnum_to_TPU_or_load; 
      T     "DB_CTS".Magazinnummer; 
      L     "DI_FB_IF_FASTEMS".platznum_to_TPU_or_load; 
      T     "DB_CTS".Platznummer; 
      S     "DB_CTS".Req_WZ_melden; 

n16a: NOP   0; 
NETWORK
TITLE =Request für WZ melden von tpu in mag 

      U     "DI_FB_IF_FASTEMS".req_tool_from_TPU; 
      U     "MX_WZW_RPC_START"; 
      UN    "DB_CTS".Req_WZ_melden; 
      SPBN  n17a; 
      L     "DI_FB_IF_FASTEMS".magnum_from_TPU; 
      T     "DB_CTS".Magazinnummer; 
      L     "DI_FB_IF_FASTEMS".platznum_from_TPU; 
      T     "DB_CTS".Platznummer; 
      S     "DB_CTS".Req_WZ_melden; 

n17a: NOP   0; 
NETWORK
TITLE =Anforderung Werkzeug melden an Fertigungsleitrechner

      U     "DI_FB_IF_FASTEMS".req_tool_unload; 
      U     "MX_WZW_RPC_START"; 
      UN    "DB_CTS".Req_WZ_melden; 
      SPBN  n18a; 
      L     "DI_FB_IF_FASTEMS".magnum3; 
      T     "DB_CTS".Magazinnummer; 
      L     "DI_FB_IF_FASTEMS".platznum3; 
      T     "DB_CTS".Platznummer; 
      S     "DB_CTS".Req_WZ_melden; 
n18a: NOP   0; 
NETWORK
TITLE =CALL  "FC_SINRPC"

      U     "DB_CTS".Req_WZ_melden; 
      =     L      5.0; 
      BLD   103; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_VORBER; 
      )     ; 
      SPBNB _001; 
      CALL "FC_SINRPC" (
           I_Req_WZ_melden          := L      5.0,
           IW_Mag_nummer            := "DB_CTS".Magazinnummer,
           IW_Platznummer           := "DB_CTS".Platznummer,
           IW_T_Nummer              := "DB_CTS".Tnummer,
           O_Ready_WZ_melden        := "DB_CTS".Ready_WZ_melden,
           EA_Pal_Nr_RP             := "DB_PAL_NR".Platz[1].Pal_Nr,
           EA_Pal_Nr_MA             := "DB_PAL_NR".Platz[0].Pal_Nr);
_001: NOP   0; 
NETWORK
TITLE =

      U     "DB_CTS".Req_WZ_melden; 
      =     L      5.0; 
      BLD   103; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASREMS_V3X; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      )     ; 
      SPBNB _003; 
      CALL "FC_FMS_MC_IF" (
           IW_Mag_Nummer            := "DB_CTS".Magazinnummer,
           IW_Platznummer           := "DB_CTS".Platznummer,
           IW_T_Nummer              := "DB_CTS".Tnummer,
           I_req_wz_melden          := L      5.0,
           O_Ready_WZ_melden        := "DB_CTS".Ready_WZ_melden);
_003: NOP   0; 
NETWORK
TITLE =Anforderung Werkzeug melden an Fertigungsleitrechner zurücksetze

      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      SPBN  en22; 
      U(    ; 
      O     "DB_CTS".Ready_WZ_melden; 
      O     "M_Reset_Taste"; 
      )     ; 
      U     "DB_CTS".Req_WZ_melden; 
      U     "DI_FB_IF_FASTEMS".req_tool_to_TPU_or_load; 
      R     "DB_CTS".Req_WZ_melden; 
      R     "DI_FB_IF_FASTEMS".req_tool_to_TPU_or_load; 
      R     "MX_WZW_RPC_START"; 
      R     "DB_CTS".Ready_WZ_melden; 

      U(    ; 
      O     "DB_CTS".Ready_WZ_melden; 
      O     "M_Reset_Taste"; 
      )     ; 
      U     "DB_CTS".Req_WZ_melden; 
      U     "DI_FB_IF_FASTEMS".req_tool_from_TPU; 
      R     "DB_CTS".Req_WZ_melden; 
      R     "DI_FB_IF_FASTEMS".req_tool_from_TPU; 
      R     "MX_WZW_RPC_START"; 
      R     "DB_CTS".Ready_WZ_melden; 

      U(    ; 
      O     "DB_CTS".Ready_WZ_melden; 
      O     "M_Reset_Taste"; 
      O     "M_READY_WZ_MELDEN"; 
      )     ; 
      U     "DB_CTS".Req_WZ_melden; 
      U     "DI_FB_IF_FASTEMS".req_tool_unload; 
      U(    ; 
      L     "DB_SINRPC".PLCReq; 
      L     0; 
      ==I   ; 
      )     ; 
      R     "DB_CTS".Req_WZ_melden; 
      R     "DI_FB_IF_FASTEMS".req_tool_unload; 
      R     "MX_WZW_RPC_START"; 
      R     "DB_CTS".Ready_WZ_melden; 
      L     S5T#1S; 
      SS    "T_Verz_WZ_entl_meld"; 

      U     "T_Verz_WZ_entl_meld"; 
      S     "M_WZV_Entladen_ende"; 
      R     "T_Verz_WZ_entl_meld"; 

en22: NOP   0; 
NETWORK
TITLE =Handlingssystem: RP-Tuer links offen

FEnd: O     "I_PW_RP_TUER_1_OFFEN"; 
      O     ; 
      U     "DB_OUT".AmxSusDoorOpen; 
      U     "DB_HR".ABST.Automatische_RPT; 
      =     #tx_RPL_Tuer_offen_links; 
NETWORK
TITLE =Handlingssystem: RP-Tuer rechts offen

      O     "I_PW_RP_TUER_2_OFFEN"; 
      O     ; 
      U     "DB_OUT".AmxSusDoorOpen; 
      U     "DB_HR".ABST.Automatische_RPT; 
      =     #tx_RPL_Tuer_offen_rechts; 
NETWORK
TITLE =Handlingssystem: Roboter-Beladung mit Rüstplatz

      CALL "FC_ROB_MIT_RPL_MAIN" (
           i_ROBOT_SS_AUTOM_EIN     := "I_ROBOT_AUTO",
           i_RPL_TUER_OFFEN_LINKS   := #tx_RPL_Tuer_offen_links,
           i_RPL_TUER_OFFEN_RECHTS  := #tx_RPL_Tuer_offen_rechts,
           i_RPL_TUER_VERRIEGELT    := "I_PW_Tueren_zu",
           i_RPL_TUER_ZU            := "I_PW_Tueren_zu",
           i_ALK_SS_AUS             := FALSE,//Eingang: Schlüsselschalter Auflagenkontrolle_AUS ist aktiv
           i_RPL_ALK1_OK            := FALSE,//Eingang: Auflagenkontrolle Spann-Stelle 1 am RPL ist OK
           i_RPL_ALK2_OK            := FALSE,//Eingang: Auflagenkontrolle Spann-Stelle 2 am RPL ist OK
           i_RPL_ALK3_OK            := FALSE,//Eingang: Auflagenkontrolle Spann-Stelle 3 am RPL ist OK
           i_RPL_ALK4_OK            := FALSE,//Eingang: Auflagenkontrolle Spann-Stelle 4 am RPL ist OK
           i_KM_RPL_DUSCHE          := FALSE,//Meldung: Kühlmittel für Dusche am RPL läuft (z.B. PLC-Ausgang=1)
           i_VORR_ABBLASEN          := FALSE,//Meldung: Luft für Vorrichtung Abblasen läuft (z.B. PLC-Ausgang=1)
           i_Schutzzaun_ZuVerr      := "I_ROBOT_PROT_DOOR_CLSD",
           i_RPL_PW_GRST            := "M_PW_GRDST",
           io_RPL_PW_Freigabe       := "DB_HR".RPT.RP_PWFG_oRSP_aktiv);

NETWORK
TITLE =Ende

MEnd: NOP   0; 

END_FUNCTION

