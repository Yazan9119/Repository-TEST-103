FUNCTION "FC_OPERATOR_PANEL" : VOID
TITLE =
//$Revision: 1.19 $
//$Date: 2008/01/08 13:25:58CET $
//$Author: hgc $
//
//Logbuch:
//23.08.00 Wenn kein M3/M4 erlaubt Request for Spindle stop 
//28.03.00 Tastenbedienung nur wenn Programm abgebrochen ansteht /AB
{ S7_language := '7(1) Deutsch (Deutschland)  08.01.2008  11:15:54' }
VERSION : 5.2


VAR_INPUT
  SpindleIFNo : BYTE ;	//spindle interface number
END_VAR
VAR_TEMP
  TX_SPINDEL_STOP : BOOL ;	
  TX_SPINDEL_LEFT : BOOL ;	
  TX_SPINDEL_RIGHT : BOOL ;	
  TX_SPINDEL_IST_AX4 : BOOL ;	
  TX_SPINDEL_IST_AX6 : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =signals to ShopMill-Interface (DB82)

      L     0; 
      T     "DB_CMM_PLC".CMM_IN.transfer_base_sig; //OP32S

      L     #SpindleIFNo; 
      T     "DB_CMM_PLC".CMM_IN.spindle_interface_number; //spindle number

NETWORK
TITLE =Spindel- und Vorschubsperre in Automatik 

      UN    "I_DM_Ta_Spindel HALT"; 
      UN    "DB_ACHSE6_SPINDEL".E_Tapping; // Gewindebohren
      U(    ; 
      O     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO
      O     "MX_MDA"; //"DB_SIEM_BAG".E_MDA
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      O(    ; // 9.7.01 SCH: Stop auch in BA Jog wenn die Spindel lief
      O     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      U(    ; 
      UN    "M_Hauptspindel_steht"; 
      O     "O_FD_BETRIEBSTUNDE"; 
      )     ; 
      )     ; 
      )     ; 
      U(    ; 
      U     "DB_SIEM_KANAL_1".E_ProgInterrupt; 
      O     "DB_SIEM_KANAL_1".E_ProgStop; 
      O(    ; 
      U     "M_Hauptachsen_stehen"; 
      UN    "M_Handbetrieb"; 
      )     ; 
      UN    "DB_SIEM_KANAL_1".E_ChanReset; 
      )     ; 
      S     "M_Spindel_Sperre_Auto"; 
      S     "M_Vorschub_Sperre_Auto"; 
NETWORK
TITLE =Reset "M_Vorschub_Sperre_Auto"

      UN    "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO
      UN    "MX_MDA"; //"DB_SIEM_BAG".E_MDA
      UN    "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      O     "MSSTst_NC_Start"; 
      O     "MSSTstReset"; 
      R     "M_Vorschub_Sperre_Auto"; 
NETWORK
TITLE =Reset "M_Spindel_Sperre_Auto"

      U(    ; 
      O     "M_Automatik_Betrieb"; //"DB_SIEM_BAG".E_AUTO
      O     "MX_MDA"; //"DB_SIEM_BAG".E_MDA
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.automatic; 
      )     ; 
      U     "MSSTst_NC_Start"; 
      U     "M_Leistung_Steht_an"; // 07.06.01 SCH: Nur bei eingeschalteter Maschine sonst lief die Spindel sofort nach schliessen der Tuer an
      O     "MSSTstReset"; 
      R     "M_Spindel_Sperre_Auto"; 

NETWORK
TITLE =Meldung Spindel Vorschub Halt, weiter mit NC-Start

      U     "M_Spindel_Sperre_Auto"; 
      U(    ; 
      UN    "DB_ACHSE6_SPINDEL".E_PosMode; 
      UN    "DB_ACHSE4".E_SpNA; 
      O     ; 
      UN    "DB_ACHSE4".E_PosMode; 
      U     "DB_ACHSE4".E_SpNA; 
      )     ; 
      =     "DB_FEHLERMELDUNG".Meldung._702029_VSSPH_Taste_Auto; 
NETWORK
TITLE =Nahtstelle Kanal 1: Stop Achsen und Spindel

      O     "M_Vorschub_Sperre_Auto"; 
      O     "M_RESET_DISABLE"; 
// SCH 17.11.04, auf bei Spindel Halt Kanal die NC-Stoppen
      O     "M_Spindel_Halt_Kanal"; 
      =     "DB_SIEM_KANAL_1".A_NCStopASp; 
NETWORK
TITLE =Spindel stop über Shop Mill Grundprogramm

      UN    "I_DM_Ta_Spindel HALT"; 
      O     "M_M5_Einrichtbetrieb"; 
      U(    ; 
      O     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      UN    "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.manual; 
      O(    ; 
      U     "MX_MDA"; 
      O     "M_Automatik_Betrieb"; 
      U     "DB_SIEM_KANAL_1".E_ChanReset; 
      )     ; 
      )     ; 
      UN    "DB_FEHLERMELDUNG".Meldung._702029_VSSPH_Taste_Auto; 
      UN    "DB_SIEM_KANAL_1".E_ProgInterrupt; 
      O     "MSSTstReset"; 
      O     "M_M5_Einrichtbetrieb"; 
      =     "DB_CMM_PLC".CMM_IN.spindle_stop; 
      =     #TX_SPINDEL_STOP; 
NETWORK
TITLE =Spindel rechts über Shop Mill Grundprogramm

      U     "I_DM_Ta Spindel rechts"; 
      UN    "DB_ACHSE6_SPINDEL".E_PosMode; 
      U     "DB_ACHSE6_SPINDEL".E_SpNA; 
      UN    "DB_SIEM_KANAL_1".E_ProgInterrupt; 
      =     L      1.0; 
      U     L      1.0; 
      U(    ; 
      U     "DB_SIEM_KANAL_1".A_DeleteDTG; 
      UN    "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.manual; 
      )     ; 
      =     "DB_CMM_PLC".CMM_IN.spindle_left; 
      U     L      1.0; 
      U     "DB_SIEM_BAG".E_JOG; 
      =     #TX_SPINDEL_LEFT; 
NETWORK
TITLE =Spindel links über Shop Mill Grundprogramm

      U     "I_DM_Ta_Spindel links"; 
      UN    "DB_ACHSE6_SPINDEL".E_PosMode; 
      U     "DB_ACHSE6_SPINDEL".E_SpNA; 
      UN    "DB_SIEM_KANAL_1".E_ProgInterrupt; 
      =     L      1.0; 
      U     L      1.0; 
      U(    ; 
      U     "DB_SIEM_KANAL_1".A_DeleteDTG; 
      UN    "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      O     "DB_CMM_PLC".CMM_OUT.base_sig.main_mode_mill.manual; 
      )     ; 
      =     "DB_CMM_PLC".CMM_IN.spindle_right; 
      U     L      1.0; 
      U     "DB_SIEM_BAG".E_JOG; 
      =     #TX_SPINDEL_RIGHT; 
NETWORK
TITLE =Spindel Start über Shop Mill Grundprogramm

      O     "DB_CMM_PLC".CMM_IN.spindle_left; 
      O     "DB_CMM_PLC".CMM_IN.spindle_right; 
      =     "DB_CMM_PLC".CMM_IN.spindle_start; 
NETWORK
TITLE =SOLUTIONLINE vorhanden ?
//Bei Solutionline ShopMill 07.10.06 ist keine PLC-Software vorhanden, die 
//Nahtstellensignale Spindel linkslauf/rechtslauf/stop werden direkt gebildet
//
//Erweiterungen der PLC - Schnittstelle ShopMill 6.4 nach ShopMill 07.10:
//Die ShopMill PLC entfällt. Die Anpassung der Nahtstellensignale ist in der 
//ShopMill Funktionsbeschreibung in Kapitel 6 beschrieben.
      UN    "DB_PLC_MD_DB20".UDHex._31_Bit1_SOLUTIONLINE; 
      SPB   ENDE; 
NETWORK
TITLE =Spindel Achsnummer

      U     "m_eins"; 
      =     L      1.0; 
      U     L      1.0; 
      U(    ; 
      L     4; 
      L     #SpindleIFNo; 
      ==I   ; 
      )     ; 
      =     #TX_SPINDEL_IST_AX4; 
      U     L      1.0; 
      U(    ; 
      L     6; 
      L     #SpindleIFNo; 
      ==I   ; 
      )     ; 
      =     #TX_SPINDEL_IST_AX6; 
NETWORK
TITLE =Nahtstelle Achse-DB: Spindel stop
//Bei Solutionline ShopMill 07.10.06 ist keine PLC-Software vorhanden, die 
//Nahtstellensignale Spindel linkslauf/rechtslauf/stop werden direkt gebildet
      U     #TX_SPINDEL_STOP; 
      =     L      1.0; 

      U     L      1.0; 
      U     #TX_SPINDEL_IST_AX4; 
      =     "DB_ACHSE4".A_StopSpindle; 
      U     L      1.0; 
      U     #TX_SPINDEL_IST_AX6; 
      =     "DB_ACHSE6_SPINDEL".A_StopSpindle; 
NETWORK
TITLE =Nahtstelle Achse-DB: Spindel rechts
//Bei Solutionline ShopMill 07.10.06 ist keine PLC-Software vorhanden, die 
//Nahtstellensignale Spindel linkslauf/rechtslauf/stop werden direkt gebildet
      U     #TX_SPINDEL_LEFT; 
      =     L      1.0; 
      U     L      1.0; 
      U     #TX_SPINDEL_IST_AX4; 
      =     "DB_ACHSE4".A_StartCCW; 
      U     L      1.0; 
      U     #TX_SPINDEL_IST_AX6; 
      =     "DB_ACHSE6_SPINDEL".A_StartCCW; 
NETWORK
TITLE =Nahtstelle Achse-DB: Spindel links
//Bei Solutionline ShopMill 07.10.06 ist keine PLC-Software vorhanden, die 
//Nahtstellensignale Spindel linkslauf/rechtslauf/stop werden direkt gebildet
      U     #TX_SPINDEL_RIGHT; 
      =     L      1.0; 
      U     L      1.0; 
      U     #TX_SPINDEL_IST_AX4; 
      =     "DB_ACHSE4".A_StartCW; 
      U     L      1.0; 
      U     #TX_SPINDEL_IST_AX6; 
      =     "DB_ACHSE6_SPINDEL".A_StartCW; 
NETWORK
TITLE =signals from ShopMill-Interface (DB82)

ENDE: U     "DB_CMM_PLC".CMM_OUT.spindle_right; 
      U     "DB_CMM_PLC".CMM_OUT.spindle_start_req; 
      =     "DM_LED Y-Achse plus"; //spindle right
      U     "DB_CMM_PLC".CMM_OUT.spindle_left; 
      U     "DB_CMM_PLC".CMM_OUT.spindle_start_req; 
      =     "DM_LED 4-Achse rechts"; //spindle left
      U     "DB_CMM_PLC".CMM_OUT.spindle_stop_req; 
      =     "DM_LED Z-Achse plus(3)"; //spindle stopped

END_FUNCTION

