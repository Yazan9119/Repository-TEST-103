FUNCTION "FC_KMTL_KNOLL" : VOID
TITLE =
//$Revision: 1.19 $
//$Date: 2008/11/26 14:14:58CET $
//$Author: fleschuf $
//
NAME : KM_VRF
VERSION : 0.1


VAR_INPUT
  I_KUEHLMITTEL_1_OK : BOOL ;	
  I_KUEHLMITTEL_2_OK : BOOL ;	
  I_MELDUNG_KSA : BOOL ;	
  I_NOT_AUS_KSA : BOOL ;	
  I_SPINDEL_VORSCHUB_HALT : BOOL ;	
  I_SPI_VORSCHUB_HALT_G0 : BOOL ;	
  I_FEHLER_SCHALTERDIAGNOS : BOOL ;	
  I_KUEHLMITTEL_AUTOMATIK : BOOL ;	
  I_MSS_KSA : BOOL ;	
  I_MSS_KSA_MELDUNG : BOOL ;	
  I_KSA_FUELLSTAND_MAX : BOOL ;	
  I_KSA_FUELLSTAND_MIN_VOR : BOOL ;	
  I_KSA_FUELLSTAND_MIN : BOOL ;	
  I_TPF_PAPIER_ENDE : BOOL ;	
  I_TPF_PAPIER_VORWARNUNG : BOOL ;	
  I_QUIT_H21 : BOOL ;	
  I_KM_MIN_MAX_NIVEAU : BOOL ;	
  I_KM_VOR_MIN_NIVEAU : BOOL ;	
  I_SF_UEBERLAUF : BOOL ;	
  IW_FEHLERNUMMER : WORD ;	
  IW_RUECKMELD_DRUCKSTUFE : WORD ;	
END_VAR
VAR_OUTPUT
  O_KABINE_GESCHLOSSEN : BOOL ;	
  O_MASCHINE_BTB : BOOL ;	
  O_ALARM_RESET : BOOL ;	
  O_KUEHLMITTEL_1_EIN : BOOL ;	
  O_KUEHLMITTEL_2_EIN : BOOL ;	
  O_PUMPE_SPRUEHPISTOL_EIN : BOOL ;	
  O_SPAENESPUEL_LASER_EIN : BOOL ;	
  O_DACHSPUELUNG_EIN : BOOL ;	
  O_VENTIL_TISCHSPU_PW_EIN : BOOL ;	
  O_SPAENESPUEL_TUER_EIN : BOOL ;	
  OX_PC_ACTIVE : BOOL ;	
  OX_TOOL_CHANGE_ACTIVE : BOOL ;	
  OX_ERROR : BOOL ;	
  OX_PROGRAM_END : BOOL ;	
  OX_STROBE_FUNCTION1 : BOOL ;	
  OW_ANWAHL_DRUCKSTUFE : WORD ;	
  OW_KSA_KONFIGURATION : WORD ;	
  O_COOLANT_BACKFLOW : BOOL ;	
  O_Pneumatic_OK : BOOL ;	
  OX_KM_MIN_MAX_NIVEAU : BOOL ;	
  OX_KM_VOR_MIN_NIVEAU : BOOL ;	
  OX_SF_UEBERLAUF : BOOL ;	
  OW_FUNCTION_1 : WORD ;	
  OW_PARAMETER_2 : WORD ;	
END_VAR
VAR_IN_OUT
  Meldung_Papierband_Vorw : BOOL ;	
  Meldung_Papierband_Ende : BOOL ;	
  Meldung_Fuell_Min : BOOL ;	
  Meldung_Fuell_Min_Vorw : BOOL ;	
  Meldung_Fuell_Max : BOOL ;	
  Meldung_MSS_KSA_Meldung : BOOL ;	
  Meldung_MSS_KSA_Fehler : BOOL ;	
  Meldung_Schalterdiagnose : BOOL ;	
  Meldung_SPI_VS_Halt_G0 : BOOL ;	
  Meldung_SPI_VS_Halt : BOOL ;	
  Meldung_Not_Aus_KSA : BOOL ;	
  Meldung_KSA_Warnung : BOOL ;	
  IO_Strobe_Druckumsch : BOOL ;	
END_VAR
VAR_TEMP
  PI_O_KUEHLMITTEL_2_EIN : BOOL ;	//pos.Flanke von O_KUEHLMITTEL_2_EIN
  MW_ANWAHL_DRUCKSTUFE_INT : INT ;	//Anwahl Druckstufe als Integerwert
  MB_ANWAHL_DRUCKST_1_geta : BYTE ;	//Anwahl Druckstufe INT Byte 1 getauscht
  MB_ANWAHL_DRUCKST_2_geta : BYTE ;	//Anwahl Druckstufe INT Byte 2 getauscht
  MW_RUECKMELD_DRUCKSTUFE : INT ;	//Rückmeldung Druckstufe als Integerwert
  MB_RUECKMELD_DRUCKST_1_g : BYTE ;	//Rückmeldung Druckstufe INT Byte 1 getauscht
  MB_RUECKMELD_DRUCKST_2_g : BYTE ;	//Rückmeldung Druckstufe INT Byte 2 getauscht
END_VAR
BEGIN
NETWORK
TITLE =KM: Ruecksetzen Fehlermeldungen
// Netzwerk-Übersicht
// ==================
// NW 1: KM: Ruecksetzen Fehlermeldungen
// NW 2: Umwandlung Druckstufe
// NW 3: Signal-Ausgabe an Kuehlmittelanlage
// NW 4: pos.Flanke von I_KUEHLMITTEL_AUTOMATIK
// NW 5: Signal-Ausgabe: #IO_Strobe_Druckumsch
// NW 6: Fehler- und Meldungsausgabe
// NW 7: 700522 "MEL, SONDEROPTION MELDUNG STEHT AN"
      U     "M_Ruecksetze_Fehler"; 
      =     #O_ALARM_RESET; 
      R     #Meldung_KSA_Warnung; 
      R     #Meldung_Not_Aus_KSA; 
      R     #Meldung_SPI_VS_Halt; 
      R     #Meldung_SPI_VS_Halt_G0; 
      R     #Meldung_Schalterdiagnose; 
      R     #Meldung_MSS_KSA_Fehler; 
      R     #Meldung_MSS_KSA_Meldung; 
      R     #Meldung_Fuell_Max; 
      R     #Meldung_Fuell_Min_Vorw; 
      R     #Meldung_Fuell_Min; 
      R     #Meldung_Papierband_Ende; 
      R     #Meldung_Papierband_Vorw; 

NETWORK
TITLE =PW_ACTIVE

      U     "M_PW_Aktiv"; 
      =     #OX_PC_ACTIVE; 
NETWORK
TITLE =TC ACTIVE

      U     "M_WZW_Aktiv"; 
      =     #OX_TOOL_CHANGE_ACTIVE; 
NETWORK
TITLE =Error
// Fehlermeldungen
      O     "DB_FAMILIEN_MODUL".E_Fehler.Not_Aus; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.NC_Start_Sperre; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.NC_Start_Sperre; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_Kanal; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Spindel_Halt_Kanal; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_Haupt_Achs; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Einlesesperre; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Werkzeugwechsel; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Palettenwechsel; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Kopf_schwenken; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Sonderaktion; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess1; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess2; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess3; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess4; 
      =     #OX_ERROR; 


NETWORK
TITLE =Programm Ende
// Programm_Ende
      U     "M_Hauptprogramm_Ende"; //"O_Programm_Ende"           // hoier muss für ARI rein M67 damit DM_PW nicht M30 meldet
      UN    "MX_Jobbetrieb"; 
      O     ; 
      U     "MX_Jobbetrieb"; 
      U     "DB_DM_M_FUNKTION".MX_M[67]; 
      =     #OX_PROGRAM_END; 


NETWORK
TITLE =Umwandlung Druckstufe

      L     "DB_H_FUNKTION".HX_Real_Wert[7]; 
      TRUNC ; 
      U     "m_UiSpi"; 
      SPBN  EUIS; 
      L     30; 
      >I    ; 
      SPB   EUIS; 
      TAK   ; 
EUIS: T     #MW_ANWAHL_DRUCKSTUFE_INT; 

//Ausgabe als INT, hier müssen die Bytes getauscht werden
      U     "DB_PLC_MD_DB20".UDHex._54_Bit0_Soko_KSA_Drucks; 
      SPBNB _001; 
      L     LB     2; 
      T     #MB_ANWAHL_DRUCKST_2_geta; 
      L     LB     3; 
      T     #MB_ANWAHL_DRUCKST_1_geta; 
      L     LW     4; 
      T     #OW_ANWAHL_DRUCKSTUFE; 
_001: NOP   0; 

//Ausgabe als BCD
      UN    "DB_PLC_MD_DB20".UDHex._54_Bit0_Soko_KSA_Drucks; 
      SPBNB _002; 
      L     #MW_ANWAHL_DRUCKSTUFE_INT; 
      DTB   ; 
      T     #OW_ANWAHL_DRUCKSTUFE; 
_002: NOP   0; 

NETWORK
TITLE =Signal-Ausgabe an Kuehlmittelanlage

      U     "I_AL_BTB_PNOZ"; 
      U     "I_AL_NEG_Not_Aus"; 
      U     "I_AL_MASCHINE_EIN"; 
      =     #O_MASCHINE_BTB; 

      U     "I_AL_Kabine_zu"; 
      U     "I_AL_Kabine_Verriegelt"; 
      =     #O_KABINE_GESCHLOSSEN; 

      U     "O_KM_M8_Ein"; 
      =     #O_KUEHLMITTEL_1_EIN; 

      U     "O_KM_M7_Ein"; 
      =     #O_KUEHLMITTEL_2_EIN; 

      U     "O_KM_Spruehpistole_ein"; 
      =     #O_PUMPE_SPRUEHPISTOL_EIN; 

      U     "O_KM_Spuelpumpe_ein"; 
      =     #O_SPAENESPUEL_LASER_EIN; 
      =     #O_SPAENESPUEL_TUER_EIN; 

      U     "O_KM_Pumpe_Dach"; 
      =     #O_DACHSPUELUNG_EIN; 

      U     "O_PW_Ventil_Tischspuel"; 
      =     #O_VENTIL_TISCHSPU_PW_EIN; 


      U     "O_SF_PUMPE_EIN"; 
      =     #O_COOLANT_BACKFLOW; 

      U     "I_AL_Druckluft_OK"; 
      =     #O_Pneumatic_OK; 

      U     #I_KM_MIN_MAX_NIVEAU; 
      =     #OX_KM_MIN_MAX_NIVEAU; 

      U     #I_KM_VOR_MIN_NIVEAU; 
      =     #OX_KM_VOR_MIN_NIVEAU; 

      U     #I_SF_UEBERLAUF; 
      =     #OX_SF_UEBERLAUF; 
NETWORK
TITLE =pos.Flanke von O_KUEHLMITTEL_2_EIN

      U     #O_KUEHLMITTEL_2_EIN; 
      FP    "DB_HR".KSA.HM_PI_KSA_Knoll_AUTO; 
      =     #PI_O_KUEHLMITTEL_2_EIN; 
NETWORK
TITLE =Signal-Ausgabe: #IO_Strobe_Druckumsch
//Mit Flanke von #I_KUEHLMITTEL_AUTOMATIK muss immer ein
//#IO_Strobe_Druckumsch erfolgen sonst bleibt der Druck "0"
//Wird IKZ ausgeschaltet so steht #I_KUEHLMITTEL_AUTOMATIK eine Nachlaufzeit an.
//Wird während der Nachlaufzeit IKZ wieder eingeschaltet, so kommt der Druck 
//korrekt. Bei gleichem Druck wurde kein Strobe abgesetzt.
//
//11.11.2005 / Riedl
//STROBE wird bei KNOLL nur als pos.Flanke ausgewertet.
//Zum Einschalten von KM2 benötigt KNOLL immer das STROBE-Signal.
//Auch während der AUTOMTIK-Nachlaufzeit bei KNOLL darf das STROBE gegeben werden.
//
//Bei Ausgabe Druckstufe als Integer
      U     "DB_PLC_MD_DB20".UDHex._54_Bit0_Soko_KSA_Drucks; 
      SPBN  INT; 

// Die Bytes müssen getauscht werden
      L     #IW_RUECKMELD_DRUCKSTUFE; 
      T     #MW_RUECKMELD_DRUCKSTUFE; 
      L     LB     6; 
      T     #MB_RUECKMELD_DRUCKST_2_g; 
      L     LW     7; 
      T     #MB_RUECKMELD_DRUCKST_1_g; 

      L     #MW_ANWAHL_DRUCKSTUFE_INT; 
      L     #MW_RUECKMELD_DRUCKSTUFE; 
      <>I   ; 
      U     #O_KUEHLMITTEL_2_EIN; 
      O     ; 
      O     #PI_O_KUEHLMITTEL_2_EIN; 
      S     #IO_Strobe_Druckumsch; 


      L     #MW_ANWAHL_DRUCKSTUFE_INT; 
      L     #MW_RUECKMELD_DRUCKSTUFE; 

      ==I   ; 
      U     #O_KUEHLMITTEL_2_EIN; 
      UN    #PI_O_KUEHLMITTEL_2_EIN; 
      O     ; 
      ON    #O_KUEHLMITTEL_2_EIN; 
      O     "M_Reset_Taste"; 
      R     #IO_Strobe_Druckumsch; 

INT:  NOP   0; 

NETWORK
TITLE =Signal-Ausgabe: #IO_Strobe_Druckumsch
//Mit Flanke von #I_KUEHLMITTEL_AUTOMATIK muss immer ein
//#IO_Strobe_Druckumsch erfolgen sonst bleibt der Druck "0"
//Wird IKZ ausgeschaltet so steht #I_KUEHLMITTEL_AUTOMATIK eine Nachlaufzeit an.
//Wird während der Nachlaufzeit IKZ wieder eingeschaltet, so kommt der Druck 
//korrekt. Bei gleichem Druck wurde kein Strobe abgesetzt.
//
//11.11.2005 / Riedl
//STROBE wird bei KNOLL nur als pos.Flanke ausgewertet.
//Zum Einschalten von KM2 benötigt KNOLL immer das STROBE-Signal.
//Auch während der AUTOMTIK-Nachlaufzeit bei KNOLL darf das STROBE gegeben werden.
//
//Bei Ausgabe Druckstufe als BCD
      UN    "DB_PLC_MD_DB20".UDHex._54_Bit0_Soko_KSA_Drucks; 
      SPBN  BCD; 

      L     #OW_ANWAHL_DRUCKSTUFE; 
      BTI   ; 
      L     #IW_RUECKMELD_DRUCKSTUFE; 
      BTI   ; 
      <>I   ; 
      U     #O_KUEHLMITTEL_2_EIN; 
      O     ; 
      O     #PI_O_KUEHLMITTEL_2_EIN; 
      S     #IO_Strobe_Druckumsch; 


      L     #OW_ANWAHL_DRUCKSTUFE; 
      BTI   ; 
      L     #IW_RUECKMELD_DRUCKSTUFE; 
      BTI   ; 
      ==I   ; 
      U     #O_KUEHLMITTEL_2_EIN; 
      UN    #PI_O_KUEHLMITTEL_2_EIN; 
      O     ; 
      ON    #O_KUEHLMITTEL_2_EIN; 
      O     "M_Reset_Taste"; 
      R     #IO_Strobe_Druckumsch; 

BCD:  NOP   0; 

NETWORK
TITLE =Fehler- und Meldungsausgabe

      L     S5T#500MS; 
      U     "I_AL_BTB_PNOZ"; 
      U     "I_AL_NEG_Not_Aus"; 
      U     "I_AL_MASCHINE_EIN"; 
      U     "M_Leistung_Steht_an"; 
      SE    "T_KNOLL_verz"; //Verzögerung da Signale nicht sofort nch Einschalten vorhanden sind

      U     "T_KNOLL_verz"; 
      SPBN  n4e; 


      UN    #I_MELDUNG_KSA; 
      S     #Meldung_KSA_Warnung; 

      UN    #I_NOT_AUS_KSA; 
      S     #Meldung_Not_Aus_KSA; 

      UN    #I_SPINDEL_VORSCHUB_HALT; 
      S     #Meldung_SPI_VS_Halt; 

      UN    #I_SPI_VORSCHUB_HALT_G0; 
      S     #Meldung_SPI_VS_Halt_G0; 

      UN    #I_FEHLER_SCHALTERDIAGNOS; 
      S     #Meldung_Schalterdiagnose; 

      UN    #I_MSS_KSA; 
      S     #Meldung_MSS_KSA_Fehler; 

      UN    #I_MSS_KSA_MELDUNG; 
      S     #Meldung_MSS_KSA_Meldung; 

      U     #I_KSA_FUELLSTAND_MAX; 
      S     #Meldung_Fuell_Max; 

      U     #I_KSA_FUELLSTAND_MIN_VOR; 
      S     #Meldung_Fuell_Min_Vorw; 

      U     #I_KSA_FUELLSTAND_MIN; 
      S     #Meldung_Fuell_Min; 

      U     #I_TPF_PAPIER_ENDE; 
      S     #Meldung_Papierband_Ende; 

      U     #I_TPF_PAPIER_VORWARNUNG; 
      S     #Meldung_Papierband_Vorw; 

n4e:  NOP   0; 

//  L     DB20.DBW   38
      L     "DB_PLC_MD_DB20".UDInt._058_SONDER_5; 
      T     #OW_KSA_KONFIGURATION; 

      L     "DB_PLC_MD_DB20".UDInt._059_SONDER_6; 
      T     #OW_PARAMETER_2; 

NETWORK
TITLE =Ausgabe Fehlernummer

      L     #IW_FEHLERNUMMER; // Meldungen von 513 bis 1024 anzeigen damit Bediener SONDERMELDUNG registriert
      L     0; 
      >I    ; 
      =     "DB_FEHLERMELDUNG".MELDUNG3._700522_SOKO_MELDUNG; 

      U(    ; //Mit Hardkey sollte das Wizardfenster aufgeblendet werden sobald Fehlernummer kommt 
      L     #IW_FEHLERNUMMER; //Funktionierte aber nicht, nur im Testrack 
      L     0; 
      <>I   ; 
      )     ; 
      =     "M_KNOLL_ERROR"; 

      U     "M_KNOLL_ERROR"; 
      UN    "M_SOKO_ERROR"; 
      FP    "M_FP_KNOLL_ERROR1"; 
      SPBN  M003; 
      L     #IW_FEHLERNUMMER; 
      T     "DB_REMANENT".Knoll_Error; 

M003: U(    ; 
      L     "DB_REMANENT".Knoll_Error; 
      L     0; 
      >I    ; 
      )     ; 
      FP    "M_FP_KNOLL_ERROR2"; 
      SPBN  M002; 
      L     61; 
      T     "DB_SIEM_MMC".A_Hardkey; 
      L     0; 
      T     "DB_REMANENT".Knoll_Error; 
M002: NOP   0; 



NETWORK
TITLE =H21
//Funktionsaufruf aus Teileprogramm
      SET   ; 
      S     "DB_H_FUNKTION".HX_QuitPflicht[21]; 

      U     "DB_H_FUNKTION".HX_M[21]; 
      SPBN  NOBS; 


      L     "DB_H_FUNKTION".HX_Real_Wert[21]; 
      RND   ; 
      T     #OW_FUNCTION_1; // wert von H21 als ganzzahl
      S     #OX_STROBE_FUNCTION1; 


NOBS: NOP   0; 

      U     #I_QUIT_H21; 
      O     "M_Reset_Taste"; 

      R     #OX_STROBE_FUNCTION1; 
      R     "DB_H_FUNKTION".HX_M[21]; 
      =     "DB_H_FUNKTION".HX_Quitt[21]; 
      SPBN  a10; 

      L     0; 
      T     #OW_FUNCTION_1; 


a10:  NOP   0; 

//------------------------

END_FUNCTION

