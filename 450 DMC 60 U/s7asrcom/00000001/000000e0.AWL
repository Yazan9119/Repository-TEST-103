FUNCTION "FC_FAMILIEN_MODUL" : VOID
TITLE =
//$Revision: 1.95 $
//$Date: 2008/09/23 08:04:06CEST $
//$Author: schmoelp $
{ S7_language := '7(1) Deutsch (Deutschland)  15.02.2008  14:59:09' }
VERSION : 0.1


VAR_TEMP
  FASTEMS : BOOL ;	
  PwGst : BOOL ;	//Pw in Grundstellung
  Klappe_nicht_zu : BOOL ;	//Magazinklappe nicht geschlossen
  T_Vorschub_Frei_Ax1 : BOOL ;	
  T_Vorschub_Frei_Ax2 : BOOL ;	
  T_Vorschub_Frei_Ax3 : BOOL ;	
  T_Vorschub_Frei_Ax4 : BOOL ;	
  T_Vorschub_Frei_Ax5 : BOOL ;	
  TX_VSH_AUFHEBEN_BEI_REF : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =NOT-Aus Anforderungen aufsammeln

      U     "DB_FAMILIEN_MODUL".E_Fehler.Not_Aus; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_NotAus; 
NETWORK
TITLE ='Spindel Vorschub Halt' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Spin_Vorschub_Halt_Kanal; 
      O     "DB_FEHLERMELDUNG".Meldung._702139_VSSPH_ASUP; 
      O     "DB_FEHLERMELDUNG".Meldung._702031_INITIALISIEREN; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Spin_Vorsch_Halt_Kanal; 
NETWORK
TITLE ='Spindel Vorschub Halt an Satzgrenze' Anforderungen aufsammeln

      U     "DB_FAMILIEN_MODUL".E_Fehler.Spin_VS_Halt_Satzwechsel; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Spin_Vorsch_Satzwechs; 
NETWORK
TITLE ='Vorschub Halt Kanal' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_Kanal; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Spin_Vorsch_Halt_Kanal; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_Kanal; 
NETWORK
TITLE ='Spindel Halt Kanal' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Spindel_Halt_Kanal; 
      O     "DB_FEHLERMELDUNG".Meldung._702132_SPH_SonderBA; 
      O     "DB_FEHLERMELDUNG".Meldung._702029_VSSPH_Taste_Auto; 
      O     "DB_FEHLERMELDUNG".Meldung._702031_INITIALISIEREN; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Spin_Vorsch_Halt_Kanal; 
      O     "m_VSH_SP_GETRIEBE"; 
      O     "M_VSH_GetrUeberw_aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Spindel_Halt; 
NETWORK
TITLE ='Einlesesperre Anforderungen aufsammeln

      O     "DB_FEHLERMELDUNG".Warnung._701911_Pneumatik_0Sig; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Einlesesperre; 
      O     "DB_FEHLERMELDUNG".Meldung._702116_ESP_M_Decoder; 
      O     "DB_FEHLERMELDUNG".Meldung._702122_Ware_Quitt_H_Fkt; 
      O     "DB_FEHLERMELDUNG".NC_Startsperre._700105_Stange_fehler; 
      O     "M_SP_Vorschm_Req"; 
      O     "m_LSpUAx"; 
      ON    "I_AL_EINLESEFREIGABE"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Einlesesperre; 
NETWORK
TITLE ='Vorschub Halt Haupt-Achsen' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_Haupt_Achs; 
      O     "DB_FEHLERMELDUNG".MELDUNG5._510229_KSA_IKZ_UNTERDRU; 
      O     "m_VSH_SP_GETRIEBE"; 
      O     ; 
      U     "DB_NC_PLC".WZW_NC_Aktiv; 
      U     "DB_FEHLERMELDUNG".VS_Halt_Async4._701831_Stoerung_Regal; 
      O     ; 
      UN    "I_AL_Druckluft_OK"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._006_ANWAHL_GETRIEBE; 
      L     3; 
      ==I   ; 
      )     ; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
NETWORK
TITLE ='Vorschub Halt 1 Achse' Anforderungen aufsammeln

      O     "DB_OUT".QmxLockXYZAxis; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_1_Achse; 
      O     "DB_FEHLERMELDUNG".Meldung._702135_VSP_X_Farber; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      O     "DB_FEHLERMELDUNG".Meldung._702129_VSHK_SK; 
      O     "DB_FEHLERMELDUNG".Meldung._702130_VSHK_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702128_VSHK_PW; 
      O     "DB_FEHLERMELDUNG".Meldung._702035_Nist_NSoll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung._702039_SPDL_Soll_0Sig; 
      O     "DB_FEHLERMELDUNG".WARNUNG3._700349; 
      O     "DB_FEHLERMELDUNG".Meldung4._702460_TT_NICHT_UNTEN; 
      O     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
      O     "M_Einricht_Sperr_Ax1"; 
      O     "M_MSST_nicht_bereit"; 
      O     ; 
      U     "DB_WW".VSP_X_Wechsler; 
      UN    "DB_REMANENT".LASER_AKTIV; 
      O     ; 
      U     "MX_WZB_G0_Sperre"; 
      UN    "DB_NC_PLC".WZW_NC_Aktiv; 
      O     ; 
      U     "M_SP_Vorschm_Req"; 
      U     "M_Satzfolge_aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_1_Achse; 
NETWORK
TITLE =Vorschub Halt für Z-Achse aufheben bei BA Ref
//Betrifft nur H-Maschinen mit REF-Reihenfolge Z X Y B - Achse
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      U     "DB_PLC_MD_DB20".UDHex._31_Bit1_SOLUTIONLINE; 
      U     "DB_ACHSE2_Y".E_TCPlus; 
      U     "DB_SIEM_BAG".E_REF; 
      =     #TX_VSH_AUFHEBEN_BEI_REF; 
NETWORK
TITLE ='Vorschub Halt 2 Achse' Anforderungen aufsammeln
//2.Achse bei H-Maschine = Z-Achse
      O     "DB_OUT".QmxLockXYZAxis; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_2_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      O     "DB_FEHLERMELDUNG".Meldung._702129_VSHK_SK; 
      O     "DB_FEHLERMELDUNG".Meldung._702130_VSHK_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702128_VSHK_PW; 
      O     "DB_FEHLERMELDUNG".Meldung._702035_Nist_NSoll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung._702039_SPDL_Soll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung4._702460_TT_NICHT_UNTEN; 
      O     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
      O     "M_Einricht_Sperr_Ax2"; 
      O     "M_MSST_nicht_bereit"; 
      O     ; 
      U(    ; 
      U     "Mx_X_ACHSE_WZW_POS"; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      O     ; 
      U(    ; 
      U     "DB_SIEM_NCK".E_SWCamPlus[15]; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      O     ; 
      UN    "DB_SIEM_NCK".E_SWCamPlus[15]; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      )     ; 
      UN    "M_WZW_Aktiv"; 
      U(    ; 
      ON    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      O     ; 
      U     "DB_SIEM_BAG".E_REF; 
      U     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      )     ; 
      )     ; 
      UN    #TX_VSH_AUFHEBEN_BEI_REF; 
      O     ; 
      U     "M_SP_Vorschm_Req"; 
      U     "M_Satzfolge_aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_2_Achse; 
NETWORK
TITLE = 'Vorschub Halt 3 Achse' Anforderungen aufsammeln

      O     "DB_OUT".QmxLockXYZAxis; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_3_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      O     "DB_FEHLERMELDUNG".Meldung._702129_VSHK_SK; 
      O     "DB_FEHLERMELDUNG".Meldung._702130_VSHK_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702128_VSHK_PW; 
      O     "DB_FEHLERMELDUNG".Meldung._702035_Nist_NSoll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung._702039_SPDL_Soll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung4._702460_TT_NICHT_UNTEN; 
      O     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
      O     "M_Einricht_Sperr_Ax3"; 
      O     "M_MSST_nicht_bereit"; 
      O     ; 
      U(    ; 
      O     "Mx_X_ACHSE_WZW_POS"; 
      O     ; 
      U(    ; 
      U     "DB_SIEM_NCK".E_SWCamPlus[15]; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      O     ; 
      UN    "DB_SIEM_NCK".E_SWCamPlus[15]; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3070_AKTIV; 
      )     ; 
      UN    "M_WZW_Aktiv"; 
      )     ; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      O     ; 
      U     "M_SP_Vorschm_Req"; 
      U     "M_Satzfolge_aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_3_Achse; 
NETWORK
TITLE ='Vorschub Halt 4 Achse' Anforderungen aufsammeln

      O     "DB_OUT".QmxLockRotationTable; 
      O     ; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      U     "DB_SIEM_NCK".E_SWCamPlus[26]; 
      U(    ; 
      O     "DB_PLC_MD_DB20".UDHex._49_Bit1_PW_Verfahrb; 
      O     ; 
      U     "DB_PLC_MD_DB20".UDHex._49_Bit6_PW_Verfahrb_MFK; 
      U     "DI_PW_ABLAUF".bVerfb_Tisch_AX; 
      )     ; 
      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_4_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      O     "DB_FEHLERMELDUNG".Meldung._702129_VSHK_SK; 
      O     "DB_FEHLERMELDUNG".Meldung._702130_VSHK_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702128_VSHK_PW; 
      O     "DB_FEHLERMELDUNG".Meldung._702035_Nist_NSoll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung._702039_SPDL_Soll_0Sig; 
      O     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
      O     "M_Einricht_Sperr_Ax4"; 
      O     ; 
      UN    "I_PW_MPL_MDK_ENTKUPPELT"; 
      U     "DB_PW_DATA".Option2.bMaschineTypFD_70_80; 
      O     ; 
      UN    "DB_PW_DATA".In.IX_PW_AR_PAL_gespannt; 
      U     "DB_PW_DATA".Option.bMaschineTypFD_100_125; 
      O     ; 
      U     "M_SP_Vorschm_Req"; 
      U     "M_Satzfolge_aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_4_Achse; 
NETWORK
TITLE ='Vorschub Halt 5 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_5_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      O     "DB_FEHLERMELDUNG".Meldung._702129_VSHK_SK; 
      O     "DB_FEHLERMELDUNG".Meldung._702130_VSHK_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702128_VSHK_PW; 
      O     "DB_FEHLERMELDUNG".Meldung._702035_Nist_NSoll_0Sig; 
      O     "DB_FEHLERMELDUNG".Meldung._702039_SPDL_Soll_0Sig; 
      O     "M_SK_Sperre"; 
      O     "M_Einricht_Sperr_Ax5"; 
      O     ; 
      U(    ; 
      O     "DB_FEHLERMELDUNG".Meldung._702136_VSP_B_Fahrber; 
      O     "Mx_Sperre_BAchse"; 
      )     ; 
      UN    "DB_SAFE_ZD".Seco_Test.Test_Bremse_laeuft; 
      O     ; 
      U     "M_SP_Vorschm_Req"; 
      U     "M_Satzfolge_aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_5_Achse; 
NETWORK
TITLE =Abschaltung Spindel nach 3 Sekunden Fehler

      U     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      L     S5T#2S; 
      SE    "TE_Hpt_Achsen_Stehen"; 
NETWORK
TITLE ='Vorschub Halt 6 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_6_Achse; 
      O(    ; 
      U     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      U     "M_Hauptachsen_stehen"; 
      O     "TE_Hpt_Achsen_Stehen"; 
      )     ; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Spindel_Halt; 
      O     "DB_FEHLERMELDUNG".Meldung._702129_VSHK_SK; 
      O     "DB_FEHLERMELDUNG".Meldung._702130_VSHK_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702128_VSHK_PW; 
      O     "DB_FEHLERMELDUNG".MELDUNG3._700523_SS_Stop_Spindel; 
      O     "DB_FEHLERMELDUNG".MELDUNG5._510311_TuerRobot_Ueberw; 
      O(    ; 
      U     "DB_FAMILIEN_MODUL".E_M3M4_Sperre.Messtaster; // Vorschubsperre wenn Messtaster 
      UN    "M_WZW_Prozess_Aktiv"; // ausserhalb M6 Aktiv
      )     ; 
      ON    "DB_STERN_DREIECK".MX_Freigabe_Impulse_Spdl; // Sperre von Stern-Dreieck
      O     "MX_2GETR_VorFreig"; // O->ON 15.10.01.AB
      O     "M_SP_Vorschm_Req"; 
      O(    ; 
      U     "DB_FEHLERMELDUNG".Meldung._702028_M3M4_sperre_Mess; 
      UN    "DB_ACHSE6_SPINDEL".E_PosMode; 
      )     ; 
      O(    ; 
      UN    "I_AL_Druckluft_OK"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._006_ANWAHL_GETRIEBE; 
      L     3; 
      ==I   ; 
      )     ; 
      )     ; 
      O(    ; 
      O     "Mx_XAx_in_WZW_RAUM"; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_RAD; 
      UN    "M_WZW_Aktiv"; 
      )     ; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_6_Achse; 
NETWORK
TITLE ='Vorschub Halt 7 und 13 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_7_Achse; 
      O     "MX_WZW_Mag_Verrieg"; 
      O     "DB_FEHLERMELDUNG".VS_Halt_Async1._701612_WZWV_Grundst_0S; 
      O     "M_WZW_Hub_Mag_req"; 
      O     "DB_FEHLERMELDUNG".VS_Halt_Async1._701604_Grundst_0Sig; 
      O     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700701_WZ_UGP_nicht_0S; 
      O     "DB_FEHLERMELDUNG".Meldung._702026_Quit_Beladen; 
      O     "DB_FEHLERMELDUNG".Meldung._702105_Quit_Entladen; 
      O     ; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MAG_RAD; 
      U     "DB_WW".VSP_WZM_Wechsler; 
      O     ; 
      U(    ; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_VKETTE_MIT_TPU; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_VKETTE_OHNE_TPU; 
      )     ; 
      UN    "M_WZW_FRG_AN_MAG"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_7_Achse; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_13_Achse; 
NETWORK
TITLE ='Vorschub Halt 8 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_8_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      O     "DB_FEHLERMELDUNG".Meldung4._702460_TT_NICHT_UNTEN; 
      O     ; 
      UN    "DB_PW_DATA".In.IX_PW_Hub_oben; 
      U(    ; 
      ON    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      ON    "DB_OEM".OEM_PW2_Rettbildaktiv; 
      ON    "DB_SIEM_BAG".E_JOG; 
      )     ; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_8_Achse; 
NETWORK
TITLE ='Vorschub Halt 9 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_9_Achse; 
      ON    "M_FRG_Drehst_Achse"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_9_Achse; 
NETWORK
TITLE ='Vorschub Halt 10 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_10_Achse; 
      ON    "M_FRG_Transp_Achse"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_10_Achse; 
NETWORK
TITLE ='Vorschub Halt 11 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_11_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_11_Achse; 
NETWORK
TITLE ='Vorschub Halt 12 Achse' Anforderungen aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.Vorschub_Halt_12_Achse; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_12_Achse; 
NETWORK
TITLE ='Vorschub Halt 14 Achse' Anforderungen aufsammeln

      O     "DB_FEHLERMELDUNG".MELDUNG2._701308; 
      O     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._012_ANWAHL_WKZ_WASCHEN; 
      L     4; 
      ==I   ; 
      )     ; 
      UN    "I_TC_POSITION_BRUSH_CLE"; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_14_Achse; 
NETWORK
TITLE ='Vorschub Halt 15 Achse' Anforderungen aufsammeln

      U     "M_PW_Aktiv"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_15_Achse; 
NETWORK
TITLE ='Vorschub Halt 16 Achse' Anforderungen aufsammeln
//    O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_HauptAch
      UN    "M_FRG_Hub_Achse"; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_16_Achse; 
NETWORK
TITLE =NC Start Sperre aufsammeln

      O     "DB_FAMILIEN_MODUL".E_Fehler.NC_Start_Sperre; 
      O     "DB_FEHLERMELDUNG".Meldung._702119_NC_STSP_WZW; 
      O     "DB_FEHLERMELDUNG".Meldung._702100_Leistung_0Signal; 
      O     "M_SOKO_NC_STARTSPERRE"; 
      O     ; 
      U(    ; 
      ON    "I_WW_1_Klappe_zu"; 
      ON    "M_WW_X_imArbeitsraum"; 
      )     ; 
      U(    ; 
      ON    "M_Programm_Aktiv_Kanal_1"; 
      O     "MX_Satzsuchlauf_gesp"; 
      )     ; 
      UN    "DB_REMANENT".LASER_AKTIV; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_ANGEWAEHLT; 
      O     ; 
      U     "DB_FEHLERMELDUNG".MELDUNG3._700518_Motor_Messsytem; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      O     ; 
      U     "M_SP_Vorschm_Req"; 
      U     "M_Satzfolge_aktiv"; 
      O     "DB_FEHLERMELDUNG".Meldung._702146_WZW_aktiv; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_NC_Start_Sperre; 
NETWORK
TITLE = Stop_Werkzeugwechsel

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Werkzeugwechsel; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Werkzeugwechsel; 
NETWORK
TITLE =Stop Palettenwechsel

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Palettenwechsel; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Palettenwechsel; 
NETWORK
TITLE =Stop Kopf schwenken

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Kopf_schwenken; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Kopf_schwenken; 
NETWORK
TITLE = Stop Sonderaktion 

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Sonderaktion; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Sonderaktion; 
NETWORK
TITLE =Stop Asychron Prozess 1   (701548-701631 -> Mag: Achse 7)

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess1; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Asychron_Prozess1; 
NETWORK
TITLE =Stop Asychron Prozess 2  (701632-701715 -> PW: Achse 8)

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess2; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Asychron_Prozess2; 
NETWORK
TITLE =Stop Asychron Prozess 3  (701716-701763 -> Pal.Speicher: Achse9)

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess3; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Asychron_Prozess3; 
NETWORK
TITLE =Stop Asychron Prozess 4  (701800-701847 ->  Mag: Achse 7)

      U     "DB_FAMILIEN_MODUL".E_Fehler.Stop_Asychron_Prozess4; 
      =     "DB_FAMILIEN_MODUL".Gesamt.A_Stop_Asychron_Prozess4; 
NETWORK
TITLE =Achssperre vom Anwender  

      CLR   ; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_X; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_Y; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_Z; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_4; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_5; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_7; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_8; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_9; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_10; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_11; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_12; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_13; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_14; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_15; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_16; 


      U     "MX_Spdl_PosBetr_84x5"; 
      SPB   rfg2; 

      U     "DB_ANWENDER_SOFTKEY".Anwender.Spindel.RFG_ein; 
      U     "M_Hauptspindel_steht"; 
      FP    "M_FP_Spi_RFG_Wizard"; 
      S     "M_RFG_Spindel_Wizard"; // setze Spindel freischalten

// bei stehender Spindel und ausgeschalteter RFG durch Wizard

rfg2: UN    "DB_ANWENDER_SOFTKEY".Anwender.Spindel.RFG_ein; 
      U     "M_Hauptspindel_steht"; 
      FP    "M_FP_Spi_inReg_Wizard"; 
      R     "M_RFG_Spindel_Wizard"; // ruecksetze Spindel Freischalten 

      U     "MX_Spdl_M3"; 
      O     "MX_Spdl_M4"; 
      O     "MX_Spdl_M3_M4"; 
      O     "MX_Spdl_PosBetr_84x5"; 
      R     "M_RFG_Spindel_Wizard"; 
      R     "DB_ANWENDER_SOFTKEY".Anwender.Spindel.RFG_ein; 

      U     "M_RFG_Spindel_Wizard"; 
      =     "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_6; 
NETWORK
TITLE =Werkzeugspanner-Typ
//Laden des Werkzeugspannertyps aus dem Maschinendatum
//Transferieren in DB65
//
      L     "DB_PLC_MD_DB20".UDInt._009_ANWAHL_WZSPANNER; 
      T     "DB_FAMILIEN_MODUL".WZ_Spanner_1_Typ; 

      SET   ; 
      =     "DB_FAMILIEN_MODUL".WZ_Spanner_1_Aktiv; 

NETWORK
TITLE =Aufsammeln der Bewegenden M-Funktionen
//
//
      O     "M_Stange_Aktiv"; 
      O     "M_WZW_Aktiv"; 
      O     "M_SK_Aktiv"; 
      O     "DI_M_DEKODER".quittung_request; 
      =     "DB_FAMILIEN_MODUL".Aktive_Funktionen; 
NETWORK
TITLE =Freigabe ASUP'S
//Freigabe ASUP'S 
      UN    "M_WZW_Aktiv"; 
      UN    "M_SK_Aktiv"; 
      UN    "M_PW_Aktiv"; 
      UN    "DB_NC_PLC".FB_NC_Aktiv; 
      =     "DB_FAMILIEN_MODUL".A_ASUP_Freigabe; 
NETWORK
TITLE =Verriegelung Kuehlmittel
//Kuehlmittel
// Bedingungen um das Kuehlmittel zu unterdruecken
      L     "DB_PLC_MD_DB20".UDInt._019_ANWAHL_KMTL_ANLAGE; 
      L     0; 
      ==I   ; 
      O     "M_PW_Aktiv"; 
      O     "M_WZW_Aktiv"; 
      ON    "I_WW_1_Klappe_zu"; 
      O     "DB_NC_PLC".FB_NC_Aktiv; 
      O     "M_24aktiv"; // oder M24 ist aktiv
      O(    ; 
      U     "DB_PLC_MD_DB20".UDHex._11_BIT6_KMTL_M0_1; // 4.7.01 SCH Anwahl bei M0/1 Wasser Aus
      U     "DB_SIEM_KANAL_1".E_M01; 
      )     ; 
      O     "DB_ANWENDER_SOFTKEY".Anwender.Kuehlmittel.Wasser_inaktiv; 
      =     "DB_FAMILIEN_MODUL".A_Wasser_Aus; 

      U     "DB_ANWENDER_SOFTKEY".Anwender.Kuehlmittel.Wasser_inaktiv; 
      =     "DB_FEHLERMELDUNG".MELDUNG2._701300_Kuehlm_inaktiv; 

// Bedingungen um das Kuehlmittel abzuschalten

      CLR   ; 
      =     "DB_FAMILIEN_MODUL".A_Wasser_Abschalten; 

NETWORK
TITLE =Verriegelung Magazin

      U     "M_WZR_Mag_Verrieg"; 
      UN    "MX_TDS_MagSperre"; 
      =     "M_Magazin_Sperre"; 
NETWORK
TITLE =Freigabe Palettenwechsler
//
//
      U(    ; 
      U     "I_FMS_APC_MOVEMENT_OK"; 
      U     "Mx_FMS_OM1_DISABLED"; 
      O     ; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASREMS_V3X; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.HASYS_FASTEMS_V3X_RPC; 
      )     ; 
      UN    "DB_OUT".AMX_PC_LOCK; 
      U(    ; 
      O     "DB_LSSchnittstelle".IX_FreigabePWvonLS; 
      ON    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.Palettenspeicher_aktiv; 
      )     ; 
      =     "DB_FAMILIEN_MODUL".A_Freigabe_PW; 
NETWORK
TITLE =Vorschub Override auf 0% 
//(Es wird im FC512 das EB3 auf 0 geschrieben)
      U     "MX_WZB_Ov_0"; 
      UN    "DB_NC_PLC".WZW_NC_Aktiv; 
      O     ; 
      UN    "I_WW_1_Klappe_zu"; 
      UN    "M_WZW_Aktiv"; 
      U     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.WZW_ANGEWAEHLT; 
      U(    ; 
      O     "M_Automatik_Betrieb"; 
      O     "MX_MDA"; 
      )     ; 
      UN    "m_wwgf_XArbRaum"; 
      UN    "DB_WW".ASUP_Rettung.X_Warteposition; 
      U(    ; 
      UN    "m_wwgf_Gr1Spi"; 
      UN    "m_wwgf_Gr1Mag"; 
      ON    "M_VERTIKALKETTE"; 
      )     ; 
      O     "Mx_Vorschub_HALT_SOKO"; 
      O     ; 
      U     "M_M7_Act"; 
      UN    "MX_IKZ_Freigabe_Vorschub"; 
      O     "DB_HR".SpH.VS_SP_SpH; 
      O     "MX_IKZ_OVR_0"; 
      O     "DB_FEHLERMELDUNG".Meldung._702040_Vor_Freig_4Ax; 
      =     "MX_MsSt_Vorschub_0"; 
NETWORK
TITLE =Werkzeugwechsel sperren, Komet-U-Achse in Spi.& aktiv

      U     "m_WzwSpUAx"; 
      =     "m_WzwSpUAx"; 
NETWORK
TITLE =Einlesesperre durch Komet-U-Achse

      U     "m_LSpUAx"; 
      =     "m_LSpUAx"; 
NETWORK
TITLE =Verwaltung aut.Grundstlg.fahren: PW in Grundstellung
//PALETTENWECHSLER IN GRUNDSTELLUNG
      U(    ; 
      O     "I_PW_RPL_PAL_GEKLEMMT"; 
      ON    "DB_PW_DATA".Option.bRP_Hydr_Klemmung; 
      O     "MX_ABST_Rundspeicher"; 
      )     ; 
      U     "M_PW_GRDST"; 
      =     #PwGst; 
NETWORK
TITLE =Verwaltung aut.Grundstlg.fahren: Reset Schritte
//Voraussetzungen zum Grundstellungsfahren:
//1. nicht Jog
//2. kein Rettungsbild aktiv 
      ON    "IX_MSST_Ta_2_4"; 
      O     "M_Handbetrieb"; 
      O     "DB_OEM".Einrichten_Aktiv; 
      R     "m_rqwwgf"; 
      R     "m_rqpwgf"; 
      R     "m_rqlagf"; 
NETWORK
TITLE =Verwaltung aut.Grundstlg.fahren: Schritt PalWechsel.Gstlg.fahren
//SCHRITT 1 PALETTENWECHSLER ZUR GRUNDSTELLUNG
      U     "IX_MSST_Ta_2_4"; 
      UN    "M_Handbetrieb"; 
      UN    "m_rqwwgf"; 
      UN    "m_rqpwgf"; 
      U(    ; 
      ON    #PwGst; 
      ON    "DB_OEM".WZW_in_Grdst; 
      )     ; 
      S     "m_rqpwgf"; 
NETWORK
TITLE =Verwaltung aut.Grundstlg.fahren: Schritt WzWechsel.Gstlg.fahren
//SCHRITT 2 PW IN GRUNDSTELLUNG -> WERKZEUGWECHSEL ZUR GRUNDSTELLUNG
      U     "IX_MSST_Ta_2_4"; 
      UN    "M_Handbetrieb"; 
      U(    ; 
      O     #PwGst; 
      ON    "DB_NC_PLC".PW_Anwahl; 
      )     ; 
      UN    "DI_PWGF".SkStrt; 
      R     "m_rqpwgf"; 
      S     "m_rqwwgf"; 
NETWORK
TITLE =Verwaltung aut.Grundstlg.fahren: Schritt Laser Gstlg.fahren
//SCHRITT 3 WERKZEUGWECHSEL IN GRUNDSTELLUNG -> LASER ZUR GRUNDSTELLUNG
      U     "IX_MSST_Ta_2_4"; 
      UN    "M_Handbetrieb"; 
      U(    ; 
      O     #PwGst; 
      ON    "DB_NC_PLC".PW_Anwahl; 
      )     ; 
      U     "DB_OEM".WZW_in_Grdst; 
      UN    "DB_WWZ".s.SkStrt; 
      UN    "DB_VWWZ".s.SkStrt; 
      R     "m_rqwwgf"; 
      S     "m_rqlagf"; 
NETWORK
TITLE =Verwaltung aut.Grundstlg.fahren: Ende, Laser in Gstlg.
//ENDE: LASER IN GRUNDSTELLUNG
      U     "m_rqlagf"; 
      U(    ; 
      O     "M_LASER_IN_GRDST"; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     0; 
      ==I   ; 
      )     ; 
      )     ; 
      R     "m_rqlagf"; 
NETWORK
TITLE =In Einrichtbetrieb nur eine Achse verfahrbar
//Im Einrichtbetrieb darf nur eine Achse mittels der Bedientafelfront verfahren 
//werden, dazu wird für jede Achse einzeln die Achsfreigabe verwaltet. Nur wenn 
//eine Achse vom Bediener angesteuert wird, wird die Achsfreigabe für diese Achse 
//bestätigt. Wird eine zweite Achse zugleich angesteuert, so wird die Freigabe 
//für 
//diese nicht vergeben und die Freigabe der ersten Achse wird zurückgezogen und 
//ein Vorschub-Halt Fehler ausgegeben.
      CLR   ; 
      =     #T_Vorschub_Frei_Ax1; 
      =     #T_Vorschub_Frei_Ax2; 
      =     #T_Vorschub_Frei_Ax3; 
      =     #T_Vorschub_Frei_Ax4; 
      =     #T_Vorschub_Frei_Ax5; 

//Erzeugen der Achsevorschubfreigaben, wenn nur eine Achse angefordert wird

      O     "DB_ACHSE1_X".E_TCMinus; 
      O     "DB_ACHSE1_X".E_TCPlus; 
      U(    ; 
      UN    "DB_ACHSE2_Y".E_TCMinus; 
      UN    "DB_ACHSE2_Y".E_TCPlus; 
      UN    "DB_ACHSE3_Z".E_TCMinus; 
      UN    "DB_ACHSE3_Z".E_TCPlus; 
      UN    "DB_ACHSE4".E_TCMinus; 
      UN    "DB_ACHSE4".E_TCPlus; 
      UN    "DB_ACHSE5".E_TCMinus; 
      UN    "DB_ACHSE5".E_TCPlus; 
      )     ; 
      =     #T_Vorschub_Frei_Ax1; 

      O     "DB_ACHSE2_Y".E_TCMinus; 
      O     "DB_ACHSE2_Y".E_TCPlus; 
      U(    ; 
      UN    "DB_ACHSE1_X".E_TCMinus; 
      UN    "DB_ACHSE1_X".E_TCPlus; 
      UN    "DB_ACHSE3_Z".E_TCMinus; 
      UN    "DB_ACHSE3_Z".E_TCPlus; 
      UN    "DB_ACHSE4".E_TCMinus; 
      UN    "DB_ACHSE4".E_TCPlus; 
      UN    "DB_ACHSE5".E_TCMinus; 
      UN    "DB_ACHSE5".E_TCPlus; 
      )     ; 
      =     #T_Vorschub_Frei_Ax2; 

      O     "DB_ACHSE3_Z".E_TCMinus; 
      O     "DB_ACHSE3_Z".E_TCPlus; 
      U(    ; 
      UN    "DB_ACHSE1_X".E_TCMinus; 
      UN    "DB_ACHSE1_X".E_TCPlus; 
      UN    "DB_ACHSE2_Y".E_TCMinus; 
      UN    "DB_ACHSE2_Y".E_TCPlus; 
      UN    "DB_ACHSE4".E_TCMinus; 
      UN    "DB_ACHSE4".E_TCPlus; 
      UN    "DB_ACHSE5".E_TCMinus; 
      UN    "DB_ACHSE5".E_TCPlus; 
      )     ; 
      =     #T_Vorschub_Frei_Ax3; 

      O     "DB_ACHSE4".E_TCMinus; 
      O     "DB_ACHSE4".E_TCPlus; 
      U(    ; 
      UN    "DB_ACHSE1_X".E_TCMinus; 
      UN    "DB_ACHSE1_X".E_TCPlus; 
      UN    "DB_ACHSE2_Y".E_TCMinus; 
      UN    "DB_ACHSE2_Y".E_TCPlus; 
      UN    "DB_ACHSE3_Z".E_TCMinus; 
      UN    "DB_ACHSE3_Z".E_TCPlus; 
      UN    "DB_ACHSE5".E_TCMinus; 
      UN    "DB_ACHSE5".E_TCPlus; 
      )     ; 
      =     #T_Vorschub_Frei_Ax4; 

      O     "DB_ACHSE5".E_TCMinus; 
      O     "DB_ACHSE5".E_TCPlus; 
      U(    ; 
      UN    "DB_ACHSE1_X".E_TCMinus; 
      UN    "DB_ACHSE1_X".E_TCPlus; 
      UN    "DB_ACHSE2_Y".E_TCMinus; 
      UN    "DB_ACHSE2_Y".E_TCPlus; 
      UN    "DB_ACHSE3_Z".E_TCMinus; 
      UN    "DB_ACHSE3_Z".E_TCPlus; 
      UN    "DB_ACHSE4".E_TCMinus; 
      UN    "DB_ACHSE4".E_TCPlus; 
      )     ; 
      =     #T_Vorschub_Frei_Ax5; 

//Achsspezifische Freigabe im Einrichtbetrieb und in BA Jog und bei nicht angewähltem 
//Handrad auf Merker für Achsfreigabe abbilden, in NW8 - NW12 wird die jeweilige Achse
//gesperrt

      UN    #T_Vorschub_Frei_Ax1; 
      U     "M_Einrichtbetrieb"; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN(   ; 
      U     "I_HR_Anwahl"; 
      U     "M_Anwahl_MBHG"; 
      )     ; 
      =     "M_Einricht_Sperr_Ax1"; 

      UN    #T_Vorschub_Frei_Ax2; 
      U     "M_Einrichtbetrieb"; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN(   ; 
      U     "I_HR_Anwahl"; 
      U     "M_Anwahl_MBHG"; 
      )     ; 
      =     "M_Einricht_Sperr_Ax2"; 

      UN    #T_Vorschub_Frei_Ax3; 
      U     "M_Einrichtbetrieb"; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN(   ; 
      U     "I_HR_Anwahl"; 
      U     "M_Anwahl_MBHG"; 
      )     ; 
      =     "M_Einricht_Sperr_Ax3"; 

      UN    #T_Vorschub_Frei_Ax4; 
      U     "M_Einrichtbetrieb"; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN(   ; 
      U     "I_HR_Anwahl"; 
      U     "M_Anwahl_MBHG"; 
      )     ; 
      =     "M_Einricht_Sperr_Ax4"; 

      UN    #T_Vorschub_Frei_Ax5; 
      U     "M_Einrichtbetrieb"; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN(   ; 
      U     "I_HR_Anwahl"; 
      U     "M_Anwahl_MBHG"; 
      )     ; 
      =     "M_Einricht_Sperr_Ax5"; 

//Erzeugen eines Spindel Vorschub Halt Fehler bei Ansteuern von mehr als einer Achse
//in BA Jog und bei nicht angewähltem Handrad

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt1._510114_Einrichtb_1Achse; 

      U     "M_Einrichtbetrieb"; 
      U     "DB_SIEM_BAG".E_JOG; 
      UN(   ; 
      O     "I_HR_plus_Taste"; 
      O     "I_HR_minus_Taste"; 
      U     "I_HR_Anwahl"; 
      )     ; 
      UN(   ; 
      X(    ; 
      O     "DB_ACHSE1_X".A_Minus; 
      O     "DB_ACHSE1_X".A_Plus; 
      )     ; 
      X(    ; 
      O     "DB_ACHSE2_Y".A_Minus; 
      O     "DB_ACHSE2_Y".A_Plus; 
      )     ; 
      X(    ; 
      O     "DB_ACHSE3_Z".A_Minus; 
      O     "DB_ACHSE3_Z".A_Plus; 
      )     ; 
      X(    ; 
      O     "DB_ACHSE4".A_Minus; 
      O     "DB_ACHSE4".A_Plus; 
      )     ; 
      X(    ; 
      O     "DB_ACHSE5".A_Minus; 
      O     "DB_ACHSE5".A_Plus; 
      )     ; 
      )     ; 
      UN(   ; 
      UN    "DB_ACHSE1_X".A_Minus; 
      UN    "DB_ACHSE1_X".A_Plus; 
      UN    "DB_ACHSE2_Y".A_Minus; 
      UN    "DB_ACHSE2_Y".A_Plus; 
      UN    "DB_ACHSE3_Z".A_Minus; 
      UN    "DB_ACHSE3_Z".A_Plus; 
      UN    "DB_ACHSE4".A_Minus; 
      UN    "DB_ACHSE4".A_Plus; 
      UN    "DB_ACHSE5".A_Minus; 
      UN    "DB_ACHSE5".A_Plus; 
      )     ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt1._510114_Einrichtb_1Achse; 

END_FUNCTION

