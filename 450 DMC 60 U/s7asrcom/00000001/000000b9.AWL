FUNCTION "FC_MA_ALLG_UEBERW" : VOID
TITLE =
//$Revision: 1.34 $
//$Date: 2008/10/09 13:48:34CEST $
//$Author: sth $
VERSION : 0.1


VAR_TEMP
  TB_Achse : BYTE ;	
  Tw_Funktion : BYTE ;	
  TB_Antriebssatz : BYTE ;	
  Tx_Bit3 : BOOL ;	
  Tx_bit2 : BOOL ;	
  Tx_Bit1 : BOOL ;	
  t_Zeit_OelNebel : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =Rücksetze Diagnosemeldungen
// Netzwerk-Übersicht
// ==================
// NW 1: Rücksetze Diagnosemeldungen
// NW 2: Überwachung Eingang Spindel-Vorschub Halt
// NW 3: Schaltschranktemperatur
// NW 4: 702310 "WAR, Motorschutzschalter Schaltschrankkühlung hat ausgel
// NW 5: PNOTZMULTI:Eingang BTB 0 Signal
// NW 6: Minimalmengen Schmierung
// NW 7: Schmierung DrehDurchführung
// NW 8: Rauch- u.Nebelabscheider
// NW 9: 700215 "SPI-VS-H, Eingang Sicherung 24V 0-Signal"
// NW 10: Parameter set selection A
// NW 11: Blasluft Drehmomentstütze
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt._700309_Err_Pb_Slave5; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt._700312_Err_Peripherie; 
NETWORK
TITLE =Warnung: Schaltschranktemperatur

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Warnung._701957_Ueb_Temp_Schalts; 
      U     "I_Ueb_Temp_Sch"; 
      S     "DB_FEHLERMELDUNG".Warnung._701957_Ueb_Temp_Schalts; 
      NOP   0; 
NETWORK
TITLE =Warnung: Motorschutzschalter Schaltschrankkühlung hat ausgelöst

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".WARNUNG4._702310_MSS_SS_Kuehlung; 
      U(    ; 
      UN    "I_AL_NEG_MSS_Kuehlung"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._011_ANWAHL_MASCH_KUEHL; 
      L     5; 
      <>I   ; 
      )     ; 
      L     S5T#3S; 
      SE    "TE_MSS_KUEHLUNG_VERZ"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      U     "TE_MSS_KUEHLUNG_VERZ"; 
      )     ; 
      S     "DB_FEHLERMELDUNG".WARNUNG4._702310_MSS_SS_Kuehlung; 
      NOP   0; 
NETWORK
TITLE =PNOTZMULTI:Eingang BTB 0 Signal

      UN    "I_AL_BTB_PNOZ"; 
      =     "DB_FEHLERMELDUNG".MELDUNG2._701301_PNOTZ_BTB_0_Sign; 
NETWORK
TITLE =Einlesesperre: Eingang Einlesesperre 0 Signal

      UN    "I_AL_EINLESEFREIGABE"; 
      =     "DB_FEHLERMELDUNG".Einlesesperre._700549_Eg_EinleseSp_1S; 
NETWORK
TITLE =Minimalmengen Schmierung
//Aktivierung: M34 Schmierung ein
//             M35 Schmierung aus
//
//
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701052_MinSchm_Fuell_0S; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701053_M7_M34_Ungueltig; 

      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._035_ANWAHL_MIN_SCHMIER; 
      L     0; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._035_ANWAHL_MIN_SCHMIER; 
      L     5; 
      >=I   ; 
      )     ; 
      SPBN  STRT; 

      SET   ; 
      R     "DB_DM_M_FUNKTION".MX_QuitPflicht[34]; 
      R     "DB_DM_M_FUNKTION".MX_M[34]; 
      R     "DB_DM_M_FUNKTION".MX_M[35]; 
      R     "O_WZ_MIN_SCH_MV1"; 
      SPA   XEND; 

STRT: L     "DB_PLC_MD_DB20".UDInt._035_ANWAHL_MIN_SCHMIER; 
      L     1; 
      ==I   ; 
      SPBN  STVO; 

      SET   ; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[34]; 
      R     "DB_DM_M_FUNKTION".MX_QuitPflicht[35]; 

      O     "M_Reset_Taste"; 
      R     "O_WZ_MIN_SCH_MV1"; 
      R     "DB_DM_M_FUNKTION".MX_M[34]; 

      UN    "I_WZ_MIN_SCH_Fuell_OK"; 
      U(    ; 
      O     "DB_DM_M_FUNKTION".MX_M[34]; 
      O     "O_WZ_MIN_SCH_MV1"; 
      )     ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701052_MinSchm_Fuell_0S; 

      U     "M_M7_Act"; 
      UN    "O_WZ_MIN_SCH_MV1"; 
      U     "DB_DM_M_FUNKTION".MX_M[34]; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701053_M7_M34_Ungueltig; 
      SPB   MEND; 



      U     "M_M7_Act"; 
      U     "O_WZ_MIN_SCH_MV1"; 
      L     S5T#1S; 
      SV    "Tv_MinSchm_M7"; 

      U     "Tv_MinSchm_M7"; 
      =     "O_MinSchm_IKZ_ausblasen"; 

      U     "O_WZ_MIN_SCH_MV1"; 
      U     "I_WZ_MIN_SCH_DRUCK_OK"; 
      =     "O_WZ_MIN_SCH_MV2"; 


      U     "DB_DM_M_FUNKTION".MX_M[34]; 
      U     "I_WZ_MIN_SCH_Fuell_OK"; 
      S     "O_WZ_MIN_SCH_MV1"; 
      R     "DB_DM_M_FUNKTION".MX_M[34]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[34]; 

      U     "DB_DM_M_FUNKTION".MX_M[35]; 
      O     "DB_DM_M_FUNKTION".MX_M[30]; 
      R     "O_WZ_MIN_SCH_MV1"; 
      R     "DB_DM_M_FUNKTION".MX_M[35]; 




//Umschaltung Minimalmengenschmierung M8

      U     "M_M8_Act"; 
      O     "M_M8_WZ_Tab"; 
      U     "O_WZ_MIN_SCH_MV1"; 
      =     "O_WZ_MIN_SCH_Ums_M8"; //=     A     75.3

//Umschaltung Minimalmengenschmierung M7

      U     "M_M7_Act"; 
      O     "M_M7_WZ_Tab"; 
      U     "O_WZ_MIN_SCH_MV1"; 
      =     "O_WZ_MIN_SCH_Ums_M7"; // =     A     75.2

MEND: NOP   0; 
NETWORK
TITLE =Minimalmengenschmierung Fa.Vogel

STVO: U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._035_ANWAHL_MIN_SCHMIER; 
      L     2; 
      >=I   ; 
      )     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._035_ANWAHL_MIN_SCHMIER; 
      L     4; 
      <=I   ; 
      )     ; 
      SPBN  XEND; 

      SET   ; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[34]; 
      R     "DB_DM_M_FUNKTION".MX_QuitPflicht[35]; 

      O     "M_Reset_Taste"; 
      R     "M_MinMg_Schmier_Aktiv"; 
      R     "DB_DM_M_FUNKTION".MX_M[34]; 

//     UN    "I_WZ_MIN_SCH_Fuell_OK"
      UN    "DB_ML_SPI".Fuellstand_iO; 
      U(    ; 
      O     "DB_DM_M_FUNKTION".MX_M[34]; 
      O     "O_WZ_MIN_SCH_MV1"; 
      O     "O_WZ_MIN_SCH_MV2"; 
      )     ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701052_MinSchm_Fuell_0S; 

//   U     "I_WZ_MIN_SCH_Fuell_OK"
      U     "DB_ML_SPI".Fuellstand_iO; 
      U     "DB_DM_M_FUNKTION".MX_M[34]; 
      S     "M_MinMg_Schmier_Aktiv"; 
      R     "DB_DM_M_FUNKTION".MX_M[34]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[34]; 

      U     "DB_DM_M_FUNKTION".MX_M[35]; 
      O     "DB_DM_M_FUNKTION".MX_M[30]; 
//      O     "Kanal1".E_M30              //-- M2/M30 aktiv
      R     "M_MinMg_Schmier_Aktiv"; 
      R     "DB_DM_M_FUNKTION".MX_M[35]; 

      U     "M_M7_Act"; 
      U     "O_WZ_MIN_SCH_MV1"; 
      L     S5T#6S; 
      SV    "Tv_MinSchm_M7"; 
      U     "Tv_MinSchm_M7"; 
      =     "O_MinSchm_IKZ_ausblasen"; 

      UN    "Tv_MinSchm_M7"; 
      U(    ; 
      U     "M_M7_Act"; 
      O     "M_M7_WZ_Tab"; 
      U     "M_MinMg_Schmier_Aktiv"; 
      )     ; 
      =     "O_WZ_MIN_SCH_Ums_M7"; 

      U     "M_M8_Act"; 
      O     "M_M8_WZ_Tab"; 
      U     "M_MinMg_Schmier_Aktiv"; 
      =     "O_WZ_MIN_SCH_Ums_M8"; 

      U     "M_M7_Act"; 
      O     "M_M8_Act"; 
      O     "M_M7_WZ_Tab"; 
      O     "M_M8_WZ_Tab"; 
      U     "M_MinMg_Schmier_Aktiv"; 
      =     "O_WZ_MIN_SCH_MV1"; 

      U     "O_WZ_MIN_SCH_MV1"; 
      U     "I_WZ_MIN_SCH_DRUCK_OK"; 
      =     "O_WZ_MIN_SCH_MV2"; 
XEND: NOP   0; 


NETWORK
TITLE =Schmierung DrehDurchführung

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701204_DD_Fuellstand; 
      R     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701205_DD_DruckS_0Sig; 
      R     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701206_DD_DruckS_1Sig; 

      L     "DB_PLC_MD_DB20".UDInt._034_ANWAHL_DREHDURCH_SM; 
      L     0; 
      ==I   ; 
      SPB   DEND; 

      UN    "I_AL_SCH_M26_Fuellstand"; 
      L     S5T#30M; 
      SE    "Te_DrehDurch_Fuell"; 

      U     "Te_DrehDurch_Fuell"; 
      S     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701204_DD_Fuellstand; 


      U     "O_M7_Luftkuehlung"; 
      FP    "M_FP_Luftkuehlung"; 
      =     "M_Imp_Luftkuehlung_ein"; 

      L     "MB_SKZ_DrehDurch"; 
      SPL   S099; 
      SPA   S000; // Pumpe ist aus Zeit runterzählen   | M3/M4 Zeit unter 0 
      SPA   S001; // Zeit  
      SPA   S002; 
      SPA   S003; 
      SPA   S004; 

S099: SPA   DEND; 

//--------------------------------------------------------------------------
// Schritt 0 Grundstellung
S000: CLR   ; 
      =     "O_AL_SCH_M26_Pumpe_ein"; 
      L     0; 
      T     "MW_DrehDurch_Zaehler"; 

      U     "O_M7_Luftkuehlung"; // lt. Festlegung Veitinger 15.6.05 Schmieren nur wenn M72/M7 aktiv
      UN    "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701205_DD_DruckS_0Sig; 
      SPBN  DEND; 

      L     DB100.DBW   18; 
      L     0; 
      <=I   ; 
      O     "M_Imp_Luftkuehlung_ein"; 
      SPBN  E001; 
      L     1; 
      T     "MB_SKZ_DrehDurch"; 
      SPA   DEND; 

E001: U     "M_Takt_1s"; 
      SPBN  DEND; 

      L     DB100.DBW   18; 
      L     1; 
      -D    ; 
      T     DB100.DBW   18; 

      SPA   DEND; 

//--------------------------------------------------------------------------
// Schritt 1 Schmierung einschalten und Warten bis Druck aufgebaut

S001: U     "I_AL_SCH_M26_Fuellstand"; 
      SPB   E011; 
      SET   ; 
      S     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701204_DD_Fuellstand; 
      L     0; 
      T     "MB_SKZ_DrehDurch"; 
      SPA   DEND; 

E011: SET   ; 
      =     "O_AL_SCH_M26_Pumpe_ein"; 

      U     "M_Takt_1s"; 
      SPBN  EO12; 

      L     "MW_DrehDurch_Zaehler"; 
      L     1; 
      +I    ; 
      T     "MW_DrehDurch_Zaehler"; 

EO12: L     "MW_DrehDurch_Zaehler"; 
      L     20; 
      >I    ; 
      SPBN  E013; 

      SET   ; 
      =     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701205_DD_DruckS_0Sig; 
      L     0; 
      T     "MB_SKZ_DrehDurch"; 
      SPA   DEND; 

E013: U     "I_AL_SCH_M26_Rueckm"; 
      SPBN  DEND; 
      L     2; 
      T     "MB_SKZ_DrehDurch"; 
      L     0; 
      T     "MW_DrehDurch_Zaehler"; 
      SPA   DEND; 

//--------------------------------------------------------------------------
// Schritt 2 Schmieren

S002: SET   ; 
      S     "O_AL_SCH_M26_Pumpe_ein"; 

      U     "M_Takt_1s"; 
      SPBN  EO22; 

      L     "MW_DrehDurch_Zaehler"; 
      L     1; 
      +I    ; 
      T     "MW_DrehDurch_Zaehler"; 

EO22: U     "I_AL_SCH_M26_Rueckm"; 
      SPB   E021; 
      L     1; 
      T     "MB_SKZ_DrehDurch"; 
      L     0; 
      T     "MW_DrehDurch_Zaehler"; 
      SPA   DEND; 

E021: L     "MW_DrehDurch_Zaehler"; 
      L     19; 
      >I    ; 
      SPBN  DEND; 

      CLR   ; 
      =     "O_AL_SCH_M26_Pumpe_ein"; 
      L     0; 
      T     "MW_DrehDurch_Zaehler"; 
      L     3; 
      T     "MB_SKZ_DrehDurch"; 
      SPA   DEND; 

//--------------------------------------------------------------------------
// Schritt 3 Schmierung aus


S003: SET   ; 
      R     "O_AL_SCH_M26_Pumpe_ein"; 

      U     "M_Takt_1s"; 
      SPBN  EO31; 

      L     "MW_DrehDurch_Zaehler"; 
      L     1; 
      +I    ; 
      T     "MW_DrehDurch_Zaehler"; 

EO31: L     "MW_DrehDurch_Zaehler"; 
      L     10; 
      >I    ; 
      SPBN  EO32; 
      SET   ; 
      =     "DB_FEHLERMELDUNG".VSP_Halt_Satzwechsel._701206_DD_DruckS_1Sig; 
      SPA   EO33; 

EO32: U     "I_AL_SCH_M26_Rueckm"; 
      SPB   DEND; 

EO33: L     4; 
      T     "MB_SKZ_DrehDurch"; 
      SPA   DEND; 

//--------------------------------------------------------------------------
// Schritt 4 Zeit neu setzen

S004: L     "DB_PLC_MD_DB20".UDInt._152_DREHDURCH_AUSZEIT; 
      L     5; 
      <=I   ; 
      SPBN  E041; 
      L     300; 
      T     DB100.DBW   18; 
      SPA   E042; 

E041: L     "DB_PLC_MD_DB20".UDInt._152_DREHDURCH_AUSZEIT; 
      L     60; 
      *D    ; 
      T     DB100.DBW   18; 

E042: L     0; 
      T     "MB_SKZ_DrehDurch"; 

DEND: NOP   0; 

NETWORK
TITLE =Rauch- u.Nebelabscheider
//      U     "O_AL_NEBLABSCH_EIN"        //A    40.1
//      =     "OX_AL_NEBLABSCH_EIN"       //M   159.3 
//     
//      U     "IX_AL_NEG_MSS_NEBELABSCH"  //E    48.0   
//      =     "I_AL_NEG_MSS_NEBELABSCH"   //M   159.0
//
//      U     "IX_AL_NEBELABSCH_EIN"      //E    48.1
//      =     "I_AL_NEBELABSCH_EIN"       //M   159.1 
//
//      U     "IX_AL_NEG_NEBELAB_FREI"    //E    48.2
//      =     "I_AL_NEG_NEBELAB_FREI"     //M   159.2
//
//
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Warnung._701955_MSS_NEBELAB; 
      R     "DB_FEHLERMELDUNG".Warnung._701954_Stoerung_NEBELAB; 
      R     "DB_FEHLERMELDUNG".SPI_VS_HALT_G0._702232_RauchNebel_Stoer; 

// Abfrage für Anwahl Nebelabscheider Volvo
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1000; 
      <I    ; 
      SPBN  Volv; 

// Wenn Kabine auf, Nachlaufzeit starten 

      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1; 
      >=I   ; 
      )     ; 
      U     "I_AL_Kabine_zu"; 
      FN    "M_FN_Kabine_ZU"; // "M_FN_Kabine_ZU"
      L     8192; // Zeitbasis 1s laden
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; // MD in BCD wandeln und mit Zeitbasis
      ITB   ; // verodern
      OW    ; 
      SV    "Tv_OENEB_NAchlZ"; //"T_OENEAB_NachlZ"

      U     "Tv_OENEB_NAchlZ"; //"T_OENEAB_NachlZ"
      O     "I_AL_Kabine_zu"; 
      U     "M_MA_Maschine_Ein"; 
      UN(   ; 
      U     "DB_PLC_MD_DB20".UDHex._28_Bit6_FLUSH_JET; 
      U     "O_KM_Spuelpumpe_ein"; 
      )     ; 
      =     "O_AL_NEBELABSCH_EIN"; //"O_AL_NEBELABSCH_EIN"

// Motorschutzschalter 
      UN    "I_AL_NEG_MSS_NEBELABSCH"; //"I_AL_NEG_MSS_NEBELABSCH"
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1; 
      >=I   ; 
      )     ; 
      =     "DB_FEHLERMELDUNG".Warnung._701955_MSS_NEBELAB; 

// Überwachung Nebelab
      UN    "I_AL_NEG_NEBELAB_FREI"; // "I_AL_NEG_NEBELAB_FREI"
      UN    "I_AL_NEBELABSCH_EIN"; //"I_AL_NEBELABSCH_EIN"
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1; 
      >=I   ; 
      )     ; 
      U     "M_Leistung_Steht_an"; 
      L     S5T#10S; 
      SE    "Te_Stoer_Nebelabscheider"; 
      U     "Te_Stoer_Nebelabscheider"; 
      UN    "DB_PLC_MD_DB20".UDHex._14_BIT5_OelNebel_Fehl_R; 
      =     "DB_FEHLERMELDUNG".Warnung._701954_Stoerung_NEBELAB; 

      U     "Te_Stoer_Nebelabscheider"; 
      O(    ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1; 
      >=I   ; 
      )     ; 
      UN    "I_AL_NEG_MSS_NEBELABSCH"; 
      )     ; 
      U     "DB_PLC_MD_DB20".UDHex._14_BIT5_OelNebel_Fehl_R; 
      S     "DB_FEHLERMELDUNG".SPI_VS_HALT_G0._702232_RauchNebel_Stoer; 

Volv: NOP   0; 
NETWORK
TITLE =Ölnebelabscheider Volvo
//Kunde bestellt zusätzlich Softwareoption "forced ventilation":
//Signal im Schaltschrank auf potentialfreien Kontakt gelegt für kundenseitige 
//Aktivierung einer verstärken Arbeitsraumabsaugung.
//1. Ausgabe des Signals nach Betätigung der Taste "Tür Öffnen": 
//Fall 1: Tür lässt sich direkt öffnen wenn seit dem letzten Tür Öffnen keinen
//        KSS oder MMS Funktion aktiv war
//Fall 2: Tür lässt sich erst nach Wartezeit (z.B. 60 Sek., muss veränderbar sein)
//        öffnen, falls eine KSS oder MMS Funktion seit dem letzten Tür öffnen 
//        aktiv war
//2. Ausgabe des Signals über Programm über M-Funktion.
      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1000; 
      >I    ; 
      SPBN  NW10; 

      L     "DB_PLC_MD_DB20".UDInt._049_ANWAHL_RAUCH_NEBEL; 
      L     1000; 
      -I    ; 
      T     #t_Zeit_OelNebel; 

      U     "O_KM_M8_Ein"; 
      O     "MX_IKZ_Pumpen_Ein"; 
      O     "M_MinMg_Schmier_Aktiv"; 
      S     "Mx_KSA_war_Aktiv"; 

      U     "M_IM_Taste_Tuerver_entr"; // anforderung entriegeln
      U     "Mx_KSA_war_Aktiv"; // KSA war aktiv
// Zeit starten
      L     8192; // Zeitbasis 1s laden
      L     #t_Zeit_OelNebel; 
      ITB   ; 
      OW    ; 
      SV    "Tv_Absau_Time"; 

      U     "Tv_Absau_Time"; 
      O     "I_AL_NEBELABSCH_EIN"; 
      =     "O_SO_EXH_ON1"; 

      U     "Tv_Absau_Time"; 
      FN    "mx_fn_rauchnebel"; 
      =     "Mx_Rauchnebel_fertig"; 

      UN    "I_AL_Kabine_zu"; 
      O     "Mx_Rauchnebel_fertig"; 
      UN(   ; 
      U     "O_KM_M8_Ein"; 
      O     "MX_IKZ_Pumpen_Ein"; 
      )     ; 
      R     "Mx_KSA_war_Aktiv"; 

      U     "O_SO_EXH_ON1"; 
      =     "DB_FEHLERMELDUNG".SAFE_MELD._701530; 

NW10: NOP   0; 

NETWORK
TITLE =700215 "SPI-VS-H, Eingang Sicherung 24V 0-Signal"

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt._700215_Sicherung_24V_0; 
      UN    "I_AL_Sicherung_24V_OK"; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt._700215_Sicherung_24V_0; 
      NOP   0; 
NETWORK
TITLE =700525 Meldung Sicherung 230V ausgelöst

      UN    "I_AL_SICHERUNG_230V_OK"; 
      =     "DB_FEHLERMELDUNG".MELDUNG3._700525_Sicherung_230V_0; 
NETWORK
TITLE =Anwahl ATC - Application Tuning Cycle

      SET   ; 
      R     "DB_H_FUNKTION".HX_QuitPflicht[10]; 

      UN    "DB_PLC_MD_DB20".UDHex._21_Bit0_DM_ATC_ON; // Anwahl ATC-Zyklus
      SPB   HEND; 

      U     "M_Reset_Taste"; 
      U     "DB_PLC_MD_DB20".UDHex._15_BIT6_AnwahlATC_Zykl; 
      SPBN  HSTR; 
      SET   ; 
      R     "DB_ACHSE1_X".A_ParA; 
      R     "DB_ACHSE1_X".A_ParB; 
      R     "DB_ACHSE1_X".A_ParC; 

      R     "DB_ACHSE2_Y".A_ParA; 
      R     "DB_ACHSE2_Y".A_ParB; 
      R     "DB_ACHSE2_Y".A_ParC; 

      R     "DB_ACHSE3_Z".A_ParA; 
      R     "DB_ACHSE3_Z".A_ParB; 
      R     "DB_ACHSE3_Z".A_ParC; 

      R     "DB_ACHSE4".A_ParA; 
      R     "DB_ACHSE4".A_ParB; 
      R     "DB_ACHSE4".A_ParC; 

      R     "DB_ACHSE5".A_ParA; 
      R     "DB_ACHSE5".A_ParB; 
      R     "DB_ACHSE5".A_ParC; 

HSTR: NOP   0; 

      SET   ; 
      S     "DB_H_FUNKTION".HX_QuitPflicht[10]; 

      U     "DB_H_FUNKTION".HX_IM[10]; 
      SPBN  H001; 

      L     "DB_H_FUNKTION".HX_Real_Wert[10]; 
      RND   ; 

      T     #Tw_Funktion; 
      L     0; 
      >I    ; 
      TAK   ; 
      U(    ; 
      L     58; 
      <=I   ; 
      )     ; 
      SPBN  H001; 

      L     #Tw_Funktion; 
      ITB   ; 
      SLW   8; 
      SRW   12; 
      T     #TB_Achse; 

      L     #Tw_Funktion; 
      ITB   ; 
      SLW   12; 
      SRW   12; 
      T     #TB_Antriebssatz; 

// Achse 1..5
      L     #TB_Achse; 
      L     0; 
      >I    ; 
      U(    ; 
      L     #TB_Achse; 
      L     5; 
      <=I   ; 
      )     ; 
      SPBN  H001; 

// Antriebssatz 1..8
      L     #TB_Antriebssatz; 
      L     0; 
      >I    ; 
      U(    ; 
      L     #TB_Antriebssatz; 
      L     8; 
      <=I   ; 
      )     ; 
      SPBN  H001; 

      L     #TB_Antriebssatz; 
      L     5; 
      >=I   ; 
      =     #Tx_Bit3; 

      L     #TB_Antriebssatz; 
      L     3; 
      >=I   ; 
      U(    ; 
      L     #TB_Antriebssatz; 
      L     4; 
      <=I   ; 
      )     ; 
      O(    ; 
      L     #TB_Antriebssatz; 
      L     7; 
      >=I   ; 
      )     ; 
      =     #Tx_bit2; 

      L     #TB_Antriebssatz; 
      L     2; 
      ==I   ; 
      O(    ; 
      L     #TB_Antriebssatz; 
      L     4; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #TB_Antriebssatz; 
      L     6; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     #TB_Antriebssatz; 
      L     8; 
      ==I   ; 
      )     ; 
      =     #Tx_Bit1; 

      L     #TB_Achse; 
      L     1; 
      ==I   ; 
      SPBN  H002; 
      AUF   "DB_ACHSE1_X"; 
      SPA   H006; 

H002: L     #TB_Achse; 
      L     2; 
      ==I   ; 
      SPBN  H003; 
      AUF   "DB_ACHSE2_Y"; 
      SPA   H006; 

H003: L     #TB_Achse; 
      L     3; 
      ==I   ; 
      SPBN  H004; 
      AUF   "DB_ACHSE3_Z"; 
      SPA   H006; 

H004: L     #TB_Achse; 
      L     4; 
      ==I   ; 
      SPBN  H005; 
      AUF   "DB_ACHSE4"; 
      SPA   H006; 

H005: L     #TB_Achse; 
      L     5; 
      ==I   ; 
      SPBN  H001; 
      AUF   "DB_ACHSE5"; 

H006: U     #Tx_Bit1; 
      =     DBX   21.0; 

      U     #Tx_bit2; 
      =     DBX   21.1; 

      U     #Tx_Bit3; 
      =     DBX   21.2; 

H001: U(    ; 
      U     DBX   21.0; 
      U     DBX   93.0; 
      O     ; 
      UN    DBX   21.0; 
      UN    DBX   93.0; 
      )     ; 
      O(    ; 
      U     DBX   21.1; 
      U     DBX   93.1; 
      O     ; 
      UN    DBX   21.1; 
      UN    DBX   93.1; 
      )     ; 
      O(    ; 
      U     DBX   21.2; 
      U     DBX   93.2; 
      O     ; 
      UN    DBX   21.2; 
      UN    DBX   93.2; 
      )     ; 
      U     "DB_H_FUNKTION".HX_M[10]; 
      S     "DB_H_FUNKTION".HX_Quitt[10]; 
      R     "DB_H_FUNKTION".HX_M[10]; 

HEND: NOP   0; 
NETWORK
TITLE =BATC (Beladungs_ATC): H11 quittierpflichtig
//Im DM_ATC_ON wird die H FKt aufgerufen und ein Wert übergeben.
//In der ATC_EXE ist ein Hotlink auf den DB109.DBD244
//Mit Änderung dieses Wertes schreibt die EXE die entsprechenden Maschinendaten 
//um.
//Ist die EXE fertig schreibt sie die Variable DB109.DBD244 wieder auf Null, und 
//quittiert somit die H-Fkt.
//Reset um 5Sekunden verzögert da wenn er sofort wirksam ist kann während die 
//abgebrochene Abandlung in der Applikation noch abgearbeitet wird schon wieder 
//eine neue gestartet werden die aber dann von der vorherigen quittiert wird.
//
      L     S5T#5S; 
      U     "M_Reset_Taste"; 
      SS    "T_Verzög_Reset"; 

      SET   ; 
      S     "DB_H_FUNKTION".HX_QuitPflicht[11]; 

      U(    ; 
      L     "DB_H_FUNKTION".HX_Real_Wert[11]; 
      L     0; 
      ==D   ; 
      )     ; 
      O     "T_Verzög_Reset"; 
      =     "DB_H_FUNKTION".HX_Quitt[11]; 
      R     "DB_H_FUNKTION".HX_M[11]; 
      R     "T_Verzög_Reset"; 
      SPBN  en13; 
      L     0; 
      T     "DB_H_FUNKTION".HX_Real_Wert[11]; 

en13: NOP   0; 
NETWORK
TITLE =Reset ATC-Fehler
//Dieser Fehler wird über den Alarmserver (ATC.ini-Datei) generiert und wird 
//durch PLC nur zurückgesetzt
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt2._702500_ATC_FEHLER; 
NETWORK
TITLE =Blasluft Drehmomentstütze

      U     "DB_PLC_MD_DB20".UDHex._07_BIT6_Opt_DMS_Blasluf; 
      SPB   DMSA; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701054_DMS_Oel_0_Signal; 
      R     "DB_DM_M_FUNKTION".MX_M[168]; 
      R     "DB_DM_M_FUNKTION".MX_M[169]; 
      SPA   DMSE; 

DMSA: U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701054_DMS_Oel_0_Signal; 

      U     "DB_DM_M_FUNKTION".MX_M[168]; 
      S     "M_SO_DMS_PNEUMAK_EIN"; 

      U     "DB_DM_M_FUNKTION".MX_M[169]; 
      O     "M_Reset_Taste"; 
      R     "M_SO_DMS_PNEUMAK_EIN"; 

      U     "DB_DM_M_FUNKTION".MX_M[168]; 
      R     "DB_DM_M_FUNKTION".MX_M[168]; 

      U     "DB_DM_M_FUNKTION".MX_M[169]; 
      R     "DB_DM_M_FUNKTION".MX_M[169]; 

      UN    "I_SO_DMS_UEBERWACH_OEL"; 
      R     "O_SO_DMS_PNEUMAK_EIN"; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701054_DMS_Oel_0_Signal; 

      U     "M_SO_DMS_PNEUMAK_EIN"; 
      U     "I_AL_Kabine_Verriegelt"; 
      =     "O_SO_DMS_PNEUMAK_EIN"; 
DMSE: NOP   0; 
END_FUNCTION

