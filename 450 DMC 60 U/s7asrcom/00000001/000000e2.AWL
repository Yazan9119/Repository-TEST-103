FUNCTION "FC_DM_MCP" : VOID
TITLE =
//$Revision: 1.31 $
//$Date: 2008/09/24 08:37:57CEST $
//$Author: hgc $
AUTHOR : SJA
FAMILY : DM_MSST
NAME : MSST
VERSION : 0.1


VAR_INPUT
  E_EN : BOOL ;	
END_VAR
VAR_IN_OUT
  EA_xplusfahren : BOOL ;	
  EA_xminusfahren : BOOL ;	
  EA_yminusfahren : BOOL ;	
  EA_yplusfahren : BOOL ;	
  EA_zplusfahren : BOOL ;	
  EA_zminusfahren : BOOL ;	
  EA_4rechtsfahren : BOOL ;	
  EA_4linksfahren : BOOL ;	
  EA_5plusfahren : BOOL ;	
  EA_5minusfahren : BOOL ;	
  EA_6plusfahren : BOOL ;	
  EA_6minusfahren : BOOL ;	
  EA_meilgang : BOOL ;	
  EA_11plusfahren : BOOL ;	
  EA_11minusfahren : BOOL ;	
  EA_12plusfahren : BOOL ;	
  EA_12minusfahren : BOOL ;	
  EA_8plusfahren : BOOL ;	
  EA_8minusfahren : BOOL ;	
END_VAR
VAR_TEMP
  dummy : BOOL ;	
  spin_over : BYTE ;	
  temp_spnr : BYTE ;	
  temp_dbb19 : BYTE ;	
  TX_TASTENSPERRE_1 : BOOL ;	
  TX_TASTENSPERRE_2 : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Increment ausblenden

      U     "DB_PLC_MD_DB20".UDHex._11_BIT2_Abw_INC_MSST; 
      R     "MSSTst var. INC"; 
      R     "MSSTst 1 INC"; 
      R     "MSSTst 10 INC"; 
      R     "MSSTst 100 INC"; 
      R     "MSSTst 1000 INC"; 

NETWORK
TITLE =CALL  "FC_MASCHTYP_MCP"
//hier wird entschieden ob Baustein abgearbeitet wird
//->Modul Enabled
//->im Jog oder Referenzbetrieb
//
//Das Modul wird vor dem FC24 mit MSST_lesen_schreiben true aufgerufen
//und Netzwerk 2 wird bearbeitet
//
//und nach dem FC24 mit MSST_lesen_schreiben false aufgerufen
//und Netzwerk 3 und 4 werden bearbeitet
//
//Die Verfahrtasten(Deckel Maho werden alle rueckgesetzt und erst am Ende)
//Netzerk 4 bearbeitet (FC24 bekommt nichts von den Verfahrebefehlen mit)
//
//Die Spindeltasten der DECKEL MAHO MSST werden in Standardeingaenge der 
//Standard-MSST umgewandel und anschliessen im FC24 bearbeitet.
//Im zweiten Aufruf (nach Abarbeitung FC24)werden Sie wieder in die DECKEL MAHO 
//MSST Eingaenge/Ausgaenge zuruckgesetzt.
      CALL "FC_MASCHTYP_MCP" (
           EAX_MSST_NC_Start        := "MSSTst_NC_Start",
           EAX_MSST_NC_Stop         := "MSSTst NC-Stop *",
           EAX_MSST_Feed_Start      := "I_DM_Ta_Feed_Start",
           EAX_MSST_Reset           := "MSSTstReset",
           EAX_MSST_SP_Overr_100    := E      0.5,
           EAX_MSST_WZAX_Rett3070   := E      6.3,
           EAX_MSST_WZAX_Rett3071   := "IX_MSST_Ta_2_4",
           EAB_MSST_Eilgang_Overr   := "MSST_Eilgang_OVR",
           EAB_MSST_Vorschub_Overr  := "MSST_Vorschub_OVR");
      NOP   0; 
NETWORK
TITLE =CALL "FC_BHG"

      CALL "FC_BHG" (
           Gesamt_Freigabe          := "M_MA_Maschine_Ein",
           Handrad_Freigabe         := FALSE,
           _4_Achsbetrieb           := TRUE,
           _5_Achsbetrieb           := TRUE,
           Zustimm_Taste            := "I_HR_Anwahl",
           Achswahl_Bit_0           := "I_HR_Achsanwahl_Bit_0",
           Achswahl_Bit_1           := "I_HR_Achsanwahl_Bit_1",
           Achswahl_Bit_2           := "I_HR_Achsanwahl_Bit_2",
           Eilgang_Taste            := "I_HR_Eilgang_Taste",
           Plus_Taste               := "I_HR_plus_Taste",
           Minus_Taste              := "I_HR_minus_Taste",
           F1_Taste                 := "I_HR_FKT_Taste_F1",
           F2_Taste                 := "I_HR_FKT_Taste_F2",
           F3_Taste                 := "I_HR_FKT_Taste_F3",
           Achskreuz                := TRUE,
           Option_MBHG              := TRUE,
           E_MBHG_Anwahl            := "I_DM_Ta_BHG",
           E_INC_VAR_Taste          := "MSSTst var. INC",
           E_INC_1_Taste            := "MSSTst 1 INC",
           E_INC_10_Taste           := "MSSTst 10 INC",
           E_INC_100_Taste          := "MSSTst 100 INC",
           E_INC_1000_Taste         := "MSSTst 1000 INC",
           E_Tuer_zu                := "I_AL_Kabine_Verriegelt",
           E_BA4_angewaehlt         := "M_4Te_Betriebsart",
           LED_Achse_Z              := #dummy,
           LED_Achse_X              := #dummy,
           LED_Achse_Y              := #dummy,
           LED_Achse_4              := #dummy,
           LED_Achse_5              := #dummy,
           LED_Spindel              := #dummy,
           Meld_BHG_Bereit          := "DB_FEHLERMELDUNG".Meldung._702003_MBHG_aktiv,
           Meld_Kein_BHG            := "DB_FEHLERMELDUNG".Meldung._702004_MBHG_not_connect,
           Meld_Axtast_Handrad      := "DB_FEHLERMELDUNG".Meldung._702001_MBHG_Bedienung,
           Meld_OVR0                := "DB_FEHLERMELDUNG".Meldung._702002_MBHG_Override0,
           LED_INC_1                := "MSSTLED 1 INC",
           LED_INC_10               := "MSSTLED 10 INC",
           LED_INC_100              := "MSSTLED 100 INC",
           LED_INC_1000             := "MSSTLED 1000 INC",
           LED_INC_var              := "MSSTLED var. INC",
           Pos_Fl                   := "M_HM_PF_MBHG",
           Neg_Fl                   := "M_HM_NF_MBHG",
           EA_BHG_angewaehlt        := "M_Anwahl_MBHG",
           EA_LED_MBHG_Anwahl       := A      5.7,
           LED_Zustand_Retten       := "LED_Zustand_Retten",
           Inkrement_normal         := "DB_REMANENT".Handrad_Inkrement_normal,
           Inkrement_Eilgang        := "DB_REMANENT".Handrad_Inkrement_Eilgan,
           Taste_BA_JOG             := "MSSTst BA JOG");

NETWORK
TITLE =CALL  "FC_CS_BHG_MPI"

      CALL "FC_CS_BHG_MPI" ;
      NOP   0; 

NETWORK
TITLE =SM RESET Taste BA_AUTO / BA_JOG 

      U     "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      UN    "DB_SIEM_KANAL_1".E_ChanReset; 
      UN    "DB_SIEM_KANAL_1".E_ChanInterrupt; 
      U(    ; 
      O     "MSSTst BA AUTO"; 
      O     "MSSTst BA JOG"; 
      )     ; 
      R     "MSSTst BA AUTO"; 
      R     "MSSTst BA JOG"; 
NETWORK
TITLE =SM RESET Taste BA_MDA


      U     "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      UN    "DB_SIEM_KANAL_1".E_ChanReset; 
      U     "MSSTst BA MDA"; 
      R     "MSSTst BA MDA"; 
NETWORK
TITLE =Positive Flanke 4-Achse ist Spindel

      U     "DB_ACHSE4".E_SpNA; 
      FP    "M_FP_Achse4_Spindel"; 
      =     "M_IMP_Achse4_Spindel"; 
NETWORK
TITLE =Negative Flanke 4-Achse ist Spindel

      U     "DB_ACHSE4".E_SpNA; 
      FN    "M_FN_Achse4_Spindel"; 
      =     "M_IMP_Achse4_Achse"; 
NETWORK
TITLE =Spindelnummer Achse 6 laden

      L     6; 
      T     #temp_spnr; 
      NOP   0; 

NETWORK
TITLE =Spindeloverride Achse 6 laden

      L     "DB_ACHSE6_SPINDEL".A_SpOR; 
      T     #temp_dbb19; 
      NOP   0; 
NETWORK
TITLE =4-Achse ist Spindel

      U     "DB_ACHSE4".E_SpNA; 
      SPBN  M001; 
NETWORK
TITLE =Spindelnummer Achse 4 laden

      L     4; 
      T     #temp_spnr; 
      NOP   0; 
NETWORK
TITLE =Spindeloverride Achse 4 laden

      L     "DB_ACHSE4".A_SpOR; 
      T     #temp_dbb19; 
      NOP   0; 
NETWORK
TITLE =Aufruf des Konzernbedienpultes 
//(Angepasster FC24 aus dem SIEMENS Grundprogramm)

M001: CALL "FC_DM_KONZ_MCP_FC24" (
           BAGNo                    := B#16#1,
           ChanNo                   := B#16#1,
           SpindleIFNo              := #temp_spnr,
           FeedHold                 := "M_FeedHold_from_FC24",
           SpindleHold              := "M_SpindleHold",
           SpindleDir               := "M_SpindleDir");

NETWORK
TITLE =Aufruf FC_OPERATOR_PANEL

      CALL "FC_OPERATOR_PANEL" (
           SpindleIFNo              := #temp_spnr);

NETWORK
TITLE =Override Umwandlung Greycode nach Binär

      CALL "FC_UT_GRAY_TO_BIN" (
           Greycode                 := #temp_dbb19,
           Binaerwert               := #spin_over);

NETWORK
TITLE =Spindeloverride nach Achsnahtstelle 

      O     "M_Einrichtbetrieb"; 
      O     "M_3Te_Betriebsart"; 
      O     "M_4Te_Betriebsart"; 
      U(    ; 
      L     #spin_over; 
      L     11; 
      >I    ; 
      )     ; 
      O     "MX_Override100"; 
      O     "M_IMP_Achse4_Spindel"; 
      O     "M_IMP_Achse4_Achse"; 
      SPBN  M002; 
      L     B#16#A; 
      T     "DB_SIEM_SYSTEM4".feld19[2]; 

      L     W#16#E; 
      T     "DB_ACHSE4".A_SpOR; 
      T     "DB_ACHSE6_SPINDEL".A_SpOR; 

M002: NOP   0; 

NETWORK
TITLE =Fahrbefehl durch Achstasten

      O     "I_DM_Ta X-Achse plus"; 
      O     "I_DM_Ta X-Achse minus"; 
      O     "I_DM_Ta Y-Achse plus"; 
      O     "I_DM_Ta Y-Achse minus"; 
      O     "I_DM_Ta Z-Achse plus"; 
      O     "I_DM_Ta Z-Achse minus"; 
      O     "I_DM_Ta 4-Achse rechts"; 
      O     "I_DM_Ta 4-Achse links"; 
      O     "I_DM_Ta_5/6-Achse plus"; 
      O     "I_DM_Ta_5/6-Achse minus"; 
      =     "MX_Verfahrbefehl"; 
NETWORK
TITLE =Verfahrtasten sperren
// Freifahrfunktion für Wiegekopf. Z kann plus fahren
      U(    ; 
      O(    ; 
      L     "DB_OEM".Bildnummer; 
      L     0; 
      <>I   ; 
      )     ; 
      O     "Mx_XAx_in_WZW_RAUM"; 
      )     ; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      =     L      5.0; 
      U     L      5.0; 
      UN    "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      =     #TX_TASTENSPERRE_1; 
      U     L      5.0; 
      BLD   102; 
      =     #TX_TASTENSPERRE_2; 
NETWORK
TITLE =Taste "I_DM_Ta_Spindel HALT"

      U     "m_eins"; 
      =     "I_DM_Ta_Spindel HALT"; 
NETWORK
TITLE =Taste "I_DM_Ta Spindel rechts"

      U     "I_DM_Ta Spindel rechts"; 
      UN    "M_MTaster_in_Spi"; 
      UN    "Mx_XAx_in_WZW_RAUM"; 
      UN    #TX_TASTENSPERRE_2; 
      =     "I_DM_Ta Spindel rechts"; 
NETWORK
TITLE =Taste "I_DM_Ta_Spindel links"

      U     "I_DM_Ta_Spindel links"; 
      UN    "M_MTaster_in_Spi"; 
      UN    "Mx_XAx_in_WZW_RAUM"; 
      UN    #TX_TASTENSPERRE_2; 
      =     "I_DM_Ta_Spindel links"; 
NETWORK
TITLE =Taste "I_DM_Ta Eilgang"

      U     "I_DM_Ta Eilgang"; 
      =     #EA_meilgang; 
      =     "DM_LED 5/6Achsanwahl"; 
      =     "I_DM_Ta Eilgang"; 
NETWORK
TITLE =Taste "I_DM_Ta_BHG"

      U     "I_DM_Ta_BHG"; 
      =     "I_DM_Ta_BHG"; 
NETWORK
TITLE =Achse X Jog minus und Taste "I_DM_Ta X-Achse minus"

      U     "I_DM_Ta X-Achse minus"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_xminusfahren; 
      =     "DM_LED X-Achse plus(1)"; 
      =     "I_DM_Ta X-Achse minus"; 
NETWORK
TITLE =Achse X Jog plus und Taste "I_DM_Ta X-Achse plus"

      U     "I_DM_Ta X-Achse plus"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_xplusfahren; 
      =     "DM_LED X-Achse minus(1)"; 
      =     "I_DM_Ta X-Achse plus"; 
NETWORK
TITLE =Achse Y Jog minus und Taste "I_DM_Ta Y-Achse minus"

      U     "I_DM_Ta Y-Achse minus"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_yminusfahren; 
      =     "DM_LED Y-Achse plus"; 
      =     "I_DM_Ta Y-Achse minus"; 
NETWORK
TITLE =Achse Y Jog plus und Taste "I_DM_Ta Y-Achse plus"

      U     "I_DM_Ta Y-Achse plus"; 
      UN    #TX_TASTENSPERRE_1; 
      =     #EA_yplusfahren; 
      =     "DM_LED Y-Achse minus"; 
      =     "I_DM_Ta Y-Achse plus"; 
NETWORK
TITLE =Achse Z Jog plus und Taste "I_DM_Ta Z-Achse plus"

      U     "I_DM_Ta Z-Achse plus"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_zplusfahren; 
      =     "DM_LED Z-Achse plus(3)"; 

      =     "I_DM_Ta Z-Achse plus"; 
NETWORK
TITLE =Achse Z Jog minus und Taste "I_DM_Ta Z-Achse minus"

      U     "I_DM_Ta Z-Achse minus"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_zminusfahren; 
      =     "DM_LED Z-Achse minus(3)"; 
      =     "I_DM_Ta Z-Achse minus"; 
NETWORK
TITLE =Achse 4 Jog minus und Taste "I_DM_Ta 4-Achse links"

      U     "I_DM_Ta 4-Achse links"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_4linksfahren; 
      =     "MSSTLED C-Achse (6)"; 
      =     "I_DM_Ta 4-Achse links"; 
NETWORK
TITLE =Achse 4 Jog plus und Taste "I_DM_Ta 4-Achse rechts"

      U     "I_DM_Ta 4-Achse rechts"; 
      UN    #TX_TASTENSPERRE_2; 
      =     #EA_4rechtsfahren; 
      =     "DM_LED 4-Achse rechts"; 
      =     "I_DM_Ta 4-Achse rechts"; 
NETWORK
TITLE =Achse 5 Jog plus / minus

      U(    ; 
      L     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      L     0; 
      ==I   ; 
      )     ; 
      UN    "DB_SIEM_BAG".E_REF; 
      UN    #TX_TASTENSPERRE_2; 
      =     L      5.0; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse plus"; 
      =     #EA_5plusfahren; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse minus"; 
      =     #EA_5minusfahren; 
NETWORK
TITLE =Achse 6 Jog plus / minus

      U(    ; 
      L     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      L     1; 
      ==I   ; 
      )     ; 
      UN    #TX_TASTENSPERRE_2; 
      =     L      5.0; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse plus"; 
      =     #EA_6plusfahren; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse minus"; 
      =     #EA_6minusfahren; 
NETWORK
TITLE =Taste 8-Achse (PW) #temp8_plus/#temp8_minus
//Eintrag 99 erfolgt im FC38
      U(    ; 
      L     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      L     99; 
      ==I   ; 
      )     ; 
      UN    #TX_TASTENSPERRE_2; 
      =     L      5.0; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse minus"; 
      =     #EA_8plusfahren; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse plus"; 
      =     #EA_8minusfahren; 
NETWORK
TITLE =Achse 11 Jog plus / minus

      U(    ; 
      L     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      L     2; 
      ==I   ; 
      )     ; 
      UN    #TX_TASTENSPERRE_2; 
      =     L      5.0; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse plus"; 
      =     #EA_11plusfahren; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse minus"; 
      =     #EA_11minusfahren; 
NETWORK
TITLE =Achse 12 Jog plus / minus

      U(    ; 
      L     "DB_ANWENDER_SOFTKEY".Achsanwahl._Umschalt2; 
      L     3; 
      ==I   ; 
      )     ; 
      UN    #TX_TASTENSPERRE_2; 
      =     L      5.0; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse plus"; 
      =     #EA_12plusfahren; 
      U     L      5.0; 
      U     "I_DM_Ta_5/6-Achse minus"; 
      =     #EA_12minusfahren; 
NETWORK
TITLE =Taste "I_DM_Ta_5/6-Achse plus" / LED

      O     #EA_5plusfahren; 
      O     #EA_6plusfahren; 
      O     #EA_8plusfahren; 
      O     #EA_11plusfahren; 
      O     #EA_12plusfahren; 
      =     "DM_LED 5/6-Achse plus"; 
      =     "I_DM_Ta_5/6-Achse plus"; 
NETWORK
TITLE =Taste "I_DM_Ta_5/6-Achse minus" / LED

      O     #EA_5minusfahren; 
      O     #EA_6minusfahren; 
      O     #EA_8minusfahren; 
      O     #EA_11minusfahren; 
      O     #EA_12minusfahren; 
      =     "DM_LED 5/6-Achse minus"; 
      =     "I_DM_Ta_5/6-Achse minus"; 
NETWORK
TITLE =Beschreiben MSSTLED-Ausgang INC1

      U     "DB_ACHSE1_X".E_INC1; 
      =     "MSSTLED 1 INC"; 


NETWORK
TITLE =Beschreiben MSSTLED-Ausgang INC10

      U     "DB_ACHSE1_X".E_INC10; 
      =     "MSSTLED 10 INC"; 
NETWORK
TITLE =Beschreiben MSSTLED-Ausgang INC100

      U     "DB_ACHSE1_X".E_INC100; 
      =     "MSSTLED 100 INC"; 
NETWORK
TITLE =Beschreiben MSSTLED-Ausgang INC1000

      U     "DB_ACHSE1_X".E_INC1000; 
      =     "MSSTLED 1000 INC"; 
NETWORK
TITLE =Beschreiben MSSTLED-Ausgang INCVar

      U     "DB_ACHSE1_X".E_INCVar; 
      =     "MSSTLED var. INC"; 
NETWORK
TITLE =Umschaltung MKS/WKS

      U     "DB_SIEM_MMC".E_ActWCS; 
      O     E      4.0; 
      FP    "M_FP_MKS/WKS"; 
      SPBN  M003; 
      UN    "DB_SIEM_MMC".A_ActWCS; 
      =     "DM_LED_MKS_WKS"; 
      =     "DB_SIEM_MMC".A_ActWCS; 
M003: NOP   0; 
NETWORK
TITLE =Rettung aktiv ?

      U(    ; 
      L     "DB_OEM".Bildnummer; 
      L     0; 
      <>I   ; 
      )     ; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      SPB   M004; 
NETWORK
TITLE =GEO-Achsen plus/minus ablöschen

      U     "m_null"; 
      =     "DB_SIEM_KANAL_1".A_Geo[1].Plus; 
      =     "DB_SIEM_KANAL_1".A_Geo[1].Minus; 
      =     "DB_SIEM_KANAL_1".A_Geo[2].Minus; 
      =     "DB_SIEM_KANAL_1".A_Geo[2].Plus; 
      =     "DB_SIEM_KANAL_1".A_Geo[3].Plus; 
      =     "DB_SIEM_KANAL_1".A_Geo[3].Minus; 
NETWORK
TITLE =Sprung ans Bausteinende

      ON    "DB_ICON"._1.UI_17; 
      ON    "DB_SIEM_MMC".A_ActWCS; 
      O     "DB_CONFIG".ANWAHL_DURCH_USER_DATA.MASCHTYP_3071_AKTIV; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._042_ANWAHL_KINE_TYP; 
      L     1; 
      ==I   ; 
      )     ; 
      SPB   M004; 
NETWORK
TITLE =GEO-Achsen Eilgang

      U     "I_DM_Ta Eilgang"; 
      =     "DB_SIEM_KANAL_1".A_Geo[1].RapidTrOR; 
      =     "DB_SIEM_KANAL_1".A_Geo[2].RapidTrOR; 
      =     "DB_SIEM_KANAL_1".A_Geo[3].RapidTrOR; 
NETWORK
TITLE =GEO-Achse 1 plus

      U     "I_DM_Ta X-Achse plus"; 
      UN    "DB_SIEM_BAG".E_REF; 
      =     "DB_SIEM_KANAL_1".A_Geo[1].Plus; 
      R     #EA_xplusfahren; 
NETWORK
TITLE =GEO-Achse 1 minus

      U     "I_DM_Ta X-Achse minus"; 
      UN    "DB_SIEM_BAG".E_REF; 
      =     "DB_SIEM_KANAL_1".A_Geo[1].Minus; 
      R     #EA_xminusfahren; 
NETWORK
TITLE =GEO-Achse 2 plus

      U     "I_DM_Ta Y-Achse plus"; 
      UN    "DB_SIEM_BAG".E_REF; 
      =     "DB_SIEM_KANAL_1".A_Geo[2].Plus; 
      R     #EA_yplusfahren; 
NETWORK
TITLE =GEO-Achse 2 minus

      U     "I_DM_Ta Y-Achse minus"; 
      UN    "DB_SIEM_BAG".E_REF; 
      =     "DB_SIEM_KANAL_1".A_Geo[2].Minus; 
      R     #EA_yminusfahren; 
NETWORK
TITLE =GEO-Achse 3 plus

      U     "I_DM_Ta Z-Achse minus"; 
      UN    "DB_SIEM_BAG".E_REF; 
      =     "DB_SIEM_KANAL_1".A_Geo[3].Plus; 
      R     #EA_zplusfahren; 
NETWORK
TITLE =GEO-Achse 3 minus

      U     "I_DM_Ta Z-Achse plus"; 
      UN    "DB_SIEM_BAG".E_REF; 
      =     "DB_SIEM_KANAL_1".A_Geo[3].Minus; 
      R     #EA_zminusfahren; 

NETWORK
TITLE =Bausteinende

M004: NOP   0; 

END_FUNCTION

