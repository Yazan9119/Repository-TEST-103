FUNCTION "FC_DMC_ROB_Entladen" : VOID
TITLE =
//$Revision: 1.9 $
//$Date: 2009/01/29 08:43:02CET $
//$Author: riedlh $
//
//Ablauf Spannstelle entladen
//===========================
VERSION : 0.1


VAR_INPUT
  EX_BEF_ROB_Entspannen : BOOL ;	
  EX_MEL_DMC_SpSt_entsp : BOOL ;	
  EX_MEL_DMC_Teil_niO : BOOL ;	
  EX_MEL_DMC_Teil_ni_bearb : BOOL ;	
  EX_MEL_ROB_SpSt_entladen : BOOL ;	
  EX_MEL_ROB_SpSt_sich_auf : BOOL ;	//Rob-Meldung Spannstelle "sicher offen"
  EX_ABST_Sicher_Offen : BOOL ;	//Ausbaustufe: Meldung entspannt mit "sicher offen"-Abfrage
  EX_RPL_Stat_1_Leerpalett : BOOL ;	//RPL-Paletten-Status = 1: Leerpalette
  EX_RPL_Stat_2_Rohteil : BOOL ;	//RPL-Paletten-Status = 2: Rohteil
  EX_RPL_Stat_3_inArbeit : BOOL ;	//RPL-Paletten-Status = 3: in Arbeit
  EX_RPL_Stat_4_Fertig : BOOL ;	//RPL-Paletten-Status = 4: Fertigteil
  EX_RPL_Stat_5_Defekt : BOOL ;	//RPL-Paletten-Status = 5: Störung/Defekt
  EX_Spannst_konfiguriert : BOOL ;	
  EX_RPL_Tuer_offen : BOOL ;	
END_VAR
VAR_IN_OUT
  EAX_FRG_DMC_Entladen : BOOL ;	
  EAX_AKT_DMC_Spannstelle : BOOL ;	
  EAX_BEF_DMC_SpSt_entsp : BOOL ;	
  EAX_MEL_DMC_entsp_oFehl : BOOL ;	
  EAX_MEL_DMC_entsp_mFehl : BOOL ;	
  EAX_MEL_DMC_Teil_niO : BOOL ;	
  EAX_MEL_DMC_Teil_ni_bear : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Netzwerk-Übersicht
// Netzwerk-Übersicht
// ==================
// NW 1: Freigabe an Roboter für Entladen
// NW 2: Meldung an Roboter Spannstelle "entspannt ohne Fehler"
// NW 3: Meldung an Roboter Spannstelle "entspannt mit Fehler"
// NW 4: Meldung an Roboter Teil auf Spannstelle "nicht in Ordnung"
// NW 5: Meldung an Roboter Teil auf Spannstelle "nicht bearbeitet"
// NW 6: Befehl von Roboter Spannstelle entspannen
// NW 7: Anforderung Spannstelle aktivieren wegen Entspannen
      NOP   0; 

NETWORK
TITLE =Freigabe an Roboter für Entladen
//Roboter muss kontrollieren, ob RPL-Tür offen ist, da auch bei geschlossener 
//RPL-Tür ein Paletten-Wechsel möglich sein muss und der Roboter dann die 
//Freigabe 
//zum Entladen benötigt.
//Ein Teil, dass in Arbeit ist kann nicht entnommen werden.
//
//Mögliche Stati von Spannstelle:
//a) Teil fertig berabeitet   #EX_RPL_Status_Fertig
//b) Teil nicht in Ordnung    #EX_RPL_Status_Defekt  
//c) Teil in Arbeit           #EX_MEL_DMC_Teil_ni_bearb
//d) kein Teil vorhanden      #EX_RPL_Status_Leerpalett
      U(    ; 
      O     #EX_RPL_Stat_4_Fertig; 
      O     #EX_RPL_Stat_5_Defekt; 
      )     ; 
      UN    #EX_MEL_DMC_Teil_ni_bearb; 
      U     #EX_Spannst_konfiguriert; 
      UN    #EX_MEL_DMC_SpSt_entsp; 
      UN    #EX_RPL_Stat_1_Leerpalett; 
      UN    #EX_MEL_ROB_SpSt_entladen; 
//      U     #EX_RPL_Tuer_offen          // FG muss auch bei geschlossener RPL-Tür erfolgen
      U(    ; 
      O     "DB_HR".ROB_Interface.WS_TYP_gleich; 
      O     "DB_CONFIG".ABST.ROB_keine_PRG_CTRL; 
      )     ; 
      S     #EAX_FRG_DMC_Entladen; 
      U(    ; 
      O     #EX_MEL_ROB_SpSt_entladen; 
      O     #EX_RPL_Stat_1_Leerpalett; 
      O     #EX_RPL_Stat_2_Rohteil; 
      O     "DB_HR".ROB_Interface.RPL_Nr_9999; 
      O     "DB_HR".ROB_Interface.PW_Freigabe; 
      O     "DB_HR".RPT.RP_PWFG_oRSP_aktiv; 
      ON    "M_PW_GRDST"; 
      )     ; 
      R     #EAX_FRG_DMC_Entladen; 
      NOP   0; 
NETWORK
TITLE =Meldung an Roboter Spannstelle "entspannt ohne Fehler"

      U     #EX_MEL_DMC_SpSt_entsp; 
      U(    ; 
      O     #EX_MEL_ROB_SpSt_sich_auf; 
      ON    #EX_ABST_Sicher_Offen; 
      )     ; 
      =     #EAX_MEL_DMC_entsp_oFehl; 
NETWORK
TITLE =Meldung an Roboter Spannstelle "entspannt mit Fehler"

      U     #EX_MEL_DMC_SpSt_entsp; 
      UN    #EX_MEL_ROB_SpSt_sich_auf; 
      U     #EX_ABST_Sicher_Offen; 
      =     #EAX_MEL_DMC_entsp_mFehl; 
NETWORK
TITLE =Meldung an Roboter Teil auf Spannstelle "nicht in Ordnung"

      U     #EX_MEL_DMC_Teil_niO; 
      =     #EAX_MEL_DMC_Teil_niO; 
NETWORK
TITLE =Meldung an Roboter Teil auf Spannstelle "nicht bearbeitet"

      U     #EX_MEL_DMC_Teil_ni_bearb; 
      =     #EAX_MEL_DMC_Teil_ni_bear; 
NETWORK
TITLE =Befehl von Roboter Spannstelle entspannen

      U     #EX_BEF_ROB_Entspannen; 
      =     #EAX_BEF_DMC_SpSt_entsp; 
NETWORK
TITLE =Anforderung Spannstelle aktivieren wegen Entspannen

      U     #EX_BEF_ROB_Entspannen; 
      =     #EAX_AKT_DMC_Spannstelle; 

END_FUNCTION

