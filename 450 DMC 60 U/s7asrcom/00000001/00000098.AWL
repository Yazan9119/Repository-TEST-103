FUNCTION "FC_PS_Hub_Pos" : VOID
TITLE =
//$Revision: 1.2 $
//$Date: 2008/06/05 16:42:33CEST $
//$Author: hgc $
//
//Anordnung der Plätze
//
//1 = Ebene 0 unten
//2 = Ebene 0 oben 
//3 = Ebene 1 unten
//4 = Ebene 1 oben 
AUTHOR : SCH
NAME : WZM160
VERSION : 0.1


VAR_INPUT
  Eb_Position : BYTE ;	//Anforderung Platz
  EX_Fahrbefehl : BOOL ;	//Anforderung fahren 
END_VAR
VAR_IN_OUT
  EAX_Fahrbefehl_Fertig : BOOL ;	//Rückmeldung Fahrbefehl abgeschlossen
END_VAR
VAR_TEMP
  T_X_Pos : REAL ;	
  T_Z_Pos : REAL ;	
  T_Typ : BYTE ;	
  T_Platz : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =Schrittkette Positionieren
//
//
      UN    #EX_Fahrbefehl; 
      SPBN  STRT; 
      L     0; 
      T     "MB_SKZ_Hub_PS"; 
      SET   ; 
      R     "DB_Achs16_Hub_PS".MX_Anf_Positionieren; 

STRT: L     "MB_SKZ_Hub_PS"; 
      SPL   SK99; 
      SPA   SK00; 
      SPA   SK01; 
      SPA   SK02; 
      SPA   SK03; 

SK99: SPA   MEND; 

// -- Schritt 0 ----------------------------------------------------------------

SK00: UN    #EAX_Fahrbefehl_Fertig; 
      U     #EX_Fahrbefehl; 
      UN    "DB_Achs16_Hub_PS".MX_Achse_Faehrt; 

      U(    ; 
      L     #Eb_Position; 
      L     0; 
      >I    ; 
      )     ; 
      SPBN  MEND; 

      U     "DB_LS_POS".Daten_OK; 
      SPB   E001; 
      SET   ; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Async4._701806_Pos_Ungueltig; 
      SPA   MEND; 

E001: L     1; 
      T     "MB_SKZ_Hub_PS"; 

// -- Schritt 1 ----------------------------------------------------------------
// Platz Ausrechnen

SK01: L     #Eb_Position; 
      L     1; 
      ==I   ; 
      SPBN  E011; 
      L     "DB_LS_POS".Hubachse_Ebene_0_unten; 
      T     "DB_Achs16_Hub_PS".MR_SollPosition; 
      SPA   E020; 

E011: L     #Eb_Position; 
      L     2; 
      ==I   ; 
      SPBN  E012; 
      L     "DB_LS_POS".Hubachse_Ebene_0_oben; 
      T     "DB_Achs16_Hub_PS".MR_SollPosition; 
      SPA   E020; 

E012: L     #Eb_Position; 
      L     3; 
      ==I   ; 
      SPBN  E013; 
      L     "DB_LS_POS".Hubachse_Ebene_1_unten; 
      T     "DB_Achs16_Hub_PS".MR_SollPosition; 
      SPA   E020; 

E013: L     #Eb_Position; 
      L     4; 
      ==I   ; 
      SPBN  SK03; 
      L     "DB_LS_POS".Hubachse_Ebene_1_oben; 
      T     "DB_Achs16_Hub_PS".MR_SollPosition; 

E020: SET   ; 
      S     "DB_Achs16_Hub_PS".MX_Anf_Positionieren; 

      L     2; 
      T     "MB_SKZ_Hub_PS"; 

// -- Schritt 2 ----------------------------------------------------------------
// Warten bis Positioniert

SK02: U     "DB_Achs16_Hub_PS".MX_Rueck_Positionieren; 
      UN    "DB_Achs16_Hub_PS".MX_Achse_Faehrt; 
      SPBN  MEND; 

      L     3; 
      T     "MB_SKZ_Hub_PS"; 

// -- Schritt 3 ----------------------------------------------------------------
// Quittierung setzen

SK03: SET   ; 
      S     #EAX_Fahrbefehl_Fertig; 
      R     "DB_Achs16_Hub_PS".MX_Rueck_Positionieren; 

      L     0; 
      T     "MB_SKZ_Hub_PS"; 

MEND: NOP   0; 

END_FUNCTION

