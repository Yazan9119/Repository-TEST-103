FUNCTION "FC_LASER_VERWALTUNG" : VOID
TITLE =
//$Revision: 1.41 $
//$Date: 2008/10/06 08:02:48CEST $
//$Author: hgc $
//
AUTHOR : SJA
VERSION : 0.1


VAR_TEMP
  tx_laser_aus : BOOL ;	
  tx_frg_einf : BOOL ;	
  nur_Diagonalmessung : BOOL ;	
  Diagonal_und_Tisch : BOOL ;	
  nur_Tisch : BOOL ;	
  Frg_Blum_Tisch : BOOL ;	
  Frg_Blum_diagonal : BOOL ;	
  Frg_diagonal_oder_Tisch : BOOL ;	
  Tx_Taste_Blau_Skr : BOOL ;	
  Tx_Taste_Gelb_Skr : BOOL ;	
  tx_frg_laser_ein : BOOL ;	
  tx_Ax4_0Grad : BOOL ;	
  tx_frg_ausf : BOOL ;	
  tx_Laser_auto_einfahren : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Netzwerkübersicht
// Netzwerk-Übersicht
// ==================
// NW 1: Netzwerkübersicht
// NW 2: Anwahl unbeaufsichtigte Bearbeitung
// NW 3: HMI: Anwenderdatum unbeaufsichtigte Bearbeitung
// NW 4: Mestaster aktiv - > Bausteinende
// NW 5: Reset Fehlermeldungen
// NW 6: Werkzeugvermessung angewählt? Ansonsten Sprung ans Bausteinende
// NW 7: BLUM-Laser angewaehlt
// NW 8: Umschaltung Blum/Renishaw
// NW 9: Laser in Grundstellung
// NW 10: Laser aktiv im Bereich ausserhalb Arbeitsraum
// NW 11: Reset M269 (Zuückschalten der Softwarenocken an PLC gemeldet)
// NW 12: Lokalvariable: Tischachse in 0° Stellung
// NW 13: Blum Laser: Diagonalmessung
// NW 14: Diagonalmessung und Tischmessung
// NW 15: Lokalvariable: Nur Tisch
// NW 16: Blum Laser Tisch: Freigabe
// NW 17: Blum Laser diagonal: Freigabe
// NW 18: Blum Laser diagonal oder Tisch: Freigabe
// NW 19: Art der Werkzeugvermesung an NC melden
// NW 20: Laser auschalten nach Reset usw.
// NW 21: Freigabe Laser an NC melden
// NW 22: Störung: Keine Freigabe Laservermessung
// NW 23: Laser Fehler, Palette auf Störung setzen
// NW 24: Option Schwenkeinrichtung zwangsgeführt 
// NW 25: Signale an Peripherie 
// NW 26: Signale von Peripherie
// NW 27: Rettung Laser aktiv 
// NW 28: Zeile 1: Freigabe Laser einfahren
// NW 29: Zeile 1: Freigabe Laser ausfahren
// NW 30: Zeile 1: Laser ein.-/ausfahren
// NW 31: Zeile 2: Freigabe Laser deaktivieren
// NW 32: Zeile 2: Freigabe Laser aktivieren
// NW 33: Zeile 2: Freigabe Laser aktivieren/deaktivieren
// NW 34: Zeile 3: Anzeige Rückmeldungen
// NW 35: Rettung Laser deaktivieren
// NW 36: Rettung Laser aktivieren
// NW 37: Abfrage nur Diagonalmessung aktiv
// NW 38: Zylinder Taste einfahren
// NW 39: Zylinder Taste ausfahren
// NW 40: Zylinder automatisch einfahren 
// NW 41: Laser Zylinder Aus.-/Einfahren
// NW 42: Warnung: Blum Deckel nicht geschlossen
// NW 43: Störung: Zylinder nicht eingefahren
// NW 44: Störung: Schmutzblende nicht geschlossen
// NW 45: Rundachse sperren bei ausgefahrenem Laser
// NW 46: Uebergabe ans Makro dass Blum ausgefahren ist
// NW 47: Sprung ans Bausteinende
// NW 48: Messdose angewählt? Ansonsten Sprung ans Bausteinende
// NW 49: Messdose Betriebsbereit
// NW 50: Messdose abblasen verlängerter Impuls
// NW 51: Messdose abblasen
// NW 52: Messdose Überwachung Bereitschaft
// NW 53: Störung: Messdose nicht bereit
// NW 54: Verfahrbereichseinschränkung durch Laser/Kalibrierklappe
//
      NOP   0; 

NETWORK
TITLE =Anwahl unbeaufsichtigte Bearbeitung

      U     "DB_EINRICHTFUNKTION".Bewegung_143_Minus; 
      S     "DB_NC_PLC".unbeaufsichtigte_Bearb; 
      U     "DB_EINRICHTFUNKTION".Bewegung_143_Plus; 
      R     "DB_NC_PLC".unbeaufsichtigte_Bearb; 
      NOP   0; 
NETWORK
TITLE =HMI: Anwenderdatum unbeaufsichtigte Bearbeitung

      U     "m_eins"; 
      =     L      2.0; 
      BLD   103; 
      U     "m_eins"; 
      =     L      2.1; 
      BLD   103; 
      U     "DB_NC_PLC".unbeaufsichtigte_Bearb; 
      =     L      2.2; 
      BLD   103; 
      UN    "DB_NC_PLC".unbeaufsichtigte_Bearb; 
      =     L      2.3; 
      BLD   103; 
      U     "DB_NC_PLC".unbeaufsichtigte_Bearb; 
      =     L      2.4; 
      BLD   103; 
      UN    "DB_NC_PLC".unbeaufsichtigte_Bearb; 
      =     L      2.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      2.0,
           Freigabe_GST_Hand        := L      2.1,
           Endlage_AST              := L      2.2,
           Endlage_GST              := L      2.3,
           Ventil_AST               := L      2.4,
           Ventil_GST               := L      2.5,
           Bewegungsnummer          := 143);
      NOP   0; 
NETWORK
TITLE =Mestaster aktiv - > Bausteinende

      U     "MX_MTaster_Aktiv"; 
      SPB   ENDE; 

NETWORK
TITLE =Reset Fehlermeldungen

      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700741_Laser_SchmutzB_0; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700739_Laser_SE_Eingef0; 
      R     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700952; 
      R     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701051_MessDose_Bereit0; 
NETWORK
TITLE =Werkzeugvermessung angewählt? Ansonsten Sprung ans Bausteinende

      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     0; 
      ==I   ; 
      SPBN  STRT; 

      SET   ; 
      R     "DB_DM_M_FUNKTION".MX_QuitPflicht[265]; 
      R     "DB_DM_M_FUNKTION".MX_QuitPflicht[266]; 
      U     "DB_DM_M_FUNKTION".MX_M[265]; 
      R     "DB_DM_M_FUNKTION".MX_M[265]; 
      U     "DB_DM_M_FUNKTION".MX_M[266]; 
      R     "DB_DM_M_FUNKTION".MX_M[266]; 
      L     0; 
      T     "DB_NC_PLC".Anwahl_WZ_Vermessung; 
      SPA   ENDE; 
NETWORK
TITLE =BLUM-Laser angewaehlt

STRT: L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     2; 
      <=I   ; 
      O(    ; 
      TAK   ; 
      L     7; // Blum und Messtift   (FD)
      ==I   ; 
// un drehwerkzeug
      )     ; 
      O(    ; 
      TAK   ; 
      L     8; // Blum2 und Messtift   (FD)
      ==I   ; 
// un drehwerkzeug
      )     ; 
      O(    ; 
      TAK   ; 
      L     4; // Diagonal
      ==I   ; 
// un drehwerkzeug
      )     ; 
      O(    ; 
      TAK   ; 
      L     5; // Diagonal und Tisch
      ==I   ; 
// un drehwerkzeug
      )     ; 
      O(    ; 
      TAK   ; 
      L     9; // Renishaw NC4
      ==I   ; 
// un drehwerkzeug
      )     ; 
      O(    ; 
      TAK   ; 
      L     10; // Renishaw NC4 + TS27R
      ==I   ; 
// un drehwerkzeug
      )     ; 
      SPBN  DOSE; 


NETWORK
TITLE =Umschaltung Blum/Renishaw

      U     "DB_NC_PLC".LASER_1; 
      UN    "DB_NC_PLC".LASER_Aktiv; 
      =     "O_AL_Messsignal_Umsch"; 
NETWORK
TITLE =Laser in Grundstellung

      UN    "O_LA_Sender_Ein"; 
      UN    "O_LA_SCHMUTZB_AUF"; 
      UN    "O_LA_Empfaenger_Ein"; 
      UN    "O_LA_Sperrluft_Aus"; 
      UN    "O_LA_ENABLE_EMPFAENGER2"; 
      U(    ; 
      O     "I_LA_eingeschwenkt"; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     4; 
      ==I   ; 
      )     ; 
      )     ; 
      =     "M_LASER_IN_GRDST"; 
NETWORK
TITLE =Laser aktiv im Bereich ausserhalb Arbeitsraum
//M268 Umschalten der Softwarenocken für Laser ausserhalb an PLC melden
//M269 Zurückschalten der Softwarenocken an PLC melden (durch Progevent)
      U     "DB_PLC_MD_DB20".UDHex._36_Bit0_Laser_auserhalb; 
      U     "DB_NC_PLC".LASER_Aktiv; 
      U     "DB_DM_M_FUNKTION".MX_M[268]; 
      S     "DB_REMANENT".LASER_AKTIV; 
      U(    ; 
      O     "DB_DM_M_FUNKTION".MX_M[266]; 
      O     "DB_DM_M_FUNKTION".MX_M[269]; 
      )     ; 
      R     "DB_REMANENT".LASER_AKTIV; 
      U     "DB_REMANENT".LASER_AKTIV; 
      R     "DB_DM_M_FUNKTION".MX_M[268]; 
NETWORK
TITLE =Reset M269 (Zuückschalten der Softwarenocken an PLC gemeldet)

      U     "DB_DM_M_FUNKTION".MX_M[269]; 
      UN    "DB_REMANENT".LASER_AKTIV; 
      R     "DB_DM_M_FUNKTION".MX_M[269]; 
NETWORK
TITLE =Lokalvariable: Tischachse in 0° Stellung

      U(    ; 
      UN    "DB_REMANENT".Achse_4_modulo; 
      U     "DB_SIEM_NCK".E_SWCamPlus[10]; 
      U     "DB_SIEM_NCK".E_SWCamMinus[10]; 
      O     ; 
      U     "DB_REMANENT".Achse_4_modulo; 
      U     "DB_SIEM_NCK".E_SWCamPlus[10]; 
      )     ; 
      U     "DB_ACHSE4".E_SWCam; 
      =     #tx_Ax4_0Grad; 
NETWORK
TITLE =Blum Laser: Diagonalmessung

      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     4; 
      ==I   ; 
      =     #nur_Diagonalmessung; 
NETWORK
TITLE =Diagonalmessung und Tischmessung

      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     5; 
      ==I   ; 
      =     #Diagonal_und_Tisch; 
NETWORK
TITLE =Lokalvariable: Nur Tisch

      UN    #nur_Diagonalmessung; 
      UN    #Diagonal_und_Tisch; 
      =     #nur_Tisch; 
NETWORK
TITLE =Blum Laser Tisch: Freigabe

      U(    ; 
      O     "I_LA_ausgefahren"; 
      O     #Diagonal_und_Tisch; 
      O     #nur_Tisch; 
      )     ; 
      U(    ; 
      O     "M_Handbetrieb"; 
      O     "m_rqlagf"; 
      )     ; 
      =     #Frg_Blum_Tisch; 
NETWORK
TITLE =Blum Laser diagonal: Freigabe

      U(    ; 
      O     #Diagonal_und_Tisch; 
      O     #nur_Diagonalmessung; 
      )     ; 
      U     "M_Handbetrieb"; 
      =     #Frg_Blum_diagonal; 
NETWORK
TITLE =Blum Laser diagonal oder Tisch: Freigabe

      U(    ; 
      O     #Frg_Blum_diagonal; 
      O     #Frg_Blum_Tisch; 
      )     ; 
      U     "M_Handbetrieb"; 
      =     #Frg_diagonal_oder_Tisch; 
NETWORK
TITLE =Art der Werkzeugvermesung an NC melden

      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      T     "DB_NC_PLC".Anwahl_WZ_Vermessung; 

NETWORK
TITLE =Laser auschalten nach Reset usw.

      U     "M_Reset_Taste"; 
      SPBN  nw2a; 

      L     0; 
      T     "DB_NC_PLC".SKZ_LASER; 

nw2a: SET   ; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[265]; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[266]; 
NETWORK
TITLE =Freigabe Laser an NC melden

      UN    "M_3Te_Betriebsart"; 
      UN    "M_Einrichtbetrieb"; 
      U     "M_Leistung_Steht_an"; 
      U(    ; 
      O     "DB_NC_PLC".LASER_Aktiv; 
      O     "DB_NC_PLC".LASER_1; 
      )     ; 
      UN(   ; 
      U     "DB_ICON"._1.UI_17; // geschwenkt oder TRAORI
      U     "DB_NC_PLC".SK_Kopf_Horizontal; 
      )     ; 
      =     "DB_NC_PLC".LASER_Freigabe; 
NETWORK
TITLE =Störung: Keine Freigabe Laservermessung

      U     "DB_ICON"._1.UI_17; // geschwenkt oder TRAORI
      U     "DB_NC_PLC".SK_Kopf_Horizontal; 
      O     "M_3Te_Betriebsart"; 
      O     "M_Einrichtbetrieb"; 
      U(    ; 
      U     "DB_NC_PLC".LASER_Aktiv; 
      O     "DB_NC_PLC".LASER_1; 
      )     ; 
      U(    ; 
      L     "DB_NC_PLC".SKZ_LASER; 
      L     1; 
      ==I   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700952; 
NETWORK
TITLE =Laser Fehler, Palette auf Störung setzen

      U     "DB_NC_PLC".LASER_FEHLER; 
      FP    "M_FP_Pal_Störung"; 
      S     "MX_Pal_auf_Stoerung"; // Palette auf Störung setzen (FC184)
NETWORK
TITLE =Option Schwenkeinrichtung zwangsgeführt 
//
//
      U     "DB_PLC_MD_DB20".UDHex._36_Bit1_Laserkl_zwangsg; 
      U     "I_LA_eingeschwenkt"; 
      O     "I_LA_Klappe_zu"; 
      =     "Mx_LA_Klappe_zu"; 
NETWORK
TITLE =Rettung aktiv ?

      U     "DB_OEM".OEM_BlumRettaktiv; // Rettung Blum Laser aktivieren angewaehlt
      SPB   RETT; 
NETWORK
TITLE =Override mask $A_OUTA[1]

      ON    "M_MA_Maschine_Ein"; 
      O     "DB_DM_M_FUNKTION".MX_M[30]; 
      O     "M_Reset_Taste"; 
      O     "DB_SIEM_KANAL_1".E_M30; //-- M2/M30 aktiv
      =     "DB_SIEM_NCK".A_OvMask_anaOut1; 
NETWORK
TITLE =Analog NCK output: Sender einschalten

      U     DB10.DBX  211.0; //-- Freigabe Sender
      =     "O_LA_Sender_Ein"; 
NETWORK
TITLE =Analog NCK output: Empfänger einschalten

      U     DB10.DBX  211.1; //-- Freigabe Empfänger1
      =     "O_LA_Empfaenger_Ein"; 
NETWORK
TITLE =Analog NCK output: Schmutzblende öffnen

      U     DB10.DBX  211.2; //-- Ventil Schmutzblende
      =     "O_LA_SCHMUTZB_AUF"; 
NETWORK
TITLE =Analog NCK output: Sperrluft ausschalten

      U     DB10.DBX  211.3; //-- Ventil Sperrluft
      =     "O_LA_Sperrluft_Aus"; 
NETWORK
TITLE =Analog NCK output: Empfänger 2 einschalten
//// -- bei neuem BLUM Laser
      U     DB10.DBX  211.4; 
      =     "O_LA_ENABLE_EMPFAENGER2"; //-- Freigabe Empfänger 2
NETWORK
TITLE =Analog NCK output: Blasluft einschalten

      U     DB10.DBX  211.5; 
      =     "O_LA_WZ_ABBLASEN"; //-- Blasluft Werkzeug / Laser
      =     "O_AL_Messdose_abblasen"; 
NETWORK
TITLE =Set value $A_INA[1]

      L     0; 
      T     "DB_SIEM_NCK".A_Setval_anaIn1; // analog Eingang abnullen
      NOP   0; 
NETWORK
TITLE =Analog NCK input: Laser ok

      U     "I_LA_BTB"; 
      =     DB10.DBX  149.0; //-- Signal Laser o.k.
NETWORK
TITLE =Analog NCK input: Laser Signal dynamisch

      U     "DB_SIEM_NCK".E_InspProbe2; //-- Messtaster 2
      =     DB10.DBX  149.1; // dyn. Signal Laser
NETWORK
TITLE =Analog NCK input: Laser Signal statisch

      U     "I_LA_statisches_Signal"; 
      =     DB10.DBX  149.2; //-- stat.Signal Laser
NETWORK
TITLE =Analog NCK input: Laser ausgefeahren

      U     "M_Blum_Laser_Ausgef"; 
      U     "I_LA_ausgefahren"; 
      UN    "I_LA_eingeschwenkt"; 
      =     DB10.DBX  149.3; 
NETWORK
TITLE =Input mask $A_INA[1]
// -- $A_INA[1]
      U     "m_eins"; 
      =     "DB_SIEM_NCK".A_InMask_anaIn1; //-- Analogeingang wird von PLC beschrieben!!
NETWORK
TITLE =Rettung Laser aktiv 

RETT: O     "DB_OEM".OEM_BlumRettaktiv; 
      O     "m_rqlagf"; 
      SPBN  kret; 
NETWORK
TITLE =Zeile 1: Freigabe Laser einfahren
////****************** Zeile 1 ***********************************************
////Laser aus-/einfahren
//
      U     #Frg_Blum_Tisch; 
      =     #tx_frg_einf; 
NETWORK
TITLE =Zeile 1: Freigabe Laser ausfahren
////****************** Zeile 1 ***********************************************
////Laser aus-/einfahren
//
      U     #Frg_Blum_Tisch; 
      U     #tx_Ax4_0Grad; 
      =     #tx_frg_ausf; 
NETWORK
TITLE =Zeile 1: Laser ein.-/ausfahren
////****************** Zeile 1 ***********************************************
////Laser aus-/einfahren
//
      U     #tx_frg_ausf; 
      =     L      2.0; 
      BLD   103; 
      U     #tx_frg_einf; 
      =     L      2.1; 
      BLD   103; 
      U     "I_LA_ausgefahren"; 
      =     L      2.2; 
      BLD   103; 
      U     "I_LA_eingeschwenkt"; 
      =     L      2.3; 
      BLD   103; 
      U     "O_LA_ausschwenken"; 
      =     L      2.4; 
      BLD   103; 
      U     "O_LA_einschwenken"; 
      =     L      2.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      2.0,
           Freigabe_GST_Hand        := L      2.1,
           Endlage_AST              := L      2.2,
           Endlage_GST              := L      2.3,
           Ventil_AST               := L      2.4,
           Ventil_GST               := L      2.5,
           Bewegungsnummer          := 49);
      NOP   0; 
NETWORK
TITLE =Zeile 2: Freigabe Laser deaktivieren
////****************** Zeile 2 ***********************************************
////Laser aktivieren/deaktivieren
//
      UN    "O_LA_Sender_Ein"; 
      =     #tx_laser_aus; 
NETWORK
TITLE =Zeile 2: Freigabe Laser aktivieren
////****************** Zeile 2 ***********************************************
////Laser aktivieren/deaktivieren
//
      U     #Frg_Blum_Tisch; 
      U     "I_LA_ausgefahren"; 
      =     #tx_frg_laser_ein; 
NETWORK
TITLE =Zeile 2: Freigabe Laser aktivieren/deaktivieren
////****************** Zeile 2 ***********************************************
////Laser aktivieren/deaktivieren
//
      U     #tx_frg_laser_ein; 
      =     L      2.0; 
      BLD   103; 
      U     "m_eins"; 
      =     L      2.1; 
      BLD   103; 
      U     "O_LA_Sender_Ein"; 
      =     L      2.2; 
      BLD   103; 
      U     #tx_laser_aus; 
      =     L      2.3; 
      BLD   103; 
      U     "O_LA_Sender_Ein"; 
      =     L      2.4; 
      BLD   103; 
      U     #tx_laser_aus; 
      =     L      2.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      2.0,
           Freigabe_GST_Hand        := L      2.1,
           Endlage_AST              := L      2.2,
           Endlage_GST              := L      2.3,
           Ventil_AST               := L      2.4,
           Ventil_GST               := L      2.5,
           Bewegungsnummer          := 50);
      NOP   0; 
NETWORK
TITLE =Zeile 3: Anzeige Rückmeldungen
////****************** Zeile 3 ***********************************************
////Laser Anzeige Rückmeldungen
      U     "m_null"; 
      =     L      2.0; 
      BLD   103; 
      U     "m_null"; 
      =     L      2.1; 
      BLD   103; 
      U     "O_LA_ENABLE_EMPFAENGER2"; 
      =     L      2.2; 
      BLD   103; 
      U     "Mx_LA_Klappe_zu"; 
      =     L      2.3; 
      BLD   103; 
      U     "O_LA_Empfaenger_Ein"; 
      =     L      2.4; 
      BLD   103; 
      U     "O_LA_Sender_Ein"; 
      =     L      2.5; 
      BLD   103; 
      CALL "FC_HMI_IO_TO_TLINE" (
           Freigabe_AST_Hand        := L      2.0,
           Freigabe_GST_Hand        := L      2.1,
           Endlage_AST              := L      2.2,
           Endlage_GST              := L      2.3,
           Ventil_AST               := L      2.4,
           Ventil_GST               := L      2.5,
           Bewegungsnummer          := 51);
      NOP   0; 
NETWORK
TITLE =Rettung Laser deaktivieren

      U     "DB_EINRICHTFUNKTION".Bewegung_50_Plus; 
      U     "I_LA_ausgefahren"; 
      O     ; 
      U     "DB_EINRICHTFUNKTION".Bewegung_49_Plus; 
      U     "O_LA_Sender_Ein"; 
      O     "m_rqlagf"; 
      R     "O_LA_Sender_Ein"; 
      R     "O_LA_SCHMUTZB_AUF"; 
      R     "O_LA_Empfaenger_Ein"; 
      R     "O_LA_Sperrluft_Aus"; 
      R     "O_LA_ENABLE_EMPFAENGER2"; 
NETWORK
TITLE =Rettung Laser aktivieren

      U     "DB_EINRICHTFUNKTION".Bewegung_50_Minus; 
      U     "I_LA_ausgefahren"; 
      S     "O_LA_Sender_Ein"; 
      S     "O_LA_Empfaenger_Ein"; 
      S     "O_LA_SCHMUTZB_AUF"; 
      S     "O_LA_Sperrluft_Aus"; 
      S     "O_LA_ENABLE_EMPFAENGER2"; 
NETWORK
TITLE =Abfrage nur Diagonalmessung aktiv

kret: U     #nur_Diagonalmessung; 
      SPB   _001; 
NETWORK
TITLE =Zylinder Taste einfahren
//Laser Zylinder Aus.-/Einfahren
      U     "DB_EINRICHTFUNKTION".Bewegung_49_Plus; 
      U     "DB_OEM".OEM_BlumRettaktiv; 
      U     #tx_frg_einf; 
      =     #Tx_Taste_Blau_Skr; 
NETWORK
TITLE =Zylinder Taste ausfahren
//Laser Zylinder Aus.-/Einfahren
      U     "DB_EINRICHTFUNKTION".Bewegung_49_Minus; 
      U     "DB_OEM".OEM_BlumRettaktiv; 
      U     #tx_frg_ausf; 
      =     #Tx_Taste_Gelb_Skr; 
NETWORK
TITLE =Zylinder automatisch einfahren 
//1. über automatisches Grundstellungsfahren
//2. durch M-Funktion "DB_DM_M_FUNKTION".MX_M[266]
      U     "m_rqlagf"; 
      U     #tx_frg_einf; 
      O     "DB_DM_M_FUNKTION".MX_M[266]; 
      =     #tx_Laser_auto_einfahren; 
NETWORK
TITLE =Reset M-Funktion (Laser einfahren/ausfahren) bei Endlage
//Notwendig bei SL Shopmill Messen im Jog
      U     "DB_CMM_PLC".CMM_OUT.cmm_mmc_activ; 
      U     "DB_PLC_MD_DB20".UDHex._31_Bit1_SOLUTIONLINE; 
      =     L      2.0; 
      U     L      2.0; 
      U     "DB_DM_M_FUNKTION".MX_Quitt[265]; 
      R     "DB_DM_M_FUNKTION".MX_M[265]; 
      R     "DB_DM_M_FUNKTION".MX_Quitt[265]; 
      U     L      2.0; 
      U     "DB_DM_M_FUNKTION".MX_Quitt[266]; 
      R     "DB_DM_M_FUNKTION".MX_M[266]; 
      R     "DB_DM_M_FUNKTION".MX_Quitt[266]; 
NETWORK
TITLE =Laser Zylinder Aus.-/Einfahren
//Laser Zylinder Aus.-/Einfahren
      CALL "FC_ZYLINDER" (
           E_I_Grundstellung        := "I_LA_eingeschwenkt",
           E_I_Hub                  := "I_LA_ausgefahren",
           E_Ruecksetze_Fehler      := "M_Ruecksetze_Fehler",
           E_Vorschub_Halt          := "M_Vorschub_Halt_Haupt",
           E_Reset                  := "M_Reset_Taste",
           E_Init                   := "M_3_ter_OB1_Zyklus",
           E_Maschine_ein           := "M_MA_Maschine_Ein",
           E_Sperre_Ueberwachung    := FALSE,
           E_SoftkeyRettung         := "DB_OEM".OEM_BlumRettaktiv,
           E_Taste_Blau_Skr         := #Tx_Taste_Blau_Skr,
           E_Taste_Gelb_Skr         := #Tx_Taste_Gelb_Skr,
           E_Timer_Ueberwachung     := "TE_Blum_FehlUebw",
           E_Zeit_Bewegung          := 10,
           E_Req_Grundstellung      := #tx_Laser_auto_einfahren,
           E_Req_Hub                := "DB_DM_M_FUNKTION".MX_M[265],
           E_Ready_Grundstellung    := "DB_DM_M_FUNKTION".MX_Quitt[266],
           E_Ready_Hub              := "DB_DM_M_FUNKTION".MX_Quitt[265],
           EA_O_Grundstellung       := "O_LA_einschwenken",
           EA_O_Hub                 := "O_LA_ausschwenken",
           EA_Hub_Ausgefahren       := "M_Blum_Laser_Ausgef",
           EA_FE_Grundstlg_0Signal  := "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700739_Laser_SE_Eingef0,
           EA_FE_Hub_0Signal        := "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700738_Laser_SE_Ausgef0,
           EA_FE_GrdSt_Hub_Beide1   := "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700740_Laser_SE_Beide1,
           EA_Flanke_1              := "M_Blum_Flanke1",
           EA_Flanke_2              := "M_Blum_Flanke2",
           EA_pos_Trigg_Blau_Taste  := "Mx_FP_pos_Trigg_Blau",
           EA_neg_Trigg_Blau_Taste  := "Mx_FN_Blau",
           EA_pos_Trigg_Gelb_Taste  := "Mx_Fp_GElb",
           EA_neg_Trigg_Gelb_Taste  := "Mx_Fn_Gelb");


NETWORK
TITLE =Warnung: Blum Deckel nicht geschlossen
// generelle Ueberwachung wenn kein Messzyklus aktiv ist dass der
// Laser eingefahren ist
_001: U(    ; 
      UN    "DB_NC_PLC".LASER_Aktiv; 
      UN    "DB_NC_PLC".LASER_1; 
      U     "I_LA_ausgefahren"; 
      UN(   ; 
      U     "DB_OEM".OEM_BlumRettaktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      )     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     2; 
      ==I   ; 
      )     ; 
      U(    ; 
      U     "DB_ACHSE4".E_TCMinus; 
      O     "DB_ACHSE4".E_TCPlus; 
      )     ; 
      )     ; 
      O(    ; 
      U     "I_LA_ausgefahren"; 
      U     "M_WZW_Prozess_Aktiv"; 
      UN    "DB_NC_PLC".LASER_Aktiv; 
      UN    "DB_NC_PLC".LASER_1; 
      )     ; 
      =     "DB_FEHLERMELDUNG".Warnung._701913_Blum_nicht_zu; // "DB_FehlerMeldung_DB102".Spi_VS_Halt_Haupt._700741_Laser_SchmutzB_0
NETWORK
TITLE =Störung: Zylinder nicht eingefahren

      UN    "I_LA_eingeschwenkt"; 
      U(    ; 
      U     "M_FahrAnf_HauptAchs"; 
      UN    "DB_OEM".OEM_BlumRettaktiv; 
      UN    "DB_NC_PLC".LASER_Aktiv; 
      UN    "DB_NC_PLC".LASER_1; 
      O     ; 
      U     "DB_OEM".OEM_BlumRettaktiv; 
      UN    "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      U(    ; 
      O     "DB_ACHSE2_Y".E_TCMinus; 
      O     "DB_ACHSE2_Y".E_TCPlus; 
      O     "DB_ACHSE3_Z".E_TCMinus; 
      O     "DB_ACHSE4".E_TCMinus; 
      O     "DB_ACHSE4".E_TCPlus; 
      )     ; 
      O     "DB_ACHSE4".E_TCMinus; 
      O     "DB_ACHSE4".E_TCPlus; 
      O     "M_PW_Aktiv"; 
      )     ; 
      U(    ; 
      L     "DB_NC_PLC".MFunktion; 
      L     28; 
      <>I   ; 
      )     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     1; 
      >=I   ; 
      )     ; 
      UN    #nur_Diagonalmessung; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700739_Laser_SE_Eingef0; 
NETWORK
TITLE =Störung: Schmutzblende nicht geschlossen

      UN    "I_LA_eingeschwenkt"; 
      ON    "Mx_LA_Klappe_zu"; 
      UN(   ; 
      U     "DB_OEM".OEM_BlumRettaktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      )     ; 
      U(    ; 
      U     "M_FahrAnf_HauptAchs"; 
      UN    "DB_NC_PLC".LASER_Aktiv; 
      UN    "DB_NC_PLC".LASER_1; 
      O     "M_PW_Aktiv"; 
      )     ; 
      UN(   ; 
      L     "DB_NC_PLC".MFunktion; 
      L     28; 
      ==I   ; 
      )     ; 
      U(    ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     2; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     7; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     8; 
      ==I   ; 
      )     ; 

// Bei Renishaw wird wenn keine Klappe vorhanden ist der Eingang auf 1 gelegt (Festlegung mit Hr. Keppeler)

      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     9; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     10; 
      ==I   ; 
      )     ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700741_Laser_SchmutzB_0; 


      UN    "I_LA_eingeschwenkt"; 
      UN(   ; 
      U     "DB_OEM".OEM_BlumRettaktiv; 
      U     "M_Handbetrieb"; // "DB_SIEM_BAG".E_JOG
      )     ; 
      U     "M_PW_Aktiv"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     1; 
      ==I   ; 
      )     ; 
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700741_Laser_SchmutzB_0; 
NETWORK
TITLE =Rundachse sperren bei ausgefahrenem Laser

      U(    ; 
      UN    "Mx_LA_Klappe_zu"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     2; 
      ==I   ; 
      )     ; 
      O     ; 
      UN    "I_LA_eingeschwenkt"; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     1; 
      ==I   ; 
      )     ; 
      )     ; 
      UN    #nur_Diagonalmessung; 
      =     "MX_Blum_C_sperren"; 
NETWORK
TITLE =Uebergabe ans Makro dass Blum ausgefahren ist

      UN    "I_LA_eingeschwenkt"; 
      =     "DB_NC_PLC".WZW57_0; 
NETWORK
TITLE =Sprung ans Bausteinende

      SPA   ENDE; 

NETWORK
TITLE =Messdose angewählt? Ansonsten Sprung ans Bausteinende

DOSE: L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     3; 
      ==I   ; 
      SPBN  ENDE; 


NETWORK
TITLE =Vorbesetzung Quitierpflich M266/M266

      U     "m_eins"; 
      S     "DB_DM_M_FUNKTION".MX_QuitPflicht[265]; 
      R     "DB_DM_M_FUNKTION".MX_QuitPflicht[266]; 
NETWORK
TITLE =Reset M265/M266

      O     "DB_DM_M_FUNKTION".MX_M[30]; 
      O     "M_Reset_Taste"; 
      R     "M_24aktiv"; 
      R     "DB_DM_M_FUNKTION".MX_M[265]; 
      R     "DB_DM_M_FUNKTION".MX_M[266]; 
NETWORK
TITLE =Messdose ein

      U     "DB_DM_M_FUNKTION".MX_M[265]; 
      S     "M_24aktiv"; 
NETWORK
TITLE =Quittierung M265 (Messdose aktivieren)

      U     "I_MessDose_BB"; 
      U     "DB_DM_M_FUNKTION".MX_M[265]; 
      R     "DB_DM_M_FUNKTION".MX_M[265]; 
      S     "DB_DM_M_FUNKTION".MX_Quitt[265]; 
NETWORK
TITLE =Reset M266 (Messdose deaktivieren)

      U     "DB_DM_M_FUNKTION".MX_M[266]; 
      U     "M_24aktiv"; 
      R     "M_24aktiv"; 
      R     "DB_DM_M_FUNKTION".MX_M[266]; 
NETWORK
TITLE =Messdose einschalten

      U     "M_24aktiv"; 
      =     "O_MessDose_Start"; 
      =     "DB_NC_PLC".WZW57_0; 
      =     "I_MessDose_BB"; //sth 10.03.05 Brücke aus Schaltplan enttfehrnt
NETWORK
TITLE =Messdose abblasen verlängerter Impuls

      U     "M_24aktiv"; 
      L     S5T#2S; 
      SV    "T_Messd_abbl"; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
      NOP   0; 
NETWORK
TITLE =Messdose abblasen

      U     "T_Messd_abbl"; 
      =     "O_AL_Messdose_abblasen"; 
NETWORK
TITLE =Messdose Überwachung Bereitschaft

      U     "M_24aktiv"; // Taster aktiv
      UN    "I_MessDose_BB"; 
      L     S5T#500MS; 
      SE    "T_Messdose_Bereit"; 
NETWORK
TITLE =Störung: Messdose nicht bereit

      U     "T_Messdose_Bereit"; 
      U     "M_24aktiv"; 
      UN    "I_MessDose_BB"; 
      S     "DB_FEHLERMELDUNG".VS_Halt_Haupt._701051_MessDose_Bereit0; 
NETWORK
TITLE =Verfahrbereichseinschränkung durch Laser/Kalibrierklappe
//Verfahrbereichseinschränkung wegen neuer Blum-Klappe bei Maschinen mit A-Achse
ENDE: U(    ; 
      ON    "I_MT_KALIBRIER_EING"; 
      O     "I_MT_KALIBRIER_AUSG"; 

      ON    "I_MT_KAL_KLAPPE_ZU"; 


      )     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDReal._16_ANWAHL_KALIBRIERRING; 
      L     1.000000e+000; 
      ==R   ; 
      )     ; 
      O     ; 
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     3; 
      ==I   ; 
      )     ; 
      NOT   ; 
      U(    ; 
      ON    "I_LA_eingeschwenkt"; 
      O     "I_LA_ausgefahren"; 
      ON    "Mx_LA_Klappe_zu"; 
      )     ; 

      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._032_ANWAHL_WZVERMESSUNG; 
      L     0; 
      >I    ; 
      )     ; 
      =     "DB_ACHSE2_Y".A_SWLimit2Minus; 


END_FUNCTION

