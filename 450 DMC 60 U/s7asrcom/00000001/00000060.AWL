FUNCTION "FC_SK_FehlerUe" : VOID
TITLE =
//$Revision: 1.10 $
//$Date: 2008/03/03 11:26:13CET $
//$Author: kee $
//
//Logbuch:
//
//V0.1 / 18.01.2000 / SCH
//
//- Modul Erstellt
{ S7_language := '7(1) Deutsch (Deutschland)  26.02.2008  09:47:27' }
AUTHOR : SCH
FAMILY : DM_WP
NAME : FB_PST
VERSION : 0.1


VAR_INPUT
  E_Enabled : BOOL ;	//Modul Freigabe
END_VAR
VAR_TEMP
  T_Klm_SK_Beide_1Sig : BOOL ;	//Fehler Klemmung beide 1-Signal
  T_SK_Stellung_Beide_1S : BOOL ;	//Fehler Stellung SK beide 1-Signal
  T_SK_Stellung_Undef : BOOL ;	//Fehler SK Stellung undefiniert
  T_SK_Klemmung_Auf_0S : BOOL ;	//Fehler SK Klemmung auf 0Sig 
  T_SK_Klemmung_Zu_0S : BOOL ;	//Fehler SK Klemmung zu 0Sig
  T_SK_Vertikal_0Sig : BOOL ;	//Fehler SK Stellung Vertikal 0-Signal
  T_SK_Horizontal_0Sig : BOOL ;	//Fehler SK Stellung Horizontal 0-Signal
  T_Kabinentuer_Zu_0Sig : BOOL ;	//Fehler Kabinentuer Zu 0 Signal
  T_SK_Freigabe_0Sig : BOOL ;	//Fehler Freigabe SK 0-Signal
  T_SK_Nicht_Erlaubt : BOOL ;	//Fehler WZW nicht erlaubt
  T_Dummy : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =SK: Fehlerbit

      CLR   ; 
      =     #T_Klm_SK_Beide_1Sig; 
      =     #T_SK_Stellung_Undef; 
      =     #T_SK_Stellung_Beide_1S; 
      =     #T_SK_Klemmung_Auf_0S; 
      =     #T_SK_Klemmung_Zu_0S; 
      =     #T_SK_Vertikal_0Sig; 
      =     #T_SK_Horizontal_0Sig; 
      =     #T_Kabinentuer_Zu_0Sig; 
      =     #T_SK_Freigabe_0Sig; 
      =     #T_SK_Nicht_Erlaubt; 


      U     "DB_OEM".OEM_SKRettaktiv; 
      R     "M_SK_ERR"; 

      U     #E_Enabled; // Freigabe Modul
      U(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     1; 
      ==I   ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     2; 
      ==I   ; 
      )     ; 
      O(    ; 
      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     4; 
      ==I   ; 
      )     ; 
      )     ; 
      SPBN  MEND; // nicht vorhanden => Ende

// Ueberwachung Endlagen beide 1-Signal
//          

//Schwenkkopfüberwachung
      U     "M_Ruecksetze_Fehler"; 
      R     "DB_FEHLERMELDUNG".Meldung._702133; 


      L     "MB_aktiver_Verfahrbereic"; 
      L     53; 
      ==I   ; 
      UN    "DB_NC_PLC".SK_Kopf_Horizontal; 
      UN    "M_SK_Aktiv"; 
      O     ; 
      L     "MB_aktiver_Verfahrbereic"; 
      L     54; 
      ==I   ; 
      UN    "DB_NC_PLC".SK_Kopf_Vertikal; 
      UN    "M_SK_Aktiv"; 
      CLR   ; 
      S     "DB_FEHLERMELDUNG".Meldung._702133; 





      U     "DB_NC_PLC".SK_SK_Typ; 
      SPB   SFK1; 

      CLR   ; 
      =     #T_Dummy; 

      CALL "FC_UT_SLAVE_UEBERW" (
           E_Slave_1                := "DB_SLAVE_DIAGNOSE".slave1A[27],
           E_Slave_2                := "DB_SLAVE_DIAGNOSE".slave1A[28],
           E_Slave_3                := FALSE,
           EA_Fehler_Slave_1        := "DB_FEHLERMELDUNG".Spi_VS_Halt._700303_Err_ASI_Slave_27,
           EA_Fehler_Slave_2        := "DB_FEHLERMELDUNG".Spi_VS_Halt._700304_Err_ASI_Slave_28,
           EA_Fehler_Slave_3        := #T_Dummy,
           EA_Fehler_Slave          := #T_Dummy);



      U     "I_SK_Klemmung_geklemmt"; // Schwenkeinheit beide
      U     "I_SK_Klemmung_geloest"; // Endlagen 1-Signal
      SPBN  SK1; 
      SET   ; 
      =     #T_Klm_SK_Beide_1Sig; 

SK1:  U     "I_SK_HOR_Ebene"; // SK Stellung 
      U     "I_SK_VER_Ebene"; // undefiniert
      UN    "M_SK_Aktiv"; 
      SPBN  SK2; 
      SET   ; 
      =     #T_SK_Stellung_Beide_1S; 

SK2:  U     "DB_OEM".OEM_SKRettaktiv; 
      SPB   SK00; 

NETWORK
TITLE =
//
// Grundstellungsueberwachung
// 
SFK1: U     "M_SK_Aktiv"; //  #E_PW_Aktiv
      SPB   ENDL; 

      L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     1; 
      ==I   ; 
      SPBN  SK00; 

      U     "I_SK_HOR_Ebene"; // Schwenkeinheit beide
      XN    "I_SK_VER_Ebene"; // Endlagen 0-Signal
      =     #T_SK_Stellung_Undef; 

      UN    "I_SK_Klemmung_geklemmt"; // Hub-Einheit 
      =     #T_SK_Klemmung_Zu_0S; 
      SPA   SK00; 
NETWORK
TITLE =
//Ueberwachung der Endlagen während des Schwenkens (Statisch)
      O     "M_3Te_Betriebsart"; 
      O     "M_4Te_Betriebsart"; 
      O     "M_Einrichtbetrieb"; 
      ON    "M_Leistung_Steht_an"; 
      =     #T_SK_Nicht_Erlaubt; 

ENDL: L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     1; 
      ==I   ; 
      SPBN  E003; 

      O     "M_SK_Klemm_Bewegung"; // M260-253
      SPB   E001; 

      U     "O_SK_Klemmung_loesen"; 
      UN    "I_SK_Klemmung_geloest"; 
      SPBN  E002; 
      SET   ; 
      =     #T_SK_Klemmung_Auf_0S; 

E002: U     "O_SK_Klemmung_zu"; 
      UN    "I_SK_Klemmung_geklemmt"; 
      SPBN  E001; 
      SET   ; 
      =     #T_SK_Klemmung_Zu_0S; 

E001: U     "M_SK_Kopf_Bewegung"; 
      SPB   E003; 
      U     "O_SK_HOR_schwenken"; 
      UN    "I_SK_HOR_Ebene"; 
      SPBN  E004; 
      SET   ; 
      =     #T_SK_Horizontal_0Sig; 

E004: U     "O_SK_VER_schwenken"; 
      UN    "I_SK_VER_Ebene"; 
      SPBN  E003; 
      SET   ; 
      =     #T_SK_Vertikal_0Sig; 

E003: U     "M_Spritz_Tuer_Zu"; 
      U     "M_Leistung_Steht_an"; 
      O     ; 
      U     "DB_PLC_MD_DB20".UDHex._37_Bit5_LA_AUSF_BA4; 
      U     "M_4Te_Betriebsart"; 

      SPB   E005; 
      SET   ; 
      =     #T_Kabinentuer_Zu_0Sig; 

E005: U     "I_SK_Freigabe"; 
      SPB   E009; 
      SET   ; 
      =     #T_SK_Freigabe_0Sig; 

NETWORK
TITLE =SK: Timer ScnkwenkkopfFehlerueberwachung
//Zeitueberwachung Abhaengig von angestossener Bewegung (Dynamisch)
E009: L     "DB_PLC_MD_DB20".UDInt._007_ANWAHL_FRAESKOPF; 
      L     1; 
      ==I   ; 
      SPBN  SK00; 

      U     #T_Klm_SK_Beide_1Sig; 
      O     #T_SK_Stellung_Undef; 
      O     #T_SK_Stellung_Beide_1S; 
      O     #T_SK_Klemmung_Auf_0S; 
      O     #T_SK_Klemmung_Zu_0S; 
      O     #T_SK_Vertikal_0Sig; 
      O     #T_SK_Horizontal_0Sig; 
      O     #T_Kabinentuer_Zu_0Sig; 
      O     #T_SK_Freigabe_0Sig; 
      O     #T_SK_Nicht_Erlaubt; 
      UN    "M_SK_ERR"; 
      UN    "M_SK_Stat"; 
      L     S5T#1S; 
      SE    "Te_SK_Entprellzeit"; 


      CALL "FC_UT_Bit_auf_Byte" (
           E_Bit0                   := "O_SK_Klemmung_loesen",
           E_Bit1                   := "O_SK_Klemmung_zu",
           E_Bit2                   := "O_SK_VER_schwenken",
           E_Bit3                   := "O_SK_HOR_schwenken",
           E_Bit4                   := FALSE,
           E_Bit5                   := FALSE,
           E_Bit6                   := FALSE,
           E_Bit7                   := FALSE,
           EA_result                := "B_SPos_Speicher");


// Zeit starten abhängig ob eine Bewegung gestartet wird

      L     "B_SPos_Speicher"; 
      L     "B_SPos_Speicher_Alt"; 

      ==I   ; 
      SPB   B001; 

      L     S5T#10S; 
      SET   ; 
      SV    "Te_SK_FehlerUebw"; 
      CLR   ; 
      SV    "Te_SK_FehlerUebw"; 

      L     "B_SPos_Speicher"; 
      T     "B_SPos_Speicher_Alt"; 
      SPA   SK00; 

B001: U     "M_SK_ERR"; 
      SPB   SK00; 

      U     "M_SK_Klemm_Bewegung"; 
      SPBN  B003; 
      U     "O_SK_Klemmung_loesen"; 
      UN    "I_SK_Klemmung_geloest"; 
      UN    "Te_SK_FehlerUebw"; 
      SPBN  B002; 
      SET   ; 
      =     #T_SK_Klemmung_Auf_0S; 
      SPA   B010; 

B002: U     "O_SK_Klemmung_zu"; 
      UN    "I_SK_Klemmung_geklemmt"; 
      UN    "Te_SK_FehlerUebw"; 
      SPBN  B003; 
      SET   ; 
      =     #T_SK_Klemmung_Zu_0S; 
      SPA   B010; 

B003: U     "M_SK_Kopf_Bewegung"; 
      SPBN  SK00; 
      U     "O_SK_VER_schwenken"; 
      UN    "I_SK_VER_Ebene"; 
      UN    "Te_SK_FehlerUebw"; 
      SPBN  B005; 
      SET   ; 
      =     #T_SK_Vertikal_0Sig; 
      SPA   B010; 

B005: U     "O_SK_HOR_schwenken"; 
      UN    "I_SK_HOR_Ebene"; 
      UN    "Te_SK_FehlerUebw"; 
      SPBN  SK00; 
      SET   ; 
      =     #T_SK_Horizontal_0Sig; 
      SPA   B010; 

B010: L     2; 
      T     "B_SKZ_SErr"; 
      SPA   SK02; 

NETWORK
TITLE =SK: Timer Schwenkopf Entprellen

SK00: L     "B_SKZ_SErr"; // Schritt 0 ?
      L     0; 
      ==I   ; 
      SPBN  SK01; // Nein => Schrtitt 1

// -----------------------------------------------------------------------------


// -----------------------------------------------------------------------------

      U     #T_Klm_SK_Beide_1Sig; 
      O     #T_SK_Stellung_Undef; 
      O     #T_SK_Stellung_Beide_1S; 
      O     #T_SK_Klemmung_Auf_0S; 
      O     #T_SK_Klemmung_Zu_0S; 
      O     #T_SK_Vertikal_0Sig; 
      O     #T_SK_Horizontal_0Sig; 
      O     #T_Kabinentuer_Zu_0Sig; 
      O     #T_SK_Freigabe_0Sig; 
      O     #T_SK_Nicht_Erlaubt; 
      UN    "M_SK_ERR"; 
      UN    "M_SK_Stat"; 
      SPBN  SE01; 

//      L     S5T#300MS
//      SET   
//      SV    "Te_SK_Entprellzeit"
//      CLR   
//      SV    "Te_SK_Entprellzeit"

      L     1; 
      T     "B_SKZ_SErr"; 
      SPA   MEND; 

SE01: U     "M_Reset_Taste"; 
      SPBN  MEND; 

      L     3; 
      T     "B_SKZ_SErr"; 
      SPA   SK03; 
NETWORK
TITLE =Schritt 1

SK01: L     "B_SKZ_SErr"; // Schritt 1 ?
      L     1; 
      ==I   ; 
      SPBN  SK02; // Nein => Schrtitt 2

// -----------------------------------------------------------------------------


// -----------------------------------------------------------------------------

      U     #T_Klm_SK_Beide_1Sig; 
      O     #T_SK_Stellung_Undef; 
      O     #T_SK_Stellung_Beide_1S; 
      O     #T_SK_Klemmung_Auf_0S; 
      O     #T_SK_Klemmung_Zu_0S; 
      O     #T_SK_Vertikal_0Sig; 
      O     #T_SK_Horizontal_0Sig; 
      O     #T_Kabinentuer_Zu_0Sig; 
      O     #T_SK_Freigabe_0Sig; 
      O     #T_SK_Nicht_Erlaubt; 
      SPB   E101; 
      L     0; 
      T     "B_SKZ_SErr"; 
      BEA   ; 

E101: UN    "Te_SK_Entprellzeit"; 
      SPB   MEND; 
      L     2; 
      T     "B_SKZ_SErr"; 
      BEA   ; 

NETWORK
TITLE =Schritt 2

SK02: L     "B_SKZ_SErr"; // Schritt 2 ?
      L     2; 
      ==I   ; 
      SPBN  SK03; // Nein => Schrtitt 3

// -----------------------------------------------------------------------------
//    
      U     #T_Klm_SK_Beide_1Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700643_Klm_SK_Beide_1S; 

      U     #T_SK_Stellung_Undef; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700645_SK_Stlg_Undef; 

      U     #T_SK_Stellung_Beide_1S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700644_SK_Stlg_Beide_1S; 

      U     #T_SK_Klemmung_Auf_0S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700646_SK_Klemm_Auf_0S; 

      U     #T_SK_Klemmung_Zu_0S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700647_SK_Klemm_Zu_0; 

      U     #T_SK_Vertikal_0Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700648_SK_Vertikal_0Sig; 

      U     #T_SK_Horizontal_0Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700649_SK_Horizontal_0S; 

      U     #T_Kabinentuer_Zu_0Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700651_KabinenTuer_Zu_0; 

      U     #T_SK_Freigabe_0Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700650_SK_Freigabe_0Sig; 

      U     #T_SK_Nicht_Erlaubt; 
//      =     "DB_FehlerMeldung_DB102".Spi_VS_Halt._700152_WZW_Nicht_Erlaub
      S     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700925_WZW_ni_erlaubt; 

      SET   ; 
      =     "M_SK_ERR"; 

// -----------------------------------------------------------------------------

      L     3; 
      T     "B_SKZ_SErr"; 

NETWORK
TITLE =Schritt 3
//Warten bis Fehler Quittierung oder Reset 
SK03: L     "B_SKZ_SErr"; // Schritt 3 ?
      L     3; 
      ==I   ; 
      SPBN  MEND; 
// -----------------------------------------------------------------------------

      U     "M_Reset_Taste"; 
      O     "M_Fehler_Quit_Taste"; 
      SPBN  MEND; 

      L     0; 
      T     "B_SKZ_SErr"; 

      CLR   ; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700643_Klm_SK_Beide_1S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700645_SK_Stlg_Undef; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700644_SK_Stlg_Beide_1S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700646_SK_Klemm_Auf_0S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700647_SK_Klemm_Zu_0; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700648_SK_Vertikal_0Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700649_SK_Horizontal_0S; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700651_KabinenTuer_Zu_0; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700650_SK_Freigabe_0Sig; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt._700152_WZW_Nicht_Erlaub; 
      =     "DB_FEHLERMELDUNG".Spi_VS_Halt_Haupt._700925_WZW_ni_erlaubt; 
      =     "M_SK_ERR"; 

MEND: NOP   0; 
END_FUNCTION

