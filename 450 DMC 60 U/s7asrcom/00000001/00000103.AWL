FUNCTION "FC_ACHS_9" : VOID
TITLE =
//$Revision: 1.19 $
//$Date: 2008/10/11 09:30:58CEST $
//$Author: hgc $
VERSION : 0.1


VAR_TEMP
  T_Vorschub_Halt_Ax9 : BOOL ;	
  Tx_Override_aktiv : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =9-Achse: Aktivierung Softwarenocken nachdem Achse referiert ist

      UN    "DB_PLC_MD_DB20".UDHex._03_BIT0_ACHSE9_MESSYST; 
      U     "DB_ACHSE9".E_RefSyn1; 
      O     ; 
      U     "DB_PLC_MD_DB20".UDHex._03_BIT0_ACHSE9_MESSYST; 
      U     "DB_ACHSE9".E_RefSyn2; 
      =     "DB_ACHSE9".A_SWCam; 
NETWORK
TITLE =9-Achse: Vorschub Halt

      O     "M_Vorschub_Halt_Asyn3"; 
      O     "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_Kanal; 
      =     #T_Vorschub_Halt_Ax9; 
NETWORK
TITLE =9-Achse: Override aktivieren

      O     "DB_PLC_MD_DB20".UDHex._04_BIT4_EINRICHT_BETR; 
      O     "DB_PLC_MD_DB20".UDHex._04_BIT0_OVR_HILFSACHSEN; 
      O     ; 
      U(    ; 
      O     "DB_OEM".OEM_PW_Rettbildaktiv; 
      O     "DB_OEM".OEM_SpeEinrbildaktiv; 
      O     "DB_OEM".OEM_SpeRettbildaktiv; 
      O     "DB_OEM".OEM_SpeRettbildaktiv3; // "DB_SIEM_BAG".E_JOG
      ON    "MX_Jobbetrieb"; 
      )     ; 
      U     "M_Handbetrieb"; 
      =     #Tx_Override_aktiv; 
NETWORK
TITLE =9. Achse: Aufruf FC_ACHSE_HILF

      CALL "FC_ACHSE_HILF" (
           EX_EN                    := TRUE,
           EX_UDHex_00_IB_Bit_Achse := "DB_PLC_MD_DB20".UDHex._01_BIT0_ACHSE9_AUSBLEND,
           EX_UDHex_01_Ax_Messystem := "DB_PLC_MD_DB20".UDHex._03_BIT0_ACHSE9_MESSYST,
           EX_UDHex_RPF_Richtung    := FALSE,
           EX_Option_Safety         := "O_INFEED_ENABLE_NCU",// Bei Solutionline bleibt RF und ImpFrg für 3 Sek. nach Not-Aus anstehen
           EX_Safe_Bremsentest      := FALSE,
           EX_Safe_StopC_aktiv      := FALSE,
           EX_Safe_StopA_aktiv      := "DB_ACHSE9".E_StopAB,
           EX_Safe_Teststop_laeuft  := "DB_SPL".IPLC_ZD_GR2_RUN,
           EX_Maschine_Ein          := "M_MA_Maschine_Ein",
           EX_Antrieb_Sperre        := "DB_FAMILIEN_MODUL".Achssperre_extern.E_Ax_9,
           EX_Freigabe_Regler       := "DB_Achs9_DrehSt".MX_Ax_Leistung_Aktiv,
           EX_Leistung_steht_an     := "DB_Achs9_DrehSt".MX_Ax_Leistung_Aktiv,
           EX_VSHalt_EinzelAchse    := "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_9_Achse,
           EX_VSHalt_Gruppe         := #T_Vorschub_Halt_Ax9,
           EX_VSHalt_Kanal          := "DB_FAMILIEN_MODUL".Gesamt.A_Vorschub_Halt_Kanal,
           EX_RefPkt_Achse          := FALSE,
           EX_Abbruch_Positionieren := "DB_Achs9_DrehSt".MX_Abbruch_Positionieren,
           EX_Ax_KorrWirksam_1x7    := #Tx_Override_aktiv,
           EX_Ax_E_RefSyn1_60x4     := "DB_ACHSE9".E_RefSyn1,
           EX_Ax_E_FahrAF_61x0      := "DB_ACHSE9".E_TravRequ,
           EX_Ax_E_Steht_61x4       := "DB_ACHSE9".E_Stat,
           EX_Ax_E_LageRegler_61x5  := "DB_ACHSE9".E_PositContr,
           EX_Ax_E_DrehzRegler_61x6 := "DB_ACHSE9".E_SpeedContr,
           EX_Ax_E_StromRegler_61x7 := "DB_ACHSE9".E_CurrentContr,
           EX_Ax_E_PLC_Achse_63x1   := "DB_ACHSE9".E_PLCCtrlAx,
           EX_Ax_E_FBMinus_64x6     := "DB_ACHSE9".E_TCMinus,
           EX_Ax_E_FBPlus_64x7      := "DB_ACHSE9".E_TCPlus,
           EX_Ax_E_ChPoss_68x5      := "DB_ACHSE9".E_ChPoss,
           EX_Ax_E_NeutrASp_68x6    := "DB_ACHSE9".E_NeutrASp,
           EX_Ax_E_MotOK_93x5       := "DB_ACHSE9".E_MotOK,
           EX_Ax_E_IntSperr_93x6    := "DB_ACHSE9".E_IntegratDisable,
           EX_Ax_E_ImpFrei_93x7     := "DB_ACHSE9".E_PulseEnable,
           EX_Ax_E_MTempWarn_94x0   := "DB_ACHSE9".E_MTempWarn,
           EX_Ax_E_KTempWarn_94x1   := "DB_ACHSE9".E_HTempWarn,
           EX_FahrAnf_Positiv       := "DB_Achs9_DrehSt".MX_FahrAnf_Plus,
           EX_FahrAnf_Negativ       := "DB_Achs9_DrehSt".MX_FahrAnf_Minus,
           EX_Inch_Umrechnen        := FALSE,
           EB_Achs_Typ              := B#16#3,
           EB_MSST_Vorschub_OVR     := "MSST_Vorschub_OVR",
           EB_Positierung           := B#16#0,
           EI_Teilung               := 0,
           EI_Achs_Nr               := 9,
           ER_SollPosition          := "DB_Achs9_DrehSt".MR_SollPosition,
           ER_Istposition           := "DB_Achs9_DrehSt".MR_IstPosition,
           ER_Geschwindigkeit       := 0.000000e+000,
           ET_Timer_Ax_Freigabe     := "Te_Achs9",
           EAX_Ax_Anforderung_Pos   := "DB_Achs9_DrehSt".MX_Anf_Positionieren,
           EAX_Ax_Rueckmeldung_Pos  := "DB_Achs9_DrehSt".MX_Rueck_Positionieren,
           EAX_Ax_Fehler_Pos        := "DB_Achs9_DrehSt".MX_Fehler_Positionieren,
           EAX_Ax_Anf_Referenzieren := "DB_Achs9_DrehSt".MX_Anf_Referieren,
           EAX_Ax_A_ASpSperre_1x3   := "DB_ACHSE9".A_ASpDisable,
           EAX_Ax_A_NachfBetr_1x4   := "DB_ACHSE9".A_Follow_upMode,
           EAX_Ax_A_LageMess1_1x5   := "DB_ACHSE9".A_PosMeas1,
           EAX_Ax_A_LageMess2_1x6   := "DB_ACHSE9".A_PosMeas2,
           EAX_Ax_A_KorrW_1x7       := "DB_ACHSE9".A_ORactive,
           EAX_Ax_A_ReglerFrei_2x1  := "DB_ACHSE9".A_ContrEnable,
           EAX_Ax_A_RestWegRe_2x2   := "DB_ACHSE9".A_DelDTGSpReset,
           EAX_Ax_A_VSSpHalt_4x3    := "DB_ACHSE9".A_FDSpStop,
           EAX_Ax_A_Minus_4x6       := "DB_ACHSE9".A_Minus,
           EAX_Ax_A_Plus_4x7        := "DB_ACHSE9".A_Plus,
           EAX_Ax_A_NCASpChA_8x0    := "DB_ACHSE9".A_NCASpChanA,
           EAX_Ax_A_NCASpStrobe_8x4 := "DB_ACHSE9".A_NCASpStrobe,
           EAX_Ax_A_PLCASp_8x7      := "DB_ACHSE9".A_PLCASp,
           EAX_Ax_A_RefVerzoeg_12x7 := "DB_ACHSE9".A_DelayRef,
           EAX_Ax_A_ImpFrei_21x7    := "DB_ACHSE9".A_PulseEnable,
           EAX_Ax_A_PLC_Achse_28x7  := "DB_ACHSE9".A_PLCAxis,
           EAX_AX_RefSync1          := "DB_Achs9_DrehSt".MX_Achs_Referiert,
           EAX_Alarm_Regler         := "DB_FEHLERMELDUNG".Spi_VS_Halt._700124_9_IMP_REGFRG_0S,
           EAX_Alarm_VSSP_Mot_Temp  := "DB_FEHLERMELDUNG".Spi_VS_Halt._700136_9_TEMP_ZU_HOCH,
           EAX_Alarm_VSSP_KK_Temp   := "DB_FEHLERMELDUNG".Spi_VS_Halt1._510131_9_KK_TEMP_ZH,
           EAX_Alarm_NA_MotOK       := "DB_FEHLERMELDUNG".Spi_VS_Halt3._701140_9_DR_READY_0,
           EAX_Alarm_FehlerPos      := "DB_FEHLERMELDUNG".Warnung._701916_Rueckm_Achs_9_un,
           EAX_M_Bremse_oeffnen     := "eax_9.AX_Bremse_oeffnen",
           EAX_Anf_Lesen_AxPos      := "DB_Achs9_DrehSt".MX_Ax_Position_Lesen,
           EAX_Rdy_Lesen_AxPos      := "DB_Achs9_DrehSt".M_Ax_Pos_Lesen_Fertig,
           EAX_Achse_Fahranf        := "DB_Achs9_DrehSt".MX_Ax_FahrAnf,
           EAX_Achse_InPosition     := "DB_Achs9_DrehSt".MX_Ax_InPosition,
           EAX_Achse_Fehler_Pos     := "DB_Achs9_DrehSt".MX_Ax_Fehler_Pos,
           EAX_Achse_Positioniert   := "DB_Achs9_DrehSt".MX_Ax_Positioniert,
           EAX_InPos_Merker         := "DB_Achs9_DrehSt".MX_FP_Achse_in_Pos,
           EAB_Ax_VS_Korrektur_DBB0 := "DB_ACHSE9".A_FD_OR,
           EAB_SKZ_Achse            := "DB_Achs9_DrehSt".MB_SKZ_Achse);

NETWORK
TITLE =Achse Reset Fahranforderung

      U     "DB_Achs9_DrehSt".MX_Abbruch_Positionieren; 
      =     "DB_ACHSE9".A_AxReset; 
      R     "DB_Achs9_DrehSt".MX_Abbruch_Positionieren; 
      R     "DB_Achs9_DrehSt".MX_Ax_FahrAnf; 
NETWORK
TITLE =Achse fährt

      ON    "DB_ACHSE9".E_Stat; 
      O     "DB_ACHSE9".E_TCMinus; 
      O     "DB_ACHSE9".E_TCPlus; 
      =     "DB_Achs9_DrehSt".MX_Achse_Faehrt; 
NETWORK
TITLE =Achse Bremse öffnen abfallverzögert

      U     "eax_9.AX_Bremse_oeffnen"; 
      L     S5T#2S; 
      SA    "Ta_9.Achse_Bremse_auf"; 
NETWORK
TITLE =Achse Bremse öffnen

      U     "Ta_9.Achse_Bremse_auf"; 
      =     "O_PS_DREHST_BREMSE_AUF"; 
END_FUNCTION

